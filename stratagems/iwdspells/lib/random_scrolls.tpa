DEFINE_ACTION_FUNCTION random_scrolls BEGIN



   ACTION_IF is_bg2 BEGIN
   
      OUTER_SPRINT string_bg2_1 "iwd1"
      ACTION_FOR_EACH spell IN
       SNILLOCS_SNOWBALL_SWARM
       DECASTAVE
       CATS_GRACE
       ICELANCE
       LANCE_OF_DISRUPTION
       BELTYNS_BURNING_BLOOD
       EMOTION_COURAGE
       EMOTION_HOPE
       EMOTION_FEAR
       EMOTION_HOPELESSNESS
       MORDENKAINENS_FORCE_MISSILES
       SHOUT
       VITRIOLIC_SPHERE
       CONJURE_LESSER_WATER_ELEMENTAL
       CONJURE_LESSER_FIRE_ELEMENTAL
       EXPEDITIOUS_RETREAT
       SHADOW_MONSTERS
       DEMI_SHADOW_MONSTERS
       SUMMON_SHADOW
       MONSTER_SUMMONING_4
      BEGIN
       OUTER_SPRINT spell "WIZARD_%spell%_SCROLL"
       OUTER_SPRINT scroll EVAL "%%spell%%"
       OUTER_SPRINT string_bg2_1 "%string_bg2_1% %scroll%"
      END
  
      OUTER_SPRINT string_bg2_2 "iwd2"
      ACTION_FOR_EACH spell IN
       SHROUD_OF_FLAME
       OTILUKES_FREEZING_SPHERE
       DARTS_OF_BONE
       SOUL_EATER
       TROLLISH_FORTITUDE
       CONJURE_WATER_ELEMENTAL
       MONSTER_SUMMONING_5
       ANTIMAGIC_SHELL
       LICH_TOUCH
       SHADES
       MALAVONS_RAGE
       MONSTER_SUMMONING_7
       MONSTER_SUMMONING_6
       ACID_STORM
       SUFFOCATE
       MIND_BLANK
       GREAT_SHOUT
       IRON_BODY
       MORDENKAINENS_SWORD_IWD
       POWER_WORD_BLIND
      BEGIN
       OUTER_SPRINT spell "WIZARD_%spell%_SCROLL"
       OUTER_SPRINT scroll EVAL "%%spell%%"
       OUTER_SPRINT string_bg2_2 "%string_bg2_2% %scroll%"
      END



      LAF add_to_random_scrolls STR_VAR string="%string_bg2_1%" RET entry_low=filename END
      LAF add_to_random_scrolls STR_VAR string="%string_bg2_2%" RET entry_high=filename END
      LAF write_table_entry STR_VAR file=rndtreas row=WELL_OFF column=12 arguments="%entry_low%" END
      LAF write_table_entry STR_VAR file=rndtreas row=WEALTHY column=14 arguments="%entry_low%" END
      LAF write_table_entry STR_VAR file=rndtreas row=RICH column=7 arguments="%entry_high%" END
      LAF write_table_entry STR_VAR file=rndmagic row=LOW column=15 arguments="%entry_low%" END
      LAF write_table_entry STR_VAR file=rndmagic row=HIGH column=17 arguments="%entry_high%" END
   END

   ACTION_IF is_bg1 BEGIN
   
      OUTER_SPRINT string_bg1 "iwd3"
      ACTION_FOR_EACH spell IN
       EXPEDITIOUS_RETREAT
       SNILLOCS_SNOWBALL_SWARM
       DECASTAVE
       CATS_GRACE
       ICELANCE
       LANCE_OF_DISRUPTION
       BELTYNS_BURNING_BLOOD
       EMOTION_COURAGE
       EMOTION_HOPE
       EMOTION_FEAR
       MORDENKAINENS_FORCE_MISSILES
       SHOUT
       VITRIOLIC_SPHERE
       SHADOW_MONSTERS
       POLYMORPH_OTHER
       PROTECTION_FROM_FIRE
       PROTECTION_FROM_COLD
       DEAFNESS
       SNILLOCS_SNOWBALL_SWARM
       ICELANCE
      BEGIN
       OUTER_SPRINT spell "WIZARD_%spell%_SCROLL"
       OUTER_SPRINT scroll EVAL "%%spell%%"
       OUTER_SPRINT string_bg1 "%string_bg1% %scroll%"
      END


      LAF add_to_random_scrolls STR_VAR string="%string_bg1%" RET entry_bg1=filename END
      ACTION_IF GAME_IS eet BEGIN
         LAF write_table_entry STR_VAR file=rndtreas row=WEAL_BG1 column=11 arguments="%entry_bg1%" END
         LAF write_table_entry STR_VAR file=rndtreas row=RICH_BG1 column=11 arguments="%entry_bg1%" END
         LAF write_table_entry STR_VAR file=rndmagic row=POOP_BG1 column=12 arguments="%entry_bg1%" END
         LAF write_table_entry STR_VAR file=rndmagic row=HIGH_BG1 column=15 arguments="%entry_bg1%" END
      END ELSE BEGIN
         LAF write_table_entry STR_VAR file=rndtreas row=WEALTHY column=11 arguments="%entry_bg1%" END
         LAF write_table_entry STR_VAR file=rndtreas row=RICH column=11 arguments="%entry_bg1%" END
         LAF write_table_entry STR_VAR file=rndmagic row=POOP column=12 arguments="%entry_bg1%" END
         LAF write_table_entry STR_VAR file=rndmagic row=HIGH column=15 arguments="%entry_bg1%" END
      END

   END
END

DEFINE_ACTION_FUNCTION add_to_random_scrolls
    STR_VAR string=""
    RET filename
BEGIN
    COPY_EXISTING "rndscrol.2da" override
         COUNT_2DA_ROWS 21 rowcount
         INSERT_2DA_ROW rowcount 21 "%string%"
    BUT_ONLY
    OUTER_SET number=rowcount + 1
    ACTION_IF number<10 BEGIN
       OUTER_SPRINT filename "RNDSCR0%number%"
    END ELSE BEGIN
       OUTER_SPRINT filename "RNDSCR%number%"
    END
END