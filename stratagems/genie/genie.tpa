DEFINE_ACTION_FUNCTION genie STR_VAR version="" BEGIN

   ACTION_IF version=1 BEGIN
         LAF make_label STR_VAR label=dw#genie_hp END
   END
   LAF action_check_ini STR_VAR ini=genies_cast_slowly RET value=value END
   ACTION_IF value=0 BEGIN
         LAF make_label STR_VAR label=dw#genie_innate END
   END


         LAF genie_scripts END
         LAF genie_resources END
         LAF make_efreeti END
         LAF make_djinni END
         LAF make_dao END
         LAF genie_compatibility END

END

/////////////////////////////////////////////////////////////////////////////////////////////
////   compile scripts
/////////////////////////////////////////////////////////////////////////////////////////////


DEFINE_ACTION_FUNCTION genie_scripts BEGIN

   // efreeti

   LAF ssl_to_bcs STR_VAR script=efreet01 location=ssl END
   LAF clone_script STR_VAR script=~efreet01=>efreet04~ END
   LAF ssl_to_bcs STR_VAR script=efreet01 location=ssl variables=~NoCloud=True~ rename_to=ddguard END
   LAF clone_script STR_VAR script=~ddguard=>dw#efree~ END


   // djinni
   
   LAF ssl_to_bcs STR_VAR script=~djinni01 sumdj01~ location=ssl END
   LAF clone_script STR_VAR script=~djinni01=>djinni04~ END
   LAF ssl_to_bcs STR_VAR script=djinni01 location=ssl variables=~NoCloud=True~ rename_to=ppdjinn2 END

   // dao
   
   LAF ssl_to_bcs STR_VAR script=dao01 location=ssl END

END

/////////////////////////////////////////////////////////////////////////////////////////////
////   assemble needed resources
/////////////////////////////////////////////////////////////////////////////////////////////


DEFINE_ACTION_FUNCTION genie_resources BEGIN

	////	Copy over Demi's gas-form stuff and the djinni innate ring

        LAF install STR_VAR files=~dvgasfrm.spl dvgasfrm.itm dw#djinn.itm~ location=resource END

        // the Wave Halberd targets Efreeti by class, which is broken by them becoming FMCs in non-EE instlals.
        // We retarget by Race=Genie, Align=LE, which is extensionally equivalent
        
        ACTION_IF !enhanced_edition BEGIN
         LAF install STR_VAR files=dw#kilef.eff location=resource END

         MAKE_PATCH
           type=>effect
           match=>"opcode=55 and parameter1=195 and parameter2=5"
           parameter1=>147
           parameter2=>4
         END
         LAF edit_item STR_VAR item=halb09 editstring=~patch_effect=>patch_data~ END
        END

END


/////////////////////////////////////////////////////////////////////////////////
////////////////////  Conditional hitpoint patch
////////////////////////////////////////////////////////////////////////////////


DEFINE_PATCH_FUNCTION genie_hp
BEGIN
   LPF CRE_read_hp_max RET hitpoints=value END
   LPF check_label STR_VAR label=dw#genie_hp RET value=value END
   PATCH_IF value BEGIN
          LPF check_ini STR_VAR ini=genie_staying_power RET value=value END
          SET arguments=(hitpoints * value) / 100
          LPF CRE_hitpoints STR_VAR arguments END
   END



END

/////////////////////////////////////////////////////////////////////////////////////////////
////   efreeti CRE files
/////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION make_efreeti BEGIN

        // generics

        MAKE_PATCH
            class=>"if enhanced_edition then no_change else FIGHTER_MAGE"
            gender=>MALE
            levelGT=>12
            level2GT=>12
            genie_hp=>null
            remove_spells=>all
            add_spells=>~FLAME_STRIKE FIREBALL(2) FLAME_ARROW(2) INVISIBILITY(3) MAGIC_MISSILE(3)~
            strip_scs_scripts=>null
            make_casting_innate=>dw#genie_innate
        END
        LAF edit_creature STR_VAR creature=~efreetsu sumefree ddguard1 ddguard2 ddguard3 ddguard4 ddguard5 ddguard6 genefr01~  edits=patch_data END

	/// nobles

        MAKE_PATCH
            class=>"if enhanced_edition then no_change else FIGHTER_MAGE"
            gender=>MALE
            levelGT=>14
            level2GT=>14
            genie_hp=>null
            remove_spells=>all
            add_spells=>~FIRE_STORM
                         FLAME_STRIKE(2)
                         SHADOW_DOOR(2)
                         FIREBALL(3) 
                         FLAME_ARROW(3) 
                         INVISIBILITY(3) 
                         MAGIC_MISSILE(5)~
            strip_scs_scripts=>null
            make_casting_innate=>dw#genie_innate
        END
        LAF edit_creature STR_VAR creature=~genefn01 obsfir01~  edits=patch_data  END

	// the efreeti summoned by the efreeti bottle needs a script (it only gets WTASIGHT)

        LAF edit_creature STR_VAR creature=sumefree editstring=~insert_script_low=>dw#efree~ END


	// The Smarter Mages component only bothers to summon upgraded efreeti, so
	// we allow for that here

        LAF clone_spell STR_VAR spell=~spwi717=>dw#sumef~ END



END

/////////////////////////////////////////////////////////////////////////////////////////////
////  djinni CRE files
/////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION make_djinni BEGIN


	// general

        MAKE_PATCH
            class=>"if enhanced_edition then no_change else FIGHTER_MAGE"
            gender=>MALE
            levelGT=>11
            level2GT=>11
            genie_hp=>null
            remove_spells=>all
            add_spells=>~PROTECTION_FROM_MAGIC_WEAPONS
                         GLOBE_OF_INVULNERABILITY
                         SHADOW_DOOR
                         WIZARD_LIGHTNING_BOLT
                         STINKING_CLOUD(2)
                         MAGIC_MISSILE(4)~
            strip_scs_scripts=>null
            make_casting_innate=>dw#genie_innate
            add_items=>dw#djinn
        END
        LAF edit_creature STR_VAR creature=~gendji01 sumdjinn djinnisu~  edits=patch_data  END

        // noble

        MAKE_PATCH
            class=>"if enhanced_edition then no_change else FIGHTER_MAGE"
            gender=>MALE
            levelGT=>13
            level2GT=>13
            genie_hp=>null
            remove_spells=>all
            add_spells=>~PROTECTION_FROM_MAGIC_WEAPONS(2)
                         GLOBE_OF_INVULNERABILITY(2)
                         CLOUDKILL
                         SHADOW_DOOR(2)
                         WIZARD_LIGHTNING_BOLT(3)
                         STINKING_CLOUD(3)
                         INVISIBILITY(3)
                         MAGIC_MISSILE(4)~
            strip_scs_scripts=>null
            make_casting_innate=>dw#genie_innate
            add_items=>dw#djinn
        END
        LAF edit_creature STR_VAR creature=~gendjn01 ppdjinn2~  edits=patch_data END

	// Install and customise

        LAF edit_creature STR_VAR creature=ppdjinn2 editstring=~insert_script=>"djinni01 below gensht01~ END




END

////////////////////////
///// Dao
/////////////////////////

DEFINE_ACTION_FUNCTION make_dao BEGIN

        MAKE_PATCH
            class=>"if enhanced_edition then no_change else FIGHTER_MAGE"
            gender=>MALE
            levelGT=>12
            level2GT=>12
            genie_hp=>null
            remove_spells=>all
            add_spells=>~IMPROVED_HASTE FLESH_TO_STONE STONE_SKIN(2) INVISIBILITY(3)~
            strip_scs_scripts=>null
            make_casting_innate=>dw#genie_innate
        END
        LAF edit_creature STR_VAR creature=~trgeni01 trgeni02 trgeni03~ edits=patch_data END
END


/////////////////////////////////////////////////////////////////////////////////////////////
///   third-party compatibility
/////////////////////////////////////////////////////////////////////////////////////////////


DEFINE_ACTION_FUNCTION genie_compatibility BEGIN

   ACTION_IF FILE_EXISTS_IN_GAME ~d0sumefr.bcs~ BEGIN
      LAF clone_script STR_VAR script=~efreet01=>d0sumefr~ END
   END

	// Allow for Spell Revisions [note 10-9-12: we have a dw#dvdji but don't currently use it]

   ACTION_IF FILE_EXISTS_IN_GAME ~dvimhere.mrk~ THEN BEGIN
         LAF ssl_to_bcs STR_VAR script=~dw#dvefr~ location=~genie/ssl~ END
         LAF swap_text STR_VAR files=~dvefreet.eff dw#sumef.spl~ swaps=~dvefreet=>dw#dvefr~ END
         LAF clone_creature STR_VAR creature=~dvefreet=>dw#dvefr~ editstring=~swap_script=>"dvefreet=>dw#dvefr"~ END
   END

END

