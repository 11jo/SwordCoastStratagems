DEFINE_ACTION_FUNCTION resurrection_rod BEGIN

	itm.edit[rods03|edit_strrefs_in_place:i=1]
	[
		m_identified_description:=@5100
		m.ab.alter{s_icon:=sppr504b s_number_charges=5 s_when_drained:=Recharges}
		m.ab.clone{s_icon:=sppr607b s_target=1}
		m.ab_fx.delete{(s_opcode=17 AND p_target==LivingActor) OR ((s_opcode=32 OR s_opcode=215 OR s_opcode=309) AND p_target==DeadActor)}
		
	]

   APPEND tooltip.2da ~RODS03 12117 1453 -1~

	COPY_EXISTING_REGEXP - ".*\.cre" nowhere
		PATCH_IF INDEX_BUFFER (CASE_INSENSITIVE rods03)>=0 BEGIN
			SPRINT $rod_array_cre("%SOURCE_RES%") ""
		END
	COPY_EXISTING_REGEXP - ".*\.are" nowhere
		PATCH_IF INDEX_BUFFER (CASE_INSENSITIVE rods03)>=0 BEGIN
			SPRINT $rod_array_are("%SOURCE_RES%") ""
		END
	COPY_EXISTING_REGEXP - ".*\.sto" nowhere
		PATCH_IF INDEX_BUFFER (CASE_INSENSITIVE rods03)>=0 BEGIN
			SPRINT $rod_array_sto("%SOURCE_RES%") ""
		END

		
	ACTION_PHP_EACH rod_array_cre AS creature=>discard BEGIN
		cre.edit[%creature%]
		[
			m.item.alter{s_charges_1=5 s_charges_2=5|match="s_resref STR_EQ rods03"}
		]
	END
	ACTION_PHP_EACH rod_array_are AS area=>discard BEGIN
		are.edit[%area%]
		[
			m.container_item.alter{s_charges_1=5 s_charges_2=5|match="s_resref STR_EQ rods03"}
		]
	END

	ACTION_PHP_EACH rod_array_sto AS store=>discard BEGIN
		sto.edit[%store%]
		[
			m.item.alter{s_charges_1=5 s_charges_2=5|match="s_resref STR_EQ rods03"}
		]
	END


END