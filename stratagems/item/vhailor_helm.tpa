DEFINE_ACTION_FUNCTION vhailor_helm BEGIN

   ACTION_IF RESOURCE_CONTAINS wmart2.sto wa2helm BEGIN
	   OUTER_SPRINT difficulty_variable DMWW_genai_difficulty // do this manually as difficulty system probably isn't installed yet
	   LAF ssl_to_bcs STR_VAR script=dw#grvha location=ssl END

		sto.edit[wmart2][ m.item.delete{~%s_resref%~ STR_EQ wa2helm}]
		cre.edit[gromnir]
		[
			m.cre_remove_items{helmnoan}
			m.cre_add_items{wa2helm}
			m.cre_insert_script{dw#grvha|loc=high}
		]
		
	   ACTION_IF RESOURCE_CONTAINS simulacr.spl d0simul BEGIN
			   COPY_EXISTING simulacr.spl override
				  REPLACE_TEXTUALLY d0simul ~dw#image~ (8)
	   END ELSE BEGIN
			  COPY_EXISTING simulacr.spl override
				  LPF DELETE_EFFECT INT_VAR match_opcode=82 STR_VAR match_resource="dw#image" END
				  LPF ADD_SPELL_EFFECT INT_VAR opcode=82 target=2 timing=1 parameter2=6 STR_VAR resource=dw#image END
	   END

	   COPY_EXISTING "gromnir.cre" override
		  READ_ASCII 0x268 grom_script
	   BUT_ONLY

<<<<<<<< .../stratagems-inline/image_top.baf
IF
  Allegiance(Myself,ENEMY)
  HasItem("wa2helm",Myself)
  AreaCheck("ar5002")
  Global("set_script","LOCALS",0)
THEN
    RESPONSE #100
       SetGlobal("set_script","LOCALS",1)
       ChangeAIScript("dw#grsim","OVERRIDE")
       ChangeAIScript("%grom_script%","CLASS")
END
>>>>>>>>

		ACTION_IF !FILE_EXISTS_IN_GAME "dw#image.bcs" BEGIN
			COPY ".../stratagems-inline/blank" "override/dw#image.bcs" COMPILE_BAF_TO_BCS
		END
		LAF extend INT_VAR inline=1 STR_VAR script=dw#image top=image_top END

<<<<<<<< .../stratagems-inline/dw#grsim.baf
IF
  Global("initialise_sim","LOCALS",0)
THEN
    RESPONSE #100
         FillSlot(SLOT_WEAPON)
         EquipMostDamagingMelee()
         DestroyItem("potn55")
         SetGlobal("initialise_sim","LOCALS",1)
         Continue()
END
>>>>>>>>

		COMPILE ".../stratagems-inline/dw#grsim.baf"



  END ELSE BEGIN
      LAF warning STR_VAR warning="Not moving Vhailor's Helm; some other mod has already moved it" END
  END



END
