DEFINE_ACTION_FUNCTION fine_weapons STR_VAR version="shatter" BEGIN

<<<<<<<< .../stratagems-inline/fineweapon.2da
nonmg wpn  mgc wpn   hit   dmg      cost  BAM1     BAM2	    kept copy  desc    desc_ee name
ax1h01	   ax1h02    1     1        500   dw#axe01 dw#axe01 dw#axe1    401     801     402
blun06     blun07    1     1        600   dw#mstar dw#mstar dw#msta1   403     803     404
bow03      bow04     2     1        800   dw#bow02 dw#bow02 dw#lbow1   405     805     406
bow05      bow06     1     1        700   dw#bow03 dw#bow03 dw#sbow1   407     807     408
dagg01     dagg02    1     1        100   dw#dagg1 dw#dagg1 dw#dagg1   409     809     410
halb01     halb02    1     1        700   dw#halb1 dw#halb1 dw#halb1   411     811     412
hamm01     hamm02    1     2        500   dw#hamm1 dw#hamm1 dw#hamm1   413     813     414
staf01     staf02    1     1        150   dw#stf01 dw#stf01 dw#stf01   415     815     416
sw1h01     sw1h02    1     1        800   dw#swd01 dw#swd01 dw#swd01   417     817     418
sw1h04     sw1h05    1     1        400   dw#swd02 dw#swd02 dw#swd02   419     819     420
sw1h07     sw1h08    1     1        250   dw#swd03 dw#swd03 dw#swd03   421     821     422
sper01	   sper02    1	   1	    300   isper02  isper02  dw#sper1   423     823     424
blun04     blun05    1     1        500   iblun05  iblun05  dw#mace1   425     825     426
sw2h01     sw2h02    1     1        200   isw2h02  isw2h02  dw#swd04   427     827     428
sw1h20     sw1h22    1     1        350   isw1h20  isw1h20  dw#scim1   429     829     430
bow01      bow02     2     1        850   ibow02   ibow02   dw#cbow1   431     831     432
blun02     blun03    1     1        750   iblun03  iblun03  dw#flai1   433     833     434
>>>>>>>>

   COPY - ~.../stratagems-inline/fineweapon.2da~ ~override~
        READ_2DA_ENTRIES_NOW 2da_array 11
        SPRINT var_list ~nonmagic_wpn magic_wpn hit dmg cost BAM1 BAM2 kept_copy desc_string desc_string_ee name_string~
        FOR (2da_row=1;2da_row<2da_array;2da_row=2da_row+1) BEGIN
              LAUNCH_PATCH_MACRO read_in_2da_row
              INNER_ACTION BEGIN
                 LAF make_fine_weapon INT_VAR hit dmg cost desc_string desc_string_ee name_string STR_VAR nonmagic_wpn magic_wpn BAM1 BAM2 kept_copy version END
              END
        END
   BUT_ONLY
    COPY ~%scsroot%/%component_loc%/icons~ ~override~

    ACTION_IF is_bg1=1 BEGIN
       LAF bg1_items END
    END

END

DEFINE_ACTION_FUNCTION make_fine_weapon
     INT_VAR hit=0
             dmg=0
             cost=0
             desc_string=0
             desc_string_ee=0
             name_string=0
     STR_VAR nonmagic_wpn=""
             magic_wpn=""
             BAM1=""
             BAM2=""
             kept_copy=""
             version="shatter"
BEGIN
    ACTION_IF !FILE_EXISTS_IN_GAME ~%kept_copy%.itm~ BEGIN
       LAF clone_item STR_VAR item=EVALUATE_BUFFER ~%magic_wpn%=>%kept_copy%~ END
       ACTION_CLEAR_ARRAY patch_data
       ACTION_IF enhanced_edition BEGIN
          OUTER_SET desc_string=desc_string_ee
       END
       ACTION_DEFINE_ASSOCIATIVE_ARRAY patch_data BEGIN
            say_both_names=>~%name_string%~
            say_both_descriptions=>~%desc_string%~
            icon=>~%BAM1%~
            price=>~%cost%~
            patch_cond_inline=>~type=>ability ability_icon=>%BAM2% to_hit=>%hit% damage_bonus=>%dmg%~
       END
       LAF clone_item STR_VAR item=EVALUATE_BUFFER ~%tutu_var%%nonmagic_wpn%=>%tutu_var%%magic_wpn%~ edits=patch_data END
       LAF ironcrisis STR_VAR nonmagic_wpn magic_wpn version END
       ACTION_IF (GAME_IS ~bgt~ && ~%version%~ STRING_EQUAL_CASE ~shatter~) BEGIN
            LAF ironcrisis_bgt STR_VAR magic_wpn version END
       END
    END
END

DEFINE_ACTION_FUNCTION ironcrisis
    STR_VAR magic_wpn=""
            nonmagic_wpn=""
            version="shatter"
BEGIN
          ACTION_IF ~%version%~ STRING_EQUAL_CASE shatter BEGIN
             ACTION_DEFINE_ASSOCIATIVE_ARRAY cond_data BEGIN
                match=>ironcrisis_check
                match_parameter=>~%nonmagic_wpn%~
                check=>1
                type=>effect
                resource=>~%magic_wpn%~
             END
             LAF edit_item STR_VAR item=EVALUATE_BUFFER ~%magic_wpn%~ editstring=~patch_conditionally=>cond_data~ END
          END ELSE BEGIN
             ACTION_DEFINE_ASSOCIATIVE_ARRAY cond_data BEGIN
                match=>ironcrisis_check
                match_parameter=>~%nonmagic_wpn%~
                check=>1
                type=>effect
                opcode=>999
             END
             LAF edit_item STR_VAR item=EVALUATE_BUFFER ~%magic_wpn%~ editstring=~patch_conditionally=>cond_data delete_opcodes=>999~ END
          END

END

DEFINE_PATCH_FUNCTION ironcrisis_check
         INT_VAR offset_secondary=0
         STR_VAR parameter=""
         RET value
BEGIN
         PATCH_IF GAME_IS ~bgt~ BEGIN
            SET correct_opcode=309
         END ELSE BEGIN
            SET correct_opcode=146
         END
         LPF ITM_read_opcode INT_VAR offset_secondary RET opcode=value END
         LPF ITM_read_resource INT_VAR offset_secondary RET resource=value END
         PATCH_IF (opcode=correct_opcode && ~%resource%~ STRING_EQUAL_CASE ~%parameter%~) BEGIN
              SET value=1
         END ELSE BEGIN
              SET value=0
         END
END

DEFINE_ACTION_FUNCTION ironcrisis_bgt BEGIN
      <<<<<<<< .../stratagems-inline/crisis_add.baf
         IF
	InParty(Myself)
	Global("ENDOFBG1","GLOBAL",0)
	HasItemEquipedReal("%magic_wpn%",Myself) // Flail
	Global("%magic_wpn%","LOCALS",1)
THEN
	RESPONSE #50
		DisplayStringHead(Myself,74465) // My weapon has shattered!
		SetGlobal("%magic_wpn%","LOCALS",0)
		TakeItemReplace("BGMISC56","%magic_wpn%",Myself) // Flail
	RESPONSE #50
		SetGlobal("%magic_wpn%","LOCALS",0)
		Continue()
END
      >>>>>>>>
    EXTEND_BOTTOM ~dplayer2.bcs~ ~.../stratagems-inline/crisis_add.baf~ EVALUATE_BUFFER

END




DEFINE_ACTION_FUNCTION bg1_items BEGIN
// place a few magic copies in the game
// Axes
     LAF add_items_to_store STR_VAR store=inn3351 tv=yes item=dw#axe1 END
// Morning stars
     LAF edit_creature STR_VAR creature=drasus tv=yes editstring=EVALUATE_BUFFER ~swap_items=>"%tutu_var%blun07=>dw#msta1"~ END
// Daggers
     LAF edit_creature STR_VAR creature=wheber tv=yes editstring=EVALUATE_BUFFER ~swap_items=>"%tutu_var%dagg02=>dw#dagg1"~ END
// Halberds
     LAF edit_area STR_VAR area=EVALUATE_BUFFER ~%DryadFalls%~ editstring=EVALUATE_BUFFER ~swap_items=>"%tutu_var%halb02=>dw#halb1"~ END
// Hammers
     LAF edit_creature STR_VAR creature=diyab tv=yes editstring=EVALUATE_BUFFER ~swap_items=>"%tutu_var%hamm02=>dw#hamm1"~ END
// Staffs
     LAF edit_creature STR_VAR creature=silke tv=yes editstring=EVALUATE_BUFFER ~swap_items=>"%tutu_var%staf02=>dw#stf01"~ END
// Bastard swords
     LAF edit_creature STR_VAR creature=ogrmba tv=yes editstring=EVALUATE_BUFFER ~swap_items=>"%tutu_var%sw1h02=>dw#swd01"~ END
// Long swords
     LAF edit_creature STR_VAR creature=~telano tuth angelo desret~ tv=yes editstring=EVALUATE_BUFFER ~swap_items=>"%tutu_var%sw1h05=>dw#swd02"~ END
// Short swords
     LAF edit_creature STR_VAR creature=~nimbul thaldo~ tv=yes editstring=EVALUATE_BUFFER ~swap_items=>"%tutu_var%sw1h08=>dw#swd03"~ END
END
