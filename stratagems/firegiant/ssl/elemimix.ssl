BEGIN_ACTION_DEFINITION
	Name(ForceSpellRES)
	TRIGGER
		!GlobalTimerNotExpired("castspell","LOCALS")
		!CheckStat(scstarget,2,WIZARD_SPELL_TRAP) // PM scroll
	ACTION
		RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		ForceSpellRES(scsargument1,scstarget)
END

BEGIN_ACTION_DEFINITION
	Name(ForceSpellMyself)
	TRIGGER
		!GlobalTimerNotExpired("castspell","LOCALS")
	ACTION
		RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		ForceSpell(Myself,scsargument1)
END

BEGIN_ACTION_DEFINITION
	Name(Attack)
	TRIGGER
	ACTION
		RESPONSE #scsprob1
		SetGlobal("validtarget","LOCALS",1)
		EquipMostDamagingMelee()
		AttackOneRound(scstarget)
END

BEGIN_ACTION_DEFINITION
	Name(AttackIfEmpty)
	TRIGGER
		ActionListEmpty()
	ACTION
		RESPONSE #scsprob1
		SetGlobal("validtarget","LOCALS",1)
		EquipMostDamagingMelee()
		AttackOneRound(scstarget)
END

BEGIN_ACTION_DEFINITION
	Name(MoronAttack)
	TRIGGER
		False()
	ACTION
		RESPONSE #100
		NoAction()
END

BEGIN_ACTION_DEFINITION
	Name(MoveIfEmpty)
	TRIGGER
		ActionListEmpty()
	ACTION
		RESPONSE #scsprob1
		MoveToObject(scstarget)
END

/////////////////////////////////////////////////////////
/////              Rest and recover
////////////////////////////////////////////////////////

IF
	See(NearestEnemyOf(Myself))
	Global("rest","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobalTimer("rtime","LOCALS",2000)
		SetGlobal("rest","LOCALS",1)
		Continue()
END

IF
	Global("rest","LOCALS",1)
	!GlobalTimerNotExpired("rtime","LOCALS")
	!See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
// reset all variables here
		ApplySpell(Myself,RESTORE_FULL_HEALTH) //healing spell
		Rest()
END

IF
	!Detect(NearestEnemyOf(Myself))
	!Global("inafight","LOCALS",1)
THEN
	RESPONSE #100
		NoAction()
END

///////////////////////////////////////////////////////////////////////////////
///	Bluster
///////////////////////////////////////////////////////////////////////////////

IF
	Detect([PC])
	Global("Display","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("Display","LOCALS",1)
		DisplayStringHead(Myself,72786) // Who calls Imix, Prince of Fire!
END

//////////////////////////////////////////////////////////////////////////////////
///	Whirlwind
//////////////////////////////////////////////////////////////////////////////////


IF TRIGGER
	!StateCheck(Myself,STATE_DISABLED)
	!GlobalTimerNotExpired("whirlwind","LOCALS")
	See(NearestEnemyOf(Myself))
	!CheckStat(Myself,5,SCRIPTINGSTATE4)
THEN DO
	Action(ForceSpellMyself,WARRIOR_GREATER_WHIRLWIND)
	SetGlobalTimer("whirlwind","LOCALS",24)
END

////////////////////////////////////////////////////////////////////////////////////
///	Fireburst
////////////////////////////////////////////////////////////////////////////////////

IF
	!StateCheck(Myself,STATE_DISABLED)
	!GlobalTimerNotExpired("castspell","LOCALS")
	!GlobalTimerNotExpired("fireburst","LOCALS")
THEN
	RESPONSE #100
		DisplayStringHead(Myself,72787) // Feel the wrath of Imix!
		SetGlobalTimer("fireburst","LOCALS",18)
		SetGlobalTimer("castspell","LOCALS",6)
		ReallyForceSpell(Myself,RED_DRAGON_HIT)
		ReallyForceSpellRES("dw#imsta",Myself)
END

////////////////////////////////////////////////////////////////////////////////////
///	Firestorm
////////////////////////////////////////////////////////////////////////////////////

IF
	!StateCheck(Myself,STATE_DISABLED)
	!GlobalTimerNotExpired("castspell","LOCALS")
	!GlobalTimerNotExpired("firestorm","LOCALS")
THEN
	RESPONSE #100
		SetGlobalTimer("firestorm","LOCALS",18)
		SetGlobalTimer("castspell","LOCALS",6)
		ReallyForceSpell(NearestEnemyOf(Myself),CLERIC_FIRE_STORM)
		ReallyForceSpell(SecondNearestEnemyOf(Myself),CLERIC_FIRE_STORM)
		ReallyForceSpell(ThirdNearestEnemyOf(Myself),CLERIC_FIRE_STORM)
END
	


////////////////////////////////////////////////////////////////////////////////////
////	Flame Strike
////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	!StateCheck(Myself,STATE_DISABLED)
	TargetBlock(PCsPreferringWeak)
	TriggerBlock(Disabled|MR|ResistFire|Enemy|SIEvocation)
	!HasItemEquiped("clck26",scstarget)
THEN DO
	Action(ForceSpellRES,"spimix01"|80|20)
	DisplayStringHead(Myself,72787) // Feel the wrath of Imix!
END

////////////////////////////////////////////////////////////////////////////////////
///	Combat
////////////////////////////////////////////////////////////////////////////////////


INCLUDE FILE(scsii/genai/dw#wtacor.ssl)
INCLUDE FILE(scsii/genai/move.ssl)
INCLUDE FILE(scsii/genai/chase.ssl)
