// The skull's powers aren't actually psionic but it's convenient to use the psionic code


INCLUDE FILE (scsii\psionic\psionic_defs.ssl)

/////////////////////////////////////////////////////////////////////////////
/////		Explode on death
/////////////////////////////////////////////////////////////////////////////

IF
	Die()
THEN
	RESPONSE #50
		ReallyForceSpellDead(Myself,SKULL_EXPLODE)
		DestroySelf()
END

/////////////////////////////////////////////////////////
/////              Rest and recover
////////////////////////////////////////////////////////

IF
	See(NearestEnemyOf(Myself))
	Global("rest","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobalTimer("rtime","LOCALS",2000)
		SetGlobal("rest","LOCALS",1)
		Continue()
END

IF
	Global("rest","LOCALS",1)
	GlobalTimerExpired("rtime","LOCALS")
	!See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
// reset all variables here
		ApplySpell(Myself,RESTORE_FULL_HEALTH) //healing spell
		Rest()
END

IF
	!Detect(NearestEnemyOf(Myself))
	!Global("inafight","LOCALS",1)
THEN
	RESPONSE #100
		NoAction()
END

//////////////////////////////////////////////////////////////////////////////////////
///	Begin stoneskinned
//////////////////////////////////////////////////////////////////////////////////////

IF
	See(NearestEnemyOf(Myself))
	!GlobalTimerNotExpired("stoneskin","LOCALS")
THEN
	RESPONSE #100
		SetGlobalTimer("stoneskin","LOCALS",1000)
		ApplySpell(Myself,WIZARD_STONE_SKIN)
		Continue()
END

//////////////////////////////////////////////////////////////////////////////////////
///	Incendiary Cloud
//////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(EnemiesInOrderShort)
THEN DO
	Action(Psionic,WIZARD_INCENDIARY_CLOUD,belch,36|100|100)
END

///////////////////////////////////////////////////////////////////////////////////////
////	Fireball
///////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(EnemiesInOrder)
	!HasItemEquiped("clck26",scstarget)
	TriggerBlock(MR|ResistFire|Enemy|MinorGlobe|SIEvocation)
THEN DO
	Combine()
	Action(Psionic,DEATHKNIGHT_FIREBALL,fireball,18|200|100)
END

IF TRIGGER
	TargetBlock(EnemiesInOrder)
THEN DO
	Action(Psionic,DEATHKNIGHT_FIREBALL,fireball,18|100|100)
END

/////////////////////////////////////////////////////////////////////////
////   Flame Arrows 
/////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTFIRE))
	TargetBlock(PCsPreferringWeak)
	!HasItemEquiped("clck26",scstarget)
	TriggerBlock(MR|Enemy|ArrowSafe|SpellTurn|Stoneskin|MinorGlobe|MirrorImage|Helpless|SIConjuration)
THEN DO
	Action(Psionic,WIZARD_FLAME_ARROW,flamearrow,18|150|50)
END

/////////////////////////////////////////////////////////////////////////////
//	Magma ball
//////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTFIRE))
	TargetBlock(PCsPreferringWeak)
	!HasItemEquiped("clck26",scstarget)
	TriggerBlock(MR|Enemy|MinorGlobe)
THEN DO
	Action(PsionicRES,"spin539",magma,6)
END

///////////////////////////////////////////////////////////////////////////////
///	Run away!
///////////////////////////////////////////////////////////////////////////////


IF
	See(NearestEnemyOf(Myself))
	Range(LastSeenBy(Myself),10)
THEN
	RESPONSE #100
		RunAwayFrom(NearestEnemyOf(Myself),15)
		Continue()
END

////////////////////////////////////////////////////////////////////////////////
////	Seek the heat
////////////////////////////////////////////////////////////////////////////////

IF
	OR(2)
		Heard([ANYONE],405)
		Heard([ANYONE],409)
	!Range(LastHeardBy(Myself),15)
THEN
	RESPONSE #100
		MoveToObject(LastHeardBy(Myself))
END
