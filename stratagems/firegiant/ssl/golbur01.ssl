// Its powers aren't actually psionic but it's convenient to use the psionic code


INCLUDE FILE (%scsroot%\psionic\ssl\psionic_defs.ssl)




/////////////////////////////////////////////////////////
/////              Rest and recover
////////////////////////////////////////////////////////

IF
	See(NearestEnemyOf(Myself))
	Global("rest","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobalTimer("rtime","LOCALS",2000)
		SetGlobal("rest","LOCALS",1)
		Continue()
END

IF
	Global("rest","LOCALS",1)
	GlobalTimerExpired("rtime","LOCALS")
	!See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
// reset all variables here
		ApplySpell(Myself,RESTORE_FULL_HEALTH) //healing spell
		Rest()
END

IF
	!Detect(NearestEnemyOf(Myself))
	!Global("inafight","LOCALS",1)
THEN
	RESPONSE #100
		NoAction()
END

///////////////////////////////////////////////////////////////////
///	Keening
///////////////////////////////////////////////////////////////////

IF
	See(NearestEnemyOf(Myself))
	!GlobalTimerNotExpired("BurningManFearTimer","LOCALS")
	!GlobalTimerNotExpired("DMWWTimestopRunning","GLOBAL")
THEN
	RESPONSE #100
		ReallyForceSpell(NearestEnemyOf(Myself),BURNING_MAN_KEENING)
		SetGlobalTimer("BurningManFearTimer","LOCALS",24)
END

/////////////////////////////////////////////////////////////////////
///	Cinder shower
/////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(EnemiesInOrderShort)
THEN DO
	Action(Psionic,BURNING_MAN_CINDER_SHOWER,cindershower,36|200|50)
END

////////////////////////////////////////////////////////////////
///	Banefire (dispelling bolt)
/////////////////////////////////////////////////////////////////

// target protective magic

IF TRIGGER
	TargetBlock(PCsInOrder)
	TriggerBlock(Enemy|Disabled)
	OR(12)
		CheckStatGT(scstarget,0,STONESKINS)
		CheckStatGT(scstarget,0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		CheckStatGT(scstarget,0,MINORGLOBE)
		StateCheck(scstarget,STATE_INVISIBLE)
		StateCheck(scstarget,STATE_IMPROVEDINVISIBILITY)
		StateCheck(scstarget,STATE_BLESS)
		StateCheck(scstarget,STATE_CHANT)
		StateCheck(scstarget,STATE_HASTED)
		StateCheck(scstarget,STATE_MIRRORIMAGE)
		CheckStatGT(scstarget,0,WIZARD_SPELL_TURNING)
		CheckStatGT(scstarget,0,WIZARD_SPELL_DEFLECTION)
		CheckStatGT(scstarget,0,WIZARD_SPELL_TRAP)
THEN DO
	Combine()
	Action(PsionicRES,"dw#bmdis",antimagicray,12|100|50)
END

// target randoms

IF TRIGGER
	TargetBlock(PCsInOrder)
	TriggerBlock(Enemy|Disabled)
THEN DO
	Combine()
	Action(PsionicRES,"dw#bmdis",antimagicray,12|100|50)
END

////////////////////////////////////////////////////////////////////////////
////   Combat block
////////////////////////////////////////////////////////////////////////////


BEGIN LOOP(MyWeaponStrength||3)
BEGIN LOOP(MyWeaponDamageType||Crushing)
INCLUDE FILE(%scsroot%/genai/ssl/dw#wtacor.ssl)
INCLUDE FILE(%scsroot%/genai/ssl/move.ssl)
END LOOP
END LOOP

////////////////////////////////////////////////////////////////////////////
///	Seek the heat
////////////////////////////////////////////////////////////////////////////

IF
	OR(2)
		Heard([ANYONE],405)
		Heard([ANYONE],409)
	!Range(LastHeardBy(Myself),15)
THEN
	RESPONSE #100
		MoveToObject(LastHeardBy(Myself))
END
