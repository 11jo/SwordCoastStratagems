DEFINE_ACTION_FUNCTION skip_candlekeep STR_VAR version=2 BEGIN
  ACTION_MATCH "%version%" WITH
  classic BEGIN
     LAF candlekeep_classic END
     LAF candlekeep_no_tutors END
  END
  notutors BEGIN
     LAF candlekeep_no_tutors END
  END
  DEFAULT
     FAIL "Unrecognised Candlekeep component"
  END
END

DEFINE_ACTION_FUNCTION candlekeep_no_tutors BEGIN

	are.edit[%Candlekeep% %Candlekeep_Inn_L1%]
	[
		m.actor.delete{~%s_name%~ STR_EQ TUTOR OR ~%s_name%~ STR_EQ JONDALAR OR ~%s_resref%~ STR_EQ _MONKTU1}
	]
	
	COPY_EXISTING "%Candlekeep_BCS%.bcs" override	
		DECOMPILE_AND_PATCH BEGIN
			PATCH_IF is_eet BEGIN
				REPLACE_TEXTUALLY "DisplayString(Myself,231107)" ""			
				REPLACE_TEXTUALLY "DisplayString(Myself,216214)" ""			
			END ELSE
			PATCH_IF enhanced_edition BEGIN
				REPLACE_TEXTUALLY "DisplayString(Myself,16214)" ""
				REPLACE_TEXTUALLY "DisplayString(Myself,31107)" ""
			END ELSE
			PATCH_IF is_tutu BEGIN
				REPLACE_TEXTUALLY "DisplayStringHead(Myself,86017)" ""
			END ELSE BEGIN
				REPLACE_TEXTUALLY "DisplayStringHead(Myself,16214)" ""			
			END
		END
	BUT_ONLY
    ACTION_IF FILE_EXISTS_IN_GAME "macinit.bcs" BEGIN
		COPY_EXISTING "macinit.bcs" override
			DECOMPILE_AND_PATCH BEGIN
				REPLACE_TEXTUALLY ~SetGlobal("MACGameInit","GLOBAL",1)~ ~SetGlobal("MACGameInit","GLOBAL",1)EscapeArea()~ 
			END
    END


END


DEFINE_ACTION_FUNCTION candlekeep_classic BEGIN

    OUTER_SPRINT local_loc candleskip
	cre.copy[%tutu_var%watch3=>dw#skcan]
	[
		m_script_class:=dw#skcan
		m_dialog:=dw#skcan
	]
	
    LAF install STR_VAR files="dw#skcan.baf dw#skcan.d" END

	are.edit[%Candlekeep%]
	[
		m.actor.add{s_name:=CandlekeepSkipper s_resref:=DW#SKCAN s_x=1178 s_y=640}
	]
    LAF extend INT_VAR tv=1 STR_VAR file=gorion top=addtogorion END


END
