DEFINE_ACTION_FUNCTION skip_candlekeep STR_VAR version=2 BEGIN
  ACTION_IF version=1 BEGIN
     LAF candlekeep_no_tutors END
  END ELSE BEGIN
     LAF candlekeep_classic END
  END

END

DEFINE_ACTION_FUNCTION candlekeep_no_tutors BEGIN

    LAF edit_area STR_VAR area=EVALUATE_BUFFER ~%Candlekeep%~ editstring=~delete_entries_inline=>"type=>actor match=>breaks_fourth_wall check=>1"~ END
    LAF get_area_script STR_VAR area=EVALUATE_BUFFER ~%Candlekeep%~ RET script=script END

    ACTION_IF enhanced_edition BEGIN
      OUTER_SPRINT swapline ~DisplayString(Myself,31107)~
    END ELSE BEGIN
      ACTION_IF GAME_IS ~tutu tutu_totsc~ BEGIN
       OUTER_SPRINT ~swapline~ ~DisplayStringHead(Myself,86017)~
      END ELSE BEGIN
       OUTER_SPRINT ~swapline~ ~DisplayStringHead(Myself,16214)~
      END
    END
    LAF swap_text STR_VAR files=EVALUATE_BUFFER ~%script%.bcs~ swaps=EVALUATE_BUFFER ~%swapline%=>null~ END



END


DEFINE_PATCH_FUNCTION breaks_fourth_wall STR_VAR offset_base="" RET value BEGIN
   LPF ARE_read_actor_name STR_VAR offset_base RET actor=value END
   TO_UPPER actor
   PATCH_MATCH ~%actor%~ WITH
      TUTOR JONDALAR
   BEGIN
      SET value=1
   END
   DEFAULT
      SET value=0
   END


END

DEFINE_ACTION_FUNCTION candlekeep_classic BEGIN

    OUTER_SPRINT local_loc candleskip
    LAF clone_creature STR_VAR creature=EVALUATE_BUFFER ~%tutu_var%watch3 => dw#skcan~ editstring=~script_class=>dw#skcan dialog=>dw#skcan~ END

    LAF install STR_VAR files="dw#skcan.baf dw#skcan.d" END

    LAF get_area_script STR_VAR area=EVALUATE_BUFFER ~%Candlekeep%~ RET file=script END
    LAF extend STR_VAR file bottom=addtoarea END
    LAF extend STR_VAR file=gorion tv=yes top=addtogorion END


END