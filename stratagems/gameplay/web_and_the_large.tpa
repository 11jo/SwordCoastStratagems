DEFINE_ACTION_FUNCTION web_and_the_large BEGIN

   /// re-map the Sphere of Chaos paralyzation power, the beholder eyestalk ray, and Spell Revisions' Chromatic Orb, to 175

   LAF edit_spell STR_VAR spell=~spwi118f spin960 spwi711~
                          editstring=~patch_cond_inline=>"match=>SPL_read_opcode type=>effect check=>109 opcode=>175"~
                          allow_missing=yes
   END

   /// define the animation codes that correspond to largeness

      ACTION_FOR_EACH anim_num IN 32572 32517 32518 29472 29473 29440 29456 29457
                               29458 29459 32574 32519 32516 30980 57568 57360
                               57585 57584 57488 59648 59664 32520 29442 30976
                               30978 30979 30977  4352 32529  4096 32537
                               BEGIN
          OUTER_SPRINT $web_anim_array(EVALUATE_BUFFER ~%anim_num%~) ~~
   END

   // find creatures in hardcode (speedier)
   OUTER_SPRINT largelist ~~
   COPY_EXISTING_REGEXP GLOB ~.*\.cre~ ~override~
        SPRINT filename ~%SOURCE_RES%~
        LPF CRE_is_large STR_VAR filename RET value=value END
        PATCH_IF value=1 BEGIN
           PUSH largelist ~%filename%~
        END
   BUT_ONLY

   // mark up protected creatures

  MAKE_PATCH
      immunity_to_opcode=>~109 157~ // hold, web overlay
      immunity_to_string=> 14102    //  "Held"
      immunity_to_spell=>~ENTANGLE BLACK_DRAGON_PLANT~
   END
   LAF edit_creature STR_VAR creature=EVALUATE_BUFFER ~%largelist%~ edits=patch_data END


END

///////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION CRE_is_large
    STR_VAR filename=""
    RET value
BEGIN
    LPF CRE_read_animation_code RET anim=value END
    PATCH_IF VARIABLE_IS_SET $web_anim_array(EVALUATE_BUFFER ~%anim%~) BEGIN
       SET value=1
    END ELSE BEGIN
       SET value=0
    END
END
