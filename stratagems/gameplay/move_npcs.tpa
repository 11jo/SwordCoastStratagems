DEFINE_ACTION_FUNCTION move_npcs BEGIN
   ACTION_IF GAME_IS bgt BEGIN
      OUTER_SPRINT local_loc start_location
      LAF include STR_VAR file=move_bgt.tph END
      LAF move_NPCs_BGT END
   END ELSE BEGIN
      LAF move_npc_core INT_VAR xloc=1391 yloc=2524 STR_VAR crename=sharte area= ~%NorthNashkelRoad%~ END
      LAF move_npc_core INT_VAR xloc=1455 yloc=253 STR_VAR crename=viconi area= ~%SouthBeregostRoad%~ END
      LAF move_npc_core INT_VAR xloc=753 yloc=872 STR_VAR crename=eldoth area= ~%CoastWay%~ check_for=X#EldothMove END
      LAF move_npc_core INT_VAR xloc=2115 yloc=2203 STR_VAR crename=tiax area= ~%Beregost%~ check_for=X#TiaxMove END
      LAF move_npc_core INT_VAR xloc=1870 yloc=3081 STR_VAR crename=quayle area= ~%NashkelCarnival%~ check_for=X#QuayleMove END
      LAF swap_text STR_VAR files=~%tutu_var%viconi%eet_var%.dlg~ swaps=~605.901 => 1620.420 507.806=> 1520.320~ END
   END
   ACTION_IF enhanced_edition BEGIN
      LAF move_npc_sharteel_ee END
   END



END


DEFINE_ACTION_FUNCTION move_npc_core 
    INT_VAR xloc=0
            yloc=0
    STR_VAR crename=""
            area=""
            check_for=""
BEGIN
    ACTION_IF (~%check_for%~ STRING_EQUAL_CASE "" || !FILE_EXISTS ~override/%check_for%.G3~) BEGIN
       COPY_EXISTING baldur.gam override
            READ_LONG 0x30 non_joined_npcs_off
            READ_LONG 0x34 non_joined_npcs_count
            FOR (i = 0; i < non_joined_npcs_count; i+=1) BEGIN
                 SET cre_offset = non_joined_npcs_off + i * 0x160
                 READ_ASCII 0xc + cre_offset ~CREName~
                 PATCH_IF !(~%CREName%~ STRING_COMPARE_CASE ~%tutu_var%%crename%~) BEGIN
                 WRITE_ASCIIE cre_offset + 0x18 ~%area%~ #8
                 WRITE_SHORT cre_offset + 0x20 ~%xloc%~
                 WRITE_SHORT cre_offset + 0x22 ~%yloc%~
  END
END
       BUT_ONLY
    END
END

DEFINE_ACTION_FUNCTION move_npc_sharteel_ee
BEGIN
   LAF get_area_script STR_VAR area= ~%MutaminsGarden%~ RET script=script END
   LAF swap_text STR_VAR files= ~%script%.bcs~ swaps=~SetGlobal=>SETGLOBALPLACEHOLDER Global("SHARTEELFIGHT","GLOBAL",\[12\])=>False() SETGLOBALPLACEHOLDER=>SetGlobal~ END
   LAF extend_area_script STR_VAR area= ~%NorthNashkelRoad%~ top=sharteel_ee location=resource END
END
