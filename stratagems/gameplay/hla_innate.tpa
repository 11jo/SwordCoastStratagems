DEFINE_ACTION_FUNCTION hla_innate BEGIN
    LAF read_in_2da STR_VAR file=luabbr RET_ARRAY luabbr_data=array_data END
    ACTION_PHP_EACH luabbr_data AS var=>val BEGIN
       OUTER_SPRINT filename "lu%val%"
       ACTION_IF FILE_EXISTS_IN_GAME "%filename%.2da" BEGIN
          COPY_EXISTING "%filename%.2da" override
               LPF get_table_column_coordinate STR_VAR column=NUM_ALLOWED RET num_allowed_column=colnum END
               COUNT_2DA_COLS colcount
               READ_2DA_ENTRIES_NOW lua_entries colcount
               FOR (row=0;row<lua_entries;row+=1) BEGIN
                  SET check=0
                  READ_2DA_ENTRY_FORMER lua_entries row 1 ability
                  TO_UPPER ability
                  // preliminary patch: Paladin/Blackguard summoning should be limited to one use
                  PATCH_MATCH "%ability%" WITH
                     "GA_%PALADIN_SUMMON_DEVA%" "GA_%BLACKGUARD_SUMMON_FALLEN_DEVA%" BEGIN

                           SET_2DA_ENTRY row num_allowed_column colcount 1
                     END
                     DEFAULT
                  END
                  
                  // main patch
                  SET edit=0
                  INNER_PATCH "%ability%" BEGIN
                     REPLACE_EVALUATE "^GA_\(.*\)$" BEGIN
                        SPRINT resref "%MATCH1%"
                        TO_UPPER resref
                        PATCH_IF VARIABLE_IS_SET $hla_resref_map("%resref%") BEGIN
                           SPRINT new_resref $hla_resref_map("%resref%")
                           SET edit=1
                        END ELSE
                        PATCH_IF VARIABLE_IS_SET $hla_no_change_map("%resref%") BEGIN
                           // do nothing, no change
                           END ELSE BEGIN
                           INNER_ACTION BEGIN
                                COPY_EXISTING "%resref%.spl" "%workspace%"
                                     SET type= SHORT_AT 0x1c
                                BUT_ONLY
                           END
                           PATCH_IF (type=1||type=2) BEGIN
                               SET edit=1
                               INNER_ACTION BEGIN
                                   LAF make_hla_innate STR_VAR resref RET new_resref END
                               END
                               TO_UPPER new_resref
                               SPRINT $hla_resref_map("%resref%") "%new_resref%"
                           END ELSE BEGIN
                               SPRINT $hla_no_change_map("%resref%") ""
                               SPRINT new_resref "%resref%"
                           END
                        END
                     END
                     ""
                  END
                  PATCH_IF edit BEGIN
                     SET_2DA_ENTRY row num_allowed_column colcount 1
                     PATCH_IF "%resref%" STRING_COMPARE_CASE "%new_resref%" BEGIN
                         SET_2DA_ENTRY row 1 colcount "GA_%new_resref%"
                     END
                  END
               END
          BUT_ONLY
       END
    END
    COPY "%data_loc_root%/dw_shared/dw#hla_feat.2da" "%data_loc_root%/dw_shared"
         PHP_EACH hla_resref_map AS old=>new BEGIN
            REPLACE_TEXTUALLY CASE_INSENSITIVE "%old%" "%new%"
         END
    BUT_ONLY
END

DEFINE_ACTION_FUNCTION make_hla_innate
   STR_VAR resref=""
   RET new_resref
BEGIN
   OUTER_SPRINT new_resref "%resref%"
   ACTION_IF FILE_EXISTS_IN_GAME "%resref%.spl" BEGIN
      COPY_EXISTING "%resref%.spl" "%workspace%"
            SET type= SHORT_AT 0x1c
      BUT_ONLY
      ACTION_IF (type=1 || type=2) BEGIN
         ACTION_MATCH "%resref%" WITH
            "SPWI.*" BEGIN
               LAF find_unique_filename STR_VAR prefix=dwhlam extension=spl RET new_resref=filename END
            END
            "SPPR.*" BEGIN
               LAF find_unique_filename STR_VAR prefix=dwhlap extension=spl RET new_resref=filename END
            END
            DEFAULT
               OUTER_SPRINT new_resref "%resref%"
         END
         COPY_EXISTING "%resref%.spl" "override/%new_resref%.spl"
             WRITE_SHORT 0x1c 4
         BUT_ONLY
         ACTION_FOR_EACH copy_spell IN projimag simualcr BEGIN
            ACTION_IF FILE_EXISTS_IN_GAME "%copy_spell%.spl" BEGIN
               COPY_EXISTING "%copy_spell%.spl" override
                   LPF ADD_SPELL_EFFECT INT_VAR opcode=172 target=1 timing=1 STR_VAR resource="%resref_new%" END
               BUT_ONLY
            END
         END
      END
   END ELSE BEGIN
      LAF warning STR_VAR warning="Missing HLA in the LUA tables: %resref%" repeat=no END
   END
END
