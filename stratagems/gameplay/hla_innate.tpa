DEFINE_ACTION_FUNCTION hla_innate BEGIN


	// start by removing underlevelled effects from wizard/priest HLAs
	
	LAF run STR_VAR file=hla_minimum_level END


	LAF 2daq_extract STR_VAR resref=luabbr column=ABBREV RET_ARRAY luabbr_data=array END
	ACTION_PHP_EACH luabbr_data AS k=>v BEGIN
		ACTION_IF !VARIABLE_IS_SET $luabbr_list("%v%") BEGIN
			
			OUTER_SPRINT $luabbr_list("%v%") ""
			OUTER_SPRINT filename "lu%v%"
			COPY_EXISTING "%filename%.2da" override
				LPF 2da_read RET_ARRAY lu_array=array lu_rows=rows END
				PHP_EACH lu_rows AS row=>discard BEGIN
					SPRINT ability $lu_array("%row%" ABILITY)
					TO_UPPER ability					
					// preliminary patch: Paladin/Blackguard summoning should be limited to one use
					PATCH_MATCH "%ability%" WITH
					"GA_%PALADIN_SUMMON_DEVA%" "GA_%BLACKGUARD_SUMMON_FALLEN_DEVA%" BEGIN
						SET $lu_array("%row%" NUM_ALLOWED)=1
					END
					DEFAULT
					END
					// main loop
					PATCH_IF !VARIABLE_IS_SET $done_innate_hla("%ability%") BEGIN
						SET $done_innate_hla("%ability%")=1
						SPRINT resref ""
						INNER_PATCH "%ability%" BEGIN
							REPLACE_EVALUATE "^GA_\(.*\)$" BEGIN
								SPRINT resref "%MATCH1%"
								TO_UPPER resref
							END
						""					
						END
						PATCH_IF "%resref%" STR_CMP "" && FILE_EXISTS_IN_GAME "%resref%.spl" BEGIN
							INNER_ACTION BEGIN
								COPY_EXISTING - "%resref%.spl" nowhere
									type=SHORT_AT 0x1c
								BUT_ONLY
								ACTION_IF type=1 || type=2 BEGIN
									LAF spl_make_hla_class_ability STR_VAR resref RET new_resref END
									ACTION_TO_UPPER new_resref
									OUTER_SPRINT $hla_resref_map("%resref%") "%new_resref%"
									OUTER_SPRINT $hla_ga_map("%ability%") "GA_%new_resref%"
								END
							END
						END
					END
					PATCH_IF VARIABLE_IS_SET $hla_ga_map("%ability%") BEGIN
						SPRINT $lu_array("%row%" ABILITY) $hla_ga_map("%ability%")
					END					
					
				END			
			BUT_ONLY
			IF_EXISTS
		END
	END
	// now we need to go around again to replace the old with the new
	ACTION_PHP_EACH luabbr_list AS v=>discard BEGIN
		COPY_EXISTING "lu%v%.2da" override
			LPF 2da_read RET_ARRAY lu_rows=rows lu_array=array END
			PHP_EACH lu_rows AS row=>discard BEGIN
				PATCH_FOR_EACH col IN ABILITY PREREQUISITE EXCLUDED_BY BEGIN
					SPRINT ability $lu_array("%row%" "%col%")
					TO_UPPER ability
					PATCH_IF VARIABLE_IS_SET $hla_ga_map("%ability%") BEGIN
						SPRINT $lu_array("%row%" "%col%") $hla_ga_map("%ability%")
					END
				END
			END
			LPF 2da_write STR_VAR array=lu_array END
		BUT_ONLY
		IF_EXISTS
	END

	// update the SCS copy

    COPY "%data_loc_shared%/dw_feat_data.2da" "%data_loc_shared%"
         PHP_EACH hla_resref_map AS old=>new BEGIN
            REPLACE_TEXTUALLY CASE_INSENSITIVE "%old%" "%new%"
         END
    BUT_ONLY

END





