DEFINE_ACTION_FUNCTION ~no_chunk~ BEGIN

    // main patch

    OUTER_SPRINT pc_list ~~
    COPY_EXISTING_REGEXP GLOB ~.*\.cre~ ~override~
          SPRINT filename ~%SOURCE_RES%~
          LPF CRE_is_PC RET value=value END
          PATCH_IF value=1 BEGIN
             PUSH pc_list ~%filename%~
          END
    BUT_ONLY

    MAKE_PATCH
       type=>none
       match=>combine_checks
       match_parameter=>~CRE_is_PC=>1 CRE_read_effect_type=>1~
       check=>1
       add_effect_inline=>~opcode=>295 parameter2=>1 timing=>9 target=>1~
    END
    LAF edit_creature STR_VAR creature=EVALUATE_BUFFER ~%pc_list%~ editstring=~patch_conditionally=>patch_data~ END

    ACTION_IF is_bg2=1 BEGIN
       LAF no_chunk_selfishness END
    END

END

////////////////////////////////////////////////////
// make the Selfishness test still chunk the victim
///////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION no_chunk_selfishness BEGIN

	// modify HELL_EXPLODE to have a delay time of 1

        MAKE_PATCH
           type=>effect
           timing=>4
        END
        LAF edit_spell STR_VAR spell=EVALUATE_BUFFER ~%HELL_EXPLODE%~ editstring=~patch_conditionally=>patch_data~ END

        // amend area script
        
        LAF get_area_script STR_VAR area=ar2904 RET script=script END
        LAF swap_text STR_VAR files=EVALUATE_BUFFER ~%script%.bcs~ 
                              swaps=~ReallyForceSpell(Player2,HELL_EXPLODE) =>ReallyForceSpell(Player2,HELL_EXPLODE)ActionOverride(Player2,LeaveParty())Wait(1)
                                     ReallyForceSpell("hellvict",HELL_EXPLODE) =>ReallyForceSpell("hellvict",HELL_EXPLODE)ActionOverride("hellvict",LeaveParty())Wait(1)~ END

END
