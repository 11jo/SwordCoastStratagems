IF
        GlobalTimerNotExpired("DMWWpanickedwolves","GLOBAL")
        !See([ENEMY.HUMANOID])
THEN
        RESPONSE #100
                 EscapeArea()
END

IF
        Global("DMWWWolvesHostile","GLOBAL",1)
        !Allegiance(Myself,ENEMY)
         AreaType(OUTDOOR)
        !Detect([PC])
THEN
     RESPONSE #100
              Enemy()
              Continue()
END

IF
         Global("DMWWWolvesHostile","GLOBAL",0)
         Allegiance(Myself,ENEMY)
         AreaType(OUTDOOR)
         !Detect([PC])
THEN
    RESPONSE #100
             ChangeEnemyAlly(Myself,NEUTRAL)
END

IF
        OR(2)
              Range([PC],15)
              AttackedBy([PC],DEFAULT)
        !Allegiance(Myself,ENEMY)
        !GlobalTimerNotExpired("DMWWcowardlywolves","GLOBAL")
THEN
        RESPONSE #200
                 SetGlobalTimer("DMWWcowardlywolves","GLOBAL",12)
        RESPONSE #100
                 Enemy()
END

IF
      !Allegiance(Myself,ENEMY)
      GlobalTimerNotExpired("DMWWcowardlywolves","GLOBAL")
      Range([PC],20)
THEN
    RESPONSE #100
             RunAwayFrom([PC],120)
END

IF
   !Allegiance(Myself,ENEMY)
   !GlobalTimerNotExpired("wolfwander","LOCALS")
   OR(2)
        !See([0.0.117])
        Range([0.0.117],20)
THEN
    RESPONSE #100
             SetGlobalTimer("wolfwander","LOCALS",5)
             RandomWalk()
    RESPONSE #100
             SetGlobalTimer("wolfwander","LOCALS",5)
END

IF

  !Allegiance(Myself,ENEMY)
  See([0.0.117])
  !Range([0.0.117],20)
  !Range([PC],20)
THEN
    RESPONSE #100
             MoveToObject(LastSeenBy(Myself))
END

IF
  Allegiance(Myself,ENEMY)
  See([0.0.117])
  !Range([0.0.117],20)
  !See(NearestEnemyOf(Myself))
THEN
    RESPONSE #100
             MoveToObject(LastSeenBy(Myself))
END

IF
  !Allegiance(Myself,ENEMY)
  Range([ENEMY.0.117],20)
THEN
    RESPONSE #100
             Enemy()
END

IF
  !Global("hurtwolf","LOCALS",1)
  AreaType(OUTDOOR)
  HPPercentLT(Myself,60)
  !NumCreatureGT([ENEMY],3)
THEN
    RESPONSE #100
             SetGlobal("hurtwolf","LOCALS",1)
             SetGlobalTimer("DMWWpanickedwolves","GLOBAL",30)
    RESPONSE #100
             SetGlobal("hurtwolf","LOCALS",1)
END


IF
  StateCheck(Myself,STATE_REALLY_DEAD)
  !Global("deadwolf","LOCALS",1)
  AreaType(OUTDOOR)
THEN
    RESPONSE #100
             SetGlobal("deadwolf","LOCALS",1)
             SetGlobalTimer("DMWWpanickedwolves","GLOBAL",30)
    RESPONSE #100
             SetGlobal("deadwolf","LOCALS",1)
END


IF
	See(NearestEnemyOf(Myself))
	!Race(LastSeenBy(Myself),WOLF)
THEN
	RESPONSE #100
		AttackReevaluate(NearestEnemyOf(Myself),15)
END
