DEFINE_ACTION_FUNCTION ~move_hell_items~ BEGIN    //legacy

	// take the stuff with you from Hell to Ellesime's bedroom

	COPY_EXISTING ~cut59a.bcs~ ~override~
		DECOMPILE_AND_PATCH BEGIN
		     REPLACE_TEXTUALLY ~LeaveAreaLUAPanic("AR2812","",\[467.264\],8)~ ~CopyGroundPilesTo("AR2812",[467.264])LeaveAreaLUAPanic("AR2812","",[467.264],8)~
		END


	// move directly to expansion area and take the stuff with you

	ACTION_IF !FILE_EXISTS_IN_GAME ~dw#wkearly.mrk~ THEN BEGIN
		OUTER_SPRINT ~area~ ~AR4000~
		OUTER_SPRINT ~location~ ~[1300.1840]~
	END ELSE BEGIN // if WKearly installed
		OUTER_SPRINT ~area~ ~AR3000~
		OUTER_SPRINT ~location~ ~[2550.1630]~
	END
	
	COPY_EXISTING 
		~cut100a.bcs~ ~override~
		~cut100b.bcs~ ~override~
			DECOMPILE_AND_PATCH BEGIN
			REPLACE_TEXTUALLY ~MoveToExpansion()~
			~CopyGroundPilesTo("%area%",%location%)
			LeaveAreaLUAPanic("%area%","",%location%,0)
			LeaveAreaLUA("%area%","",%location%,0)
			ActionOverride(Player2,LeaveAreaLUA("%area%","",%location%,0))
			ActionOverride(Player3,LeaveAreaLUA("%area%","",%location%,0))
			ActionOverride(Player4,LeaveAreaLUA("%area%","",%location%,0))
			ActionOverride(Player5,LeaveAreaLUA("%area%","",%location%,0))
			ActionOverride(Player6,LeaveAreaLUA("%area%","",%location%,0))~
			END

	// move to expansion once you get there



	ACTION_IF !FILE_EXISTS_IN_GAME ~dw#wkearly.mrk~ THEN BEGIN

<<<<<<<< .../stratagems-inline/oldar4000_ee.baf
IF
	Global("FORCETOBPARTYMOVE4000","GLOBAL",0)
THEN
	RESPONSE #100
		StartCutSceneMode()
		LeaveAreaLUAPanic("AR4000","",[1300.1840],4)
		ActionOverride(Player1,LeaveAreaLUA("AR4000","",[1300.1840],4))
		ActionOverride(Player2,LeaveAreaLUA("AR4000","",[1300.1840],4))
		ActionOverride(Player3,LeaveAreaLUA("AR4000","",[1300.1840],4))
		ActionOverride(Player4,LeaveAreaLUA("AR4000","",[1300.1840],2))
		ActionOverride(Player5,LeaveAreaLUA("AR4000","",[1300.1840],0))
		ActionOverride(Player6,LeaveAreaLUA("AR4000","",[1300.1840],0))
		SetGlobal("FORCETOBPARTYMOVE4000","GLOBAL",1)
		EndCutSceneMode()
END
>>>>>>>>

<<<<<<<< .../stratagems-inline/oldar4000_vanilla.baf
IF
	Global("FORCETOBPARTYMOVE4000","GLOBAL",0)
THEN
	RESPONSE #100
		LeaveAreaLUAPanic("AR4000","",[1300.1840],4)
		ActionOverride(Player1,LeaveAreaLUA("AR4000","",[1300.1840],4))
		ActionOverride(Player2,LeaveAreaLUA("AR4000","",[1300.1840],4))
		ActionOverride(Player3,LeaveAreaLUA("AR4000","",[1300.1840],4))
		ActionOverride(Player4,LeaveAreaLUA("AR4000","",[1300.1840],2))
		ActionOverride(Player5,LeaveAreaLUA("AR4000","",[1300.1840],0))
		ActionOverride(Player6,LeaveAreaLUA("AR4000","",[1300.1840],0))
		SetGlobal("FORCETOBPARTYMOVE4000","GLOBAL",1)
END
>>>>>>>>

<<<<<<<< .../stratagems-inline/newar4000.baf
IF
	Global("FORCETOBPARTYMOVE4000","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("FORCETOBPARTYMOVE4000","GLOBAL",1)
		MoveToExpansion()
END

>>>>>>>>
          ACTION_IF !GAME_IS bgt BEGIN
          
                ACTION_IF enhanced_edition BEGIN
                   OUTER_SPRINT type ee
                END ELSE BEGIN
                   OUTER_SPRINT type vanilla
                END
		COPY_EXISTING ~ar4000.bcs~ ~override~
			REPLACE_BCS_BLOCK ~.../stratagems-inline/oldar4000_%type%.baf~ ~.../stratagems-inline/newar4000.baf~
		BUT_ONLY_IF_IT_CHANGES // will be moot if WKEarly is installed	
          END

	END ELSE BEGIN

<<<<<<<< .../stratagems-inline/oldar3000.baf

IF
	Global("EnteredArea","AR3000",0)
THEN
	RESPONSE #100
		SetGlobal("EnteredArea","AR3000",1)
		ActionOverride(Player1,JumpToPoint([2548.1631]))
		ActionOverride(Player2,JumpToPoint([2555.1717]))
		ActionOverride(Player3,JumpToPoint([2490.1713]))
		ActionOverride(Player4,JumpToPoint([2492.1712]))
		ActionOverride(Player5,JumpToPoint([2579.1637]))
		ActionOverride(Player6,JumpToPoint([2505.1642]))
		MoveViewPoint([2548.1631],INSTANT)
		AmbientActivate("DoorMist01",FALSE)
		AmbientActivate("DoorMist02",FALSE)
		AmbientActivate("DoorMist03",FALSE)
		AmbientActivate("DoorMist04",FALSE)
		AmbientActivate("DoorMist05",FALSE)
		Wait(1)
		TextScreen("SCRODRE")
END
>>>>>>>>

<<<<<<<< .../stratagems-inline/newar3000.baf

IF
	Global("DMWWWKExpansion","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("DMWWWKExpansion","GLOBAL",1)
		MoveToExpansion()
END

IF
	Global("EnteredArea","AR3000",0)
THEN
	RESPONSE #100
		SetGlobal("EnteredArea","AR3000",1)
		ActionOverride(Player1,JumpToPoint([2548.1631]))
		ActionOverride(Player2,JumpToPoint([2555.1717]))
		ActionOverride(Player3,JumpToPoint([2490.1713]))
		ActionOverride(Player4,JumpToPoint([2492.1712]))
		ActionOverride(Player5,JumpToPoint([2579.1637]))
		ActionOverride(Player6,JumpToPoint([2505.1642]))
		MoveViewPoint([2548.1631],INSTANT)
		AmbientActivate("DoorMist01",FALSE)
		AmbientActivate("DoorMist02",FALSE)
		AmbientActivate("DoorMist03",FALSE)
		AmbientActivate("DoorMist04",FALSE)
		AmbientActivate("DoorMist05",FALSE)
		Wait(1)
		TextScreen("SCRODRE")
END

>>>>>>>>

		COPY_EXISTING ~ar3000.bcs~ ~override~
			REPLACE_BCS_BLOCK ~.../stratagems-inline/oldar3000.baf~ ~.../stratagems-inline/newar3000.baf~

	END

END
