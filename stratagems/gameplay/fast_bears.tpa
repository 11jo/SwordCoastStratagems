DEFINE_ACTION_FUNCTION fast_bears BEGIN

/////////////////////////////////////////////////////////////////////////
/// BG:EE speed override
/////////////////////////////////////////////////////////////////////////

ACTION_IF FILE_EXISTS_IN_GAME ~extspeed.2da~ BEGIN
         COPY_EXISTING ~extspeed.2da~ override 
                 COUNT_2DA_ROWS 2 rowcount
                 FOR (i=1;i<rowcount;i+=1) BEGIN
                    READ_2DA_ENTRY i 0 2 anim
                    PATCH_IF (anim=29184 || anim=29185 || anim=29186 || anim=29187) BEGIN
                       SET_2DA_ENTRY i 1 2 ~4~
                    END
                 END
         BUT_ONLY
END


/////////////////////////////////////////////////////////////////////////
/// speed spell
/////////////////////////////////////////////////////////////////////////

LAF install STR_VAR file=dw#bfst.spl location=resource END

/////////////////////////////////////////////////////////////////////////
/// script
/////////////////////////////////////////////////////////////////////////

<<<<<<<<.../scs-inline/bearadd.baf
IF
  !GlobalTimerNotExpired("fastbear","LOCALS")
  Allegiance(Myself,ENEMY)
  See(NearestEnemyOf(Myself))
  DifficultyGT(EASY)
THEN
    RESPONSE #100
             ApplySpellRES("dw#bfst",Myself)
             SetGlobalTimer("fastbear","LOCALS",500)
             Continue()
END
>>>>>>>>

    ACTION_FOR_EACH script IN ~%tutu_var%cbear~ ~%tutu_var%bear~ BEGIN
      EXTEND_TOP ~%script%.bcs~ ~.../scs-inline/bearadd.baf~
    END

/////////////////////////////////////////////////////////////////////////
// find bears (we do this in code to speed things up)
/////////////////////////////////////////////////////////////////////////

OUTER_SPRINT bearlist ~~
COPY_EXISTING_REGEXP GLOB ~.*\.cre~ ~override~
     SPRINT bearname ~%SOURCE_RES%~
     LPF CRE_is_bear RET value=value END
     PATCH_IF value=1 BEGIN
        LPF CRE_has_bear_script RET value=value END
        PATCH_IF value=0 BEGIN
           PUSH bearlist ~%bearname%~
        END
     END
BUT_ONLY


/////////////////////////////////////////////////////////////////////////
// patched velocity for always-hostile bears
/////////////////////////////////////////////////////////////////////////

    MAKE_PATCH
       add_effect_inline=>~opcode=>176 target=>1 parameter1=>8 parameter2=>1 timing=>1~
    END

    LAF edit_creature STR_VAR creature=~%bearlist%~ edits=~patch_data~ END

/////////////////////////////////////////////////////////////////////////
// shapeshift spells
/////////////////////////////////////////////////////////////////////////
// this creates problems with a persistent effect; we could sectype around it on non-EE but it's a pain so I'll wait to do it on request

    ACTION_IF enhanced_edition BEGIN
        LAF edit_spell STR_VAR spell=~DRUID_SHAPESHIFT_BROWNBEAR DRUID_SHAPESHIFT_BLACKBEAR WIZARD_POLYMORPH_BROWN_BEAR WIZARD_POLYMORPH_BLACK_BEAR~
                        editstring=~add_effect_inline=>"opcode=>126 target=>1 parameter1=>8 parameter2=>1 duration=>3600000"~
        END
        MAKE_PATCH
            add_effect_inline=>"number_to_add=>2 opcode=>321 target=>1 resource=>~entry_index from [%WIZARD_POLYMORPH_BLACK_BEAR% %WIZARD_POLYMORPH_BROWN_BEAR%]~"
        END
        LAF edit_spell STR_VAR spell=WIZARD_POLYMORPH_NATURAL_FORM edits=patch_data END
        MAKE_PATCH
            add_effect_inline=>"number_to_add=>2 opcode=>321 target=>1 resource=>~entry_index from [%DRUID_SHAPESHIFT_BLACKBEAR% %DRUID_SHAPESHIFT_BROWNBEAR%]~"
        END
        LAF edit_spell STR_VAR spell=spin123 edits=patch_data END
    END
END

/////////////////////////////////////////////////////////////////////////
// auxiliary functions
/////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION CRE_is_bear
     RET value
BEGIN
     LPF CRE_read_animation_code RET anim=value END
     PATCH_IF (anim=0x7200 || anim=0x7201 || anim=0x7202 || anim=0x7203) BEGIN
        SET value=1
     END ELSE BEGIN
        SET value=0
     END
END
DEFINE_PATCH_FUNCTION CRE_has_bear_script
     RET value
BEGIN
     LPF CRE_match_script STR_VAR arguments=~%tutu_var%bear~ RET value1=value END
     LPF CRE_match_script STR_VAR arguments=~%tutu_var%cbear~ RET value2=value END
     SET value=value1 OR value2
END


