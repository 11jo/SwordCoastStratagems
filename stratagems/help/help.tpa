DEFINE_ACTION_FUNCTION help BEGIN

        LAF build_core_scripts END
        ACTION_IF is_bg1=1 BEGIN
           LAF install_bg1_help END
           LAF change_other_scripts_BG1 END
        END
        ACTION_IF is_bg2=1 BEGIN
           LAF change_other_scripts_BG2 END
        END

END

/////////////////////////////////////////////////////////////////////////////////
///   Construct the 100%-help scripts
////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION build_core_scripts
BEGIN
  // default scripts
  
  // on a BG:EE install, we skip SHOUT

  ACTION_IF enhanced_edition BEGIN
       OUTER_SPRINT normal ~~
       LAF install STR_VAR file=shout.baf location=baf END
  END ELSE BEGIN
       OUTER_SPRINT normal ~%tutu_var%shout~
  END
  LAF make_help_script INT_VAR first=57 second=99 STR_VAR normal=EVALUATE_BUFFER ~%normal%~ moron=dw#shtmo  END
  LAF make_help_script INT_VAR first=89 second=189 STR_VAR normal=gensht01 moron=dw#genmo  END
  // standard groupcheck-making scripts - include whining
  LAF make_help_script INT_VAR first=151 second=251 whiner=1 STR_VAR normal=gpshout groupcheck="InMyGroup(LastHeardBy(Myself))" END
  COPY_EXISTING ~gpshout.bcs~ ~override/grpsht01.bcs~
  // non-whining versions of gpshout
  LAF make_help_script INT_VAR first=151 second=251 STR_VAR normal=dw#gpsht moron=dw#gpshm groupcheck="InMyGroup(LastHeardBy(Myself))" END
  // a propagating, whining script that checks groups
  LAF make_help_script INT_VAR first=151 second=151 whiner=1 STR_VAR normal=dw#prshw groupcheck="InMyGroup(LastHeardBy(Myself))" END
  // a propagating, non-whining script that checks groups
  LAF make_help_script INT_VAR first=151 second=151 whiner=0 STR_VAR normal=dw#prsht groupcheck="InMyGroup(LastHeardBy(Myself))" END

  // some specific BG2 scripts
  ACTION_IF is_bg2=1 BEGIN
     LAF make_help_script INT_VAR first=125 second=225 whiner=1 STR_VAR normal=dw#gphlp groupcheck="InMyGroup(LastHeardBy(Myself))" END
     LAF make_help_script INT_VAR first=115 second=215 whiner=1 STR_VAR normal=gp115 END
     LAF make_help_script INT_VAR first=116 second=216 whiner=1 STR_VAR normal=gp116 END
     LAF make_help_script INT_VAR first=101 second=201 whiner=1 STR_VAR normal=genshtja END
     LAF make_help_script INT_VAR first=199 second=299 STR_VAR normal=drdsht01 END
  END
  // the "macshout" script used in EasyTutu
  ACTION_IF GAME_IS ~tutu tutu_totsc~ BEGIN
     LAF make_help_script INT_VAR first=272 second=273 STR_VAR normal=macshout END
  END



END

/////////////////////////////////////////////////////////////////////////////////
///   Script-assembly program
////////////////////////////////////////////////////////////////////////////////


DEFINE_ACTION_FUNCTION make_help_script
     INT_VAR first=0
             second=0
             whiner=0
     STR_VAR normal=""
             moron=""
             groupcheck=""
BEGIN
     OUTER_SPRINT variables ~666=%first%&777=%second%&groupcheck=%groupcheck%~
     ACTION_IF whiner=1 BEGIN
        OUTER_SPRINT variables ~%variables%&IsWhiner=True~
     END
     ACTION_IF ~%normal%~ STRING_COMPARE ~~ BEGIN
        LAF ssl_to_bcs STR_VAR script=help variables location=EVALUATE_BUFFER ~%component_loc%/ssl~ rename_to=EVALUATE_BUFFER ~%normal%~ END
     END
     ACTION_IF ~%moron%~ STRING_COMPARE ~~ BEGIN
         LAF ssl_to_bcs STR_VAR script=help variables=EVALUATE_BUFFER ~%variables%&IsStupid=true~ location=EVALUATE_BUFFER ~%component_loc%/ssl~ rename_to=EVALUATE_BUFFER ~%moron%~ END
     END
END

/////////////////////////////////////////////////////////////////////////////////
///   Systematic assignment of scripts in BG1
////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION install_bg1_help BEGIN

// undead (specific=70)

   LAF edit_creature STR_VAR creature=~ghoul ghast iskelet skelet skelets zombie~ tv=yes editstring=~insert_script_high=>dw#gpshm specifics=>70~ END
   LAF edit_creature STR_VAR creature=EVALUATE_BUFFER ~%tutu_skeleton_warrior%02 %tutu_skeleton_warrior%03 %tutu_scripts%kelet_a
                                                       %tutu_scripts%kelet_b %tutu_scripts%kelet_c %tutu_scripts%kelet03
                                                       %tutu_scriptz%ombie_a %tutu_scriptz%ombie_b %tutu_scriptz%ombie_c
                                                       %tutu_scriptz%ombie_d %tutu_scriptz%ombie_e %tutu_scriptg%houllor~
                             editstring=~strip_script=>dw#gpshm insert_script_high=>dw#gpshm specifics=>70~
   END

// gnolls (specific=71)

    LAF edit_creature STR_VAR creature=~britik flind gnoll gnoll_a gnoll_b gnoll_c gnoll_d gnoll_e ingot~
                              editstring=EVALUATE_BUFFER ~strip_script=>"%tutu_var%gnollf dw#gpsht" insert_script_high=>dw#gpsht specifics=>71~
                              tv=yes
    END
    LAF edit_creature STR_VAR creature=EVALUATE_BUFFER ~%tutu_scriptg%nollper~
                              editstring=EVALUATE_BUFFER ~strip_script=>"%tutu_var%gnollf dw#gpsht" insert_script_high=>dw#gpsht specifics=>71~
    END

// hobgoblins (specific=72)

    LAF edit_creature STR_VAR creature=~geltik malkax bhobgob hobgob hobjoia zargal baruk hakt ihobgob hobgo5~
                              editstring=EVALUATE_BUFFER ~strip_script=>"%tutu_var%hobgobf %tutu_var%hobgoba" insert_script_high=>dw#gpsht specifics=>72~
                              tv=yes
    END
    LAF edit_creature STR_VAR creature=EVALUATE_BUFFER ~%tutu_scripth%obelite %tutu_scripth%obgob_a %tutu_scripth%obgob_b %tutu_scripth%obgob_c %tutu_scripth%obgob_d %tutu_scripth%obgob_e 
                                                        %tutu_scripth%obelite %tutu_scripth%obgoa_a %tutu_scripth%obgoa_b %tutu_scripth%obgoa_c %tutu_scripth%obgoa_d %tutu_scripth%obgoa_e
                                                        %tutu_scripth%obgzhur~
                              editstring=EVALUATE_BUFFER ~strip_script=>"%tutu_var%hobgobf %tutu_var%hobgoba dw#gpsht" insert_script_high=>dw#gpsht specifics=>72~
    END

// kobolds (specific=73)

    LAF edit_creature STR_VAR creature=~ikobold kobcomm kobold kobold3 kobold7 kobolda~
                              editstring=EVALUATE_BUFFER ~strip_script=>"%tutu_var%kobolda %tutu_var%koboldf dw#gpsht" insert_script_high=>dw#gpsht specifics=>73~
                              tv=yes
    END
    ACTION_FOR_EACH suffix IN  ~_A~ ~_B~ ~_C~ ~_D~ ~_E~ ~AL~ ~FL~ BEGIN
      LAF edit_creature STR_VAR creature=EVALUATE_BUFFER ~%tutu_scriptk%obold%suffix%~
                              editstring=EVALUATE_BUFFER ~strip_script=>"%tutu_var%kobolda %tutu_var%koboldf dw#gpsht" insert_script_high=>dw#gpsht specifics=>73~
      END
    END
    ACTION_FOR_EACH suffix IN  ~_A~ ~_B~ ~_C~ ~_D~ ~_E~ BEGIN
      LAF edit_creature STR_VAR creature=EVALUATE_BUFFER ~%tutu_scriptk%obola%suffix%~
                              editstring=EVALUATE_BUFFER ~strip_script=>"%tutu_var%kobolda %tutu_var%koboldf dw#gpsht" insert_script_high=>dw#gpsht specifics=>73~
      END
    END
    
    // kobolds need combat scripts (if GENAI not installed)
    
    LAF check_label STR_VAR label=dw#genai RET value=value END
    ACTION_IF !value BEGIN
      LAF edit_creature STR_VAR creature=EVALUATE_BUFFER ~%tutu_var%kobold3 %tutu_var%kobold7 %tutu_scriptk%obold_a %tutu_scriptk%obold_b %tutu_scriptk%obold_c %tutu_scriptk%obold_d %tutu_scriptk%obold_e~
                                editstring=~strip_script=>wtasight insert_script_low=>%tutu_scriptw%tasight~
      END

    END


/*
// gibberlings (specific=74)

    <<<<<<<< .../stratagems-inline/gibber_add.baf
IF
	HitBy([ANYONE],CRUSHING)
THEN
	RESPONSE #50
		Attack(NearestEnemyOf(Myself))
	RESPONSE #50
		RunAwayFrom(NearestEnemyOf(Myself),75)
END
    >>>>>>>>

 //   COPY_EXISTING ~dw#gpshm.bcs~ ~override/%tutu_var%gibber.bcs~
    EXTEND_TOP ~%tutu_var%gibber.bcs~ ~.../stratagems-inline/gibber_add.baf~
                                 */

// basilisks (specific=75)

    LAF edit_creature STR_VAR creature=EVALUATE_BUFFER ~%tutu_var%basilg %tutu_scriptbg%basill %tutu_scriptb%asilmut %tutu_var%mutami~
                              editstring=~strip_script=>dw#gpshm insert_script_high=>dw#gpshm specifics=>75~
    END
                              
// wyverns (specific=76)

    LAF edit_creature STR_VAR creature=~wyvern wyverba~ editstring=~strip_script=>dw#gpshm insert_script_high=>dw#gpshm specifics=>76~ tv=yes END

// tasloi (specific=77)

    LAF edit_creature STR_VAR creature=tasloi tv=yes editstring=~strip_script=>dw#gpsht insert_script_high=>dw#gpsht specifics=>77~ END

// spiders (specific=78)

    LAF edit_creature STR_VAR creature=~spidgi spidhu spidsw spidwr etterc etterc1 spidph~
                              editstring=~strip_script=>dw#gpshm insert_script_high=>dw#gpshm specifics=>78~
                              tv=yes
    END
    LAF edit_creature STR_VAR creature=EVALUATE_BUFFER ~%tutu_scripts%pidland %tutu_scripts%pidphas~
                              editstring=~strip_script=>dw#gpshm insert_script_high=>dw#gpshm specifics=>78~
    END

// wolves (specifics=79)

    LAF edit_creature STR_VAR creature=~worg wolfdi wolfdr wolfva worgar~
                              editstring=~strip_script=>dw#gpshm insert_script_high=>dw#gpshm specifics=>79~
                              tv=yes
    END
    LAF edit_creature STR_VAR creature=EVALUATE_BUFFER ~%tutu_scriptbg%wolf~
                              editstring=~strip_script=>dw#gpshm insert_script_high=>dw#gpshm specifics=>79~
    END

// ogres (specifics=80)

    LAF edit_creature STR_VAR creature=EVALUATE_BUFFER ~%tutu_var%ogre %tutu_var%ogre02 %tutu_scripto%greberz %tutu_scripto%grebjor
                                                        %tutu_scripto%gremiri %tutu_var%ogregr %tutu_scripto%gregr_b %tutu_scripto%gregr_c
                                                        %tutu_scripto%gregr_d %tutu_var%ogreha %tutu_scripto%greha_b %tutu_var%arghai %tutu_scripto%grema02~
                              editstring=~strip_script=>dw#gpsht insert_script_high=>dw#gpsht specifics=>80~
    END
    LAF edit_creature STR_VAR creature=EVALUATE_BUFFER ~%tutu_var%ogres~ editstring=~strip_script=>dw#gpsht strip_script=>%tutu_var%ogres insert_script_high=>dw#gpsht insert_script_high=>%tutu_var%ogres~ END
    
// carrion crawlers (specifics=81)

    LAF edit_creature STR_VAR creature=carrio tv=yes editstring=~strip_script=>dw#gpshm insert_script_high=>dw#gpshm specifics=>81~ END

// werewolves (specifics=82)

    LAF edit_creature STR_VAR creature=EVALUATE_BUFFER ~%tutu_scriptd%aesewlf %tutu_var%werewo~
                              editstring=~strip_script=>dw#gpsht insert_script_high=>dw#gpsht specifics=>82~
    END
    
// wolfweres (specifics=83)

   LAF edit_creature STR_VAR creature=EVALUATE_BUFFER ~%tutu_var%wolfwe %tutu_scriptw%olfwegl %tutu_scriptw%olfwegr~
                              editstring=~strip_script=>dw#gpsht insert_script_high=>dw#gpsht specifics=>83~
    END

// xvarts (specifics=84; propagates (because of village)

   LAF edit_creature STR_VAR creature=~xvart ursa~ tv=yes editstring=~strip_script=>dw#prsht insert_script_high=>dw#prsht specifics=>84~ END

// bandits in camp (specifics=85; propagates)

   LAF edit_creature STR_VAR creature=~bhobgob hobgobc bandcr credus taugos glart ardeno banditc~ tv=yes editstring=EVALUATE_BUFFER ~strip_script=>"dw#prsht %tutu_var%shout" insert_script_high=>dw#prsht specifics=>85~ END

// Kelddath and his sirines (specifics=86)

   LAF edit_creature STR_VAR creature=EVALUATE_BUFFER ~%tutu_scripts%irine02 %tutu_var%keldda~
                              editstring=~strip_script=>dw#gpsht insert_script_high=>dw#gpsht specifics=>86~
    END

//  The cultists (specifics=87)

   LAF edit_creature STR_VAR creature=~cult1 cult2 cult3 cultt1 cultt2 cultt3 cultt4~
                             editstring=~strip_script=>"cultsht macshout dw#gpsht" insert_script_high=>dw#gpsht specifics=>87~
                             tv=yes
   END

// dogs (specifics=88)

   LAF edit_creature STR_VAR creature=~dogwa dogwi~
                             editstring=~strip_script=>dw#gpshm insert_script_high=>dw#gpshm specifics=>88~
                             tv=yes
   END
   
// slythe and krystin (specifics=89)

   LAF edit_creature STR_VAR creature=~slythe krysti~
                             editstring=~strip_script=>dw#gpsht insert_script_high=>dw#gpsht specifics=>89~
                             tv=yes
   END



END

/////////////////////////////////////////////////////////////////////////////////
///   Make localised changes in other scripts (BG1 version)
////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION change_other_scripts_BG1 BEGIN

   // messengers shouldn't have help scripts
   
   LAF edit_creature STR_VAR creature=~roger wilco~ tv=yes editstring=EVALUATE_BUFFER ~strip_script=>%tutu_var%shout~ END

   // hobgoblin elites in this area are allied with the ogres

   COPY_EXISTING ~%LonelyPeaks%.are~ ~override~
	REPLACE_TEXTUALLY ~%tutu_scripth%obelite~ ~dw#oghob~

   LAF clone_creature STR_VAR creature=EVALUATE_BUFFER ~%tutu_scripth%obelite=>dw#oghob~ editstring=~specifics=>80~ END

   // hobgoblins in Cloakwood Mines level 3 need to respond to human SHOUTs

   LAF clone_creature STR_VAR creature=EVALUATE_BUFFER ~%tutu_scripth%obelite=>dw#mihoe %tutu_scripth%obgoa_a=>dw#mihoa %tutu_scripth%obgoa_e=>dw#mihob~
                              editstring=EVALUATE_BUFFER ~script_override=>%tutu_var%shout~
   END

   COPY_EXISTING ~%CloakwoodMines_L3%.are~ ~override~
    REPLACE_TEXTUALLY CASE_INSENSITIVE ~%tutu_scripth%obelite%~ ~dw#mihoe~
    REPLACE_TEXTUALLY CASE_INSENSITIVE ~%tutu_scripth%obgoa_a%~ ~dw#mihoa~
    REPLACE_TEXTUALLY CASE_INSENSITIVE ~%tutu_scripth%obgoa_b%~ ~dw#mihob~

   // one or two creatures should call for help but not respond to others' calls - they have an item which prevents them responding

   LAF clone_item STR_VAR item=~misc16=>dw#nohlp~ editstring=~droppable=>0~ END
   LAF edit_creature STR_VAR creature=~iron3 iron4 iron11~ tv=yes editstring=~remove_items=>dw#nohlp add_items=>dw#nohlp~ END

   // Nashkel guards - shoot Nimbul, but in due course go to melee

   LAF install STR_VAR file=~dw#nimso.baf~ location=baf END
   LAF edit_creature STR_VAR creature=~amnis bardol~ tv=yes editstring=~swap_script=>"%tutu_scripts%eeenemy=>dw#nimso"~ END

END

/////////////////////////////////////////////////////////////////////////////////
///   Make localised changes in other scripts (BG2 version)
////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION change_other_scripts_BG2 BEGIN

// let's experiment with giving the githyanki indefinite propagation

     LAF make_help_script INT_VAR first=175 second=275 STR_VAR normal=dw#gith1 END
     LAF edit_creature STR_VAR creature=~gith01 gith02 gith03 gith04 gith05 gith06 gorgit gorgith2~ editstring=~swap_script=>"grpsht01=>dw#gith1 gpshout=>dw#gith1"~ END

// spiders

     LAF make_help_script INT_VAR first=76 second=86 STR_VAR moron=dw#sphlp END
     LAF edit_creature STR_VAR creature=~spidgi01 spspid05 spidhu01 spidwr01 spspid07
                                         spidsw01 spidsm01 spidsm02 spspider spidph01
                                         spspid06 spidvo01 etter01~
                               editstring=~insert_script_high=>dw#sphlp~
     END

// avoid problems with the rakshasa

    LAF edit_creature STR_VAR creature=trrak01 editstring=~invert_scripts=>"gpshout=>trrak01"~ END

// succubus alu-fiends need to go hostile properly

   LAF swap_text STR_VAR files=gorsuc01.dlg swaps=~Enemy()=>Shout(151)Enemy()~ END


// CC beastmaster

   MAKE_PATCH
      type=>actor
      match=>is_CC_beast
      check=>1
      script_override=>dw#beast
   END
   DEFINE_PATCH_FUNCTION is_CC_beast STR_VAR offset_base="" RET value BEGIN
      LPF ARE_read_crefile STR_VAR offset_base RET cre=value END
      TO_UPPER cre
      PATCH_MATCH ~%cre%~ WITH GIBMUT01 ICMIN01 CATLEO01 CATJAG01 GLBEAR2 BEARGR COPLION BEGIN
         SET value=1
      END
      DEFAULT
         SET value=0
      END
   END
   LAF edit_area STR_VAR area=ar0406 editstring=~patch_conditionally=>patch_data~ END
<<<<<<<< .../stratagems-inline/dw#beast.baf
IF
  !See(NearestEnemyOf(Myself))
  !Dead("beast")
  !Global("beastOpenTheDoors","GLOBAL",0)
THEN
    RESPONSE #100
             MoveToObject("beast")
END
>>>>>>>>

        COMPILE ~.../stratagems-inline/dw#beast.baf~

// UB's Suna Seni can go hostile if the ruffians do. Not sure if that's an UB bug, but I'll fix it anyway [legacy code]

        LAF edit_creature STR_VAR creature=suna editstring=~strip_script=>gpshout~ END


// prevent Amnish guards attacking Cowled Wizards

	 LAF clone_item STR_VAR item=~misc01=>dw#cowle~ editstring=~droppable=>0~ END
         LAF edit_creature STR_VAR creature=~cscowl9 cucow1 cucow2 cucow3 cuwiz1 cuwiz2 cuwiz3 cuwiz4 cuwiz5 cuwiz6 cuwizc cuwizsu~ editstring=~add_items=>dw#cowle~ END
         LAF swap_text STR_VAR files=seeenemy.bcs swaps=~!Class(LastSeenBy(Myself),FLAMING_FIST)=>!Class(LastSeenBy(Myself),FLAMING_FIST)!HasItem("dw#cowle",LastSeenBy(Myself))~ END

// Twisted Rune

        <<<<<<<< .../stratagems-inline/dw#ruhlp.baf
IF
  !Dead("hlshang")  
  Global("ShangalarMove","AR1008",1)
  !Detect(NearestEnemyOf(Myself))
	!Detect(Player1)
	!Detect(Player2)
	!Detect(Player3)
	!Detect(Player4)
	!Detect(Player5)
	!Detect(Player6)
	THEN
   RESPONSE #100
       MoveToObject(Player1)
END
        >>>>>>>>

        COMPILE ~.../stratagems-inline/dw#ruhlp.baf~
        LAF edit_creature STR_VAR creature=~hlvaxal hlshyr hllayen hlrevan~ editstring=~insert_script_high=>dw#ruhlp~ END

// Ghaunadans
        OUTER_SPRINT ghaun_swap ~otyugh01=>dw#ghoty jelmus01=>ghjmu jelgre01=>dw#ghgre jeloch01=>dw#ghoch jelgra01=>dw#ghgra~
        LAF clone_creature STR_VAR creature=EVALUATE_BUFFER ~%ghaun_swap%~ editstring=~swap_script=>"gensht01=>grpsht01" specifics=>133~ END
        LAF get_area_script STR_VAR area=ar2200 RET script=script END
        LAF swap_text STR_VAR files=EVALUATE_BUFFER ~%script%.bcs~ swaps=EVALUATE_BUFFER ~%ghaun_swap%~ END
        LAF edit_creature STR_VAR creature=~daghaun1 daghaun2 dagmag01~ editstring=~insert_script_high=>grpsht01 specifics=>133~ END

// haegan

   LAF edit_creature STR_VAR creature=haegan editstring=~swap_script=>"haegan=>gpshout"~ END

// the dryads in Chateau Irenicus, who need to be insulated from duergar etc

    LAF edit_creature STR_VAR creature=~idryad01 idryad02 idryad03~ editstring=~specifics=>90 swap_script=>"shout=>grpsht01"~ END

// misc allocations of scripts to creatures

    // shout
    
   LAF edit_creature STR_VAR creature=~duecla01 drofod02 drofod03 drofod04 jael01 jael02 jael03~ editstring=~insert_script_high=>shout~ END

    // gpshout

   LAF edit_creature STR_VAR creature=~werewo01 werewodr weregrdr weregr01 c6were1
                                       gorwowe wolfgr01 wolfwe01 wolfwe02 shawol01
                                       pirsea01 pirsea03~
                             editstring=~insert_script_high=>grpsht01~ 
   END

    // grpsht01

   LAF edit_creature STR_VAR creature=~~
                             editstring=~insert_script_high=>grpsht01~ 
   END


    // gensht01

    LAF edit_creature STR_VAR creature=~troll01 skelwa01 skelwa02 skelwa03 skelwa 
                                        mummy mummy01 mumgre01 icgob03 icgob04
                                        icyuan01 icyuan02 icyuan03 icyuan04 icyuan05
                                        obssah02 uddeath uddeath2 udkuo01~
                              editstring=~insert_script_high=>gensht01~
    END

// not technically a script

    LAF edit_creature STR_VAR creature=sahramb3 editstring=~allegiance=>ENEMY~ END



END
