INCLUDE ~%MOD_FOLDER%/lib/ai_wrap.tph~

DEFINE_ACTION_FUNCTION kobold BEGIN
        LAF kobold_core END
        LAF kobold_ambush END

END

DEFINE_ACTION_FUNCTION kobold_core BEGIN


      LAF check_label STR_VAR label=dw#help RET value END
      
      ACTION_IF value BEGIN
         OUTER_SPRINT help dw#gpsht
      END ELSE BEGIN
         OUTER_SPRINT help ~%tutu_var%koboldf~
      END

      // items

      ACTION_IF enhanced_edition BEGIN
         OUTER_SET dagger_desc1=RESOLVE_STR_REF (@21316)
         OUTER_SET dagger_desc2=RESOLVE_STR_REF (@21317)
         OUTER_SET arrow_desc=RESOLVE_STR_REF (@21319)
      END ELSE BEGIN
         OUTER_SET dagger_desc1=RESOLVE_STR_REF (@21306)
         OUTER_SET dagger_desc2=RESOLVE_STR_REF (@21307)
         OUTER_SET arrow_desc=RESOLVE_STR_REF (@21309)
      END

      // daggers

	itm.copy[dagg01=>dw#kob01]
	[
		m_identified_name:=@21304
		m_unidentified_description:="%dagger_desc1%"
		m_identified_description:="%dagger_desc2%"
		m_desc_image:=cdagg03
		m_icon:=idagg03
		m.fx.alter{PATCH_MATCH s_parameter1 WITH 72 BEGIN s_parameter2=21 END 62 BEGIN s_parameter2=20 END 91 BEGIN s_parameter2=16 END DEFAULT END}
		m.ab_fx.add{s_opcode=25 s_target=2 s_duration=6 s_parameter1=1 s_parameter2=2 s_save_vs_poison=1}
		m.ab_fx.add{s_opcode=25 s_target=1 s_probability=4 s_duration=6 s_parameter1=1 s_parameter2=2 s_save_vs_poison=1}	
	]

     
      // arrows
	  
	itm.copy[arow01=>dw#kob02]
	[
		m_identified_name:=@21308
		m_identified_description:="%arrow_desc%"
		m.ab_fx.add{s_opcode=25 s_target=2 s_duration=6 s_parameter1=2 s_parameter2=2 s_save_vs_poison=1}
		m.ab_fx.add{s_opcode=25 s_target=1 s_probability=4 s_duration=6 s_parameter1=2 s_parameter2=2 s_save_vs_poison=1}
	]

      // install needed files
      
      LAF install STR_VAR files=~dw#kobsp.baf dw#kobch.cre dw#kobgd.cre dw#kobsh.cre galtok.d~ location=resource END

      // add kobold-summoning script

      ACTION_FOR_EACH koboldtype IN obola_a obola_c obola_e obold_a obold_c obold_e oboldal BEGIN
         LAF edit_creature STR_VAR creature= ~%tutu_scriptk%%koboldtype%~ editstring=~insert_script_high=>dw#kobsp~ END
      END
      LAF edit_creature INT_VAR tv=1 STR_VAR creature=kobolda editstring=~insert_script_high=>dw#kobsp~ END

       // Commander
      
      MAKE_PATCH
          steal_sounds_from=>~%tutu_var%kobold~
          say_both_names=>21301
          script_race=>~%help%~
          remove_items=>~_arow01 _bow05~
          add_items=>~%tutu_var%bow05 %tutu_var%arow01(20,quiver2)~
          enforce_charclass=>is_bg1
      END
      LAF edit_creature STR_VAR creature=dw#kobch edits=patch_data END

      // guards
      
      MAKE_PATCH
          steal_sounds_from=>~%tutu_var%kobold~
          say_both_names=>21302
          script_race=>~%help%~
          enforce_charclass=>is_bg1
      END
      LAF edit_creature STR_VAR creature=dw#kobgd edits=patch_data END

      // shaman

      MAKE_PATCH
          steal_sounds_from=>~%tutu_var%kobold~
          say_both_names=>21303
		  strip_script=>all
          script_race=>~%help%~
          enforce_charclass=>is_bg1
          enforce_mage=>null
      END

      LAF edit_creature STR_VAR creature=dw#kobsh edits=patch_data END


	  
END
DEFINE_ACTION_FUNCTION kobold_ambush BEGIN

     LAF extend_area_script STR_VAR area=~%NashkelMines_L3%~ top=ambush location=resource END

     // the "specifics" change means that they won't hear other kobolds' help cries

     LAF clone_creature STR_VAR creature=~dw#kobch=>dw#kbach %tutu_var%kobcomm=>dw#kbacm dw#kobsh=>dw#kbash dw#kobsh=>dw#kbas2 dw#kobgd=>dw#kbagd~
                                editstring=~specifics=>77~
     END

     LAF check_label STR_VAR label=dw#mage RET value END
     ACTION_IF value BEGIN

        LAF edit_creature STR_VAR creature=dw#kbash editstring=~kit=>INVOKER enforce_mage=>null~ END
        LAF edit_creature STR_VAR creature=dw#kbas2 editstring=~kit=>ENCHANTER enforce_mage=>null~ END
     END
END
