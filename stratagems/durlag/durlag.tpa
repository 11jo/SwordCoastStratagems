INCLUDE ~%scsroot%/lib_stratagems/ai_wrap.tph~
LAF include STR_VAR file=fiend_shared.tph locbase=fiend END

DEFINE_ACTION_FUNCTION durlag BEGIN

     LAF include STR_VAR file=creature_swaps.tpa locbase=tactical_bg1 END
     LAF creature_swaps END

     LAF action_check_ini STR_VAR ini=integrated RET value=value END
     ACTION_IF !value BEGIN
        LAF durlag_lovefight END
        LAF durlag_doppelgangers END
     END

     LAF durlag_chess END
     LAF durlag_mirror END



END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////


DEFINE_ACTION_FUNCTION durlag_lovefight BEGIN

   // love
   
   MAKE_PATCH
      hitpoints=>120
      morale_break=>1
      add_items=>~immune1 ring95~
      immunity_to_spell=>~%tutu_var%sw1h01~
   END
   LAF edit_creature STR_VAR creature=lovem tv=yes END
   LAF ssl_to_bcs STR_VAR script=love location=~durlag/ssl~ rename_to=EVALUATE_BUFFER ~%tutu_var%love~ END

   // fear
   
   MAKE_PATCH
      add_items=>~trollreg(boots)~
      morale_break=>1
   END
   LAF edit_creature STR_VAR creature=fearm tv=yes END

   // avarice

   MAKE_PATCH
      add_items=>~immune1 ring95~
      morale_break=>1
      level=>12
      level2=>12
      hitpoints=>90
      immunity_to_spell=>~%tutu_var%sw1h08~
   END 
   LAF edit_creature STR_VAR creature=EVALUATE_BUFFER ~%tutu_scripta%varicem~ END
   LAF ssl_to_bcs STR_VAR script=greed location=~durlag/ssl~ rename_to=EVALUATE_BUFFER ~%tutu_var%greed~ END

   // pride

   MAKE_PATCH
      add_items=>~immune1 ring95~
      morale_break=>1
      hitpoints=>100
      level=>12
      level2=>12
      attacks=>2
      class=>FIGHTER_CLERIC
      immunity_to_spell=>~%tutu_var%ax1h01~
   END
   LAF edit_creature STR_VAR creature=pridem tv=yes END
   LAF ssl_to_bcs STR_VAR script=pride location=~durlag/ssl~ rename_to=EVALUATE_BUFFER ~%tutu_var%pride~ END


END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION durlag_doppelgangers BEGIN



/// handle odd failure of the Kiel setpiece battle

    LAF edit_creature STR_VAR creature=islann tv=yes editstring=~insert_script_high=>dw#islan~ END
    LAF edit_creature STR_VAR creature=fuerne tv=yes editstring=~insert_script_high=>dw#fuern~ END
    LAF install STR_VAR files=~dw#islan.baf dw#fuern.baf~ location=resource END
    
    LAF swap_text STR_VAR files=EVALUATE_BUFFER ~%tutu_var%dopdur3.dlg~ 
                          swaps=~ReallyForceSpell("islanne",WIZARD_CLOUDKILL)=>ReallyForceSpellPoint([1806.895],WIZARD_CLOUDKILL)
                                 ReallyForceSpell("fuernebol",WIZARD_STINKING_CLOUD)=>ReallyForceSpellPoint([1638.749],WIZARD_STINKING_CLOUD)
                                 ReallyForceSpell(Myself,WIZARD_STINKING_CLOUD)=>ReallyForceSpellPoint([2082.580],WIZARD_STINKING_CLOUD)Wait(1)~
    END

    EXTEND_BOTTOM ~%tutu_var%dopdop.bcs~ ~override/%tutu_scriptd%opspell.bcs~

    // doppleganger weapons don't break
    
    LAF edit_creature STR_VAR creature=~dopdur dopdur1 dopdur2 dopdur3~ tv=yes editstring=EVALUATE_BUFFER ~immunity_to_spell=>%tutu_var%ax1h01~ END

    ACTION_FOR_EACH script IN dopdop dopdur dopfue dopisl dopkie BEGIN
        LAF ssl_to_bcs STR_VAR script location=~durlag/ssl~ rename_to=EVALUATE_BUFFER ~%tutu_var%%script%~ END
    END

    
    MAKE_PATCH
       add_items=>dw#nopre
       alignment=>NEUTRAL_EVIL
       script_race=>~%tutu_var%mage5~
       enforce_charclass=>is_bg1
    END
    LAF edit_creature STR_VAR creature=dopisl tv=yes END

    MAKE_PATCH
       add_items=>dw#nopre
       alignment=>NEUTRAL_EVIL
       immunity_to_spell=>~%tutu_var%blun01~
       script_race=>~%tutu_var%priest5~
       enforce_charclass=>is_bg1
    END
    LAF edit_creature STR_VAR creature=dopkie tv=yes END

END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION durlag_chess BEGIN
      
       LAF ssl_to_bcs STR_VAR script=~dw#ches1 dw#ches2 dw#ches3~ location=~durlag/ssl~ END

       // pawns

       MAKE_PATCH
          resist_electricity=>100
          hitpoints=>30
          add_items=>~immune1 ring95~
          immunity_to_spell=>~cdbreak1 cdbreak9 %tutu_var%hamm01~
          strip_script=>dw#swpdn
          insert_script_high=>dw#swpdn
       END
       LAF clone_creature STR_VAR creature=EVALUATE_BUFFER ~%tutu_scriptbg%pawn=>dw#pawn~ edits=patch_data END
       LAF edit_creature STR_VAR creature=EVALUATE_BUFFER ~%tutu_scriptbg%pawn~ editstring=~strip_script=>dw#swpup insert_script_high=>dw#swpup~ END

       // knights
       
       MAKE_PATCH
          resist_electricity=>100
          insert_script=>~dw#ches1 below kingdead~
          add_items=>~immune1 ring95(amulet)~
          immunity_to_spell=>~cdbreak1 cdbreak6 %tutu_var%sw1h04~
          strip_script=>dw#swpdn
          insert_script_high=>dw#swpdn
          dv=>KNIGHT
       END
       LAF clone_creature STR_VAR creature=EVALUATE_BUFFER ~%tutu_scriptbg%knight=>dw#knigh~ edits=patch_data END
       LAF edit_creature STR_VAR creature=EVALUATE_BUFFER ~%tutu_scriptbg%knight~ editstring=~strip_script=>dw#swpup insert_script_high=>dw#swpup dv=>KNIGHT~ END

       // rooks
       
       MAKE_PATCH
          resist_electricity=>100
          insert_script=>~dw#ches2 below kingdead~
          add_items=>~immune1 ring95(amulet)~
          immunity_to_spell=>~cdbreak1 %tutu_var%sw1h04~
          strip_script=>dw#swpdn
          insert_script_high=>dw#swpdn
          dv=>ROOK
       END
       LAF clone_creature STR_VAR creature=EVALUATE_BUFFER ~%tutu_scriptbg%rook=>dw#rook~ edits=patch_data END
       LAF edit_creature STR_VAR creature=EVALUATE_BUFFER ~%tutu_scriptbg%rook~ editstring=~strip_script=>dw#swpup insert_script_high=>dw#swpup dv=>ROOK~ END

       // bishops

      MAKE_PATCH
          resist_electricity=>100
          insert_script=>~dw#ches3 below kingdead~
          add_items=>~immune1 ring95(amulet) dw#nopre~
          immunity_to_spell=>~%tutu_var%blun04~
          strip_script=>dw#swpdn
          insert_script_high=>dw#swpdn
          dv=>BISHOP
      END
      LAF clone_creature STR_VAR creature=EVALUATE_BUFFER ~%tutu_scriptbg%bishop=>dw#bisho~ edits=patch_data END
      LAF edit_creature STR_VAR creature=EVALUATE_BUFFER ~%tutu_scriptbg%bishop~ editstring=~strip_script=>dw#swpup insert_script_high=>dw#swpup dv=>BISHOP~ END
      // king

      MAKE_PATCH
          resist_electricity=>100
          class=>FIGHTER_MAGE
          add_items=>dw#move0
          strip_script=>king
          enforce_charclass=>is_bg1
      END
      LAF clone_creature STR_VAR creature=EVALUATE_BUFFER ~%tutu_scriptbg%king=>dw#king~ edits=patch_data END
      LAF edit_creature STR_VAR creature=EVALUATE_BUFFER ~%tutu_scriptbg%king~ editstring=~strip_script=>dw#swpup insert_script_high=>dw#swpup~ END

      //queen

      MAKE_PATCH
          resist_electricity=>100
          class=>MAGE
          level=>8
          insert_script=>~dw#ches3 below kingdead~
          enforce_charclass=>is_bg1
      END
      LAF clone_creature STR_VAR creature=EVALUATE_BUFFER ~%tutu_scriptbg%queen=>dw#queen~ edits=patch_data END
      LAF edit_creature STR_VAR creature=EVALUATE_BUFFER ~%tutu_scriptbg%queen~ editstring=~strip_script=>dw#swpup insert_script_high=>dw#swpup~ END

   /* this is irrelevant, as we're now blocking preps for the chess team
      // queen (no preps)
      
      LAF clone_creature STR_VAR creature=EVALUATE_BUFFER ~%tutu_scriptbg%queen=>dw#queen~
                                 editstring=~add_items=>dw#nopre~
      END
      
      MAKE_PATCH
         patch_cond_inline=>~type=>effect match=>resource check=>%tutu_var%queen resource=>dw#queen~
      END
      LAF edit_spell STR_VAR spell=spwi942 END

      // buffing window before chessboard
      
      LAF install STR_VAR file=chessdelay.d location=resource END

      // to complicate matters, all four areas with elemental guardians use the same area script
      
      LAF get_area_script STR_VAR area=EVALUATE_BUFFER ~%DurlagsTower_IceChamber%~ RET script_ice=script END
      LAF swap_text STR_VAR files=EVALUATE_BUFFER ~%script_ice%.bcs~
                            swaps=~Dead("jellspa")=>GlobalTimerExpired("DMWWChess","GLOBAL")Global("DMWWChessDelay","GLOBAL",2)
                                   ActionOverride(Player1,SetMasterArea("FW0500"))=>SetGlobal("DMWWChessDelay","GLOBAL",3)ActionOverride(Player1,SetMasterArea("FW0500"))~
      END
      LAF extend STR_VAR file=EVALUATE_BUFFER ~%script_ice%~ top=chessdelay location=resource END
      LAF get_area_script STR_VAR area=EVALUATE_BUFFER ~%DurlagsTower_D3%~ RET file=script END
      EXTEND_TOP ~%file%.bcs~ ~override/%script_ice%.bcs~
      
    */
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION durlag_mirror BEGIN

      // the knight
      
      MAKE_PATCH
         hitpoints=>250
         demon_knight=>null
         make_casting_innate=>null
         strip_scs_scripts=>null
         strip_script=>dw#swpdn
         insert_script_high=>dw#swpdn
      END
      LAF clone_creature STR_VAR creature=EVALUATE_BUFFER ~%tutu_var%deathk=>dw#death %tutu_scriptg%ooddeat=>dw#goodd~ edits=patch_data END
      LAF edit_creature STR_VAR creature=EVALUATE_BUFFER ~%tutu_var%deathk~ editstring=~insert_script_high=>dw#swpup~ END

      LAF ssl_to_bcs STR_VAR script=dw#death  location=~durlag/ssl~ END

      // Generate REAL copies of the party via a modified Simulacrum spell
      
      LAF get_area_script STR_VAR area=EVALUATE_BUFFER ~%DurlagsTower_DemonknightsChamber%~ RET script=script END
      LAF swap_text STR_VAR files=EVALUATE_BUFFER ~%script%.bcs~ 
                            swaps=EVALUATE_BUFFER
                                  ~CreateCreature("%tutu_scriptg%ooddeat",\[692.637\],0)=>null
                                   Exists(Player1)=>Exists(Player1)Global("easycopies","%DurlagsTower_DemonknightsChamber%",1)
                                   Exists(Player2)=>Exists(Player2)Global("easycopies","%DurlagsTower_DemonknightsChamber%",1)
                                   Exists(Player3)=>Exists(Player3)Global("easycopies","%DurlagsTower_DemonknightsChamber%",1)
                                   Exists(Player4)=>Exists(Player4)Global("easycopies","%DurlagsTower_DemonknightsChamber%",1)
                                   Exists(Player5)=>Exists(Player5)Global("easycopies","%DurlagsTower_DemonknightsChamber%",1)
                                   Exists(Player6)=>Exists(Player6)Global("easycopies","%DurlagsTower_DemonknightsChamber%",1)~
      END
      LAF extend STR_VAR file=EVALUATE_BUFFER ~%script%~ top=mirror location=resource END
      LAF player_clones END

END
