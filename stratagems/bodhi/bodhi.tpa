INCLUDE ~%scsroot%/lib/ai_wrap.tph~

DEFINE_ACTION_FUNCTION bodhi 
STR_VAR version=""
BEGIN

    // pro/undead scrolls

    LAF include STR_VAR file=undeadscroll.tpa locbase=tactical_bg2 END
    LAF undeadscroll END


        // creature file

	ACTION_IF !(FILE_EXISTS_IN_GAME ~ibodhi1.spl~) THEN BEGIN // officially we're incompatible with the Tactics version; unofficially, we try
		LAF bodhi_CRE INT_VAR version END
	END

        LAF edit_creature STR_VAR creature=c6bodhi editstring=~strip_script=>shoutinv~ END

        // spells

        LAF bodhi_spells INT_VAR version END

        // improved weapons
        
        ACTION_IF version=2 BEGIN
           COPY ~%scsroot%/%component_loc%/resource/bodhi_tactics.itm~ ~override/bodhi.itm~
           LAF edit_creature STR_VAR creature=~bodhi2 ppbodhi4~ editstring=~replace_items=>"bodhi(WEAPON1)"~ END
        END ELSE BEGIN
           COPY ~%scsroot%/%component_loc%/resource/bodhi_nerf.itm~ ~override/bodhi.itm~
           LAF edit_creature STR_VAR creature=~bodhi2 ppbodhi4~ editstring=~replace_items=>"vamp1(WEAPON1)"~ END
        END

        // challenge version

        MAKE_PATCH
           replace_items=>~bodhi(WEAPON1)~
           script_class=>dw#chbod
        END
        LAF edit_creature STR_VAR creature=chevil08 edits=patch_data END

        // scripts

        ACTION_IF version=2 BEGIN
                  OUTER_SPRINT variables ~IsHardCore=True~
        END ELSE BEGIN
                  OUTER_SPRINT variables ~~
        END
        LAF ssl_to_bcs STR_VAR script=~bodhi2 c6bodhi dw#chbod finbodhi~ variables location=ssl END

        // dialog

        ACTION_IF version=1 BEGIN
           LAF swap_text STR_VAR files=c6bodhi.dlg swaps=~HPLT(Myself,10)=>HPPercentLT(Myself,50)~ END
        END
        // ascension
        
        ACTION_IF FILE_EXISTS_IN_GAME finbodhi.cre BEGIN 
           LAF bodhi_ascension STR_VAR variables END
        END
        
        // mages
        
        LAF bodhi_mages END

END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION bodhi_CRE INT_VAR version=0 BEGIN


      MAKE_PATCH
         strip_script=>~wtasight vampir01~
         strip_scs_scripts=>null
         make_casting_innate=>null
         add_effect_inline=>~opcode=>193 parameter2=>1~  // detect invisibility by script
         add_effect_inline'=>~opcode=>31 parameter1=>50 parameter2=>1~ // 50% MDR
         add_effect_inline''=>~opcode=>297~ // immunity to turning
         immunity_to_opcode=>13 // protection from Azuredge et al
         immunity_to_string=>10554
         movement=>12
         ac_natural=>~-4~
         ac_effective=>~-4~
         hitpoints=>183
         resist_slashing=>25
         resist_crushing=>25
         resist_piercing=>25
         resist_missile=>25
      END
      LAF edit_creature STR_VAR creature=~c6bodhi bodhi2 chevil08~ edits=patch_data END
      LAF edit_creature STR_VAR creature=finbodh allow_missing=1 edits=patch_data END
      ACTION_IF version=2 BEGIN


           MAKE_PATCH
              thac0=>1
              attacks=>4
              add_effect_inline=>~opcode=>102 parameter1=>1~ // immune to L1,2 spells
              add_effect_inline'=>~opcode=>102 parameter1=>2~ // immune to L1,2 spells
           END
           LAF edit_creature STR_VAR creature=~c6bodhi bodhi2~ edits=patch_data END
           LAF edit_creature STR_VAR creature=finbodh edits=patch_data allow_missing=1 END
      END
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION bodhi_spells 
INT_VAR version=1
BEGIN

        MAKE_PATCH
           1=>2400
           2=>2502
           3=>2503
           4=>2504
           5=>2505
           6=>2506
           7d=>2507
           8=>2510
        END
        ACTION_PHP_EACH patch_data AS code=>string BEGIN
           LAF install STR_VAR file=~ibodhi%code%.spl~ location=resource END
           LAF edit_spell STR_VAR spell=~ibodhi%code%~ editstring=~say_name=>%string%~ END
        END
        
        ACTION_IF version=1 BEGIN
        
           // Cloud of Bats

           LAF edit_spell STR_VAR spell=ibodhi1 editstring=~patch_effect_inline=>"duration=>18"~ END

           // Abyssal Darkness, Buried Alive, Sepulchral sleep

           LAF edit_spell STR_VAR spell=~ibodhi2 ibodhi3 ibodhi6~ editstring=~patch_effect_inline=>"savebonus=>-4"~ END  // reduced from -10

        END

END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION bodhi_mages BEGIN

        // script

        LAF install STR_VAR file=dw#bodmg.baf location=resource END

        MAKE_PATCH
           level=>16
           dialog=>""
           dv=>""
           strip_script=>all
           script_override=>dw#bodmg
           script_class=>mage14a
           class=>mage
           allegiance=>enemy
           enforce_charclass=>null
        END
        LAF clone_creature STR_VAR creature=~c6bguard=>impbod1 bodtan=>impbod2~ edits=patch_data END
        LAF edit_creature STR_VAR creature=impbod1 editstring=~say_both_names=>2508~ END
        LAF edit_creature STR_VAR creature=impbod2 editstring=~say_both_names=>2509~ END

END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION bodhi_ascension
STR_VAR variables=""
BEGIN

               MAKE_PATCH
                  strip_script=>all
                  script_override=>finbodhi
                  dv=>finbodh
                  dialog=>finbodh
                  remove_items=>minhp1
                  xp_value=>26000
               END
               LAF clone_creature STR_VAR creature=~c6bodhi=>finbodh~ edits=patch_data END

               LAF ssl_to_bcs STR_VAR script=finbodhi variables location=~ssl~ END


END







