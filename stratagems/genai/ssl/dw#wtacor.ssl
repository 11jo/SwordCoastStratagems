//////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////
/////	Now the core combat block      ///////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////


///////////////////////////////////////////////////////////////////////////////////
///	Charmed creatures behave a bit oddly under the general script. If charmed,
///	just kill what's nearest.
///////////////////////////////////////////////////////////////////////////////////

IF
	StateCheck(Myself,STATE_CHARMED)
	See(NearestEnemyOf(Myself))
	!StateCheck(NearestEnemyOf(Myself),STATE_INVISIBLE)
	!Range(NearestEnemyOf(Myself),5)
THEN
	RESPONSE #100
		EquipRanged()
		Attack(NearestEnemyOf(Myself))
END

IF
	StateCheck(Myself,STATE_CHARMED)
	See(NearestEnemyOf(Myself))
	!StateCheck(NearestEnemyOf(Myself),STATE_INVISIBLE)
	Range(NearestEnemyOf(Myself),5)
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		Attack(NearestEnemyOf(Myself))
END

///////////////////////////////////////////////////////////////////////////
////	Am I a moron? If so, don't waste time on this "find a target" crap. 
////	Just kill the nearest opponent
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	RequireBlock(IsMoron)
	Target(NearestEnemyOf(Myself))
	TriggerBlock(Enemy)
	Range(scstarget,10)
THEN DO
	Action(MoronAttack)
END



////////////////////////////////////////////////////////////////////////////
////	If I'm not a moron and I'm attacking something I can't hit, STOP!
////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	IgnoreBlock(IsBG1)
	IgnoreBlock(IsMoron)
	Target(LastTargetedBy(Myself))
	CheckStat(Myself,0,WEAPON_ENCHANTMENT)
	Global("validtarget","LOCALS",1)
	OR(2)
           !CheckStat(scstarget,0,WIZARD_PROTECTION_FROM_NORMAL_WEAPONS)
           HasItem("immune1",scstarget)
THEN DO
	SetGlobal("validtarget","LOCALS",0)
	ClearActions(Myself)
END

IF TRIGGER
	IgnoreBlock(IsBG1)
	IgnoreBlock(IsMoron)
	Target(LastTargetedBy(Myself))
	CheckStatLT(Myself,2,WEAPON_ENCHANTMENT)
	Global("validtarget","LOCALS",1)
	HasItem("immune2",scstarget)
THEN DO
	SetGlobal("validtarget","LOCALS",0)
	ClearActions(Myself)
END

IF TRIGGER
	IgnoreBlock(IsBG1)
	IgnoreBlock(IsMoron)
	Target(LastTargetedBy(Myself))
	CheckStatLT(Myself,3,WEAPON_ENCHANTMENT)
	Global("validtarget","LOCALS",1)
	OR(2)
	   CheckStatGT(scstarget,0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
	   HasItem("immune3",scstarget)
THEN DO
	SetGlobal("validtarget","LOCALS",0)
	ClearActions(Myself)
END

IF TRIGGER
	IgnoreBlock(IsBG1)
	IgnoreBlock(IsMoron)
	Target(LastTargetedBy(Myself))
	CheckStat(Myself,3,WEAPON_ENCHANTMENT)
	Global("validtarget","LOCALS",1)
	OR(2)
           CheckStatGT(scstarget,1,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
           HasItem("immune4",scstarget)
THEN DO
	SetGlobal("validtarget","LOCALS",0)
	ClearActions(Myself)
END

IF TRIGGER
	IgnoreBlock(IsBG1)
	IgnoreBlock(IsMoron)
	Target(LastTargetedBy(Myself))
	CheckStat(Myself,4,WEAPON_ENCHANTMENT)
	Global("validtarget","LOCALS",1)
	CheckStatGT(scstarget,2,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
THEN DO
	SetGlobal("validtarget","LOCALS",0)
	ClearActions(Myself)
END

IF TRIGGER
	IgnoreBlock(IsBG1)
	IgnoreBlock(IsMoron)
	Target(LastTargetedBy(Myself))
	!CheckStat(Myself,0,WEAPON_ENCHANTMENT)
	Global("validtarget","LOCALS",1)
	CheckStat(scstarget,4,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
THEN DO
	SetGlobal("validtarget","LOCALS",0)
	ClearActions(Myself)
END


/////////////////////////////////////////////////////////////////////////////
////	Backstab if you're an invisible thief; 
////	don't attack stoneskinned or mirror imaged targets.
///////////////////////////////////////////////////////////////////////

IF TRIGGER
	RequireBlock(IsThief|IsAssassin|IsStalker)
	StateCheck(Myself,STATE_INVISIBLE)
	TargetBlock(PCsPreferringWeak)
	TargetBlock(CloseEnemies)
	TriggerBlock(Enemy|Disabled|BackstabSafeNew|PlusMyWeaponStrengthSafe|MyWeaponDamageTypeSafe)
	OR(2)
		Range(scstarget,4)
		Global("backstabplan","LOCALS",1)
THEN DO
	Action(Attack2)
END

/////////////////////////////////////////////////////////////////////////////
////	Kill spellcasters if you're a Wizard Slayer
///////////////////////////////////////////////////////////////////////

IF TRIGGER
	RequireBlock(IsWizardSlayer)
	TargetBlock(PCSpellcasters)
	TargetBlock(CloseEnemies)
	TriggerBlock(Enemy|Disabled|PlusMyWeaponStrengthSafe|MyWeaponDamageTypeSafe)
	Range(scstarget,10)
THEN DO
	Action(Attack)
END



///////////////////////////////////////////////////////////////////////
////	We start by looking at the very near opponents. Go for the 
////	most vulnerable of them. In the first instance, try for PCs with no stoneskins
////	and no mirror images
///////////////////////////////////////////////////////////////////////

IF TRIGGER
	IgnoreBlock(IsMoron)
	TargetBlock(PCsPreferringWeak)
	TriggerBlock(Enemy|Disabled|MirrorImage|PlusMyWeaponStrengthSafe|MyWeaponDamageTypeSafe)
	!CheckStatGT(scstarget,0,STONESKINS)
	!HPPercentGT(scstarget,20)
	Range(scstarget,4)
THEN DO
	Action(Attack)
END

IF TRIGGER
	IgnoreBlock(IsMoron)
	TargetBlock(PCsPreferringWeak)
	TriggerBlock(Enemy|Disabled|MirrorImage|PlusMyWeaponStrengthSafe|MyWeaponDamageTypeSafe)
	!CheckStatGT(scstarget,0,STONESKINS)
	!HPPercentGT(scstarget,50)
	Range(scstarget,4)
THEN DO
	Action(Attack)
END

IF TRIGGER
	IgnoreBlock(IsMoron)
	TargetBlock(PCsPreferringWeak)
	TriggerBlock(Enemy|Disabled|MirrorImage|PlusMyWeaponStrengthSafe|MyWeaponDamageTypeSafe)
	!CheckStatGT(scstarget,0,STONESKINS)
	Range(scstarget,4)
THEN DO
	Action(Attack)
END

///////////////////////////////////////////////////////////////////////
////	Don't attack the helpless (that is: the stunned etc); don't attack
////	people protected from your weapon.
////
////    If an invisible (or Assassinate-using) thief, still don't attack mirrored/stoneskinned foes
///////////////////////////////////////////////////////////////////////

IF TRIGGER
	IgnoreBlock(IsMoron)
	IgnoreBlock(IsThief)
	TargetBlock(PCsPreferringWeak)
	TargetBlock(CloseEnemies)
	TriggerBlock(Enemy|Disabled|PlusMyWeaponStrengthSafe|MyWeaponDamageTypeSafe)
	Range(scstarget,4)
THEN DO
	Action(Attack)
END

IF TRIGGER
	IgnoreBlock(IsMoron)
	RequireBlock(IsThief)
	!StateCheck(Myself,STATE_INVISIBLE)
	!CheckStat(Myself,4,SCRIPTINGSTATE4)
	TargetBlock(PCsPreferringWeak)
	TargetBlock(CloseEnemies)
	TriggerBlock(Enemy|Disabled|PlusMyWeaponStrengthSafe|MyWeaponDamageTypeSafe)
	Range(scstarget,4)
THEN DO
	Action(Attack)
END

IF TRIGGER
	IgnoreBlock(IsMoron)
	RequireBlock(IsThief)
	OR(2)
            StateCheck(Myself,STATE_INVISIBLE)
	    CheckStat(Myself,4,SCRIPTINGSTATE4)
	TargetBlock(PCsPreferringWeak)
	TargetBlock(CloseEnemies)
	TriggerBlock(Enemy|Disabled|PlusMyWeaponStrengthSafe|MyWeaponDamageTypeSafe|MirrorImage)
	!CheckStatGT(scstarget,0,STONESKINS)
	Range(scstarget,4)
THEN DO
	Action(Attack)
END

///////////////////////////////////////////////////////////////////////
////	... but if there's a nearby target who *is* helpless (and no-one else), scac'em.
///////////////////////////////////////////////////////////////////////

IF TRIGGER
	IgnoreBlock(IsMoron)
	TargetBlock(CloseEnemies)
	TriggerBlock(Enemy|PlusMyWeaponStrengthSafe|MyWeaponDamageTypeSafe)
	Range(scstarget,4)
THEN DO
	Action(Attack)
END

