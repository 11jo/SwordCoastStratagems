////////////////////////////////////////////////////////////////////////
/////	This block to go in the Initial segment of any
/////	creature that dislikes cloud spells. scsmelee should be
///// 	set to Range([PC],5) if you want to fight in a cloud, to " " 
/////   if you don't (e.g., mages, archers)
/////
////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////
///	Storm of Vengeance
////////////////////////////////////////////////////////////////////////

IF
	RequireBlock(FearsStorm)
	IgnoreBlock(IsBG1)
	IgnoreBlock(IgnoreCloud)
	Heard([ANYONE],407)
	Range(LastHeardBy(Myself),20)
	// can I be affected?

	OR(3)
		!CheckStatGT(Myself,99,RESISTACID)
		!CheckStatGT(Myself,99,RESISTFIRE)
		!CheckStatGT(Myself,99,RESISTELECTRICITY)
	// am I either not sufficiently magic-resistant, or in a "safe" situation?
	OR(2)
		!CheckStatGT(Myself,80,RESISTMAGIC)
		!See(NearestEnemyOf(Myself))
	scscloudmelee
THEN
	RESPONSE #100
		SetGlobalTimer("seencloud","LOCALS",20)
		scscloudresponse
END

///////////////////////////////////////////////////////////////////////
///	Incendiary Cloud / Firestorm / Meteor Swarm
///////////////////////////////////////////////////////////////////////

IF
	IgnoreBlock(IsBG1)
	IgnoreBlock(IgnoreCloud)
	OR(2)
		Heard([ANYONE],405)
		Heard([ANYONE],409)
	Range(LastHeardBy(Myself),20)
	// can I be affected?
	!CheckStatGT(Myself,99,RESISTFIRE)
	// am I either not sufficiently magic-resistant, or in a "safe" situation?
	OR(2)
		!CheckStatGT(Myself,80,RESISTMAGIC)
		!See(NearestEnemyOf(Myself))
	scscloudmelee
THEN
	RESPONSE #100
		SetGlobalTimer("seencloud","LOCALS",20)
		scscloudresponse
END

////////////////////////////////////////////////////////////////////////
///	Death Fog
////////////////////////////////////////////////////////////////////////

IF
	IgnoreBlock(IsBG1)
	IgnoreBlock(IgnoreCloud)
	Heard([ANYONE],408)
	Range(LastHeardBy(Myself),20)
	// can I be affected?
	!CheckStatGT(Myself,99,RESISTACID)
	// am I either not sufficiently magic-resistant, or in a "safe" situation?
	OR(2)
		!CheckStatGT(Myself,80,RESISTMAGIC)
		!See(NearestEnemyOf(Myself))
	scscloudmelee
THEN
	RESPONSE #100
		SetGlobalTimer("seencloud","LOCALS",20)
		scscloudresponse
END



/////////////////////////////////////////////////////////////////
///     Cloud Kill / Stinking Cloud
/////////////////////////////////////////////////////////////////

IF
	IgnoreBlock(IgnoreCloud)
	OR(2)
           Heard([ANYONE],411)
	   Heard([ANYONE],404)
	Range(LastHeardBy(Myself),20)
	!General(Myself,UNDEAD)
	!Race(Myself,RAKSHASA)
	!Race(Myself,DEMONIC)
	OR(2)
		!CheckStatGT(Myself,80,RESISTMAGIC)
		!See(NearestEnemyOf(Myself))
	scscloudmelee
THEN
	RESPONSE #100
		SetGlobalTimer("seencloud","LOCALS",20)
		scscloudresponse
END

/////////////////////////////////////////////////////////////////
///     Web
/////////////////////////////////////////////////////////////////

IF TRIGGER
        Target(Myself)
        TriggerBlock(Hold)
        IgnoreBlock(IgnoreCloud)
        Heard([ANYONE],410)
	Range(LastHeardBy(Myself),20)
	!HasItem("%tutu_var%antiweb",Myself)
	OR(2)
		!CheckStatGT(Myself,80,RESISTMAGIC)
		!See(NearestEnemyOf(Myself))
	scscloudmelee
THEN DO
		SetGlobalTimer("seencloud","LOCALS",20)
		scscloudresponse
END



