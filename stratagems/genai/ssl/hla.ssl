
/////////////////////////////////////////////////////
////	If you've got a defence potion, take it right
////	away. For crude convenience, we just use a 
////	global to record if we've done so.
////
////	DON'T prioritise this over going invisible/buffing if you're
////	already at least 14th level. (We repeat the block later)
/////////////////////////////////////////////////////

IF TRIGGER
	See(NearestEnemyOf(Myself))
	!StateCheck(Myself,STATE_INVISIBLE)
	!Global("takendefence","LOCALS",1)
	!CheckStatGT(Myself,13,Level)
THEN DO
	Action(Potion,"dw#ptn34",@1034)
	Action(Potion,"dw#ptn35",@1035)
	Action(Potion,"dw#ptn21",@1021)
	Action(Potion,"dw#ptn45",@1045)
	Action(Potion,"%tutu_var%potn34",@1034)
	Action(Potion,"%tutu_var%potn35",@1035)
	Action(Potion,"%tutu_var%potn21",@1021)
	Action(Potion,"%tutu_var%potn45",@1045)
	SetGlobal("takendefence","LOCALS",1)
END

IF TRIGGER
        !GlobalTimerNotExpired("summondeva","LOCALS")
        RequireBlock(IsPaladin)
	RequireBlock(IsHighLevel)
	TargetBlock(PCsPreferringWeak)
	Allegiance(Myself,ENEMY)
THEN DO
        SetGlobalTimer("summondeva","LOCALS",1000)
        Action(SpellAreaRESReplace,"dw#devag",PALADIN_SUMMON_DEVA|300|100)
END

IF TRIGGER
        !GlobalTimerNotExpired("summondeva","LOCALS")
        RequireBlock(IsBlackguard)
	RequireBlock(IsHighLevel)
	TargetBlock(PCsPreferringWeak)
	Allegiance(Myself,ENEMY)
THEN DO
        SetGlobalTimer("summondeva","LOCALS",1000)
        Action(SpellAreaRESReplace,"dw#devae",BLACKGUARD_SUMMON_FALLEN_DEVA|300|100)
END


IF TRIGGER
        RequireBlock(IsMonk)
        IgnoreBlock(IsDarkMoon)
        Target(Myself)
        TargetBlock(HumanoidVillains)
        Allegiance(scstarget,ENEMY)
        HPPercentLT(scstarget,50)
THEN DO
       Action(Spell,MONK_LAY_ON_HANDS|100|50)
END

IF TRIGGER
        RequireBlock(IsPaladin)
        Target(Myself)
        TargetBlock(HumanoidVillains)
        Allegiance(scstarget,ENEMY)
        HPPercentLT(scstarget,50)
THEN DO
       Action(Spell,PALADIN_LAY_ON_HANDS|100|50)
END

IF TRIGGER
	RequireBlock(IsHighLevel)
	RequireBlock(Refinements)
	RequireBlock(IsBarbarian)
	See(NearestEnemyOf(Myself))
	Range(NearestEnemyOf(Myself),15)
THEN DO
	Action(SpellRESMyself,"%BARBARIAN_GREATER_WAR_CRY%"|100|100)
END

IF TRIGGER
	RequireBlock(IsHighLevel)
	See(NearestEnemyOf(Myself))
	Range(NearestEnemyOf(Myself),15)
THEN DO
	Action(SpellMyself,WARRIOR_WAR_CRY|100|100)
END



IF TRIGGER
	RequireBlock(IsHighLevel)
	See(NearestEnemyOf(Myself))
	Range(NearestEnemyOf(Myself),5)
	OR(2)
		!CheckStatGT(Myself,39,RESISTCRUSHING)
		!CheckStatGT(Myself,39,RESISTSLASHING)
THEN DO
	Action(SpellMyself,WARRIOR_HARDINESS)
END

IF TRIGGER
        RequireBlock(IsBarbarian)
        RequireBlock(IsHighLevel)
        RequireBlock(Refinements)
	See(NearestEnemyOf(Myself))
	!GlobalTimerNotExpired("raging","LOCALS")
THEN DO	
        SetGlobalTimer("raging","LOCALS",60)
	Action(SpellRESMyself,"%BARBARIAN_TIRELESS_RAGE%")
END

IF TRIGGER
        RequireBlock(IsKensai)
        RequireBlock(IsHighLevel)
        RequireBlock(Refinements)
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(Enemy|Disabled|PlusMyWeaponStrengthSafe|MyWeaponDamageTypeSafe)
	Range(scstarget,5)
	See(scstarget)
	!CheckStat(Myself,2,SCRIPTINGSTATE4)
THEN DO
	Action(SpellRESMyself,"%KENSAI_TRUE_KAI%")
END


IF TRIGGER
	RequireBlock(IsHighLevel)
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(Enemy|Disabled|PlusMyWeaponStrengthSafe|MyWeaponDamageTypeSafe)
	CheckStatGT(scstarget,8,LEVEL)
	Range(scstarget,5)
	See(scstarget)
	Range(NearestEnemyOf(Myself),5)
	!CheckStat(Myself,5,SCRIPTINGSTATE4)
THEN DO
	Action(SpellMyself,WARRIOR_GREATER_WHIRLWIND)
END

IF TRIGGER
	AreaType(OUTDOOR)
	RequireBlock(IsHighLevel)
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(Enemy|Disabled|PlusMyWeaponStrengthSafe|MyWeaponDamageTypeSafe)
	CheckStatGT(scstarget,8,LEVEL)
	Range(scstarget,5)
	See(scstarget)
	!CheckStat(Myself,5,SCRIPTINGSTATE4)
THEN DO
	Action(SpellMyself,WARRIOR_SMITE)
END

IF TRIGGER
	RequireBlock(IsHighLevel)
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(Enemy|Disabled|PlusMyWeaponStrengthSafe|MyWeaponDamageTypeSafe)
	CheckStatGT(scstarget,8,LEVEL)
	Range(scstarget,5)
	See(scstarget)
	!CheckStat(Myself,3,SCRIPTINGSTATE4)
	!CheckStat(Myself,5,SCRIPTINGSTATE4)
THEN DO
	Action(SpellMyself,WARRIOR_CRITICAL_STRIKE)
END

IF TRIGGER
	RequireBlock(IsRanged)
	RequireBlock(IsArcher)
	RequireBlock(Refinements)
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(Enemy|Disabled|PlusMyWeaponStrengthSafe|MyWeaponDamageTypeSafe)
	CheckStatGT(scstarget,8,LEVEL)
	See(scstarget)
	!CheckStat(Myself,3,SCRIPTINGSTATE4)
	!CheckStat(Myself,5,SCRIPTINGSTATE4)
THEN DO
	Action(SpellRESMyself,"%ARCHER_PRECISION%")
END

IF TRIGGER
	RequireBlock(IsRanged)
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(Enemy|Disabled|PlusMyWeaponStrengthSafe|MyWeaponDamageTypeSafe)
	CheckStatGT(scstarget,8,LEVEL)
	See(scstarget)
	!CheckStat(Myself,3,SCRIPTINGSTATE4)
	!CheckStat(Myself,5,SCRIPTINGSTATE4)
THEN DO
	Action(SpellMyself,WARRIOR_CRITICAL_STRIKE)
END

IF TRIGGER
	RequireBlock(IsHighLevel)
	RequireBlock(IsThief|IsAssassin)
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(Enemy|Disabled|MirrorImage|PlusMyWeaponStrengthSafe|MyWeaponDamageTypeSafe)
	CheckStatGT(scstarget,8,LEVEL)
	Range(scstarget,5)
	See(scstarget)
	Range(NearestEnemyOf(Myself),5)
	!CheckStat(Myself,4,SCRIPTINGSTATE4)
THEN DO
	Action(SpellMyself,ROGUE_ASSASINATION)
END

IF TRIGGER
	RequireBlock(IsHighLevel)
	RequireBlock(Refinements)
	RequireBlock(IsAssassin)
	See(NearestEnemyOf(Myself))
	Range(NearestEnemyOf(Myself),5)
	!CheckStat(Myself,4,SCRIPTINGSTATE4)
THEN DO
	Action(SpellRESMyself,"%ASSASSIN_NERVE_POISON%")
END

IF TRIGGER
	RequireBlock(IsHighLevel)
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(Enemy|Disabled|PlusMyWeaponStrengthSafe|MyWeaponDamageTypeSafe)
	CheckStatGT(scstarget,8,LEVEL)
	Range(scstarget,5)
	See(scstarget)
	!CheckStatGT(Myself,3,SCRIPTINGSTATE4)
THEN DO
	Action(SpellMyself,WARRIOR_WHIRLWIND)
END

IF TRIGGER
	RequireBlock(IsHighLevel)
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(Enemy|Disabled|PlusMyWeaponStrengthSafe|MyWeaponDamageTypeSafe)
	CheckStatGT(scstarget,8,LEVEL)
	Range(scstarget,5)
	See(scstarget)
	!CheckStat(Myself,2,SCRIPTINGSTATE4)
THEN DO
	Action(SpellMyself,WARRIOR_POWER_ATTACK)
END

IF TRIGGER
        RequireBlock(IsBerserker)
	See(NearestEnemyOf(Myself))
	!GlobalTimerNotExpired("raging","LOCALS")
THEN DO
        SetGlobalTimer("raging","LOCALS",60)
	Action(SpellMyself,BERSERKER_RAGE)
END

IF TRIGGER
        RequireBlock(IsBarbarian)
	See(NearestEnemyOf(Myself))
	!GlobalTimerNotExpired("raging","LOCALS")
THEN DO	
        SetGlobalTimer("raging","LOCALS",30)
	Action(SpellMyself,BARBARIAN_RAGE)
END

IF TRIGGER
        RequireBlock(IsKensai)
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(Enemy|Disabled|PlusMyWeaponStrengthSafe|MyWeaponDamageTypeSafe)
	Range(scstarget,5)
	See(scstarget)
	!CheckStat(Myself,2,SCRIPTINGSTATE4)
THEN DO
	Action(SpellMyself,KENSAI_KIA)
END

IF TRIGGER
        HPPercentLT(Myself,75)
        RequireBlock(IsDarkMoon)
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(Enemy|Disabled|MR|MinorGlobe)
	Range(scstarget,5)
THEN DO
	Action(SpellRES,"%DARK_MOON_VAMPIRIC_TOUCH%")
END

IF TRIGGER
        HPPercentLT(Myself,75)
        RequireBlock(IsBlackguard)
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(Enemy|Disabled|MR|MinorGlobe)
	Range(scstarget,5)
THEN DO
	Action(Spell,BLACKGUARD_ABSORB_HEALTH)
END

IF TRIGGER
        RequireBlock(IsPaladin)
        TargetBlock(EnemiesInOrderShort)
        TriggerBlock(Enemy|Disabled)
THEN DO
        Combine()
        Action(SpellMyself,PALADIN_PROTECTION_FROM_EVIL|200|100)
END

IF TRIGGER
        RequireBlock(IsDarkMoon)
        TargetBlock(EnemiesInOrderShort)
        TriggerBlock(Enemy|Disabled)
THEN DO
        Combine()
        Action(SpellRES,"%DARK_MOON_MIRROR_IMAGE%"|100|100)
END

IF TRIGGER
        RequireBlock(IsDarkMoon)
        TargetBlock(EnemiesInOrderShort)
        TriggerBlock(Enemy|Disabled)
        Range(scstarget,5)
THEN DO
        Combine()
        Action(SpellRES,"%DARK_MOON_BLUR%"|100|100)
END

IF TRIGGER
        RequireBlock(IsDarkMoon)
	TargetBlock(PCFighters)
	TriggerBlock(Enemy|Helpless|MR|MinorGlobe)
	CheckStatGT(scstarget,0,SAVEVSSPELL)

THEN DO
	Action(SpellRES,"%DARK_MOON_BLINDNESS%"|150|50)
END


IF TRIGGER
        RequireBlock(IsDarkMoon)
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(Enemy|Disabled|PlusMyWeaponStrengthSafe|MyWeaponDamageTypeSafe)
	Range(scstarget,5)
	See(scstarget)
	!GlobalTimerNotExpired("DarkMoonTouch","LOCALS")
THEN DO
        SetGlobalTimer("DarkMoonTouch","LOCALS",12)
	Action(SpellRESMyself,"%DARK_MOON_CHILL_TOUCH%")
END

IF TRIGGER
        RequireBlock(IsSunSoul)
        !CheckStat(Myself,0,WIZARD_FIRE_SHIELD)
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(Enemy|Disabled)
	Range(scstarget,5)
THEN DO
        Action(SpellMyself,SUN_SOUL_GREATER_SUN)
END

IF TRIGGER
        RequireBlock(IsSunSoul)
        !GlobalTimerNotExpired("flamingfists","LOCALS")
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(Enemy|Disabled|PlusMyWeaponStrengthSafe|MyWeaponDamageTypeSafe)
	Range(scstarget,5)
	See(scstarget)
THEN DO
       Action(SpellMyself,SUN_SOUL_FLAMING_FISTS)
END

IF TRIGGER
        RequireBlock(IsSunSoul)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(Enemy|MR|ResistFire)
	Range(scstarget,10)
        TriggerBlock(DirectDamageSafe)
	!StateCheck(scstarget,STATE_MIRRORIMAGE)
THEN DO
	Combine()
	Action(SpellMyself,SUN_SOUL_SUN_SOULBEAM|150|50)
END

IF TRIGGER
        RequireBlock(IsBlackguard)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(Enemy|MR)
	Range(scstarget,10)
THEN DO
	Combine()
	Action(SpellMyself,BLACKGUARD_AURA_OF_DESPAIR|100|50)
END

IF TRIGGER
        RequireBlock(IsSunSoul)
	ConditionalTargetBlock(PCsPreferringWeak;HPPercentLT(scstarget,40))
        Target([0.UNDEAD])
	TargetBlock(PCsPreferringWeak)
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(DirectDamageSafe)
	TriggerBlock(MR|ResistFire|Enemy|SpellTurn|MinorGlobe|MirrorImage|Helpless|L1safe)
	Range(scstarget,30)
	See(scstarget)
THEN DO
       Action(Spell,SUN_SOUL_SUN_SOULRAY)
END

IF TRIGGER
        RequireBlock(IsMonk)
        IgnoreBlock(IsSunSoul)
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(Enemy|Disabled|PlusMyWeaponStrengthSafe|MyWeaponDamageTypeSafe)
	Range(scstarget,5)
	See(scstarget)
	!CheckStatGT(Myself,0,SCRIPTINGSTATE4)
THEN DO
	DisplayStringHead(Myself,72936) // Hyo-o-cha!
	Action(SpellMyself,MONK_QUIVERING_PALM)
END

IF TRIGGER
        RequireBlock(IsMonk)
        IgnoreBlock(IsSunSoul|IsDarkMoon)
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(Enemy|Disabled|PlusMyWeaponStrengthSafe|MyWeaponDamageTypeSafe)
	Range(scstarget,5)
	See(scstarget)
	!CheckStatGT(Myself,0,SCRIPTINGSTATE4)
THEN DO
	DisplayStringHead(Myself,72937) // Kyai-ya!
	Action(SpellMyself,MONK_STUNNING_BLOW)
END

IF TRIGGER
	RequireBlock(IsAssassin|IsBlackguard)
	See(NearestEnemyOf(Myself))
	Range(NearestEnemyOf(Myself),5)
	!CheckStat(Myself,4,SCRIPTINGSTATE4)
THEN DO
	Action(SpellMyself,ASSASSIN_POISON)
END

IF TRIGGER
	RequireBlock(IsArcher)
	See(NearestEnemyOf(Myself))
	!GlobalTimerNotExpired("calledshot","LOCALS")
THEN DO
	SetGlobalTimer("calledshot","LOCALS",12)
	Action(SpellMyself,ARCHER_CALL_SHOT)
END


