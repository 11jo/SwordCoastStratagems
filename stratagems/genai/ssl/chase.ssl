////////////////////////////////////////////////////////////////////////////////////////////////////
//	No sign of hostiles? Look behind doors
/////////////////////////////////////////////////////////////////////////////////////////////////////

IF
    Global("inafight","LOCALS",1)
    IgnoreBlock(IsMoron)
    Global("RR#NOPND","GLOBAL",0) // opening doors is allowed
    !AreaType(OUTDOOR)
    !Range(NearestEnemyOf(Myself),30)
    Range(NearestDoor,15) // if the nearest door is in visual range. note: this seems to auto-skip locked doors
    OpenState(NearestDoor,FALSE)
THEN
    RESPONSE #100
        MoveToObject(NearestDoor)
        OpenDoor(NearestDoor)
END

////////////////////////////////////////////////////////////////////////////////////////////////////
//	No sign of hostiles? If the fight is going on anyway, then it's fair enough to have a look
// 	for them.
/////////////////////////////////////////////////////////////////////////////////////////////////////

IF
  !Global("chasing","LOCALS",0)
  See(NearestEnemyOf(Myself))
THEN
    RESPONSE #100
    	     SetGlobal("chasing","LOCALS",0)
    	     Continue()
END


// if subsequently I can't see an enemy, chase previously targetted PC

IF
   ActionListEmpty()
   Global("inafight","LOCALS",1)
   Allegiance(Myself,ENEMY)
   !See(NearestEnemyOf(Myself))
   Global("chasing","LOCALS",0)
   !Global("chasePC","LOCALS",0)
THEN
    RESPONSE #100
             SetGlobalTimer("chasetimer","LOCALS",8)
             SetGlobal("chasing","LOCALS",1)
END

IF
	!See(NearestEnemyOf(Myself))
	GlobalTimerExpired("chasetimer","LOCALS")
	!Global("inafight","LOCALS",0)
	!Global("chasePC","LOCALS",0)
	THEN
		RESPONSE #100
			SetGlobal("inafight","LOCALS",0)
			SetGlobal("chasePC","LOCALS",0)
END



BEGIN LOOP(scsloopvar||1;2;3;4;5;6)

        IF TRIGGER
                ActionListEmpty()
		GlobalTimerNotExpired("chasetimer","LOCALS")
		Global("chasePC","LOCALS",scsloopvar)
        THEN DO
                Action(FindIfOffscreen,Playerscsloopvar)
        END

END LOOP
