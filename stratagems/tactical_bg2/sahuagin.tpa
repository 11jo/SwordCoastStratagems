INCLUDE ~%scsroot%/lib_stratagems/ai_wrap.tph~
DEFINE_ACTION_FUNCTION sahuagin BEGIN

   // scripts

   LAF ssl_to_bcs STR_VAR script=~sahamb01 sahamb02 sahamb04 sahamb05~ location=~tactical_bg2/ssl~ END

   // weapon
   
   LAF install STR_VAR file=dw#saspe.itm location=resource END

   // archers get crossbows of speed, some bolts

   MAKE_PATCH
      literal=>~ADD_CRE_ITEM "XBOW06" #0 #0 #0 "UNDROPPABLE" "WEAPON3 WEAPON4"~
      replace_items=>~sahbolt(3,QUIVER1) bolt04(10,QUIVER2) bolt01(20,QUIVER3)~
   END
   LAF edit_creature STR_VAR creature=~sahamb01 sahamb04 sahramb1 sahramb2 sahramb4 sahramb5
                                       sahrar01 sahrgr01 sahrgr02 sahrgr03 sahrnt01 sahsss01
                                       sahsss02 sahsss03 sahuag01 obssah04 obssah05 sahbar01
                                       sahbar02 sahbar03 sahchf01 sahchf02 sahcpt01 sahcpt02
                                       sahextra sahgrd01 sahgrd03 sahryl01 elesah01 elesah02 
                                       obssah01 obssah03~
   END
      
   // generic sahuagin
   
   MAKE_PATCH
      levelGT=>10
      strGT=>18
      str_exGT=>76
      dexGT=>17
      add_items=>dw#saspe
      remove_items=>~sahstaff sahspear~
      enforce_saves=>at_worst
      enforce_thac0=>at_worst
      strip_scs_scripts=>null
      insert_script_low=>wtasight
      genai=>all
      strip_script=>gpskel1
   END
   LAF edit_creature STR_VAR creature=~chevil04 sahamb02 sahamb05 sahramb2 sahamb01 sahamb04 
                                       sahramb1 sahramb2 sahramb4 sahramb5
                                       sahrar01 sahrgr01 sahrgr02 sahrgr03 sahrnt01 sahsss01
                                       sahsss02 sahsss03 sahuag01 obssah04 obssah05~
   END

   // Royal/Baronial guards and Barons/Captains

   LAF edit_creature STR_VAR creature=~sahamb01 sahamb02 sahamb04 sahamb05~ editstring=~strip_scs_scripts=>null~ END

   MAKE_PATCH
      levelGT=>12
      strGT=>20
      dexGT=>18
      add_items=>dw#saspe
      remove_items=>~sahstaff sahspear~
      enforce_saves=>at_worst
      enforce_thac0=>at_worst
      strip_scs_scripts=>null
      insert_script_low=>wtasight
      class=>fighter
      genai=>all
   END
   LAF edit_creature STR_VAR creature=~sahbar01 sahbar02 sahbar03 sahchf01 sahchf02 sahcpt01
                                       sahcpt02 sahcapt0 sahcapt1 sahextra sahgrd01 sahgrd02
                                       sahgrd03 sahryl01 elesah01 elesah02 obssah01 obssah03~
   END

   // Princes

   MAKE_PATCH
      levelGT=>16
      strGT=>22
      dexGT=>19
      add_items=>dw#saspe
      remove_items=>~sahstaff sahspear~
      enforce_saves=>at_worst
      enforce_thac0=>at_worst
      strip_scs_scripts=>null
      insert_script_low=>wtasight
      class=>fighter
      genai=>all
      attacksGT=>3
   END
   LAF edit_creature STR_VAR creature=~sahpr2 sahpri01 sahreb02 udsaha~ END

   // Kings

   MAKE_PATCH
      levelGT=>20
      strGT=>24
      dexGT=>19
      add_items=>dw#saspe
      remove_items=>~sahstaff sahspear~
      enforce_saves=>at_worst
      enforce_thac0=>at_worst
      strip_scs_scripts=>null
      insert_script_low=>wtasight
      class=>fighter
      genai=>all
      attacksGT=>4
   END
   LAF edit_creature STR_VAR creature=~sahkng01 sahkng02~ END

   // SAHAMB01-06 don't need SCS melee/range scripts

   LAF edit_creature STR_VAR creature=~sahamb01 sahamb02 sahamb03 sahamb04 sahamb05 sahamb06~ editstring=~strip_scs_scripts=>null~ END

   // Sahuagin priests
   // following PnP and the vanilla game, they get stoneskins
   
   <<<<<<<< .../stratagems-inline/dw#sahsts.baf
      IF 
           !GlobalTimerNotExpired("stoneskin","LOCALS")
           Detect(NearestEnemyOf(Myself))
      THEN
       RESPONSE #100
          ReallyForceSpellRES("%WIZARD_STONE_SKIN_PREBUFF%",Myself)
          SetGlobalTimer("stoneskin","LOCALS",1000)
          Continue()
      END
   >>>>>>>>
   
   LAF install STR_VAR file=dw#sahsts.baf inline=yes END
   LAF edit_creature STR_VAR creature=~sahpr1 sahpr3 sahpr5 sahamb03 sahamb06 sahramb3 sahpri02 obssah02~ editstring=~insert_script_high=>dw#sahsts~ END

END



