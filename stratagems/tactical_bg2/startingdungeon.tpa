INCLUDE ~%MOD_FOLDER%/lib/ai_wrap.tph~
DEFINE_ACTION_FUNCTION startingdungeon BEGIN

// build duergar patrol

    MAKE_PATCH
       allegiance=>ENEMY
       replace_items=>chan01
       remove_items=>~slng01 bull01~
       strip_script=>~dccdead duearc01~
       strip_scs_scripts=>null
       insert_script_low=>wtasight
       genai=>all
    END
    LAF clone_creature STR_VAR creature=~duearc01=>dw#duarc duegau02=>dw#duwar~ edits=patch_data END

// replace random goblin spawns with random duergar spawns

    APPEND_COL ~spawngrp.2da~ ~$ $ RDDuer 60 dw#duarc dw#duarc dw#duwar dw#duwar * * * *~

// sort out area files

   // I'm leaving this hardcoded as it's fairly esoteric

   COPY_EXISTING
          ~ar0602.are~ ~override~
          ~ar0603.are~ ~override~
      REPLACE_TEXTUALLY CASE_INSENSITIVE ~icgob01.~ ~dw#duarc~
      REPLACE_TEXTUALLY CASE_INSENSITIVE ~icgob02.~ ~dw#duarc~
      REPLACE_TEXTUALLY CASE_INSENSITIVE ~icgob03.~ ~dw#duwar~
      REPLACE_TEXTUALLY CASE_INSENSITIVE ~rdgob.~ ~rdduer~
      READ_LONG 0xc0 ~rest_off~
      SAY 0x20+~rest_off~ @5200
      WRITE_ASCII 0x48+~rest_off~ ~DW#DUWAR~
      WRITE_SHORT 0x98+~rest_off~ 1
      WRITE_SHORT 0x9a+~rest_off~ 2
      WRITE_SHORT 0xa4+~rest_off~ 4
      WRITE_SHORT 0xa8+~rest_off~ 0


   COPY_EXISTING
          ~ar0601.are~ ~override~
      READ_LONG 0xc0 ~rest_off~
      SAY 0x20+~rest_off~ @5201
      WRITE_ASCII 0x48+~rest_off~ ~MEPMIS01~
      WRITE_SHORT 0x98+~rest_off~ 1
      WRITE_SHORT 0x9a+~rest_off~ 2
      WRITE_SHORT 0xa4+~rest_off~ 4
      WRITE_SHORT 0xa8+~rest_off~ 0

// ensure chances of encounter are 100% after first rest

   LAF swap_text STR_VAR files=imoen.bcs swaps=~SetGlobal("Rested","LOCALS",1)=>SetGlobal("Rested","LOCALS",1)SetGlobal("DMWWIrenRest","GLOBAL",1)~ END

<<<<<<<< .../stratagems-inline/iren_norest.baf
IF
  Global("DMWWIrenRest","GLOBAL",0)
  Global("DMWWRestSet","MYAREA",0)
THEN
    RESPONSE #100
             SetGlobal("DMWWRestSet","MYAREA",1)
    	     SetRestEncounterProbabilityDay(0)
    	     SetRestEncounterProbabilityNight(0)
    	     Continue()
END


IF
  Global("DMWWIrenRest","GLOBAL",1)
  !Global("DMWWRestSet","MYAREA",2)
THEN
    RESPONSE #100
    	     SetGlobal("DMWWRestSet","MYAREA",2)
    	     SetRestEncounterProbabilityDay(100)
    	     SetRestEncounterProbabilityNight(100)
    	     Continue()
END
>>>>>>>>

   LAF extend_area_script STR_VAR area=~ar0601 ar0602 ar0603~ bottom=iren_norest inline=yes END

// put a shortbow in the first level

   COPY_EXISTING ~ar0602.are~ ~override~
      REPLACE_TEXTUALLY CASE_INSENSITIVE ~scrl3g~ ~bow05~  (8)




END

