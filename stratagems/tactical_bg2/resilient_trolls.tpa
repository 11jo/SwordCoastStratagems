DEFINE_ACTION_FUNCTION resilient_trolls BEGIN

     COPY ~.../stratagems-inline/trolldata.2da~ ~%workspace%~
         READ_2DA_ENTRIES_NOW trolldata_read 8
         FOR (i=1;i<trolldata_read;i+=1) BEGIN
             FOR (j=0;j<8;j+=1) BEGIN
                READ_2DA_ENTRY_FORMER trolldata_read 0 j var
                READ_2DA_ENTRY_FORMER trolldata_read i j val
                SPRINT EVALUATE_BUFFER ~%var%~ ~%val%~
             END
             INNER_ACTION BEGIN
                 LAF troll_hp_spell INT_VAR hp_max hp_now STR_VAR hp_set_spell END
                 LAF dead_troll_hp INT_VAR hp_max hp_now STR_VAR dead_cre END
                 LAF troll_script STR_VAR hp_set_spell scriptname END
                 LAF injured_troll STR_VAR old_cre new_cre END
             END
         END

     BUT_ONLY



END

/////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION troll_hp_spell
   INT_VAR hp_max=0
           hp_now=0
   STR_VAR hp_set_spell=""
BEGIN

       MAKE_PATCH
          patch_cond_inline=>~type=>effect match=>opcode check=>18 parameter1=>%hp_max%~
          patch_cond_inline'=>~type=>effect match=>opcode check=>17 parameter1=>%hp_now%~
       END
       LAF clone_spell STR_VAR spell=EVALUATE_BUFFER ~%TROLL_SETHP1%=>%hp_set_spell%~ edits=patch_data END

END

/////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION dead_troll_hp
     INT_VAR hp_max=0
             hp_now=0
     STR_VAR dead_cre=""
BEGIN
   LAF edit_creature STR_VAR creature=EVALUATE_BUFFER ~%dead_cre%~ editstring=EVALUATE_BUFFER ~hp_current=>%hp_now% hp_max=>%hp_max%~ END
END

/////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION troll_script
   STR_VAR hp_set_spell=""
           scriptname=""

BEGIN
     LAF swap_text STR_VAR files=EVALUATE_BUFFER ~%scriptname%.bcs~
                           swaps=EVALUATE_BUFFER ~ApplySpell(Myself,TROLL_SETHP1)=>ApplySpellRES("%hp_set_spell%",Myself) PlayDead(300)=>PlayDead(240)~
     END
END

/////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION injured_troll
   STR_VAR old_cre=""
           new_cre=""
BEGIN
     COPY_EXISTING ~%old_cre%.cre~ ~override/%new_cre%.cre~
		READ_SHORT 0x26 ~hp~
		SET ~hp~=~%hp%~*6
		SET ~hp~=~%hp%~/10
		WRITE_SHORT 0x24 ~%hp%~
     BUT_ONLY

     // patch injured version into recovery spell

     LAF swap_text STR_VAR files=EVALUATE_BUFFER ~%recover_spell%.spl~ swaps=EVALUATE_BUFFER ~%old_cre%=>%new_cre%~ END

END

/////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////


<<<<<<<< .../stratagems-inline/trolldata.2da
scriptname	hp_set_spell  recover_spell	hp_max	hp_now	old_cre	new_cre  dead_cre
troll02		dw#trl04	spin955		8	4	troll01	dw#trl01 troll02
trolgi02	dw#trl06	spin742		12	6	trolgi01 dw#trlgi trolgi02
trolsp02	dw#trl05	spin739		10	5	trolsp01 dw#trlsp trolsp02
trolsi02	dw#trl07	spin741		14	7	trolsi01 dw#trlsi trolsi02
hgtrl02		dw#trl15	spin537		30	15	hgtrl01	dw#trlhg hgtrl02
troll03		dw#trl02	spin849		4	2	trollsm2 dw#trlsm  troll03
trolic02	dw#trl02	spin791		4	2	trolic01 dw#trlic  trolic02
trolic04	dw#trl10	bliztrl		20	10	trolic03 dw#trlbl trolic04
trolsn02	dw#trl04	spin740		8	4	trolsn01 dw#trlsn trolsn02
trolde02	dw#trl04	spin744		8	4	trolde01 dw#trlde trolde02
rogtro02	dw#trl04	spin670		8	4	rogtro01 dw#trlse rogtro02
trolfr02	dw#trl04	spin743		8	4	trolfr01 dw#trlfr trolfr02
>>>>>>>>

