BEGIN_ACTION_DEFINITION
	Name(ForceSpell)
	TRIGGER
		!GlobalTimerNotExpired("castspell","LOCALS")
		!CheckStat(scstarget,2,WIZARD_SPELL_TRAP) // PM scroll
	ACTION
		RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		ForceSpell(scstarget,scsargument1)
END

BEGIN_ACTION_DEFINITION
	Name(ForceSpellMyself)
	TRIGGER
		!GlobalTimerNotExpired("castspell","LOCALS")
	ACTION
		RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		ForceSpell(Myself,scsargument1)
END


BEGIN_ACTION_DEFINITION
	Name(Attack)
	TRIGGER
	ACTION
		RESPONSE #scsprob1
		SetGlobal("validtarget","LOCALS",1)
		EquipMostDamagingMelee()
		AttackOneRound(scstarget)
END

BEGIN_ACTION_DEFINITION
	Name(AttackIfEmpty)
	TRIGGER
		ActionListEmpty()
	ACTION
		RESPONSE #scsprob1
		SetGlobal("validtarget","LOCALS",1)
		EquipMostDamagingMelee()
		AttackOneRound(scstarget)
END

BEGIN_ACTION_DEFINITION
	Name(MoronAttack)
	TRIGGER
		False()
	ACTION
		RESPONSE #100
		NoAction()
END

BEGIN_ACTION_DEFINITION
	Name(MoveIfEmpty)
	TRIGGER
		ActionListEmpty()
	ACTION
		RESPONSE #scsprob1
		MoveToObject(scstarget)
END

////////////////////////////////////
///	Initial summons
////////////////////////////////////

IF
	!Global("summons","LOCALS",1)
THEN
	RESPONSE #100
		SetGlobal("summons","LOCALS",1)
		ReallyForceSpell(NearestEnemyOf(Myself),CLERIC_CONJURE_EARTH_ELEMENTAL)
		ReallyForceSpell(SecondNearestEnemyOf(Myself),CLERIC_CONJURE_EARTH_ELEMENTAL)
		ReallyForceSpell(ThirdNearestEnemyOf(Myself),CLERIC_CONJURE_EARTH_ELEMENTAL)
		Wait(1)
		ReallyForceSpell(Myself,CLERIC_EARTHQUAKE)
		Wait(1)
		ReallyForceSpell(Myself,WIZARD_STONE_SKIN)
END

IF TRIGGER
	!CheckStatGT(Myself,2,STONESKINS)
	!StateCheck(Myself,STATE_REALLY_DEAD)
THEN DO
	Action(ForceSpellMyself,WIZARD_STONE_SKIN)
END

//////////////////////////////////////////////////////////////////////////////
////	Core combat block
///////////////////////////////////////////////////////////////////////////////


BEGIN LOOP(MyWeaponStrength||4)
BEGIN LOOP(MyWeaponDamageType||Crushing)
INCLUDE FILE(%scsroot%/genai/ssl/dw#wtacor.ssl)
INCLUDE FILE(%scsroot%/genai/ssl/move.ssl)
INCLUDE FILE(%scsroot%/fiend/ssl/chase.ssl)
END LOOP
END LOOP