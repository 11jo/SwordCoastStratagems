////////////////////////////// based on race //////////////////////////////////////

DEFINE_PATCH_FUNCTION mage_spell_swap_race_preparatory
    STR_VAR race=""
            spells=""
    RET spells
BEGIN
    TO_LOWER race
    // immunity to non-magical weapons
    PATCH_MATCH ~%race%~ WITH
      vampire lich rakshasa demilich immune_generic BEGIN
         LPF substitute_spells STR_VAR spells swap_out=~IMPROVED_MANTLE MANTLE ABSOLUTE_IMMUNITY~ swap_in=PROTECTION_FROM_MAGIC_WEAPONS RET spells=spells END
      END
      DEFAULT END
    // immunity to cold and electricity
    PATCH_MATCH ~%race%~ WITH
      vampire lich demilich BEGIN
         LPF substitute_spells STR_VAR spells swap_out=PROTECTION_FROM_COLD swap_in=PROTECTION_FROM_FIRE RET spells=spells END
         LPF substitute_spells STR_VAR spells swap_out=PROTECTION_FROM_ELECTRICITY swap_in=PROTECTION_FROM_ACID RET spells=spells END
      END
      DEFAULT END
    // immunity to low-level spells
    PATCH_MATCH ~%race%~ WITH
       lich rakshasa demilich BEGIN
         LPF substitute_spells STR_VAR spells swap_out=~GLOBE_OF_INVULNERABILITY MINOR_GLOBE_OF_INVULNERABILITY~ swap_in="" RET spells=spells END
       END
       DEFAULT END
    // immunity to silence
    PATCH_MATCH ~%race%~ WITH
       rakshasa BEGIN
           INNER_PATCH_SAVE spells "%spells%" BEGIN
                  LPF substitute_spells STR_VAR spells swap_out=~VOCALIZE~ swap_in="" RET spells=spells END
           END
       END
       DEFAULT END
    // Rakshasa extra spells
    PATCH_MATCH "%race%" WITH
       rakshasa BEGIN
            SPRINT spells "%spells% FIREBALL SKULL_TRAP CLOUDKILL"
       END
       DEFAULT END
END

DEFINE_PATCH_FUNCTION mage_spell_swap_race
    STR_VAR race=""
            kit=""
            spells=""
    RET spells
BEGIN
    TO_LOWER race
    PATCH_MATCH ~%race%~ WITH lich demilich rakshasa BEGIN // get cloud spells
       LPF substitute_spells STR_VAR spells swap_out=CHAOS swap_in=~CHAOS CHAOS CLOUDKILL~ RET spells=spells END
       LPF substitute_spells STR_VAR spells swap_out=DOMINATION swap_in=~DOMINATION DOMINATION CLOUDKILL~ RET spells=spells END
       LPF substitute_spells STR_VAR spells swap_out=ANIMATE_DEAD swap_in=~ANIMATE_DEAD ANIMATE_DEAD CLOUDKILL~ RET spells=spells END
       LPF substitute_spells STR_VAR spells swap_out=SUN_FIRE swap_in=~SUN_FIRE SUN_FIRE CLOUDKILL~ RET spells=spells END
    END
    DEFAULT // don't get fireball/LB if they lack MGI
       PATCH_IF INDEX (GLOBE_OF_INVULNERABILITY ~%spells%~) = ~-1~ BEGIN
          PATCH_IF demivrgvs=1 BEGIN
             LPF substitute_spells STR_VAR spells swap_out=FIREBALL swap_in=LIGHTNING_BOLT RET spells=spells END
          END ELSE BEGIN
             LPF substitute_spells STR_VAR spells swap_out=~FIREBALL LIGHTNING_BOLT~ swap_in=~HASTE MONSTER_SUMMONING_1 SLOW~ RET spells=spells END
          END
       END
    END
    PATCH_IF ((~%race% %kit%~ STRING_EQUAL_CASE ~lich necromancer~) && demivrgvs=0) BEGIN
       LPF substitute_spells STR_VAR spells swap_out=FINGER_OF_DEATH swap_in=~FINGER_OF_DEATH FINGER_OF_DEATH FINGER_OF_DEATH CACOFIEND~ RET spells=spells END
       LPF substitute_spells STR_VAR spells swap_out=PRISMATIC_SPRAY swap_in=~PRISMATIC_SPRAY PRISMATIC_SPRAY PRISMATIC_SPRAY CACOFIEND~ RET spells=spells END
       LPF substitute_spells STR_VAR spells swap_out=MORDENKAINENS_SWORD swap_in=~MORDENKAINENS_SWORD MORDENKAINENS_SWORD MORDENKAINENS_SWORD CACOFIEND~ RET spells=spells END
       LPF substitute_spells STR_VAR spells swap_out=SPHERE_OF_CHAOS swap_in=~SPHERE_OF_CHAOS SPHERE_OF_CHAOS SPHERE_OF_CHAOS CACOFIEND~ RET spells=spells END
       LPF substitute_spells STR_VAR spells swap_out=ABI_DALZIMS_HORRID_WILTING swap_in=~ABI_DALZIMS_HORRID_WILTING ABI_DALZIMS_HORRID_WILTING ABI_DALZIMS_HORRID_WILTING SUMMON_FIEND~ RET spells=spells END
    END
END


////////////////////////////// based on level //////////////////////////////////////

DEFINE_PATCH_FUNCTION mage_spell_swap_level
     INT_VAR level=0
     STR_VAR spells=""
             kit=""
     RET spells
BEGIN

     PATCH_IF (level>8 && ~%kit%~ STRING_EQUAL_CASE enchanter && demivrgvs=0) BEGIN
         LPF substitute_spells STR_VAR spells swap_out=SLEEP swap_in=SPOOK RET spells=spells END
     END

     PATCH_IF (level>6 && (~%kit%~ STRING_EQUAL_CASE necromancer || ~%kit%~ STRING_EQUAL_CASE conjurer) && demivrgvs=0) BEGIN
        LPF substitute_spells STR_VAR spells swap_out=~CHARM_PERSON SLEEP~ swap_in=~MAGIC_MISSILE CHROMATIC_ORB~ RET spells=spells END
     END

     PATCH_IF (level>6 && ~%kit%~ STRING_EQUAL_CASE invoker) BEGIN
        LPF substitute_spells STR_VAR spells swap_out=HORROR swap_in=~HORROR WEB STINKING_CLOUD~ RET spells=spells END
     END

     PATCH_IF (level>10 && ~%kit%~ STRING_EQUAL_CASE enchanter) BEGIN
        LPF substitute_spells STR_VAR spells swap_out=GLITTERDUST swap_in=MELF_ACID_ARROW RET spells=spells END
     END

     PATCH_IF (level>10 && ~%kit%~ STRING_EQUAL_CASE necromancer) BEGIN
        LPF substitute_spells STR_VAR spells swap_out=~GLITTERDUST RAY_OF_ENFEEBLEMENT~ swap_in=MELF_ACID_ARROW RET spells=spells END
     END

     PATCH_IF (level>9 && ~%kit%~ STRING_EQUAL_CASE enchanter) BEGIN
        LPF substitute_spells STR_VAR spells swap_out=DIRE_CHARM swap_in=~DIRE_CHARM FLAME_ARROW FLAME_ARROW~ RET spells=spells END
     END

     PATCH_IF (level>9 && (~%kit%~ STRING_EQUAL_CASE conjurer || ~%kit%~ STRING_EQUAL_CASE fighter_mage)) BEGIN
        LPF substitute_spells STR_VAR spells swap_out=~DIRE_CHARM MONSTER_SUMMONING_1~ swap_in=~DIRE_CHARM FLAME_ARROW FLAME_ARROW~ RET spells=spells END
        LPF substitute_spells STR_VAR spells swap_out=HOLD_PERSON swap_in=SKULL_TRAP RET spells=spells END
     END

     PATCH_IF (level>9 && ~%kit%~ STRING_EQUAL_CASE necromancer) BEGIN
        LPF substitute_spells STR_VAR spells swap_out=SKULL_TRAP swap_in=~SKULL_TRAP FLAME_ARROW~ RET spells=spells END
        LPF substitute_spells STR_VAR spells swap_out=VAMPIRIC_TOUCH swap_in=~VAMPIRIC_TOUCH FLAME_ARROW~ RET spells=spells END
     END

     PATCH_IF level>11 BEGIN
        LPF substitute_spells STR_VAR spells swap_out=MONSTER_SUMMONING_2 swap_in=CONFUSION RET spells=spells END
     END

     PATCH_IF level>15 BEGIN
        LPF substitute_spells STR_VAR spells swap_out=SPIDER_SPAWN swap_in=EMOTION_HOPELESSNESS RET spells=spells END
        LPF substitute_spells STR_VAR spells swap_out=MONSTER_SUMMONING_3 swap_in=CHAOS RET spells=spells END
     END


END


////////////////////////////// based on BG1 //////////////////////////////////////


DEFINE_PATCH_FUNCTION mage_spell_swap_bg1_preparatory
     STR_VAR spells=""
     RET spells
BEGIN
         LPF substitute_spells STR_VAR spells swap_out=~FIRE_SHIELD_BLUE FIRE_SHIELD_RED STONE_SKIN TELEPORT_FIELD SECRET_WORD SPELL_THRUST BREACH DISPEL_MAGIC~ swap_in=~~ RET spells=spells END
END

DEFINE_PATCH_FUNCTION mage_spell_swap_bg1
     STR_VAR spells=""
     RET spells
BEGIN
         LPF substitute_spells STR_VAR spells swap_out=SPIDER_SPAWN swap_in=MONSTER_SUMMONING_2 RET spells=spells END
         LPF substitute_spells STR_VAR spells swap_out=POWER_WORD_SLEEP swap_in=MELF_ACID_ARROW RET spells=spells END
         LPF substitute_spells STR_VAR spells swap_out=GLITTERDUST swap_in=INVISIBILITY RET spells=spells END
         LPF substitute_spells STR_VAR spells swap_out=STONE_SKIN swap_in=IMPROVED_INVISIBILITY RET spells=spells END
END
