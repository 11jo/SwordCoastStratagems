

//////////////////////////////////////////////////////////////////////
///  Vocalize
////////////////////////////////////////////////////////////////////

IF TRIGGER
	StateCheck(Myself,STATE_SILENCED)
THEN DO
	Action(SpellMyself,WIZARD_VOCALIZE)
END

//////////////////////////////////////////////////////////////////////
///  Clear Insect Plague
////////////////////////////////////////////////////////////////////

IF TRIGGER
       CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
       RequireBlock(HasL7)
THEN DO
	Action(SequencerMyself,1,WIZARD_IMPROVED_INVISIBILITY,WIZARD_STONE_SKIN,WIZARD_FIRE_SHIELD_RED) // II, Stoneskin, fire shield
	Action(SequencerMyself,6,WIZARD_IMPROVED_INVISIBILITY,WIZARD_FIRE_SHIELD_BLUE,WIZARD_FIRE_SHIELD_RED) // II, fire shield x2
	Action(SequencerMyself,2,WIZARD_STONE_SKIN,WIZARD_FIRE_SHIELD_BLUE,WIZARD_FIRE_SHIELD_RED) // Stoneskin, fire shield x2
	Action(SequencerMyself,7,WIZARD_HASTE,WIZARD_FIRE_SHIELD_BLUE,WIZARD_FIRE_SHIELD_RED)
END

IF TRIGGER
       CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
       RequireBlock(HasL4)
THEN DO
	Action(SpellMyself,WIZARD_FIRE_SHIELD_RED)
	Action(SpellMyself,WIZARD_FIRE_SHIELD_BLUE)
END

//////////////////////////////////////////////////////////////////////
///  Clear Blindness
////////////////////////////////////////////////////////////////////

IF TRIGGER
	StateCheck(Myself,STATE_BLIND)
	RequireBlock(TruesightCuresBlindness)
THEN DO
	Action(SpellMyself,WIZARD_TRUE_SIGHT)
END



//////////////////////////////////////////////////////////////////////
/// Pro / MW and Stoneskin, and defensive sequencers
/////////////////////////////////////////////////////////////////////

IF TRIGGER                 // this block is here to allow L16-L17 wizards to use the sequencer in their first round
        IgnoreBlock(IsNecromancer)
        RequireBlock(IsOptionThree)
	!StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
	!GlobalTimerNotExpired("initialtrigger","LOCALS")
	RequireBlock(HasL8)
	IgnoreBlock(HasL9)
THEN DO
	Action(TriggerMyself,0,WIZARD_MISLEAD,WIZARD_SPELL_IMMUNITY_DIVINATION,WIZARD_SPELL_SHIELD|100|200)  // II, Spell Im (div), Spell Shield
	Action(TriggerMyself,0,WIZARD_IMPROVED_INVISIBILITY,WIZARD_SPELL_IMMUNITY_DIVINATION,WIZARD_SPELL_SHIELD|100|100)  // II, Spell Im (div), Spell Shield
	OnContinue(SetGlobal("spelltrigger","LOCALS",2)SetGlobalTimer("initialtrigger","LOCALS",2000))
	SetGlobalTimer("impinv","LOCALS",18)
	SetGlobalTimer("initialtrigger","LOCALS",2000)
END

IF TRIGGER                 // this block is here to allow L16-L17 wizards to use the sequencer in their first round
        IgnoreBlock(IsNecromancer)
        RequireBlock(IsOptionTwo)
        GlobalTimerExpired("optiontwotimer","LOCALS")
	!StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
	!GlobalTimerNotExpired("initialtrigger","LOCALS")
	RequireBlock(HasL8)
	IgnoreBlock(HasL9)
THEN DO
	Action(TriggerMyself,0,WIZARD_MISLEAD,WIZARD_SPELL_SHIELD,WIZARD_SPELL_IMMUNITY_DIVINATION|100|200)  // II, Spell Im (div), Spell Shield
	Action(TriggerMyself,0,WIZARD_IMPROVED_INVISIBILITY,WIZARD_SPELL_SHIELD,WIZARD_SPELL_IMMUNITY_DIVINATION|100|100)  // II, Spell Im (div), Spell Shield
	OnContinue(SetGlobal("spelltrigger","LOCALS",2)SetGlobalTimer("initialtrigger","LOCALS",2000))
	SetGlobalTimer("impinv","LOCALS",18)
	SetGlobalTimer("initialtrigger","LOCALS",2000)
END

IF TRIGGER
        IgnoreBlock(IsNecromancer)
	!StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
	GlobalTimerExpired("redefend","LOCALS")
	RequireBlock(HasL8)
THEN DO
	Action(TriggerMyself,0,WIZARD_MISLEAD,WIZARD_SPELL_SHIELD,WIZARD_SPELL_IMMUNITY_DIVINATION|100|200)  // II, Spell Im (div), Spell Shield
	Action(TriggerMyself,0,WIZARD_MISLEAD,WIZARD_SPELL_IMMUNITY_DIVINATION,WIZARD_SPELL_SHIELD|100|200)  // II, Spell Im (div), Spell Shield
	Action(TriggerMyself,1,WIZARD_SPELL_DEFLECTION,WIZARD_SPELL_SHIELD,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS) 
	SetGlobalTimer("impinv","LOCALS",18)
END


IF TRIGGER
        IgnoreBlock(IsNecromancer)
	!StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
	GlobalTimerExpired("redefend","LOCALS")
	RequireBlock(HasL8)
THEN DO
	Action(TriggerMyself,1,WIZARD_SPELL_DEFLECTION,WIZARD_SPELL_SHIELD,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
        SetGlobalTimer("impinv","LOCALS",18)
END


IF TRIGGER
        IgnoreBlock(IsNecromancer)
	!OnCreation()
	!StateCheck(Myself,STATE_INVISIBLE)
	!GlobalTimerNotExpired("DMWWHaste","LOCALS")
	See(NearestEnemyOf(Myself))
	RequireBlock(HasL7)
	Global("nocaststoneskin","LOCALS",0)
THEN DO
	SetGlobalTimer("DMWWHaste","LOCALS",1000)
	Action(SequencerMyself,7,WIZARD_HASTE,WIZARD_STONE_SKIN,WIZARD_MINOR_GLOBE_OF_INVULNERABILITY)
	SetGlobalTimer("impinv","LOCALS",18)
END

IF TRIGGER
	!StateCheck(Myself,STATE_INVISIBLE)
	!GlobalTimerNotExpired("DMWWHaste","LOCALS")
	See(NearestEnemyOf(Myself))
	RequireBlock(HasL7)
THEN DO	
	SetGlobalTimer("DMWWHaste","LOCALS",1000)
	Action(SequencerMyself,7,WIZARD_HASTE,WIZARD_FIRE_SHIELD_BLUE,WIZARD_FIRE_SHIELD_RED)
END

IF TRIGGER
          RequireBlock(HasL9)
          RequireBlock(Refinements)
          GlobalTimerExpired("redefend","LOCALS")
	!GlobalTimerNotExpired("timestop","LOCALS")
	!GlobalTimerNotExpired("DMWWTimestopRunning","GLOBAL")
	!GlobalTimerNotExpired("aegi","LOCALS")
	GlobalTimerExpired("redefend","LOCALS")
	OR(3)
             !CheckStatGT(Myself,80,RESISTSLASHING)
             !CheckStatGT(Myself,80,RESISTCRUSHING)
             !CheckStatGT(Myself,80,RESISTPIERCING)
THEN DO
         Action(SpellRESMyself,"tg#aegi"|100|50)
	SetGlobalTimer("redefend","LOCALS",14) // don't get stuck in a defend-defend-defend loop and forget to do any damage
END

IF TRIGGER
          RequireBlock(HasL9)
          RequireBlock(Refinements)
          IgnoreBlock(IsNecromancer)
          IgnoreBlock(IsConjurer)
          IgnoreBlock(IsLich)         // for liches and rakshasas, Foresight is lower priority than PMW
          IgnoreBlock(IsRakshasa)
	!GlobalTimerNotExpired("timestop","LOCALS")
	!GlobalTimerNotExpired("DMWWTimestopRunning","GLOBAL")
	GlobalTimerExpired("redefend","LOCALS")
	CheckStatGT(Myself,-5,ARMORCLASS)
THEN DO
   Action(SpellRESMyself,"tg#fors"|100|50)
	SetGlobalTimer("redefend","LOCALS",8) // don't get stuck in a defend-defend-defend loop and forget to do any damage
END


IF TRIGGER
	RequireBlock(HasL8)
        OR(2)
		!CheckStatGT(Myself,0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		!CheckStatGT(Myself,0,STONESKINS)
	OR(2)
		!HaveSpell(WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		CheckStat(Myself,0,WIZARD_SPELL_TRAP)
	OR(2)
		!HaveSpell(WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		CheckStat(Myself,0,WIZARD_SPELL_TURNING)
	OR(2)
		!HaveSpell(WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		CheckStat(Myself,0,WIZARD_SPELL_DEFLECTION)
	!GlobalTimerNotExpired("timestop","LOCALS")
	!GlobalTimerNotExpired("DMWWTimestopRunning","GLOBAL")
	GlobalTimerExpired("redefend","LOCALS")
	!Global("ChainContingency","LOCALS",4)
	!Global("ChainContingency","LOCALS",5)
	!Global("ChainContingency","LOCALS",6)
	Detect(NearestEnemyOf(Myself))
	Global("nocaststoneskin","LOCALS",0)
THEN DO
	Action(TriggerMyself,2,WIZARD_SPELL_DEFLECTION,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS,WIZARD_SPELL_SHIELD)  // Spell Deflection, Pro/MW, Spell Shield
	SetGlobalTimer("redefend","LOCALS",8) // don't get stuck in a defend-defend-defend loop and forget to do any damage
END

IF TRIGGER	
	RequireBlock(HasL7)
	IgnoreBlock(IsNecromancer)
        OR(2)
		!StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
		!CheckStatGT(Myself,0,WIZARD_SPELL_DEFLECTION)
	GlobalTimerExpired("redefend","LOCALS")
	Detect(NearestEnemyOf(Myself))
THEN DO
	Action(SequencerMyself,8,WIZARD_MINOR_SPELL_DEFLECTION,WIZARD_MINOR_GLOBE_OF_INVULNERABILITY,WIZARD_IMPROVED_INVISIBILITY)  //Minor Spell Deflection, MGI, II
	SetGlobalTimer("redefend","LOCALS",8) // don't get stuck in a defend-defend-defend loop and forget to do any damage
END

IF TRIGGER
	RequireBlock(HasL9)
        !CheckStatGT(Myself,0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
	!GlobalTimerNotExpired("timestop","LOCALS")
	!GlobalTimerNotExpired("DMWWTimestopRunning","GLOBAL")
	OR(2)
		GlobalTimerExpired("redefend","LOCALS")
		OnCreation()
	Global("ChainContingencyFired","LOCALS",1)
	Detect(NearestEnemyOf(Myself))
	IgnoreBlock(IsLich|IsRakshasa|IsVampire)	
THEN DO
	Action(SpellMyself,WIZARD_ABSOLUTE_IMMUNITY)
	Action(SpellMyself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
	Action(SpellMyself,WIZARD_IMPROVED_MANTLE)
	Action(SpellMyself,WIZARD_MANTLE)
END

IF TRIGGER
        IgnoreBlock(HasL9)
	RequireBlock(HasL8)
        !CheckStatGT(Myself,0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
	!GlobalTimerNotExpired("timestop","LOCALS")
	!GlobalTimerNotExpired("DMWWTimestopRunning","GLOBAL")
	OR(2)
		GlobalTimerExpired("redefend","LOCALS")
		OnCreation()
	Detect(NearestEnemyOf(Myself))
	IgnoreBlock(IsLich|IsRakshasa|IsVampire)
THEN DO
	Action(SpellMyself,WIZARD_IMPROVED_MANTLE)
END


IF TRIGGER
        IgnoreBlock(HasL9)
	RequireBlock(HasL7)
        !CheckStatGT(Myself,0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
	!GlobalTimerNotExpired("timestop","LOCALS")
	!GlobalTimerNotExpired("DMWWTimestopRunning","GLOBAL")
	OR(2)
		GlobalTimerExpired("redefend","LOCALS")
		OnCreation()
	Detect(NearestEnemyOf(Myself))
	IgnoreBlock(IsLich|IsRakshasa|IsVampire)
THEN DO
	Action(SpellMyself,WIZARD_MANTLE)
END




IF TRIGGER
        RequireBlock(HasL6)
	!CheckStat(Myself,4,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
	!GlobalTimerNotExpired("timestop","LOCALS")
	!GlobalTimerNotExpired("DMWWTimestopRunning","GLOBAL")
	OR(2)
		Global("ChainContingencyFired","LOCALS",1)
		!CheckStatGT(Myself,17,Level)
	Detect(NearestEnemyOf(Myself))
	RequireBlock(IsVampire)
	!GlobalTimerNotExpired("justdonePMW","LOCALS")
THEN DO
	Action(SpellMyself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
	SetGlobalTimer("justdonePMW","LOCALS",8)
END

IF TRIGGER
	RequireBlock(HasL6)
        !CheckStat(Myself,4,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
	!GlobalTimerNotExpired("timestop","LOCALS")
	!GlobalTimerNotExpired("DMWWTimestopRunning","GLOBAL")
	OR(2)
		Global("ChainContingencyFired","LOCALS",1)
		!CheckStatGT(Myself,17,Level)
	Detect(NearestEnemyOf(Myself))
	RequireBlock(IsRakshasa)
	!GlobalTimerNotExpired("justdonePMW","LOCALS")
THEN DO
	Action(SpellMyself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
	SetGlobalTimer("justdonePMW","LOCALS",8)
END

IF TRIGGER
	!CheckStatGT(Myself,0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
	!GlobalTimerNotExpired("timestop","LOCALS")
	!GlobalTimerNotExpired("DMWWTimestopRunning","GLOBAL")
	OR(2)
		Global("ChainContingencyFired","LOCALS",1)
		!CheckStatGT(Myself,17,Level)
	Detect(NearestEnemyOf(Myself))
	!GlobalTimerNotExpired("justdonePMW","LOCALS")
THEN DO
	Action(SpellMyself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
	Action(SpellMyself,WIZARD_ABSOLUTE_IMMUNITY)
	Action(SpellMyself,WIZARD_IMPROVED_MANTLE)
	Action(SpellMyself,WIZARD_MANTLE)
	Action(ScrollMyself,"scrl8j")
	SetGlobalTimer("redefend","LOCALS",8) // don't get stuck in a defend-defend-defend loop and forget to do any damage
	SetGlobalTimer("justdonePMW","LOCALS",8)
END

IF TRIGGER
          RequireBlock(HasL9)
          RequireBlock(Refinements)
          IgnoreBlock(IsNecromancer)
          IgnoreBlock(IsConjurer)
          RequireBlock(IsLich|IsRakshasa)
	!GlobalTimerNotExpired("timestop","LOCALS")
	!GlobalTimerNotExpired("DMWWTimestopRunning","GLOBAL")
	GlobalTimerExpired("redefend","LOCALS")
	CheckStatGT(Myself,-5,ARMORCLASS)
THEN DO
   Action(SpellRESMyself,"tg#fors")
	SetGlobalTimer("redefend","LOCALS",8) // don't get stuck in a defend-defend-defend loop and forget to do any damage
END


IF TRIGGER
	GlobalTimerExpired("redefend","LOCALS")
	!CheckStatGT(Myself,2,STONESKINS)
	!GlobalTimerNotExpired("DMWWTimestopRunning","GLOBAL")
	!GlobalTimerNotExpired("timestop","LOCALS")
	!GlobalTimerNotExpired("stonetimer","LOCALS")
	Global("nocaststoneskin","LOCALS",0)
	Range(NearestEnemyOf(Myself),5)
	BEGIN LOOP(scsnear||Nearest;SecondNearest;ThirdNearest)
		BEGIN LOOP(scscarsomyr||sw2h10;sw2h19)
			OR(4)
				CheckStatGT(Myself,0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
				!HasItem("scscarsomyr",scsnearEnemyOf(Myself))
				StateCheck(scsnearEnemyOf(Myself),STATE_IMMOBILE)
				!Range(scsnearEnemyOf(Myself),10)
		END LOOP
	END LOOP
THEN DO	Action(SequencerMyself,1,WIZARD_IMPROVED_INVISIBILITY,WIZARD_STONE_SKIN,WIZARD_FIRE_SHIELD_RED) // II, Stoneskin, fire shield
	Action(SequencerMyself,6,WIZARD_IMPROVED_INVISIBILITY,WIZARD_FIRE_SHIELD_BLUE,WIZARD_FIRE_SHIELD_RED) // II, fire shield x2
	Action(SequencerMyself,2,WIZARD_STONE_SKIN,WIZARD_FIRE_SHIELD_BLUE,WIZARD_FIRE_SHIELD_RED) // Stoneskin, fire shield x2
	Action(SpellMyself,WIZARD_STONE_SKIN)
	SetGlobalTimer("redefend","LOCALS",8) // don't get stuck in a defend-defend-defend loop and forget to do any damage
	SetGlobalTimer("stonetimer","LOCALS",12)
END

//////////////////////////////////////////////////////////////////////
//// Full-on Invisibility in case I'm in big trouble and need to pull off
//////////////////////////////////////////////////////////////////////

IF TRIGGER
        IgnoreBlock(IsNecromancer)
        IgnoreBlock(Demivrgvs)
        IgnoreBlock(IsLich|IsVampire)
	HPPercentLT(Myself,75)
	!StateCheck(Myself,STATE_INVISIBLE)
	TriggerBlock(TruesightSafe)
	GlobalTimerExpired("redefend","LOCALS")
THEN DO
	Action(SpellMyself,WIZARD_MISLEAD)
	Action(SpellMyself,WIZARD_SHADOW_DOOR)
	Action(MinorSequencerMyself,1,WIZARD_INVISIBILITY,WIZARD_MIRROR_IMAGE)
	Action(Potion,"potn10",@1010)
	Action(SpellMyself,WIZARD_INVISIBILITY)
END

IF TRIGGER
        IgnoreBlock(IsNecromancer)
        RequireBlock(Demivrgvs)
        IgnoreBlock(IsLich|IsVampire)
        !Global("dvgldust","LOCALS",1)
	HPPercentLT(Myself,75)
	!StateCheck(Myself,STATE_INVISIBLE)
	TriggerBlock(TruesightSafe)
	GlobalTimerExpired("redefend","LOCALS")
THEN DO
	Action(SpellMyself,WIZARD_MISLEAD)
	Action(SpellMyself,WIZARD_SHADOW_DOOR)
	Action(MinorSequencerMyself,1,WIZARD_INVISIBILITY,WIZARD_MIRROR_IMAGE)
	Action(Potion,"potn10",@1010)
	Action(SpellMyself,WIZARD_INVISIBILITY)
END

////////////////////////////////////////////////////////////////////////////
//// Healing potions, but only if I'm safe
//////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	IgnoreBlock(IsLich|IsVampire)
	HPPercentLT(Myself,100)
	OR(2)
		StateCheck(Myself,STATE_INVISIBLE)
		!See(NearestEnemyOf(Myself))
	OR(2)
		HPPercentLT(Myself,75)
		!See(NearestEnemyOf(Myself))
THEN DO
	Action(Potion,"potn55",@1055)
	Action(Potion,"potn52",@1052)
	ActionCondition(Potion,"potn08",@1008;OR(2)&!See(NearestEnemyOf(Myself))&CheckStatLT(Myself,11,Level))
END




///////////////////////////////////////////////////////////////////////////
////  If I'm not Improved Invisible, change that if possible
//////////////////////////////////////////////////////////////////////////


IF TRIGGER
        IgnoreBlock(IsNecromancer)
        IgnoreBlock(Demivrgvs)
        RequireBlock(HasL5)
	!StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
	GlobalTimerExpired("redefend","LOCALS")
	!GlobalTimerNotExpired("impinv","LOCALS")
	TriggerBlock(TruesightSafe)
THEN DO
	Action(SequencerMyself,1,WIZARD_IMPROVED_INVISIBILITY,WIZARD_STONE_SKIN,WIZARD_FIRE_SHIELD_RED) // II, Stoneskin, fire shield
	Action(SequencerMyself,6,WIZARD_IMPROVED_INVISIBILITY,WIZARD_FIRE_SHIELD_BLUE,WIZARD_FIRE_SHIELD_RED) // II, fire shield x2
	Action(SpellMyself,WIZARD_MISLEAD)
	Action(SpellMyself,WIZARD_SHADOW_DOOR)
	SetGlobalTimer("impinv","LOCALS",18)
END


IF TRIGGER
        IgnoreBlock(IsNecromancer)
        RequireBlock(Demivrgvs)
        RequireBlock(HasL5)
	!StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
	GlobalTimerExpired("redefend","LOCALS")
	!GlobalTimerNotExpired("impinv","LOCALS")
	!Global("dvgldust","LOCALS",1)
	TriggerBlock(TruesightSafe)
THEN DO
	Action(SequencerMyself,1,WIZARD_IMPROVED_INVISIBILITY,WIZARD_STONE_SKIN,WIZARD_FIRE_SHIELD_RED) // II, Stoneskin, fire shield
	Action(SequencerMyself,6,WIZARD_IMPROVED_INVISIBILITY,WIZARD_FIRE_SHIELD_BLUE,WIZARD_FIRE_SHIELD_RED) // II, fire shield x2
	Action(SpellMyself,WIZARD_MISLEAD)
	Action(SpellMyself,WIZARD_SHADOW_DOOR)
	SetGlobalTimer("impinv","LOCALS",18)
END


