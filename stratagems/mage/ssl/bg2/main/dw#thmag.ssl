//////////////////////////////////////////////////////////////////////////////////
///
///   Define actions
///
////////////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/genai/ssl/genai_definitions.ssl)
BEGIN LOOP(scsspellsubstitute1||!StateCheck(scstarget,STATE_NOT_TARGETABLE))
BEGIN LOOP(scsspellsubstitute2||!StateCheck(scstarget,STATE_INVISIBLE))
	INCLUDE FILE(%scsroot%/mage/ssl/mage_definitions.ssl)
END LOOP
END LOOP


//////////////////////////////////////////////////////////////////////////////////
//      Handle fear
//////////////////////////////////////////////////////////////////////////////////

IF
  StateCheck(Myself,STATE_PANIC)
THEN
    RESPONSE #100
             RandomWalkContinuous()
END

//////////////////////////////////////////////////////////////////////////////////
////	Prep
//////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/mage/ssl/bg2/main/tmsetup.ssl)



//////////////////////////////////////////////////////////////////////////////////
////	If non-hostile, closedown - this script doesn't pretend to fight bad guys
//////////////////////////////////////////////////////////////////////////////////

IF
	!Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		NoAction()
END
////////////////////////////////////////////////////////////////////////////
///	Contingencies renew defences
////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/mage/ssl/bg2/generalblocks/contingency.ssl)

///////////////////////////////////////////////////////////////////////////
////	Flee Cloudkill spell
///////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/mage/ssl/bg2/generalblocks/fleecloud.ssl)


///////////////////////////////////////////////////////////////////////////
//	Melee
///////////////////////////////////////////////////////////////////////////

DEFAULT TRIGGER(OR(2)&GlobalTimerNotExpired("castspell","LOCALS")&StateCheck(Myself,STATE_INVISIBLE))

BEGIN LOOP(MyWeaponStrength||Unknown)
BEGIN LOOP(MyWeaponDamageType||MultipleDamageType)
INCLUDE FILE(%scsroot%/genai/ssl/dw#wtacor.ssl)
INCLUDE FILE(%scsroot%/genai/ssl/move.ssl)
END LOOP
END LOOP


//////////////////////////////////////////////////////////////////////////
// Renew and retreat
//////////////////////////////////////////////////////////////////////////

DEFAULT TRIGGER(!GlobalTimerNotExpired("castspell","LOCALS"))

INCLUDE FILE(%scsroot%/mage/ssl/bg2/generalblocks/renew.ssl)

///////////////////////////////////////////////////////////////////////////
//	Consider going invisible if possible
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	!StateCheck(Myself,STATE_INVISIBLE)
	TriggerBlock(TruesightSafe)
	GlobalTimerExpired("redefend","LOCALS")
	Target(Myself)
THEN DO
	Action(SpellMyself,WIZARD_MISLEAD|100|100)
	Action(SpellMyself,WIZARD_SHADOW_DOOR|100|100)
	Action(MinorSequencer,1,WIZARD_INVISIBILITY,WIZARD_MIRROR_IMAGE|100|100)
	Action(SpellMyself,WIZARD_INVISIBILITY|100|100)
	Action(Potion,"potn10",@1010|100|100)
END


//////////////////////////////////////////////////////////////////////////
// Look for PCs
//////////////////////////////////////////////////////////////////////////

DEFAULT TRIGGER()

INCLUDE FILE(%scsroot%/mage/ssl/bg2/generalblocks/mill.ssl)

DEFAULT TRIGGER(!GlobalTimerNotExpired("castspell","LOCALS"))


INCLUDE FILE(%scsroot%/mage/ssl/bg2/generalblocks/chase.ssl)

////////////////////////////////////////////////////////////////////////////
// Core magic block
///////////////////////////////////////////////////////////////////////////

IF
	!Global("DMWWThisWillBeZero","GLOBAL",0)
	!GlobalTimerNotExpired("PhantomBlade","LOCALS")
THEN
	RESPONSE #100
		SetGlobalTimer("castspell","LOCALS",6)
		SetGlobalTimer("PhantomBlade","LOCALS",1000)
		SpellNoDec(Myself,WIZARD_PHANTOM_BLADE)
END

BEGIN LOOP(slowspell|| )
INCLUDE FILE(%scsroot%/mage/ssl/bg2/magetypes/fighter_mage.ssl)
END LOOP




INCLUDE FILE(%scsroot%/mage/ssl/bg2/generalblocks/closedown.ssl)
