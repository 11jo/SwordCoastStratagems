////////////////////////////////////////////////////////////////////////////
////  Slightly modified dw#wtacor+move for fighter/mages
////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////
////	Don't melee in timestop if that option's disabled
////////////////////////////////////////////////////////////////////////////


IF
  !Global("DMWWNoTimestopMelee","GLOBAL",0)
  GlobalTimerNotExpired("DMWWTimestopRunning","GLOBAL")
  THEN
  RESPONSE #100
  	   NoAction()
END

////////////////////////////////////////////////////////////////////////////
////	If I'm not a moron and I'm attacking something I can't hit, STOP!
////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	IgnoreBlock(IsBG1)
	Target(LastTargetedBy(Myself))
	CheckStat(Myself,0,WEAPON_ENCHANTMENT)
	Global("validtarget","LOCALS",1)
	!CheckStat(scstarget,0,WIZARD_PROTECTION_FROM_NORMAL_WEAPONS)
THEN DO
	SetGlobal("validtarget","LOCALS",0)
	ClearAllActions()
END

IF TRIGGER
	IgnoreBlock(IsBG1)
	Target(LastTargetedBy(Myself))
	CheckStatLT(Myself,3,WEAPON_ENCHANTMENT)
	Global("validtarget","LOCALS",1)
	CheckStatGT(scstarget,0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
THEN DO
	SetGlobal("validtarget","LOCALS",0)
	ClearAllActions()
END

IF TRIGGER
	IgnoreBlock(IsBG1)
	Target(LastTargetedBy(Myself))
	CheckStat(Myself,3,WEAPON_ENCHANTMENT)
	Global("validtarget","LOCALS",1)
	CheckStatGT(scstarget,1,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
THEN DO
	SetGlobal("validtarget","LOCALS",0)
	ClearAllActions()
END

IF TRIGGER
	IgnoreBlock(IsBG1)
	Target(LastTargetedBy(Myself))
	CheckStat(Myself,4,WEAPON_ENCHANTMENT)
	Global("validtarget","LOCALS",1)
	CheckStatGT(scstarget,2,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
THEN DO
	SetGlobal("validtarget","LOCALS",0)
	ClearAllActions()
END

IF TRIGGER
	IgnoreBlock(IsBG1)
	Target(LastTargetedBy(Myself))
	!CheckStat(Myself,0,WEAPON_ENCHANTMENT)
	Global("validtarget","LOCALS",1)
	CheckStat(scstarget,4,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
THEN DO
	SetGlobal("validtarget","LOCALS",0)
	ClearAllActions()
END

///////////////////////////////////////////////////////////////////////
////	We start by looking at the very near opponents. Go for the 
////	most vulnerable of them.
////
////	Don't attack the helpless (that is: the stunned etc); don't attack
////	people protected from your weapon.
////	
////	In a timestop, don't attack stoneskinned foes if you've recently cast
////	breach, it runs the risk of looking stupid
///////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsPreferringWeak)
	TargetBlock(CloseEnemies)
	TriggerBlock(Enemy|Disabled|PlusUnknownSafe|MultipleDamageTypeSafe)
	OR(3)
		!GlobalTimerNotExpired("DMWWTimestopRunning","GLOBAL") 
		CheckStat(scstarget,0,STONESKINS)
		!GlobalTimerNotExpired("DMWW_breach","LOCALS")
	Range(scstarget,4)
THEN DO
	Action(Attack)
END

//////////////////////////////////////////////////////////////////////////
////	Intelligent creatures look for PCs - preferably easily killed ones
//// 	- and, failing that, settle for anything.
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsPreferringWeak)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(Enemy|Disabled|PlusUnknownSafe|MultipleDamageTypeSafe)
	OR(3)
		!GlobalTimerNotExpired("DMWWTimestopRunning","GLOBAL") 
		CheckStat(scstarget,0,STONESKINS)
		!GlobalTimerNotExpired("DMWW_breach","LOCALS")
THEN DO
	Action(MoveIfEmpty)
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////    If we've got this far, then there are no good targets in the vicinity. So just kill the nearest PC, 
////    even if they are confused/charmed/whatever. Failing that, kill anything that moves.
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	Allegiance(Myself,ENEMY)
	TargetBlock(PlayersInRandomOrder)
	TriggerBlock(PlusUnknownSafe)
THEN DO
	Action(MoveIfEmpty)
END

IF TRIGGER
	Allegiance(Myself,ENEMY)
	Target(NearestEnemyOf(Myself))
	TriggerBlock(PlusUnknownSafe)
THEN DO
	Action(MoveIfEmpty)
END

