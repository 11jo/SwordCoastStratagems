////////////////////////////////////////////////////////////////////////
// Missile combat block
////////////////////////////////////////////////////////////////////////

DEFAULT TRIGGER(GlobalTimerNotExpired("castspell","LOCALS"))

///////////////////////////////////////////////////
////	If attacked by an unseen foe, mill around
////	in confusion. You never know, you might
////	bump into them!
//////////////////////////////////////////////////

IF
	Allegiance(Myself,ENEMY)
    OR(12)
      AttackedBy([GOODCUTOFF.0.0.0.0.SUMMONED],DEFAULT)
      AttackedBy([PC],DEFAULT)
      AttackedBy([FAMILIAR],DEFAULT)
      AttackedBy([ALLY],DEFAULT)
      AttackedBy([CONTROLLED],DEFAULT)
      AttackedBy([CHARMED],DEFAULT)
      SpellCastOnMe([GOODCUTOFF.0.0.0.0.SUMMONED],0)
      SpellCastOnMe([PC],0)
      SpellCastOnMe([FAMILIAR],0)
      SpellCastOnMe([ALLY],0)
      SpellCastOnMe([CONTROLLED],0)
      SpellCastOnMe([CHARMED],0) 
!GlobalTimerNotExpired("whatthe","LOCALS")
  !See(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    SetGlobalTimer("whatthe","LOCALS",12)
    Continue()
END

IF
  !See(NearestEnemyOf(Myself))
  !Heard([ANYONE],404)
  GlobalTimerNotExpired("whatthe","LOCALS")
THEN
  RESPONSE #100
    RandomWalkContinuous()
END

/////////////////////////////////////////////////////////////////
///	Handle Cloud Kill spells
/////////////////////////////////////////////////////////////////

IF
	Heard([ANYONE],404)
	!General(Myself,UNDEAD)
	!Race(Myself,RAKSHASA)
	!Race(Myself,DEMONIC)
	!See([PC])
	Range(LastHeardBy(Myself),20)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		RunAwayFrom(LastHeardBy(Myself),30)
		SetInterrupt(TRUE)
END

/////////////////////////////////////////////////////////////////
///	General
/////////////////////////////////////////////////////////////////


IF TRIGGER
	OR(4)
		!Heard([ANYONE],404)
		General(Myself,UNDEAD)
		Race(Myself,DEMONIC)
		Race(Myself,RAKSHASA)
	TargetBlock(EnemiesInOrderShort)
	Range(scstarget,8)
	!GlobalTimerNotExpired("teleportfield","LOCALS")
	TriggerBlock(Enemy|Helpless)
	!HasItem("dw#move0",Myself)
	OR(2)
		Gender(Myself,ILLUSIONARY)
		!HasItem("dw#melf",Myself)
	OR(2)
		Gender(Myself,ILLUSIONARY)
		!HasItem("dw#eneb",Myself)
	OR(5)
		HaveSpell(WIZARD_CHARM_PERSON)
		HaveSpell(WIZARD_MAGIC_MISSILE)
		HaveSpell(WIZARD_FLAME_ARROW)
		HaveSpell(WIZARD_MELF_ACID_ARROW)
		HaveSpell(WIZARD_CHROMATIC_ORB) // crude check for having any spells left
THEN DO
		RandomWalkContinuous() // I'd love to say "ruuuun", but people have a bad habit of transitioning
END


IF
	See(NearestEnemyOf(Myself))
	Range(NearestEnemyOf(Myself),20)
	!GlobalTimerNotExpired("teleportfield","LOCALS")
	!HasItem("dw#move0",Myself)
	!HasItem("ghoult",Myself)
	OR(3)
		Gender(Myself,ILLUSIONARY)
		!HasItem("dw#melf",Myself)
		StateCheck(Myself,STATE_INVISIBLE)
	OR(3)
		Gender(Myself,ILLUSIONARY)
		!HasItem("dw#eneb",Myself)
		StateCheck(Myself,STATE_INVISIBLE)
	OR(5)
		HaveSpell(WIZARD_CHARM_PERSON)
		HaveSpell(WIZARD_MAGIC_MISSILE)
		HaveSpell(WIZARD_FLAME_ARROW)
		HaveSpell(WIZARD_MELF_ACID_ARROW)
		HaveSpell(WIZARD_CHROMATIC_ORB) // crude check for having any spells left
THEN	
	RESPONSE #100
		RandomWalk()
END

IF
	See(NearestEnemyOf(Myself))
	Range(NearestEnemyOf(Myself),20)
	GlobalTimerNotExpired("teleportfield","LOCALS")
	OR(3)
		Gender(Myself,ILLUSIONARY)
		!HasItem("dw#melf",Myself)
		StateCheck(Myself,STATE_INVISIBLE)
	OR(3)
		Gender(Myself,ILLUSIONARY)
		!HasItem("dw#eneb",Myself)
		StateCheck(Myself,STATE_INVISIBLE)
THEN	
	RESPONSE #100
		NoAction()
END

// in timestops, don't use missiles (a little cheesy, and also it wastes time)

IF
  GlobalTimerNotExpired("DMWWTimestopRunning","GLOBAL")
  THEN
  RESPONSE #100
  	   NoAction()
END
DEFAULT TRIGGER(!StateCheck(Myself,STATE_INVISIBLE)GlobalTimerNotExpired("castspell","LOCALS"))

BEGIN LOOP(MyWeaponStrength||Unknown)
BEGIN LOOP(NeverMelee||True)
INCLUDE FILE(%scsroot%/genai/ssl/shoot.ssl)
END LOOP
END LOOP

DEFAULT TRIGGER(GlobalTimerNotExpired("castspell","LOCALS"))


IF
	See(NearestEnemyOf(Myself))
	!HaveSpell(WIZARD_CHARM_PERSON)
	!HaveSpell(WIZARD_MAGIC_MISSILE)
	!HaveSpell(WIZARD_FLAME_ARROW)
	!HaveSpell(WIZARD_MELF_ACID_ARROW)
	!HaveSpell(WIZARD_CHROMATIC_ORB) // crude check for having any spells left
	OR(2)
	     Range(NearestEnemyOf(Myself),5)
	     !HasItem("dw#move0",Myself)
THEN
	RESPONSE #100
		AttackReevaluate(NearestEnemyOf(Myself),30)
END
