/////////////////////////////////////////////////////////
/////      Initial material
////////////////////////////////////////////////////////

INCLUDE FILE (%scsroot%/mage/ssl/bg2/generalblocks\rest.ssl)
INCLUDE FILE (%scsroot%/mage/ssl/bg2/generalblocks\findhidden.ssl)
INCLUDE FILE (%scsroot%/mage/ssl/bg2/generalblocks\gohostile.ssl)

VARIABLE(ImmuneToNormal=True)
VARIABLE(ImmuneToL5=True)
VARIABLE(IsLich=True)
VARIABLE(HasL1=True)
VARIABLE(HasL2=True)
VARIABLE(HasL3=True)
VARIABLE(HasL4=True)
VARIABLE(HasL5=True)
VARIABLE(HasL6=True)
VARIABLE(HasL7=True)
VARIABLE(HasL8=True)
VARIABLE(HasL9=True)
VARIABLE(IsConjurer=True)

///////////////////////////////////////////////////////
// Long-term defences
//////////////////////////////////////////////////////
IF
	!GlobalGT("longprep","LOCALS",0)
THEN
	RESPONSE #100
		ApplySpellRES("%WIZARD_STONE_SKIN_PREBUFF%",Myself)
		ApplySpellRES("%WIZARD_PROTECTION_FROM_MAGIC_ENERGY_PREBUFF%",Myself)
		ApplySpellRES("%WIZARD_PROTECTION_FROM_FIRE_PREBUFF%",Myself)
		ApplySpellRES("%WIZARD_PROTECTION_FROM_ACID_PREBUFF%",Myself)
		SetGlobal("longprep","LOCALS",1)
		Continue()
END

////////////////////////////////////////////////////////////////////////
/////////	triggers
////////////////////////////////////////////////////////////////////////

IF
	!Global("setuptriggers","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("SpellTrigger","LOCALS",2)
		SetGlobal("SpellSequencer","LOCALS",3)
		SetGlobal("MinorSequencer","LOCALS",2)
		SetGlobal("setuptriggers","LOCALS",1)
		Continue()
END

///////////////////////////////////////////////////////////////////
///  Set the redefend timer, to avoid recasting everything
////////////////////////////////////////////////////////////////////

IF
	Global("instantprep","LOCALS",0)
	See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		SetGlobal("instantprep","LOCALS",1)
		SetGlobalTimer("redefend","LOCALS",3)
		Continue()
END





/////////////////////////////////////////////////////////////////////////
///	Enter fight mode if you detect trouble (mostly for undead)
/////////////////////////////////////////////////////////////////////////

IF
	Allegiance(Myself,ENEMY)
    OR(12)
      AttackedBy([GOODCUTOFF.0.0.0.0.SUMMONED],DEFAULT)
      AttackedBy([PC],DEFAULT)
      AttackedBy([FAMILIAR],DEFAULT)
      AttackedBy([ALLY],DEFAULT)
      AttackedBy([CONTROLLED],DEFAULT)
      AttackedBy([CHARMED],DEFAULT)
      SpellCastOnMe([GOODCUTOFF.0.0.0.0.SUMMONED],0)
      SpellCastOnMe([PC],0)
      SpellCastOnMe([FAMILIAR],0)
      SpellCastOnMe([ALLY],0)
      SpellCastOnMe([CONTROLLED],0)
      SpellCastOnMe([CHARMED],0) 
!Global("inafight","LOCALS",1)
  !See(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    SetGlobal("inafight","LOCALS",1)
    Continue()
END	

////////////////////////////////////////////////////////////////////////
// Initialise for combat
////////////////////////////////////////////////////////////////////////

IF
	Global("inafight","LOCALS",0)
	See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		SetGlobal("inafight","LOCALS",1)
		Continue()
END


IF
	!Global("ChainContingencyFired","LOCALS",1)
	Allegiance(Myself,ENEMY)
	!Global("inafight","LOCALS",1)
	CheckStatGT(Myself,17,Level) // this is for the benefit of liches, to help them detect pro/undead enemies
	OR(7)
		Detect(NearestEnemyOf(Myself))
		BEGIN LOOP(scsplayer||1;2;3;4;5;6)
			Range(Playerscsplayer,15)
		END LOOP
THEN 
	RESPONSE #100
		SetGlobal("inafight","LOCALS",1)
		Continue()
END


//////////////////////////////////////////////////////////////////////
///CPU saver
//////////////////////////////////////////////////////////////////////

IF
	!Detect(NearestEnemyOf(Myself))
	!HPPercentLT(Myself,100)
	!Global("inafight","LOCALS",1)
THEN
	RESPONSE #100
		NoAction()
END


////////////////////////////////////////////////////////////////////////
/////////	Contingency
////////////////////////////////////////////////////////////////////////

IF
	!Global("ChainContingencyFired","LOCALS",1)
THEN
	RESPONSE #100
			SetGlobal("ChainContingencyFired","LOCALS",1)
			DisplayStringHead(Myself,26328)
			ReallyForceSpell(Myself,WIZARD_MORDENKAINENS_SWORD)
			ReallyForceSpell(Myself,WIZARD_SPELL_IMMUNITY_ABJURATION)
			ReallyForceSpell(Myself,WIZARD_SPELL_TURNING)
			DisplayStringHead(Myself,25942)
			SetGlobal("contingency","LOCALS",1)
			ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
			SetGlobalTimer("redefend","LOCALS",3)
			SetGlobal("inafight","LOCALS",1)
			Continue()
END

VARIABLE(lich_with_alacrity=fire_lich)
INCLUDE FILE (%scsroot%/mage/ssl/bg2/main\dw#lich.ssl)
