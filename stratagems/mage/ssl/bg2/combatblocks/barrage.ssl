////////////////////////////////////////////////////////////////////////////
///	This is specifically for an improved-alacrity mage using the
///	Cloak of Vecna
////////////////////////////////////////////////////////////////////////////

///// Magic Missiles

IF TRIGGER
	HaveSpell(WIZARD_MAGIC_MISSILE)
	CheckStatGT(Myself,0,WIZARD_IMPROVED_ALACRITY)
	TargetBlock(PCsPreferringWeak)
	RequireBlock(IsAzamantes)
	TriggerBlock(MR100|MinorGlobe|SpellTurnNew|Enemy|MagicMissile|ResistMagicDamage)
	!CheckStatGT(scstarget,30,MAGICDAMAGERESISTANCE)
THEN DO
	Combine()
	Action(Literal)
	Spell(scstarget,WIZARD_MAGIC_MISSILE)
	Spell(scstarget,WIZARD_MAGIC_MISSILE)
	Spell(scstarget,WIZARD_MAGIC_MISSILE)
	Continue()
END

//// Acid Arrows

IF TRIGGER
	HaveSpell(WIZARD_MELF_ACID_ARROW)
	CheckStatGT(Myself,0,WIZARD_IMPROVED_ALACRITY)
	Allegiance(Myself,ENEMY)
	TargetBlock(PCSpellcasters)
	RequireBlock(IsAzamantes)
	TriggerBlock(MR100|MirrorImage|MinorGlobe|ArrowSafe|SpellTurnNew|Enemy)
THEN DO
	Combine()
	Action(Literal)
	Spell(scstarget,WIZARD_MELF_ACID_ARROW)
	Spell(scstarget,WIZARD_MELF_ACID_ARROW)
	Spell(scstarget,WIZARD_MELF_ACID_ARROW)
	Continue()
END

////// Fireballs

IF TRIGGER
	HaveSpell(WIZARD_FIREBALL)
	CheckStatGT(Myself,0,WIZARD_IMPROVED_ALACRITY)
	Allegiance(Myself,ENEMY)
	TargetBlock(PlayersInRandomOrder)
	RequireBlock(IsAzamantes)
	TriggerBlock(MR100|ResistFire|MinorGlobe|Enemy)
THEN DO
	Action(Literal)
	Spell(scstarget,WIZARD_FIREBALL)
	Continue()
END
	
////// Breach

IF TRIGGER
	HaveSpell(WIZARD_BREACH)
	!GlobalTimerNotExpired("barragebreach","LOCALS")
	CheckStatGT(Myself,0,WIZARD_IMPROVED_ALACRITY)
	Allegiance(Myself,ENEMY)
	TargetBlock(PlayersInRandomOrder)
	RequireBlock(IsAzamantes)
	TriggerBlock(SpellTurn|SpellTurnNew|Enemy|Helpless)
	OR(5)
		CheckStatGT(scstarget,99,WIZARD_PROTECTION_FROM_MAGIC_ENERGY)
		!CheckStatGT(scstarget,-10,SAVEVSSPELL) // a rough indicator of a potion of Magic Shielding
		CheckStatGT(scstarget,0,CLERIC_CHAOTIC_COMMANDS)
		CheckStatGT(scstarget,0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		CheckStatGT(scstarget,2,STONESKINS)
THEN DO
	Action(Literal)
	SetGlobal("barragebreach","LOCALS",3)
	Spell(scstarget,WIZARD_BREACH)
	Continue()
END

/// Anti-magic

BEGIN LOOP(scsanti||WIZARD_RUBY_RAY_OF_REVERSAL;WIZARD_WARDING_WHIP;WIZARD_SECRET_WORD)


IF TRIGGER
	HaveSpell(scsanti)
	TargetBlock(PCsInOrder)
	CheckStatGT(Myself,0,WIZARD_IMPROVED_ALACRITY)
	RequireBlock(IsAzamantes)
	OR(2)
		CheckStatGT(scstarget,0,WIZARD_SPELL_DEFLECTION)
		CheckStatGT(scstarget,0,WIZARD_SPELL_TURNING)
	TriggerBlock(Enemy|Helpless)
	!GlobalTimerNotExpired("barrage_antimagic","LOCALS")
THEN DO
	Action(Literal)
	SetGlobalTimer("barrage_antimagic","LOCALS",3)
	Spell(scstarget,scsanti)
	Continue()
END

END LOOP

