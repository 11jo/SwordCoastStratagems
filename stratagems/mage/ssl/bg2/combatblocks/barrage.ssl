////////////////////////////////////////////////////////////////////////////
///	This is specifically for an improved-alacrity mage using the
///	Cloak of Vecna
////////////////////////////////////////////////////////////////////////////

///// Magic Missiles

IF TRIGGER
	HaveSpell(WIZARD_MAGIC_MISSILE)
	CheckStatGT(Myself,0,WIZARD_IMPROVED_ALACRITY)
	TargetBlock(PCsPreferringWeak)
	RequireBlock(IsAzamantes)
	!HasItemEquiped("clck26",scstarget)
	TriggerBlock(MR100|SIEvocation|MinorGlobe|SpellTurn|Enemy|MagicMissile|L1safe|ResistMagicDamage)
	!CheckStatGT(scstarget,30,MAGICDAMAGERESISTANCE)
THEN DO
	Combine()
	Action(Literal)
	Spell(scstarget,WIZARD_MAGIC_MISSILE)
	Spell(scstarget,WIZARD_MAGIC_MISSILE)
	Spell(scstarget,WIZARD_MAGIC_MISSILE)
	Continue()
END

//// Acid Arrows

IF TRIGGER
	HaveSpell(WIZARD_MELF_ACID_ARROW)
	CheckStatGT(Myself,0,WIZARD_IMPROVED_ALACRITY)
	Allegiance(Myself,ENEMY)
	TargetBlock(PCSpellcasters)
	RequireBlock(IsAzamantes)
	!HasItemEquiped("clck26",scstarget)
	TriggerBlock(MR100|SIConjuration|MirrorImage|MinorGlobe|ArrowSafe|SpellTurn|Enemy)
	!CheckStatGT(scstarget,30,MAGICDAMAGERESISTANCE)
THEN DO
	Combine()
	Action(Literal)
	Spell(scstarget,WIZARD_MELF_ACID_ARROW)
	Spell(scstarget,WIZARD_MELF_ACID_ARROW)
	Spell(scstarget,WIZARD_MELF_ACID_ARROW)
	Continue()
END

////// Fireballs

IF TRIGGER
	HaveSpell(WIZARD_FIREBALL)
	CheckStatGT(Myself,0,WIZARD_IMPROVED_ALACRITY)
	Allegiance(Myself,ENEMY)
	TargetBlock(PlayersInRandomOrder)
	RequireBlock(IsAzamantes)
	!HasItemEquiped("clck26",scstarget)
	TriggerBlock(MR100|ResistFire|SIEvocation|MinorGlobe|Enemy)
	!StateCheck(scstarget,STATE_MIRRORIMAGE)
THEN DO
	Action(Literal)
	Spell(scstarget,WIZARD_FIREBALL)
	Continue()
END
	
////// Breach

IF TRIGGER
	HaveSpell(WIZARD_BREACH)
	!GlobalTimerNotExpired("barragebreach","LOCALS")
	CheckStatGT(Myself,0,WIZARD_IMPROVED_ALACRITY)
	Allegiance(Myself,ENEMY)
	TargetBlock(PlayersInRandomOrder)
	RequireBlock(IsAzamantes)
	TriggerBlock(SpellTurn|SIAbjuration|Enemy|Helpless)
	OR(5)
		CheckStatGT(scstarget,99,WIZARD_PROTECTION_FROM_MAGIC_ENERGY)
		!CheckStatGT(scstarget,-10,SAVEVSSPELL) // a rough indicator of a potion of Magic Shielding
		CheckStatGT(scstarget,0,CLERIC_CHAOTIC_COMMANDS)
		CheckStatGT(scstarget,0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		CheckStatGT(scstarget,2,STONESKINS)
THEN DO
	Action(Literal)
	SetGlobal("barragebreach","LOCALS",3)
	Spell(scstarget,WIZARD_BREACH)
	Continue()
END

/// Anti-magic

BEGIN LOOP(scsanti||WIZARD_RUBY_RAY_OF_REVERSAL;WIZARD_WARDING_WHIP;WIZARD_SECRET_WORD)


IF TRIGGER
	HaveSpell(scsanti)
	TargetBlock(PCsInOrder)
	CheckStatGT(Myself,0,WIZARD_IMPROVED_ALACRITY)
	RequireBlock(IsAzamantes)
	OR(3)
		CheckStatGT(scstarget,0,WIZARD_SPELL_IMMUNITY)
		CheckStatGT(scstarget,0,WIZARD_SPELL_DEFLECTION)
		CheckStatGT(scstarget,0,WIZARD_SPELL_TURNING)
	TriggerBlock(Enemy|Helpless)
	!GlobalTimerNotExpired("barrage_antimagic","LOCALS")
THEN DO
	Action(Literal)
	SetGlobalTimer("barrage_antimagic","LOCALS",3)
	Spell(scstarget,scsanti)
	Continue()
END

END LOOP

