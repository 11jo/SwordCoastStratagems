///////////////////////////////////////////////////////////////////////
///  ATTACK PHASE 3: Chaos
///////////////////////////////////////////////////////////////////////

IF TRIGGER
	IgnoreBlock(Indiscriminate)
	RequireBlock(HasL5)
	IgnoreBlock(IsEvoker)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(MR|Confused|ConfuseProof|SIEnchantment|Enemy|Helpless)
	!CheckStatLT(scstarget,0,SAVEVSSPELL)
THEN DO
	Combine()
	Action(SpellArea,WIZARD_CHAOS|150|50)
END

IF TRIGGER
	IgnoreBlock(Indiscriminate)
	RequireBlock(HasL5)
	IgnoreBlock(IsEvoker)
	StateCheck(Myself,STATE_BLIND)
	TriggerBlock(PlayersInShortRange)
THEN DO
	Action(SpellMyself,WIZARD_CHAOS|150|50)
END

IF TRIGGER
	RequireBlock(Indiscriminate)
	RequireBlock(HasL5)
	IgnoreBlock(IsEvoker)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(MR|Confused|ConfuseProof|SIEnchantment|Helpless)
	!CheckStatLT(scstarget,0,SAVEVSSPELL)
THEN DO
	Combine()
	Action(SpellArea,WIZARD_CHAOS|150|50)
END

//////////////////////////////////////////////////////////////////////
///	ATTACK PHASE 4: Area-of-effect magic
///
///	Chain Lightning
//////////////////////////////////////////////////////////////////////

IF TRIGGER
	IgnoreBlock(Indiscriminate)
	RequireBlock(HasL6)
	IgnoreBlock(IsEnchanter)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(Enemy|MR|ResistElectric|SIEvocation)
	!HasItemEquiped("clck26",scstarget)
	slowspell
THEN DO
	Combine()
	Action(Spell,WIZARD_CHAIN_LIGHTNING|150|50)
END

IF TRIGGER
	RequireBlock(Indiscriminate)
	RequireBlock(HasL6)
	IgnoreBlock(IsEnchanter)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(MR|ResistElectric|SIEvocation)
	!HasItemEquiped("clck26",scstarget)
	slowspell
THEN DO
	Combine()
	Action(Spell,WIZARD_CHAIN_LIGHTNING|150|50)
END

//////////////////////////////////////////////////////////////////////
///	
///	Sunfire
//////////////////////////////////////////////////////////////////////

IF TRIGGER
	IgnoreBlock(Indiscriminate)
	IgnoreBlock(IsRakshasa)
	RequireBlock(HasL5)
	IgnoreBlock(IsEnchanter)
	IgnoreBlock(IsConjurerStreamline)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(Enemy|MR|ResistFire|SIEvocation)
	!GlobalTimerNotExpired("DMWWTimestopRunning","GLOBAL")
	Range(scstarget,10)
	!NumCreatureGT([ENEMY.HUMANOID],1)
	!NumCreatureGT([ENEMY.GIANTHUMANOID],1)
	!NumCreatureGT([ENEMY.MONSTER],1)
        !HasItemEquiped("clck26",scstarget)
	slowspell
THEN DO
	Combine()
	Action(SpellMyself,WIZARD_SUN_FIRE|150|50)
END

IF TRIGGER
	IgnoreBlock(Indiscriminate)
	RequireBlock(IsRakshasa)
	RequireBlock(HasL5)
	IgnoreBlock(IsEnchanter)
	IgnoreBlock(IsConjurerStreamline)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(Enemy|MR|ResistFire|SIEvocation)
	!GlobalTimerNotExpired("DMWWTimestopRunning","GLOBAL")
	Range(scstarget,10)
        !HasItemEquiped("clck26",scstarget)
	slowspell
THEN DO
	Combine()
	Action(SpellMyself,WIZARD_SUN_FIRE|150|50)
END

IF TRIGGER
	IgnoreBlock(IsEvokerStreamline)
	IgnoreBlock(IsConjurerStreamline)
	IgnoreBlock(IsEnchanterStreamline)
        TargetBlock(PCsPreferringWeak)
	slowspell
	RequireBlock(HasL5)
THEN DO
	Action(Spell,WIZARD_ANIMATE_DEAD|100|50)
END

