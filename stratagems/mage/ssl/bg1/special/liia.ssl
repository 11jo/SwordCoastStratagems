VARIABLE(HasL1=True)
VARIABLE(HasL2=True)
VARIABLE(HasL3=True)
VARIABLE(HasL4=True)
VARIABLE(HasL5=True)
VARIABLE(HasL6=False)
VARIABLE(HasL7=False)
VARIABLE(HasL8=False)
VARIABLE(HasL9=False)
VARIABLE(IsEnchanter=True)
VARIABLE(NeverGoHostile=True)


IF
	AttackedBy([PC],DEFAULT)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		CreateCreature("%tutu_var%WINSKI",[693.578],0) // Winski Perorate
		ActionOverride("%tutu_var%WINSKI",ForceSpell(Player1,SAREVOK_STRIKE))
		Wait(2)
		Kill(Player1)
		SetInterrupt(TRUE)
END

IF
	Dead("GDOPP7")
	Dead("GDOPP71")
	Dead("GDOPP72")
	Dead("GDOPP73")
	Dead("GDOPP74")
	Dead("GDOPP75")
	Global("SarevokBehavior","GLOBAL",0)
	Dead("Belt")
THEN
	RESPONSE #100
		Dialogue([PC])
END

IF
	Global("SarevokBehavior","GLOBAL",1)
	Global("DukeThanks","GLOBAL",0)
	Dead("Belt")
THEN
	RESPONSE #100
		Dialogue([PC])
END


//////////////////////////////////////////////////////////////////////////////////
///
///   Define actions
///
////////////////////////////////////////////////////////////////////////////////////////

BEGIN LOOP(scsspellsubstitute1||!StateCheck(scstarget,STATE_NOT_TARGETABLE))
BEGIN LOOP(scsspellsubstitute2||!StateCheck(scstarget,STATE_INVISIBLE))
	INCLUDE FILE(%scsroot%/mage/ssl/mage_definitions.ssl)
END LOOP
END LOOP

//////////////////////////////////////////////////////////////////////////////////
//      Handle fear
//////////////////////////////////////////////////////////////////////////////////

IF
  StateCheck(Myself,STATE_PANIC)
THEN
    RESPONSE #100
             RandomWalkContinuous()
END

//////////////////////////////////////////////////////////////////////////////////
////	Prep
//////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/mage/ssl/bg1/main/magesetup.ssl)


//////////////////////////////////////////////////////////////////////////
//	Melee
///////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/mage/ssl/bg1/meleeblocks/standard_mage.ssl)

//////////////////////////////////////////////////////////////////////////
// Renew and retreat
//////////////////////////////////////////////////////////////////////////

DEFAULT TRIGGER(!GlobalTimerNotExpired("castspell","LOCALS"))

INCLUDE FILE(%scsroot%/mage/ssl/bg1/generalblocks/renew.ssl)

//////////////////////////////////////////////////////////////////////////
// Combat spells - Blindness, Flame Arrow, Slow, Power Word: Sleep, Emotion: Hopelessness, Confusion (I assume that's all she might be carrying)
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
   TargetBlock(EnemiesInOrderShort)
   TriggerBlock(Disabled)
THEN DO
     Action(Spell,WIZARD_EMOTION_HOPELESSNESS|100|100)
     Action(Spell,WIZARD_CONFUSION|100|100)
END

IF TRIGGER
   TargetBlock(EnemiesInOrderShort)
   TriggerBlock(Disabled|MinorGlobe)
   HPLT(scstarget,20)
THEN DO
   Action(Spell,WIZARD_POWER_WORD_SLEEP)
END

IF TRIGGER
   TargetBlock(EnemiesInOrderShort)
   TriggerBlock(Disabled|MinorGlobe)
THEN DO
     Action(Spell,WIZARD_SLOW|100|100)
     Action(Spell,WIZARD_FLAME_ARROW|100|100)
     Action(Spell,WIZARD_BLINDNESS|100|100)
END



