///////////////////////////////////////////////////////////////
/////
/////  Intended as a low-grade mage AI at about the same level as
/////  the vanilla AI, but using the wider range of (BG1+BG2)
/////  spells SCS provides.
/////
/////  We do some minimal checks (don't use disabling attacks against
/////  already-held opponents, notice minor globes, don't fireball
/////  yourself) but basically we just cast our spells in order.
/////
/////  We buff in the first round and every three rounds thereafter.
/////
/////  A la BG2, we get a free insta-cast Stoneskin, but only at L9+
/////
/////  We use these spells (most, but not all, SCS spells):
/////
/////  OFFENSIVE
/////
/////  L6  CHAIN_LIGHTNING POWER_WORD_SILENCE
/////  L5 MS3 CHAOS SUN_FIRE CONE_OF_COLD ANIMATE_DEAD DOMINATION
/////  L4 MS2 SPIDER_SPAWN EMOTION_HOPELESSNESS CONFUSION
/////  L3 MS1 FIREBALL LIGHTNING_BOLT HOLD_PERSON FLAME_ARROW DIRE_CHARM VAMPIRIC_TOUCH SKULL_TRAP
/////  L2 MELF_ACID_ARROW  HORROR GLITTERDUST RAY_OF_ENFEEBLEMENT
/////  L1 MAGIC_MISSILE CHROMATIC_ORB CHARM_PERSON BLINDNESS
/////
/////
/////  DEFENSIVE
/////
/////  L6 GLOBE_OF_INVULNERABILITY
/////  L5 SHADOW_DOOR
/////  L4 MINOR_GLOBE_OF_INVULNERABILITY IMPROVED_INVISIBILITY STONE_SKIN
/////  L3 GHOST_ARMOR PROTECTION_FROM_NORMAL_MISSILES
/////  L2 MIRROR_IMAGE VOCALIZE
/////  L1 ARMOR SHIELD

INCLUDE FILE(%scsroot%/genai/ssl/easy_shared.ssl)
INCLUDE FILE(%scsroot%/genai/ssl/easy_definitions.ssl)




/////////////////////////
////   Buffing
/////////////////////////

//// Free stoneskin


IF TRIGGER
        RequireBlock(EasyOptionAvailable)
        
        See(NearestEnemyOf(Myself))
	Global("longprep","LOCALS",0)
	RequireBlock(HasL5)
THEN DO
		Action(Literal)
                ApplySpellRES("%WIZARD_STONE_SKIN_PREBUFF%",Myself)
		SetGlobal("longprep","LOCALS",1)
		Continue()
END


// invisibility

IF TRIGGER
        RequireBlock(EasyOptionAvailable)
        RequireBlock(HasL5)
        !StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
        
        See(NearestEnemyOf(Myself))
THEN DO
     Action(EasySpellMyself,WIZARD_SHADOW_DOOR)
END

IF TRIGGER
        RequireBlock(EasyOptionAvailable)
        RequireBlock(HasL4)
        !StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
        
        See(NearestEnemyOf(Myself))
THEN DO
     Action(EasySpellMyself,WIZARD_IMPROVED_INVISIBILITY)
END

// spell protection

IF TRIGGER
        RequireBlock(EasyOptionAvailable)
        RequireBlock(HasL6)
        !CheckStatGT(Myself,0,MINORGLOBE)

        See(NearestEnemyOf(Myself))
THEN DO
     Action(EasySpellMyself,WIZARD_GLOBE_OF_INVULNERABILITY)
END

IF TRIGGER
        RequireBlock(EasyOptionAvailable)
        RequireBlock(HasL4)
        !CheckStatGT(Myself,0,MINORGLOBE)
        
        See(NearestEnemyOf(Myself))
THEN DO
     Action(EasySpellMyself,WIZARD_MINOR_GLOBE_OF_INVULNERABILITY)
END

// MI and Stoneskin


IF TRIGGER
        RequireBlock(EasyOptionAvailable)
        RequireBlock(HasL4)
        !CheckStatGT(Myself,0,STONESKINS)
        
        See(NearestEnemyOf(Myself))
THEN DO
     Action(EasySpellMyself,WIZARD_STONE_SKIN)
END


IF TRIGGER
        RequireBlock(EasyOptionAvailable)
        RequireBlock(HasL2)
        !StateCheck(Myself,STATE_MIRRORIMAGE)
        
        See(NearestEnemyOf(Myself))
THEN DO
     Action(EasySpellMyself,WIZARD_MIRROR_IMAGE)
END

// other

IF TRIGGER
        RequireBlock(EasyOptionAvailable)
        RequireBlock(HasL3)
        
        See(NearestEnemyOf(Myself))
THEN DO
     Action(EasySpellMyself,WIZARD_GHOST_ARMOR)
     Action(EasySpellMyself,WIZARD_PROTECTION_FROM_NORMAL_MISSILES)
END

IF TRIGGER
        RequireBlock(EasyOptionAvailable)
        
        See(NearestEnemyOf(Myself))
THEN DO
     Action(EasySpellMyself,WIZARD_ARMOR)
     Action(EasySpellMyself,WIZARD_SHIELD)
END

/////////////////////////
////   Short range attacks
/////////////////////////

IF TRIGGER
        RequireBlock(EasyOptionAvailable)
        
        RequireBlock(HasL5)
        Target(NearestEnemyOf(Myself))
        Range(scstarget,5)
THEN DO
     Action(EasySpellArea,WIZARD_CONE_OF_COLD)
     Action(EasySpellArea,WIZARD_SUN_FIRE)
END

IF TRIGGER
        RequireBlock(EasyOptionAvailable)
        
        RequireBlock(HasL3)
        Target(NearestEnemyOf(Myself))
        Range(scstarget,5)
THEN DO
     Action(EasySpell,WIZARD_VAMPIRIC_TOUCH)
END

/////////////////////////
////   L5-6 sui generis spells
/////////////////////////

IF TRIGGER
        RequireBlock(EasyOptionAvailable)
        
        RequireBlock(HasL6)
        TargetBlock(EnemiesInOrderShort)
THEN DO
     Action(EasySpell,WIZARD_CHAIN_LIGHTNING)
END

IF TRIGGER
        RequireBlock(EasyOptionAvailable)
        
        RequireBlock(HasL6)
        TargetBlock(PCSpellcasters)
        TriggerBlock(Enemy)
THEN DO
     Action(EasySpell,WIZARD_POWER_WORD_SILENCE)
END

IF TRIGGER
        RequireBlock(EasyOptionAvailable)
        
        RequireBlock(HasL5)
        TargetBlock(EnemiesInOrderShort)
        TriggerBlock(Disabled)
THEN DO
     Action(EasySpell,WIZARD_DOMINATION)
END

/////////////////////////
////   L4-5 area effect spells
/////////////////////////

IF TRIGGER
        RequireBlock(EasyOptionAvailable)
        RequireBlock(HasL4)
        TargetBlock(EnemiesInOrderShort)
        TriggerBlock(Enemy|Helpless)
THEN DO
     Combine()
     Action(EasySpellArea,WIZARD_CHAOS)
     Action(EasySpellArea,WIZARD_MONSTER_SUMMONING_3)
     Action(EasySpellArea,WIZARD_CONFUSION)
     Action(EasySpellArea,WIZARD_SPIDER_SPAWN)
     Action(EasySpellArea,WIZARD_EMOTION_HOPELESSNESS)
     Action(EasySpellArea,WIZARD_MONSTER_SUMMONING_2)
END

/////////////////////////
////   L3 area-damage spells, if shielded for some
/////////////////////////

IF TRIGGER
        RequireBlock(EasyOptionAvailable)
        RequireBlock(HasL3)
        TargetBlock(EnemiesInOrderShort)
        TriggerBlock(MinorGlobe)
        OR(3)
            CheckStatGT(Myself,0,MINORGLOBE)
            HPPercentLT(Myself,50)
            CheckStatLT(Myself,7,Level)
THEN DO
        Action(EasySpellArea,WIZARD_FIREBALL)
        Action(EasySpellArea,WIZARD_SKULL_TRAP)
END

IF TRIGGER
        RequireBlock(EasyOptionAvailable)
        RequireBlock(HasL3)
        TargetBlock(EnemiesInOrderShort)
THEN DO
        Action(EasySpell,WIZARD_LIGHTNING_BOLT)
END

/////////////////////////
////   Sub-L3 area effect
/////////////////////////

IF TRIGGER
        RequireBlock(EasyOptionAvailable)
        RequireBlock(HasL2)
        TargetBlock(EnemiesInOrderShort)
        TriggerBlock(Enemy|Helpless|MinorGlobe)
THEN DO
     Combine()
     Action(EasySpellArea,WIZARD_SLOW)
     Action(EasySpellArea,WIZARD_HORROR)
     Action(EasySpellArea,WIZARD_GLITTERDUST)
END

/////////////////////////
////   Sub-L3 disabler
/////////////////////////

IF TRIGGER
        RequireBlock(EasyOptionAvailable)
        
        TargetBlock(EnemiesInOrderShort)
        TriggerBlock(Enemy|Helpless|MinorGlobe)
THEN DO
        Combine()
        Action(EasySpell,WIZARD_DIRE_CHARM)
        Action(EasySpell,WIZARD_HOLD_PERSON)
        Action(EasySpell,WIZARD_RAY_OF_ENFEEBLEMENT)
        Action(EasySpell,WIZARD_BLINDNESS)
        Action(EasySpell,WIZARD_CHARM_PERSON)
END

/////////////////////////
////   L3- attacks
/////////////////////////

IF TRIGGER
        RequireBlock(EasyOptionAvailable)

        TargetBlock(EnemiesInOrderShort)
        TriggerBlock(Enemy|MinorGlobe)
THEN DO
        Combine()
        Action(EasySpell,WIZARD_FLAME_ARROW)
        Action(EasySpell,WIZARD_MELF_ACID_ARROW)
        Action(EasySpell,WIZARD_MAGIC_MISSILE)
        Action(EasySpell,WIZARD_CHROMATIC_ORB)
END

/////////////////////////////
///   Thick-as-can-be melee block
////////////////////////////

IF TRIGGER
        RequireBlock(EasyOptionAvailable)
        TriggerBlock(EasyMinus)

        See(NearestEnemyOf(Myself))
THEN DO
        Action(Literal)
        Attack(NearestEnemyOf(Myself))
END

/////////////////////////////
///   Lock out the *real* AI
////////////////////////////

IF TRIGGER
        RequireBlock(EasyOptionAvailable)
        TriggerBlock(EasyMinus)

THEN DO
       Action(Nothing)
END

/////////////////////////////
///   Discard fake protective items if relevant
////////////////////////////

IF TRIGGER
        RequireBlock(EasyOptionAvailable)
        !Global("discardeasy","LOCALS",1)
THEN DO
         Action(Literal)
         SetGlobal("discardeasy","LOCALS",1)
         DestroyItem("%tutu_scriptm%agebrac")
         DestroyItem("%tutu_var%mage03")
         Continue()
END
