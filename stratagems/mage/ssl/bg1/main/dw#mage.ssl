//////////////////////////////////////////////////////////////////////////////////
///
///   Define actions
///
////////////////////////////////////////////////////////////////////////////////////////

BEGIN LOOP(scsspellsubstitute1||!StateCheck(scstarget,STATE_NOT_TARGETABLE))
BEGIN LOOP(scsspellsubstitute2||!StateCheck(scstarget,STATE_INVISIBLE))
	INCLUDE FILE(%scsroot%/mage/ssl/mage_definitions.ssl)
END LOOP
END LOOP


//////////////////////////////////////////////////////////////////////////////////
////	Deal with rogue undead
//////////////////////////////////////////////////////////////////////////////////

IF
	AttackedBy([ENEMY.UNDEAD],DEFAULT)
	Allegiance(LastAttackerOf(Myself),ENEMY)
	General(LastAttackerOf(Myself),UNDEAD)
THEN
	RESPONSE #100
		ClearActions(LastAttackerOf(Myself))
		Continue()
END


//////////////////////////////////////////////////////////////////////////////////
//      Handle fear
//////////////////////////////////////////////////////////////////////////////////

IF
  StateCheck(Myself,STATE_PANIC)
THEN
    RESPONSE #100
             RandomWalkContinuous()
END

//////////////////////////////////////////////////////////////////////////////////
////	Include basic block for Easy and lower difficulties
//////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/mage/ssl/bg1/generalblocks/easymage.ssl)


//////////////////////////////////////////////////////////////////////////////////
////	Prep
//////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/mage/ssl/bg1/main/magesetup.ssl)



//////////////////////////////////////////////////////////////////////////////////
////	If non-hostile, closedown - this script doesn't pretend to fight bad guys
//////////////////////////////////////////////////////////////////////////////////

IF
	!Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		NoAction()
END
////////////////////////////////////////////////////////////////////////////
///	Contingencies renew defences
////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/mage/ssl/bg1/generalblocks/contingency.ssl)


///////////////////////////////////////////////////////////////////////////
//	Melee
///////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/mage/ssl/bg1/generalblocks/melfsetup.ssl)
INCLUDE FILE(%scsroot%/mage/ssl/bg1/meleeblocks/standard_mage.ssl)

//////////////////////////////////////////////////////////////////////////
// Renew and retreat
//////////////////////////////////////////////////////////////////////////

DEFAULT TRIGGER(!GlobalTimerNotExpired("castspell","LOCALS"))

INCLUDE FILE(%scsroot%/mage/ssl/bg1/generalblocks/renew.ssl)


//////////////////////////////////////////////////////////////////////////
// Look for PCs
//////////////////////////////////////////////////////////////////////////

DEFAULT TRIGGER()

INCLUDE FILE(%scsroot%/mage/ssl/bg1/generalblocks/mill.ssl)

DEFAULT TRIGGER(!GlobalTimerNotExpired("castspell","LOCALS"))

INCLUDE FILE(%scsroot%/mage/ssl/bg1/generalblocks/chase.ssl)

////////////////////////////////////////////////////////////////////////////
// Core magic block
///////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/mage/ssl/bg1/magetypes/mage.ssl)

//////////////////////////////////////////////////////
///	If everyone is helpless, kill'em.
//////////////////////////////////////////////////////
	
IF
	See(NearestEnemyOf(Myself))
	OR(2)
		Class(Myself,MAGE)
		Class(Myself,MAGE_THIEF)
	BEGIN LOOP(scsloopvar||1;2;3;4;5;6)
		OR(3)
			!See(Playerscsloopvar)
			StateCheck(Playerscsloopvar,STATE_IMMOBILE)
			CheckStatGT(Playerscsloopvar,0,HELD)
	END LOOP
THEN
	RESPONSE #100
		AttackReevaluate(NearestEnemyOf(Myself),15)
END

INCLUDE FILE(%scsroot%/mage/ssl/bg1/generalblocks/closedown.ssl)
