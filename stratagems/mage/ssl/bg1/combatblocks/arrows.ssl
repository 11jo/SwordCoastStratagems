//////////////////////////////////////////////////////////////////////////
///  ATTACK PHASE 6: Use magic missiles to bring down mirror images
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	IgnoreBlock(IsEnchanter)
	TargetBlock(PCSpellcasters)
	TriggerBlock(MR100|Enemy|MagicMissile)
	StateCheck(scstarget,STATE_MIRRORIMAGE)
	Allegiance(Myself,ENEMY)
THEN DO
	Action(Spell,WIZARD_MAGIC_MISSILE)
END

///////////////////////////////////////////////////////////////////////////
///  ATTACK PHASE 7: Melfs and MMs & similar single-strike spells
///
///  Begin by hitting spellcasters with Melf
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	IgnoreBlock(IsInvoker)
	RequireBlock(HasL2)
	Allegiance(Myself,ENEMY)
	ConditionalTargetBlock(PCSpellcasters;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(PCSpellcasters)
	TriggerBlock(MR100|Stoneskin|MirrorImage|MinorGlobe|ArrowSafe|SpellTurn|Enemy)
THEN DO
	Action(Spell,WIZARD_MELF_ACID_ARROW|100|50)
END

//////////////////////////////////////////////////////////////////////////
///	Now hit basically anyone with MMs and Chromatic Orbs
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	IgnoreBlock(IsEnchanter)
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC)&HPPercentLT(scstarget,40))
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(MR100|MinorGlobe|SpellTurn|Enemy|MagicMissile)
THEN DO
	Combine()
	Action(Spell,WIZARD_CHROMATIC_ORB)
	Action(Spell,WIZARD_MAGIC_MISSILE)
END

IF TRIGGER
	IgnoreBlock(IsInvoker)
	RequireBlock(HasL2)
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(Enemy|MR100|Stoneskin|MirrorImage|MinorGlobe|ArrowSafe|SpellTurn)
THEN DO
	Action(Spell,WIZARD_MELF_ACID_ARROW)
END

//////////////////////////////////////////////////////////////////////////
///	At this point we put flame arrows, magic missiles and Melfs into PCs
//////////////////////////////////////////////////////////////////////////


IF TRIGGER
	TargetBlock(PlayersInOrder)
	Allegiance(Myself,ENEMY)
	TriggerBlock(MR100|SpellTurn|MinorGlobe)
THEN DO
	Action(Spell,WIZARD_FLAME_ARROW)
	Action(Spell,WIZARD_MELF_ACID_ARROW)
	Action(Spell,WIZARD_CHROMATIC_ORB)
	Action(Spell,WIZARD_MAGIC_MISSILE)
END

