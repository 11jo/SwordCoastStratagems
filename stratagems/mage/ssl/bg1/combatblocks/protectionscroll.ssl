/////////////////////////////////////////////////////////////////////////
////  If you're a high-level wizard (L10+), and alone, check if anyone 
////  nearby (i.e. nearest foe, presumably hacking away)
////  is using a Protection from Magic scroll or a potion of magic blocking. If they are, we have a 
////  two-stage response: (1) summon monsters (unless an invoker), and (2) dimension door to
////  attack someone else.
/////////////////////////////////////////////////////////////////////////

IF
	IgnoreBlock(IsInvoker)
	RequireBlock(HasL5)
	Allegiance(Myself,ENEMY)
	NumCreature([ENEMY.HUMANOID],1)
	See([PC])
	!StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)
	CheckStatGT(Myself,0,WIZARD_SPELL_TRAP)
	!Global("pmscroll","LOCALS",1)
THEN
	RESPONSE #100
		SetGlobalTimer("castspell","LOCALS",6)
		SetGlobal("pmscroll","LOCALS",1)
		SpellNoDec(LastSeenBy(Myself),WIZARD_MONSTER_SUMMONING_3)
END

IF
	RequireBlock(IsInvoker)
	RequireBlock(HasL5)
	Allegiance(Myself,ENEMY)
	NumCreature([ENEMY.HUMANOID],1)
	See([PC])
	!StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)
	CheckStatGT(Myself,0,WIZARD_SPELL_TRAP)
	!Global("pmscroll","LOCALS",1)
	CheckStatGT(Myself,9,Level)
THEN
	RESPONSE #100
		SetGlobal("pmscroll","LOCALS",1)
END


BEGIN LOOP(scsloopvar||1;2;3;4;5;6)

IF TRIGGER
	RequireBlock(HasL5)
	Global("pmscroll","LOCALS",1)
	Allegiance(Myself,ENEMY)
	!NumInParty(1)
	Multiple(!See(scsmultiple);PlayersInOrder*Playerscsloopvar)
	CheckStatGT(Myself,0,WIZARD_SPELL_TRAP)
THEN DO
	DisplayStringHead(Myself,@2112scsloopvar)	
	SetGlobal("pmscroll","LOCALS",2)
	Action(LiteralRandomTarget,SetGlobalTimer("castspell"\"LOCALS"\6)&ForceSpell(scsrandom\WIZARD_DIMENSION_DOOR),,PlayersInOrder*Playerscsloopvar)
END

END LOOP
