
////////////////////////////////////////////////////////////////////////////
///	Spell Worm
////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCSpellcasters)
	TriggerBlock(Enemy|Helpless|SIEnchantment|SpellTurn|HLAMage)
THEN DO
	Action(SpellL9RES,WIZARD_SPELL_WORM|100|100)
END

////////////////////////////////////////////////////////////////////////////
///	HLA enchantments
////////////////////////////////////////////////////////////////////////////

// mass domination before true dominion, since it might reveal who's vulnerable

IF TRIGGER
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(Charm|MR100|Helpless|Enemy|HLAMage)
	CheckStatGT(scstarget,0,SAVEVSSPELL)
THEN DO
	Combine()
	Action(SpellL9RES,WIZARD_MASS_DOMINATION|200|50)
END

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringStrong;CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(Charm|Helpless|Enemy|SpellTurn|HLAMage)
THEN DO
	Action(SpellL9RES,WIZARD_TRUE_DOMINION|200|50)
END

////////////////////////////////////////////////////////////////////////////
///	Medusa/Death Field
////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInOrder)
	Range(scstarget,20)
	TriggerBlock(Slay|MR100|Enemy|Helpless|SINecromancy|DirectDamageSafe|HLAMage)
THEN DO
	Action(SpellL9RESMyself,WIZARD_DEATH_FIELD|100|100)
END


IF TRIGGER
	StateCheck(Myself,STATE_BLIND)
	TriggerBlock(PlayersInRange|HLAMage)
THEN DO
	Action(SpellL9RESMyself,WIZARD_DEATH_FIELD|150|50)
END

IF TRIGGER
	TargetBlock(PCsInOrder)
	Range(scstarget,10)
	TriggerBlock(Slay|Enemy|Helpless|SIAlteration|HLAMage)
THEN DO
	Action(SpellL9RESMyself,WIZARD_GAZE_OF_THE_MEDUSA|100|100)
END


IF TRIGGER
	StateCheck(Myself,STATE_BLIND)
	TriggerBlock(PlayersInRange|HLAMage)
THEN DO
	Action(SpellL9RESMyself,WIZARD_WAIL_OF_THE_BANSHEE|100|100)
END

////////////////////////////////////////////////////////////////////////////
///	HLA single-target
////////////////////////////////////////////////////////////////////////////

// blood to ice

IF TRIGGER
	TargetBlock(PCsPreferringWeak)
	TriggerBlock(ResistCold100|Helpless|Enemy|SpellTurn|SINecromancy|HLAMage)
	!General(scstarget,UNDEAD)
THEN DO
	Action(SpellL9RES,WIZARD_BLOOD_TO_ICE|100|100)
END

// create golem

IF TRIGGER
	TargetBlock(EnemiesInOrder)
	TriggerBlock(Helpless|Enemy|SpellTurn|SIAlteration|HLAMage)
	CheckStatGT(scstarget,0,SAVEVSPOLY)
	!General(scstarget,UNDEAD)
	!General(scstarget,MONSTER)
	!General(scstarget,ANIMAL)
	!Race(scstarget,ELEMENTAL)
	!Race(scstarget,GOLEM)
THEN DO
	Action(SpellL9RES,WIZARD_CREATE_FLESH_GOLEM|100|100)
END

// abyssal fury

IF TRIGGER
	TargetBlock(PCsPreferringWeak)
	TargetBlock(Celestials)
	!CheckStatGT(scstarget,3,STONESKINS)
	TriggerBlock(Enemy|Helpless|SIConjuration|HLAMage)
THEN DO
	Action(SpellL9RES,WIZARD_ABYSSAL_FURY|100|100)
END



////////////////////////////////////////////////////////////////////////////
///	Dragon's Breath and Comet
////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	IgnoreBlock(IsBG1Only)
	IgnoreBlock(Indiscriminate)
	TargetBlock(PCsInOrder)
	TriggerBlock(ResistFire|Enemy|SIEvocation|HLAMage)
THEN DO
	Combine()
	Action(SpellL9AreaRES,WIZARD_DRAGONS_BREATH|200|50)
	Action(SpellL9AreaRES,WIZARD_COMET|200|50)
END

IF TRIGGER
    IgnoreBlock(IsBG1Only)
	RequireBlock(Indiscriminate)
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(ResistFire|SIEvocation|HLAMage)
	RequireBlock(HasL9)
THEN DO
	Combine()
	Action(SpellL9AreaRES,WIZARD_DRAGONS_BREATH|200|50)
	Action(SpellL9AreaRES,WIZARD_COMET|200|50)
END


///////////////////////////////////////////////////////////////////////
////   Malavon's Fury
///////////////////////////////////////////////////////////////////////

IF TRIGGER
        IgnoreBlock(IsBG1Only)
        RequireBlock(IsIWDArcane)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(Enemy|MR|SIEvocation|AoEMalavonAcid|HLAMage)
THEN DO
	Combine()
	Action(SpellL9RESMyself,WIZARD_MALAVONS_FURY|150|50)
END


////////////////////////////////////////////////////////////////////////////
/// Thunderstorm
////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	IgnoreBlock(IsBG1Only)
	IgnoreBlock(Indiscriminate)
	TargetBlock(PCsInOrder)
	TriggerBlock(ResistElectric65|Enemy|HLAMage)
	RequireBlock(HasL9)
THEN DO
	Combine()
	Action(SpellL9AreaRES,WIZARD_THUNDERSTORM|200|50)
END

IF TRIGGER
	IgnoreBlock(IsBG1Only)
	RequireBlock(Indiscriminate)
	TargetBlock(PCsInOrder)
	TriggerBlock(ResistElectric65|HLAMage)
	RequireBlock(HasL9)
THEN DO
	Combine()
	Action(SpellL9AreaRES,WIZARD_THUNDERSTORM|200|50)
END
