
//////////////////////////////////////////////////////////////////////////////
////	If you have a Planetar/Ultroloth, summon it
/////
//////////////////////////////////////////////////////////////////////////////

IF TRIGGER
        IgnoreBlock(IsBG1Only)
	TargetBlock(PCsPreferringWeak)
	TargetBlock(Celestials)
	TriggerBlock(Enemy|HLAMage)
	!See([ENEMY.0.DARKPLANATAR]) // only one at a time
	!See([ENEMY.0.PLANATAR])
	IgnoreBlock(Talents)
THEN DO
	Combine()
	Action(SpellL9AreaRESIgnoreDNT,WIZARD_SUMMON_PLANATAR_EVIL|150|50)
	Action(SpellL9AreaRESIgnoreDNT,WIZARD_SUMMON_PLANATAR_GOOD|150|50)
END

IF TRIGGER
    IgnoreBlock(IsBG1Only)
	TargetBlock(PCsPreferringWeak)
	TargetBlock(Celestials)
	TriggerBlock(Enemy|HLAMage)
	!See([ENEMY.0.DARKPLANATAR]) // only one at a time
	!See([ENEMY.0.PLANATAR])
	Alignment(Myself,MASK_GOOD)
	RequireBlock(Talents)
THEN DO
	Combine()
	Action(SpellL9AreaRESIgnoreDNT,WIZARD_SUMMON_PLANATAR_GOOD|150|50)
END

IF TRIGGER
    IgnoreBlock(IsBG1Only)
	TargetBlock(PCsPreferringWeak)
	TargetBlock(Celestials)
	TriggerBlock(Enemy|HLAMage)
	!See([ENEMY.0.DARKPLANATAR]) // only one at a time
	!See([ENEMY.0.PLANATAR])
	!Alignment(Myself,MASK_GOOD)
	RequireBlock(Talents)
THEN DO
	Combine()
	Action(SpellL9AreaRESIgnoreDNT,WIZARD_SUMMON_ULTROLOTH|150|50)
END

//////////////////////////////////////////////////////////////////////////////
////	Elemental swarm if you have it
//////////////////////////////////////////////////////////////////////////////

IF TRIGGER
    IgnoreBlock(IsBG1Only)
	TargetBlock(PCsPreferringWeak)
	TargetBlock(Celestials)
	TriggerBlock(Enemy|HLAMage)
THEN DO
	Combine()
	Action(SpellL9AreaRESIgnoreDNT,CLERIC_ELEMENTAL_SWARM_AIR|150|150)
	Action(SpellL9AreaRESIgnoreDNT,CLERIC_ELEMENTAL_SWARM_EARTH|150|150)
	Action(SpellL9AreaRESIgnoreDNT,CLERIC_ELEMENTAL_SWARM_FIRE|150|150)
	Action(SpellL9AreaRESIgnoreDNT,CLERIC_ELEMENTAL_SWARM_WATER|150|150)
END


//////////////////////////////////////////////////////////////////////////////
////	Boneguard + Dark Pact
//////////////////////////////////////////////////////////////////////////////

IF TRIGGER
    IgnoreBlock(IsBG1Only)
	TargetBlock(PCsPreferringWeak)
	TargetBlock(Celestials)
	TriggerBlock(Enemy|HLAMage)
THEN DO
	Combine()
	Action(SpellL9AreaRESIgnoreDNT,WIZARD_CREATE_GREATER_BONEGUARD|150|150)
END

IF TRIGGER
    IgnoreBlock(IsBG1Only)
	Target([ENEMY.0.0.SKELETON_WARRIOR]) // probably a boneguard
	TriggerBlock(HLAMage)
THEN DO
	Combine()
	Action(SpellL9RES,WIZARD_DARK_PACT|150|150)
END