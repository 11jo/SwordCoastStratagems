LAF include STR_VAR file=caster_shared.tph locbase=caster_shared END


//////////////////////////////////////////////////////////////////
////   Dragon weapons are at least +4 and have range 5
//////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION dragon_weapon BEGIN
   PATCH_MAKE_PATCH
      enchantmentGT=>4
      patch_ability_inline=>~ability_rangeGT=>5~
   END
   LPF apply_patches STR_VAR file_ext=ITM edits=patch_data END
END

//////////////////////////////////////////////////////////////////
////   Dragon breath bypasses MR, has long range, level 0
//////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION dragon_breath BEGIN
   PATCH_MAKE_PATCH
      secondary=>0 // not "offensive damage", so bypasses Cloak of Mirroring
      patch_ability_inline=>~ability_range=>320~
      patch_effect_inline'=>~resist_dispel=>0 power=>0~
   END
   LPF apply_patches STR_VAR file_ext=SPL edits=patch_data END
END

//////////////////////////////////////////////////////////////////
////   Baseline patch for dragons
//////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION dragon_general BEGIN
 LPF check_ini STR_VAR ini=bypass_dragon_immunity_changes RET value=value END
 PATCH_IF value=0 BEGIN
   PATCH_MAKE_PATCH
      immunity_to_opcode=>~74 13 55 211 19~//immunity to blindness/slay/death/imprisonment/Mind Flayer
      immunity_to_string=>~1474 32089~ // blindness, devour brain
      immunity_to_spell=>~%DRAGON_WING_BUFFET% %WIZARD_IMPRISONMENT%~
   END
   LPF apply_patches STR_VAR edits=patch_data file_ext=CRE END
 END
 PATCH_MAKE_PATCH
      class=>"if enhanced_edition then no_change else FIGHTER_MAGE_CLERIC"
      delete_opcodes=>~189~ // casting time speedup
      make_casting_innate=>dw#dragon_innate
      strip_scs_scripts=>null
 END
 LPF apply_patches STR_VAR edits=patch_data file_ext=CRE END
END

//////////////////////////////////////////////////////////////////
////   Extra hp for dragons
//////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION dragon_staying_power BEGIN
   LPF check_label STR_VAR label=dw#dragon_hp RET value END
   PATCH_IF value BEGIN
      LPF CRE_read_hp_current RET hp=value END
      LPF check_ini STR_VAR ini=dragon_staying_power RET value END
      SET arguments=(hp*value) / 100
      LPF CRE_hitpoints STR_VAR arguments END

   END

END
