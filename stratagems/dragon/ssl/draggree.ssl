///////////////////////////////////////////////////////////////////////
///	Usual pre-battle stuff
///////////////////////////////////////////////////////////////////////


BEGIN LOOP(scsspellsubstitute1||!GlobalTimerNotExpired("castspell","LOCALS"))
BEGIN LOOP(scsspellsubstitute2||!GlobalTimerNotExpired("castspell","LOCALS"))
	INCLUDE FILE(%scsroot%/mage/ssl/mage_definitions.ssl)
END LOOP
END LOOP
INCLUDE FILE(%scsroot%/dragon/ssl/dragondef.ssl)
INCLUDE FILE(%scsroot%/dragon/ssl/dragonsetup.ssl)

////////////////////////////////////////////////////////////////////////////////////////
////	Initial stoneskin
////////////////////////////////////////////////////////////////////////////////////////


IF	
	!GlobalTimerNotExpired("initialstoneskin","LOCALS")
	!See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		SetGlobalTimer("initialstoneskin","LOCALS",600)
		ApplySpell(Myself,DRAGON_STONE_SKIN)
		ApplySpell(Myself,WIZARD_PROTECTION_FROM_MAGIC_ENERGY)
END

////////////////////////////////////////////////////////////////////////////
////	initial dragonfear
////////////////////////////////////////////////////////////////////////////


IF TRIGGER // note we don't get here unless in a fight
   	   RequireBlock(IsFllyissetat)
	Target(Myself)
THEN DO
	Action(TriggerRESMyself,%WIZARD_IMPROVED_HASTE_INSTANT%,dw#0d592,%SPELL_DEFLECTION_NO_VISUAL_INSTANT%)
	ReallyForceSpell(NearestEnemyOf(Myself),DRAGON_FEAR)
	SetGlobalTimer("dragonbreath","LOCALS",1)
	SetGlobalTimer("redefend","LOCALS",3)
	Continue()
END

IF
   	   IgnoreBlock(IsFllyissetat)
	See(NearestEnemyOf(Myself))
	!GlobalTimerNotExpired("initialfear","LOCALS")
THEN
	RESPONSE #100
	SetGlobalTimer("initialfear","LOCALS",600)
	ReallyForceSpell(NearestEnemyOf(Myself),DRAGON_FEAR)
	SetGlobalTimer("dragonbreath","LOCALS",1)
	SetGlobalTimer("redefend","LOCALS",3)
	Continue()
END

//////////////////////////////////////////////////////////////////////////////
////	Heal
//////////////////////////////////////////////////////////////////////////////

IF
	Global("contingency","LOCALS",0)
	Detect(NearestEnemyOf(Myself))
	HPPercentLT(Myself,60)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,25942)
		SetGlobal("contingency","LOCALS",1)
		ReallyForceSpell(Myself,CLERIC_HEAL)
END

IF TRIGGER
	HPPercentLT(Myself,60)
THEN DO
	Action(SpellMyself,CLERIC_HEAL)
END

/////////////////////////////////////////////////////////////////////////////////
///	Pursue
////////////////////////////////////////////////////////////////////////////////	

//////////////////////////////////////////////////////////////////////////
///  Pursue!
///////////////////////////////////////////////////////////////////


INCLUDE FILE(%scsroot%/genai/ssl/chase.ssl)

//////////////////////////////////////////////////////////////////////////
///	Use breath weapon as often as possible
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInOrder)
	TargetBlock(EnemiesInOrder)
	!Range(scstarget,15)
	!CheckStatGT(scstarget,65,RESISTPOISON)
THEN DO
	Action(FastDragonBreath2,GREEN_DRAGON_BREATH,"DrGrBrHt","dragonbreath",black05)
END

IF
	!GlobalTimerNotExpired("dragonbreath","LOCALS")
	!GlobalTimerNotExpired("castspell","LOCALS")
	!GlobalTimerNotExpired("trytorun","LOCALS")
	OR(5)
		!CheckStatGT(NearestEnemyOf(Myself),65,RESISTPOISON)
		!CheckStatGT(SecondNearestEnemyOf(Myself),65,RESISTPOISON)
		!CheckStatGT(ThirdNearestEnemyOf(Myself),65,RESISTPOISON)
		!CheckStatGT(FourthNearestEnemyOf(Myself),65,RESISTPOISON)
		!CheckStatGT(FifthNearestEnemyOf(Myself),65,RESISTPOISON)
THEN
	RESPONSE #100
	         SetGlobalTimer("trytorun","LOCALS",6)
		RunAwayFrom(NearestEnemyOf(Myself),30)
END

//////////////////////////////////////////////////////////////////////////
///	Use wing buffet to drive away anything at all dangerous
//////////////////////////////////////////////////////////////////////////

INCLUDE FILE (%scsroot%/dragon/ssl/wingbuffet.ssl)

//////////////////////////////////////////////////////////////////////////
///	Try a dispel magic
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	!GlobalTimerNotExpired("firstdispel","LOCALS")
	TargetBlock(PCsInOrderShort)
THEN DO
	Combine()
	SetGlobalTimer("firstdispel","LOCALS",1000)
	Action(Spell,WIZARD_DISPEL_MAGIC|100|50)
	OnContinue(SetGlobalTimer("firstdispel","LOCALS",1000))
END

///////////////////////////////////////////////////////////////
///	Renew defences
////////////////////////////////////////////////////////////////


IF TRIGGER
	CheckStat(Myself,0,STONESKINS)
	GlobalTimerExpired("redefend","LOCALS")
	Target(Myself)
THEN DO
	Action(SpellInnate,DRAGON_STONE_SKIN|100|100)
END

IF TRIGGER
	CheckStat(Myself,0,CLERIC_DEFENSIVE_HARMONY)
	GlobalTimerExpired("redefend","LOCALS")
THEN DO
	Action(SpellMyself,CLERIC_DEFENSIVE_HARMONY|100|100)
END

/////////////////////////////////////////////////////////////////////////////
///	Breach enemy defences
/////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInOrder)
	TriggerBlock(SpellTurn|SpellTurnNew|Enemy|Helpless)
	!GlobalTimerNotExpired("breach","LOCALS")
	OR(4)
		CheckStatGT(scstarget,0,WIZARD_PROTECTION_FROM_MAGIC_ENERGY)
		CheckStatGT(scstarget,0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		CheckStatGT(scstarget,2,STONESKINS)
		CheckStat(scstarget,4,WIZARD_SPELL_IMMUNITY)
THEN DO
	Combine()
	SetGlobalTimer("breach","LOCALS",12)
	Action(Spell,WIZARD_BREACH|200|100)
END

	
/////////////////////////////////////////////////////////////////
///	High-powered offensive sequencer
/////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringStrong;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(MR100|ResistMagicDamage|Enemy)
THEN DO
	Action(Sequencer,CLERIC_UNHOLY_BLIGHT,CLERIC_UNHOLY_BLIGHT,CLERIC_UNHOLY_BLIGHT)
END

//////////////////////////////////////////////////////////////////////////////
///	Insects
//////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsPreferringWeak)
	!GlobalTimerNotExpired("insects","LOCALS")
THEN DO
	SetGlobalTimer("insects","LOCALS",18)
	Action(SpellInnate,BLACK_DRAGON_INSECT|200|100)
	Action(Spell,CLERIC_INSECT_PLAGUE|100|100)
END

//////////////////////////////////////////////////////////////////////////
/////	True Sight
//////////////////////////////////////////////////////////////////////////


IF TRIGGER
	TargetBlock(EnemiesInOrder)
	!CheckStatGT(FourthNearest([ENEMY]),0,TRUE_SIGHT)
	!CheckStatGT(ThirdNearest([ENEMY]),0,TRUE_SIGHT)
	!CheckStatGT(SecondNearest([ENEMY]),0,TRUE_SIGHT)
	!CheckStatGT([ENEMY],0,TRUE_SIGHT)
	!CheckStatGT(Myself,0,TRUE_SIGHT)
	OR(3)
		StateCheck(scstarget,STATE_IMPROVEDINVISIBILITY)
		StateCheck(scstarget,STATE_MIRRORIMAGE)
		Gender(scstarget,ILLUSIONARY)
	!Allegiance(scstarget,ENEMY)
	TriggerBlock(SIDivination)
THEN DO
	Action(SpellMyself,WIZARD_TRUE_SIGHT)
END

///////////////////////////////////////////////////////////////////////////////
///	Unholy Blight
///////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringStrong;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(MR100|ResistMagicDamage|Enemy)
THEN DO
	Action(Spell,CLERIC_UNHOLY_BLIGHT|200|100)
END


////////////////////////////////////////////////////////////////////////////
////	Attack magical defences
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInOrder)
	OR(5)
		CheckStatGT(scstarget,0,WIZARD_SPELL_TURNING)
		CheckStatGT(scstarget,0,WIZARD_SPELL_DEFLECTION)
		CheckStat(scstarget,1,WIZARD_SPELL_TRAP)
		CheckStatGT(scstarget,0,MINORGLOBE)
		CheckStatGT(scstarget,0,CLERIC_SHIELD_OF_THE_ARCHONS)
	TriggerBlock(Enemy|Helpless)
THEN DO
	Combine()
	Action(Spell,WIZARD_WARDING_WHIP|100|100)
	Action(Spell,WIZARD_SPELL_THRUST|100|100)
END

/////////////////////////////////////////////////////////////////
////	Power Words
//////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////////
///// Power Word: Silence
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCSpellcasters;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(PCSpellcasters)
	TriggerBlock(MR100|SpellTurnNew|SIEnchantment)
	TriggerBlock(Enemy|Helpless|Silence)
THEN DO
	Action(Spell,WIZARD_POWER_WORD_SILENCE|150|50)
END

////////////////////////////////////////////////////////////////////////
////  Power Word: Stun
/////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(PCsInOrderShort)
	TargetBlock(Celestials)
	TriggerBlock(MR100|SpellTurnNew|Stun|Enemy|Helpless|SIEnchantment)
	OR(3)
		HPPercentLT(scstarget,60)
		Class(scstarget,MAGE)
		Class(scstarget,MAGE_THIEF)
THEN DO
	Action(Spell,WIZARD_POWER_WORD_STUN|150|50)
END

/////////////////////////////////////////////////////////////////
///	Domination
//////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(MR|Charm|SIEnchantment|SpellTurnNew|Enemy|Helpless)
	!CheckStatLT(scstarget,0,SAVEVSSPELL)
THEN DO
	Action(Spell,WIZARD_DOMINATION|150|50)
END




//////////////////////////////////////////////////////////////////////////
/// Kill swords
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	Target([GOODCUTOFF.0.SWORD])
THEN DO
	Action(Spell,WIZARD_DEATH_SPELL)
	Action(Spell,WIZARD_MAGIC_MISSILE)
END

//////////////////////////////////////////////////////////////////////////
///  Use magic missiles to bring down mirror images
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCSpellcasters)
	TriggerBlock(MR100|Enemy|MinorGlobe|SpellTurnNew)
	StateCheck(scstarget,STATE_MIRRORIMAGE)
THEN DO
	Combine()
	Action(Spell,WIZARD_MAGIC_MISSILE)
END

///////////////////////////////////////////////////////////////////////////
// Hit spellcasters with Melf
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCSpellcasters;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(PCSpellcasters)
	TriggerBlock(MR100|MinorGlobe|ArrowSafe|SpellTurnNew|Enemy)
THEN DO
	Combine()
	Action(MinorSequencer,WIZARD_MAGIC_MISSILE,WIZARD_MAGIC_MISSILE)
	Action(Spell,WIZARD_MELF_ACID_ARROW)
END

//////////////////////////////////////////////////////////////////////////
///	Now hit basically anyone with MMs and acid arrows
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC)&HPPercentLT(scstarget,40))
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(MR100|MinorGlobe|SpellTurnNew|Enemy)
THEN DO
	Combine()
	Action(Spell,WIZARD_MAGIC_MISSILE)
END

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(MR100|MirrorImage|MinorGlobe|ArrowSafe|SpellTurnNew)
THEN DO
	Combine()
	Action(Spell,WIZARD_MELF_ACID_ARROW)
END

//////////////////////////////////////////////////////////////////////////////////
/////	Melee phase
//////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/dragon/ssl/melee.ssl)

////////////////////////////////////////////////////////
///	Closedown: avoid interaction with other scripts
////////////////////////////////////////////////////////////

IF
	!See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		NoAction()
END
