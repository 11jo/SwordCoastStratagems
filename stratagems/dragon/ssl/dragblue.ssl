IF
	HPLT(Myself,20)
	Global("Death","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("Death","LOCALS",1)
		StartDialogueNoSet(Player1)
END

///////////////////////////////////////////////////////////////////////
///	Usual pre-battle stuff
///////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////////////

BEGIN LOOP(scsspellsubstitute1||!GlobalTimerNotExpired("castspell","LOCALS"))
BEGIN LOOP(scsspellsubstitute2||!GlobalTimerNotExpired("castspell","LOCALS"))
	INCLUDE FILE(%scsroot%/mage/ssl/mage_definitions.ssl)
END LOOP
END LOOP
INCLUDE FILE(%scsroot%/dragon/ssl/dragondef.ssl)
INCLUDE FILE(%scsroot%/dragon/ssl/dragonsetup.ssl)




////////////////////////////////////////////////////////////////////////////
////	Chain Contingency, plus dragonfear
////////////////////////////////////////////////////////////////////////////


IF
	!Global("ChainContingencyFired","LOCALS",1)
THEN
	RESPONSE #100
		SetGlobal("ChainContingencyFired","LOCALS",1)
		DisplayStringHead(Myself,26328)
		ReallyForceSpell(Myself,PROTECTION_MAGIC_WEAPON_NOEFF)
		ReallyForceSpell(Myself,WIZARD_MORDENKAINENS_SWORD)
		ReallyForceSpell(Myself,SPELL_DEFLECTION_NO_VISUAL)
		ReallyForceSpell(NearestEnemyOf(Myself),DRAGON_FEAR)
		SetGlobalTimer("ElectrifiedFloorTimer","LOCALS",6)
		SetGlobalTimer("TailSlap","LOCALS",12)
		SetGlobalTimer("dragonbreath","LOCALS",1)
		SetGlobalTimer("redefend","LOCALS",3)
		Continue()
END

//////////////////////////////////////////////////////////////////////////
///	Spawn Tamah (will have no effect without Ascension installed)
//////////////////////////////////////////////////////////////////////////

IF
	RequireBlock(IsAscension)
	!Global("spawnedTamah","LOCALS",1)
THEN
	RESPONSE #100
		SetGlobal("spawnedTamah","LOCALS",1)
		CreateCreatureOffScreen("abazdg02",0)
		Continue()
END


//////////////////////////////////////////////////////////////////////////
///  Pursue!
///////////////////////////////////////////////////////////////////


INCLUDE FILE(%scsroot%/genai/ssl/chase.ssl)

//////////////////////////////////////////////////////////////////////////
////	Fire off a defensive trigger
////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	Target(Myself)
THEN DO
	PlaySound("abazig02")
	Action(TriggerRESMyself,dw#0d590,%WIZARD_IMPROVED_HASTE_INSTANT%,%DRAGON_STONE_SKIN_INSTANT%)
END

////////////////////////////////////////////////////////////////////////////
////	Contingency
////////////////////////////////////////////////////////////////////////////

IF
	Global("contingency","LOCALS",0)
	!CheckStatGT(Myself,0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
	Detect(NearestEnemyOf(Myself))
	!StateCheck(Myself,STATE_INVISIBLE)
	!GlobalTimerNotExpired("DMWWTimestopRunning","GLOBAL")
	GlobalTimerExpired("redefend","LOCALS")
THEN
	RESPONSE #100
		DisplayStringHead(Myself,25942)
		SetGlobal("contingency","LOCALS",1)
		ReallyForceSpell(Myself,PROTECTION_MAGIC_WEAPON_NOEFF)
END


//////////////////////////////////////////////////////////////////////////
///	Use breath weapon as often as possible
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInOrder)
	TargetBlock(EnemiesInOrder)
	!Range(scstarget,10)
	TriggerBlock(ResistElectric65)
THEN DO
	Action(FastDragonBreath,BLUE_DRAGON_BREATH,"dragonbreath",tra_09)
END

IF
	!GlobalTimerNotExpired("dragonbreath","LOCALS")
	!GlobalTimerNotExpired("castspell","LOCALS")
	!GlobalTimerNotExpired("trytorun","LOCALS")
	OR(5)		
		!CheckStatGT(NearestEnemyOf(Myself),65,RESISTELECTRICITY)
		!CheckStatGT(SecondNearestEnemyOf(Myself),65,RESISTELECTRICITY)
		!CheckStatGT(ThirdNearestEnemyOf(Myself),65,RESISTELECTRICITY)
		!CheckStatGT(FourthNearestEnemyOf(Myself),65,RESISTELECTRICITY)
		!CheckStatGT(FifthNearestEnemyOf(Myself),65,RESISTELECTRICITY)
THEN
	RESPONSE #100
	         SetGlobalTimer("trytorun","LOCALS",6)
		RunAwayFrom(NearestEnemyOf(Myself),30)
END

//////////////////////////////////////////////////////////////////////////
///	Use tail slap
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	!GlobalTimerNotExpired("DMWWTimestopRunning","GLOBAL")
	TargetBlock(PCsPreferringWeak)
	GlobalTimerExpired("TailSlap","LOCALS")
	Range(scstarget,20)
	TriggerBlock(Disabled)
THEN DO
		Action(Literal)
		SetGlobalTimer("TailSlap","LOCALS",30)
		SwingOnce()
		DisplayString(Myself,74022) // Tail Slap
		ReallyForceSpell(scstarget,CLERIC_EARTHQUAKE)
END

///////////////////////////////////////////////////////////////////////////////
////	Use shockwave
///////////////////////////////////////////////////////////////////////////////

IF
	!GlobalTimerNotExpired("DMWWTimestopRunning","GLOBAL")
	GlobalTimerExpired("ElectrifiedFloorTimer","LOCALS")
	AreaCheck("ar6005") // for testing
THEN
	RESPONSE #100
		SetGlobalTimer("ElectrifiedFloorTimer","LOCALS",21)
		DisplayString(Myself,72905) // Abazigal Shockwave
		PlaySound("am6005e")
		ReallyForceSpell(Myself,ABAZIGAL_SHOCKWAVE)
END

//////////////////////////////////////////////////////////////////////////
///	Use wing buffet to drive away anything at all dangerous
//////////////////////////////////////////////////////////////////////////

INCLUDE FILE (%scsroot%/dragon/ssl/wingbuffet.ssl)




///////////////////////////////////////////////////////////////////////////
///	Try a Remove Magic sequencer
///////////////////////////////////////////////////////////////////////////


IF TRIGGER
	TargetBlock(PCsInOrder)
	!GlobalTimerNotExpired("castspell","LOCALS")
	OR(14)
		CheckStatGT(scstarget,0,STONESKINS)
		CheckStatGT(scstarget,0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		StateCheck(scstarget,STATE_IMPROVEDINVISIBILITY)
		StateCheck(scstarget,STATE_BLESS)
		StateCheck(scstarget,STATE_CHANT)
		CheckStat(scstarget,4,WIZARD_SPELL_IMMUNITY)
		StateCheck(scstarget,STATE_MIRRORIMAGE)
		CheckStatGT(scstarget,0,CLERIC_DEFENSIVE_HARMONY)
		CheckStatGT(scstarget,0,PROTECTION_FROM_EVIL)
		CheckStatGT(scstarget,0,CLERIC_PHYSICAL_MIRROR)
		CheckStatGT(scstarget,0,CLERIC_CHAOTIC_COMMANDS)
		CheckStatGT(scstarget,0,WIZARD_PROTECTION_FROM_PETRIFICATION)
		CheckStatGT(scstarget,0,SCRIPTINGSTATE2)
		CheckStatGT(scstarget,50,RESISTELECTRICITY)
	TriggerBlock(SIAbjuration|Enemy)
THEN DO
	Action(Sequencer,WIZARD_DISPEL_MAGIC,WIZARD_DISPEL_MAGIC,WIZARD_GREATER_MALISON)
END


///////////////////////////////////////////////////////////////////////////
///	Lower Electrical Resistance - we'll
///	cast it fairly blind
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInOrder)
	!GlobalTimerNotExpired("castspell","LOCALS")
THEN DO
	Action(SpellInnate,DRAGON_LOWER_ELECTRICAL)
END


/////////////////////////////////////////////////////////////////////////////
///	Renew defences
/////////////////////////////////////////////////////////////////////////////


IF TRIGGER	
	!GlobalTimerNotExpired("castspell","LOCALS")
	!CheckStatGT(Myself,0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
	!GlobalTimerNotExpired("redefend","LOCALS")
	Detect(NearestEnemyOf(Myself))
THEN DO
	Action(SpellMyself,PROTECTION_MAGIC_WEAPON_NOEFF)
	SetGlobal("redefend","LOCALS",8) // don't get stuck in a defend-defend-defend loop and forget to do any damage
END

IF TRIGGER
	!GlobalTimerNotExpired("castspell","LOCALS")
	CheckStat(Myself,0,STONESKINS)
	GlobalTimerExpired("redefend","LOCALS")
	Target(Myself)
THEN DO
	Action(SpellInnate,DRAGON_STONE_SKIN|100|100)
END

//////////////////////////////////////////////////////////////////////////
////  Imprisonment
/////////////////////////////////////////////////////////////////////////
		
IF TRIGGER
	RequireBlock(IsAscension)
	TargetBlock(PCsInOrderShort)
	TargetBlock(Celestials)
	OR(2)
		!GlobalTimerNotExpired("DMWWTimestopRunning","GLOBAL")
		!GlobalTimerNotExpired("timestopmaze","LOCALS")
	TriggerBlock(SpellTurnNew|Enemy|Helpless)
	OR(2)
		!InPartySlot(scstarget,0) // isn't Player 1
		Global("DMWWImprisonPlayer","GLOBAL",1)
	Range(scstarget,10)
THEN DO
	Action(Spell,WIZARD_IMPRISONMENT|150|50)
	SetGlobalTimer("timestopmaze","LOCALS",24)
	Wait(2) // this is to stop them using spells immediately on imprisoned creatures (Imprisonment takes all round to cast)
END


///////////////////////////////////////////////////////////////////////////
////	Kill swords (don't worry about other summons)
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	Target([GOODCUTOFF.0.SWORD])
	Gender(scstarget,SUMMONED)
	TriggerBlock(Enemy|Helpless)
	HasItem("morsword",scstarget)
THEN DO
	Combine()
	Action(Spell,WIZARD_DEATH_SPELL|150|50)
	Action(MinorSequencer,WIZARD_MAGIC_MISSILE,WIZARD_MAGIC_MISSILE)
	Action(Spell,WIZARD_MAGIC_MISSILE)
END



/////////////////////////////////////////////////////////////////////////////
/////  Finger of Death
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(MR100|SpellTurnNew|Enemy)
THEN DO
	Action(Spell,WIZARD_FINGER_OF_DEATH|150|50)
END



//////////////////////////////////////////////////////////////////////////
/////	True Sight
//////////////////////////////////////////////////////////////////////////


IF TRIGGER
	TargetBlock(EnemiesInOrder)
	!CheckStatGT(FourthNearest([ENEMY]),0,TRUE_SIGHT)
	!CheckStatGT(ThirdNearest([ENEMY]),0,TRUE_SIGHT)
	!CheckStatGT(SecondNearest([ENEMY]),0,TRUE_SIGHT)
	!CheckStatGT([ENEMY],0,TRUE_SIGHT)
	!CheckStatGT(Myself,0,TRUE_SIGHT)
	OR(3)
		StateCheck(scstarget,STATE_IMPROVEDINVISIBILITY)
		StateCheck(scstarget,STATE_MIRRORIMAGE)
		Gender(scstarget,ILLUSIONARY)
	!Allegiance(scstarget,ENEMY)
	TriggerBlock(SIDivination)
THEN DO
	Action(SpellMyself,WIZARD_TRUE_SIGHT)
END

/////////////////////////////////////////////////////////////////////////////
///	Breach enemy defences
/////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInOrder)
	TriggerBlock(SpellTurn|SpellTurnNew|Enemy|Helpless)
	OR(5)
		CheckStatGT(scstarget,0,WIZARD_PROTECTION_FROM_MAGIC_ENERGY)
		CheckStatGT(scstarget,50,RESISTELECTRICITY) // we're assuming that anything this high probably comes from a spell
		CheckStatGT(scstarget,0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		CheckStatGT(scstarget,2,STONESKINS)
		CheckStat(scstarget,4,WIZARD_SPELL_IMMUNITY)
THEN DO
	Combine()
	Action(Spell,WIZARD_BREACH|200|100)
END

////////////////////////////////////////
////	Power Word: Blind
///////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsPreferringStrong)
	TargetBlock(Celestials)
	!Gender(scstarget,SUMMONED)
	TriggerBlock(MR|Enemy|SIEnchantment|SpellTurnNew)
THEN DO
	Combine()
	Action(Spell,WIZARD_POWER_WORD_BLIND|200|100)
END

/////////////////////////////////////////////////////////////////////////
////  Maze
/////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInOrderShort)
	TargetBlock(Celestials)
	OR(2)
		!GlobalTimerNotExpired("DMWWTimestopRunning","GLOBAL")
		!GlobalTimerNotExpired("timestopmaze","LOCALS")
	TriggerBlock(Maze|SpellTurnNew|MR|Enemy|Helpless)
	Range(scstarget,6)
	OR(2)
		InPartySlot(scstarget,1) // isn't Player 1
		Global("DMWWMazePlayer","GLOBAL",0)
THEN DO
	Action(Spell,WIZARD_MAZE|150|50)
	SetGlobalTimer("timestopmaze","LOCALS",24)
	Wait(2) // this is to stop them using spells immediately on imprisoned creatures (Imprisonment takes all round to cast)
END

///////////////////////////////////////////////////////////////////////////
////  If I'm not Improved Invisible, change that if possible
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	!StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
	GlobalTimerExpired("redefend","LOCALS")
	TriggerBlock(TruesightSafe)
	Target(Myself)
THEN DO
	Action(SpellMyself,WIZARD_SHADOW_DOOR)
END	

////////////////////////////////////////////////////////////////////////////
////	Attack magical defences
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInOrder)
	CheckStatGT(scstarget,0,WIZARD_SPELL_SHIELD)
	TriggerBlock(Enemy|Helpless)
THEN DO
	Combine()
	Action(SpellArea,WIZARD_SECRET_WORD)
END

IF TRIGGER
	TargetBlock(PCsInOrder)
	OR(5)
		CheckStatGT(scstarget,0,WIZARD_SPELL_TURNING)
		CheckStatGT(scstarget,0,WIZARD_SPELL_DEFLECTION)
		CheckStat(scstarget,1,WIZARD_SPELL_TRAP)
		CheckStatGT(scstarget,0,MINORGLOBE)
		CheckStatGT(scstarget,0,CLERIC_SHIELD_OF_THE_ARCHONS)
	TriggerBlock(Enemy|Helpless)
THEN DO
	Combine()
	Action(Spell,WIZARD_RUBY_RAY_OF_REVERSAL|100|100)
	Action(Spell,WIZARD_WARDING_WHIP|100|100)
END


///////////////////////////////////////////////////////////////////////
///  Chaos
///////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(EnemiesInOrder)
	TriggerBlock(MR|Enemy|Helpless)
THEN DO
	Combine()
	Action(Spell,WIZARD_CHAOS|150|50)
END

////////////////////////////////////////////////////////////////////////////
////	Slow
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(MR100|Slow|Enemy|MinorGlobe|Helpless)
THEN DO
	Combine()
	Action(Spell,WIZARD_SLOW|150|50)
END



//////////////////////////////////////////////////////////////////////////
///  Use magic missiles to bring down mirror images
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCSpellcasters)
	TriggerBlock(MR100|Enemy|MinorGlobe|SpellTurnNew)
	StateCheck(scstarget,STATE_MIRRORIMAGE)
THEN DO
	Combine()
	Action(Spell,WIZARD_MAGIC_MISSILE)
END

//////////////////////////////////////////////////////////////////////////
///	Now hit basically anyone with MMs 
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC)&HPPercentLT(scstarget,40))
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(MR100|MinorGlobe|SpellTurnNew|Enemy)
THEN DO
	Combine()
	Action(Spell,WIZARD_MAGIC_MISSILE)
END

//////////////////////////////////////////////////////////////////////////////////
/////	Melee phase
//////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/dragon/ssl/melee.ssl)

////////////////////////////////////////////////////////
///	Closedown: avoid interaction with other scripts
////////////////////////////////////////////////////////////

IF
	!See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		NoAction()
END
