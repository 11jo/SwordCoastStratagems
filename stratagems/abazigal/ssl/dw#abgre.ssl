/// Based on WK dragon, but:
///
/// - defensive sequencer instead of UB
/// - PMW instead of PW:silence, used when at 40% hp
/// - righteous magic instead of IP, used when at 65% hp
/// - we don't get the WK lethally-fast breath weapon



///////////////////////////////////////////////////////////////////////
///	Usual pre-battle stuff
///////////////////////////////////////////////////////////////////////


BEGIN LOOP(scsspellsubstitute1||!StateCheck(scstarget,STATE_NOT_TARGETABLE))
BEGIN LOOP(scsspellsubstitute2||!StateCheck(scstarget,STATE_INVISIBLE))
	INCLUDE FILE(%scsroot%/mage/ssl/mage_definitions.ssl)
END LOOP
END LOOP
INCLUDE FILE(%scsroot%/dragon/ssl/dragondef.ssl)
INCLUDE FILE(%scsroot%/dragon/ssl/dragonsetup.ssl)

////////////////////////////////////////////////////////////////////////////////////////
////	Initial stoneskin
////////////////////////////////////////////////////////////////////////////////////////


IF	
	!GlobalTimerNotExpired("initialstoneskin","LOCALS")
THEN
	RESPONSE #100
		SetGlobalTimer("initialstoneskin","LOCALS",600)
		ApplySpell(Myself,DRAGON_STONE_SKIN)
		ApplySpell(Myself,WIZARD_PROTECTION_FROM_MAGIC_ENERGY)
END

////////////////////////////////////////////////////////////////////////////
////	initial dragonfear
////////////////////////////////////////////////////////////////////////////

IF
	See(NearestEnemyOf(Myself))
	!GlobalTimerNotExpired("initialfear","LOCALS")
THEN
	RESPONSE #100
	SetGlobalTimer("initialfear","LOCALS",600)
	ReallyForceSpell(NearestEnemyOf(Myself),DRAGON_FEAR)
	SetGlobalTimer("dragonbreath","LOCALS",1)
	SetGlobalTimer("redefend","LOCALS",3)
	Continue()
END

//////////////////////////////////////////////////////////////////////////////
////	Heal
//////////////////////////////////////////////////////////////////////////////

IF
	Global("contingency","LOCALS",0)
	Detect(NearestEnemyOf(Myself))
	HPPercentLT(Myself,60)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,25942)
		SetGlobal("contingency","LOCALS",1)
		ReallyForceSpell(Myself,CLERIC_HEAL)
END

IF TRIGGER
	HPPercentLT(Myself,60)
THEN DO
	Action(SpellMyself,CLERIC_HEAL)
END

/////////////////////////////////////////////////////////////////////////////////
///	Pursue
////////////////////////////////////////////////////////////////////////////////	

//////////////////////////////////////////////////////////////////////////
///  Pursue!
///////////////////////////////////////////////////////////////////


INCLUDE FILE(%scsroot%/genai/ssl/chase.ssl)

//////////////////////////////////////////////////////////////////////////
///	Use breath weapon as often as possible
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInOrder)
	TargetBlock(EnemiesInOrder)
	!Range(scstarget,15)
	!CheckStatGT(scstarget,65,RESISTPOISON)
THEN DO
	Action(DragonBreath3,GREEN_DRAGON_BREATH,"DrGrBrHt","dragonbreath",black05)
END

IF
	!GlobalTimerNotExpired("dragonbreath","LOCALS")
	!GlobalTimerNotExpired("castspell","LOCALS")
	!GlobalTimerNotExpired("trytorun","LOCALS")
	OR(5)		
		!CheckStatGT(NearestEnemyOf(Myself),65,RESISTPOISON)
		!CheckStatGT(SecondNearestEnemyOf(Myself),65,RESISTPOISON)
		!CheckStatGT(ThirdNearestEnemyOf(Myself),65,RESISTPOISON)
		!CheckStatGT(FourthNearestEnemyOf(Myself),65,RESISTPOISON)
		!CheckStatGT(FifthNearestEnemyOf(Myself),65,RESISTPOISON)
THEN
	RESPONSE #100

	         SetGlobalTimer("trytorun","LOCALS",6)
		RunAwayFrom(NearestEnemyOf(Myself),30)
END

//////////////////////////////////////////////////////////////////////////
///	Use wing buffet to drive away anything at all dangerous
//////////////////////////////////////////////////////////////////////////

INCLUDE FILE (%scsroot%/dragon/ssl/wingbuffet.ssl)

//////////////////////////////////////////////////////////////////////////
///	Try a dispel magic
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	!GlobalTimerNotExpired("firstdispel","LOCALS")
	TargetBlock(PCsInOrderShort)
THEN DO
	Combine()
	SetGlobalTimer("firstdispel","LOCALS",1000)
	Action(Spell,WIZARD_DISPEL_MAGIC|100|50)
	OnContinue(SetGlobalTimer("firstdispel","LOCALS",1000))
END

///////////////////////////////////////////////////////////////
///	Renew defences
////////////////////////////////////////////////////////////////

IF TRIGGER
	HPPercentLT(Myself,40)
THEN DO
	Action(SpellMyself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
END

IF TRIGGER
	CheckStat(Myself,0,STONESKINS)
	GlobalTimerExpired("redefend","LOCALS")
	Target(Myself)
THEN DO
	Action(Sequencer,DRAGON_STONE_SKIN,WIZARD_HASTE,SPELL_DEFLECTION_NO_VISUAL)
	Action(SpellInnate,DRAGON_STONE_SKIN|100|100)
END

IF TRIGGER
	HPPercentLT(Myself,75)
THEN DO
	Action(SpellMyself,CLERIC_RIGHTEOUS_MAGIC)
END

IF TRIGGER
	CheckStat(Myself,0,CLERIC_DEFENSIVE_HARMONY)
	GlobalTimerExpired("redefend","LOCALS")
THEN DO
	Action(SpellMyself,CLERIC_DEFENSIVE_HARMONY|100|100)
END

/////////////////////////////////////////////////////////////////////////////
///	Breach enemy defences
/////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInOrder)
	TriggerBlock(SpellTurn|Enemy|Helpless)
	!GlobalTimerNotExpired("breach","LOCALS")
	OR(3)
		CheckStatGT(scstarget,0,WIZARD_PROTECTION_FROM_MAGIC_ENERGY)
		CheckStatGT(scstarget,0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		CheckStatGT(scstarget,2,STONESKINS)
THEN DO
	Combine()
	SetGlobalTimer("breach","LOCALS",12)
	Action(Spell,WIZARD_BREACH|200|100)
END

	


//////////////////////////////////////////////////////////////////////////////
///	Insects
//////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsPreferringWeak)
	!GlobalTimerNotExpired("insects","LOCALS")
THEN DO
	SetGlobalTimer("insects","LOCALS",18)
	Action(SpellInnate,BLACK_DRAGON_INSECT|200|100)
END

//////////////////////////////////////////////////////////////////////////
/////	True Sight
//////////////////////////////////////////////////////////////////////////


IF TRIGGER
	TargetBlock(EnemiesInOrder)
	!CheckStatGT(FourthNearest([ENEMY]),0,TRUE_SIGHT)
	!CheckStatGT(ThirdNearest([ENEMY]),0,TRUE_SIGHT)
	!CheckStatGT(SecondNearest([ENEMY]),0,TRUE_SIGHT)
	!CheckStatGT([ENEMY],0,TRUE_SIGHT)
	!CheckStatGT(Myself,0,TRUE_SIGHT)
	OR(3)
		StateCheck(scstarget,STATE_IMPROVEDINVISIBILITY)
		StateCheck(scstarget,STATE_MIRRORIMAGE)
		Gender(scstarget,ILLUSIONARY)
	!Allegiance(scstarget,ENEMY)
	TriggerBlock(SIDivination)
THEN DO
	Action(SpellMyself,WIZARD_TRUE_SIGHT)
END

///////////////////////////////////////////////////////////////////////////////
///	Unholy Blight
///////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringStrong;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(EnemiesInOrderShort)
	!Alignment(scstarget,MASK_EVIL)
	!HasItemEquiped("clck26",scstarget)
	TriggerBlock(MR100|SINecromancy|ResistMagicDamage|Enemy|MirrorImage)
THEN DO
	Action(Spell,CLERIC_UNHOLY_BLIGHT|200|100)
END


////////////////////////////////////////////////////////////////////////////
////	Attack magical defences
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInOrder)
	OR(5)
		CheckStatGT(scstarget,0,WIZARD_SPELL_TURNING)
		CheckStatGT(scstarget,0,WIZARD_SPELL_DEFLECTION)
		CheckStat(scstarget,1,WIZARD_SPELL_TRAP)
		CheckStatGT(scstarget,0,WIZARD_SPELL_IMMUNITY)
		CheckStatGT(scstarget,0,MINORGLOBE)
	TriggerBlock(Enemy|Helpless)
THEN DO
	Combine()
	Action(Spell,WIZARD_WARDING_WHIP|100|100)
	Action(Spell,WIZARD_SPELL_THRUST|100|100)
END

/////////////////////////////////////////////////////////////////
////	Power Words
//////////////////////////////////////////////////////////////////



////////////////////////////////////////////////////////////////////////
////  Power Word: Stun
/////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(PCsInOrderShort)
	TargetBlock(Celestials)
	TriggerBlock(MR100|SpellTurn|Stun|Enemy|Helpless|SIConjuration)
	OR(3)
		HPPercentLT(scstarget,60)
		Class(scstarget,MAGE)
		Class(scstarget,MAGE_THIEF)
THEN DO
	Action(Spell,WIZARD_POWER_WORD_STUN|150|50)
END




//////////////////////////////////////////////////////////////////////////
/// Kill swords
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	Target([GOODCUTOFF.0.SWORD])
THEN DO
	Action(Spell,WIZARD_DEATH_SPELL)
	Action(Spell,WIZARD_MAGIC_MISSILE)
END

/////////////////////////////////////////////////////////////////
///	Domination
//////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(MR|Charm|SIEnchantment|SpellTurn|Enemy|Helpless)
	!CheckStatLT(scstarget,0,SAVEVSSPELL)
THEN DO
	Action(Spell,WIZARD_DOMINATION|150|50)
END



//////////////////////////////////////////////////////////////////////////
///  Use magic missiles to bring down mirror images
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCSpellcasters)
	TriggerBlock(MR100|SIEvocation|Enemy)
	StateCheck(scstarget,STATE_MIRRORIMAGE)
THEN DO
	Combine()
	Action(Spell,WIZARD_MAGIC_MISSILE)
END

///////////////////////////////////////////////////////////////////////////
// Hit spellcasters with Melf
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCSpellcasters;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(PCSpellcasters)
	TriggerBlock(MR100|SIConjuration|Stoneskin|MirrorImage|MinorGlobe|ArrowSafe|SpellTurn|Enemy)
THEN DO
	Combine()
	Action(MinorSequencer,WIZARD_MELF_ACID_ARROW,WIZARD_MELF_ACID_ARROW)
	Action(Spell,WIZARD_MELF_ACID_ARROW)
END

//////////////////////////////////////////////////////////////////////////
///	Now hit basically anyone with MMs and acid arrows
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC)&HPPercentLT(scstarget,40))
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(MR100|SIEvocation|MinorGlobe|SpellTurn|Enemy)
THEN DO
	Combine()
	Action(Spell,WIZARD_MAGIC_MISSILE)
END

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(MR100|SIConjuration|Stoneskin|MirrorImage|MinorGlobe|ArrowSafe|SpellTurn)
THEN DO
	Combine()
	Action(Spell,WIZARD_MELF_ACID_ARROW)
END

//////////////////////////////////////////////////////////////////////////////////
/////	Melee phase
//////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/dragon/ssl/melee.ssl)

////////////////////////////////////////////////////////
///	Closedown: avoid interaction with other scripts
////////////////////////////////////////////////////////////

IF
	!See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		NoAction()
END
