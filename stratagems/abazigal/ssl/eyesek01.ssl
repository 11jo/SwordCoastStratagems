BEGIN_ACTION_DEFINITION
	Name(ForceSpell)
	TRIGGER
		!GlobalTimerNotExpired("castspell","LOCALS")
		!CheckStatGT(scstarget,0,SANCTUARY)
		!CheckStat(scstarget,2,WIZARD_SPELL_TRAP) // PM scroll
	ACTION
		RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",3)
		SetGlobal("SeekerAttack","LOCALS",0)
		ForceSpell(scstarget,scsargument1)
END

IF
	Die()
THEN
	RESPONSE #100
		CreateVisualEffectObject("ICRMPARI",Myself)
		DestroySelf()
END

IF
	!GlobalTimerNotExpired("runonce","LOCALS")
	See(NearestEnemyOf(Myself))
	Range(NearestEnemyOf(Myself),7)
THEN
	RESPONSE #100
		SetGlobalTimer("runonce","LOCALS",18)
		RunAwayFrom(NearestEnemyOf(Myself),30)
END


IF	
	!Global("initiate","LOCALS",1)
	See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		SetGlobal("initiate","LOCALS",1)
		SetGlobalTimer("seedefences","LOCALS",6)
	RESPONSE #100
		SetGlobal("initiate","LOCALS",1)
		SetGlobalTimer("seedefences","LOCALS",12)
END


IF
	!GlobalTimerNotExpired("castspell","LOCALS")
	Global("SeekerAttack","LOCALS",0)
	See(NearestEnemyOf(Myself))
THEN
	RESPONSE #80
		SetGlobal("SeekerAttack","LOCALS",1)
	RESPONSE #80
		SetGlobal("SeekerAttack","LOCALS",2)
	RESPONSE #100
		SetGlobal("SeekerAttack","LOCALS",3)
	RESPONSE #100
		SetGlobal("SeekerAttack","LOCALS",4)
END


BEGIN LOOP(scsloopvar||!GlobalTimerExpired("seedefences","LOCALS");TriggerBlock(MR|ResistFire|Enemy|ArrowSafe|SpellTurn|Stoneskin|MinorGlobe|MirrorImage|Helpless|SIConjuration))


IF TRIGGER
	Global("SeekerAttack","LOCALS",1)
	ConditionalTargetBlock(PCsPreferringWeak;HPPercentLT(scstarget,40))
	TargetBlock(PCsPreferringWeak)
	!HasItemEquiped("clck26",scstarget)
	scsloopvar
THEN DO
	Action(ForceSpell,WIZARD_FLAME_ARROW|150|50)
END

IF TRIGGER
	Global("SeekerAttack","LOCALS",4)
	ConditionalTargetBlock(PCsPreferringWeak;HPPercentLT(scstarget,40))
	TargetBlock(PCsPreferringWeak)
	!HasItemEquiped("clck26",scstarget)
	scsloopvar
THEN DO
	Action(ForceSpell,WIZARD_AGANNAZAR_SCORCHER|150|50)
END

END LOOP

BEGIN LOOP(scsloopvar||!GlobalTimerExpired("seedefences","LOCALS");TriggerBlock(MR100|SIConjuration|Stoneskin|MirrorImage|MinorGlobe|ArrowSafe|SpellTurn|Enemy))

IF TRIGGER
	Global("SeekerAttack","LOCALS",2)
	ConditionalTargetBlock(PCSpellcasters;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(PCSpellcasters)
	TargetBlock(PCsInOrderShort)
	!HasItemEquiped("clck26",scstarget)
	scsloopvar
THEN DO
	Action(ForceSpell,WIZARD_MELF_ACID_ARROW|100|50)
END

END LOOP

BEGIN LOOP(scsloopvar||!GlobalTimerExpired("seedefences","LOCALS");TriggerBlock(MR100|SIEvocation|MinorGlobe|SpellTurn|Enemy|MagicMissile|L1safe))




IF TRIGGER
	Global("SeekerAttack","LOCALS",3)
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC)&HPPercentLT(scstarget,40))
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(EnemiesInOrderShort)
	!HasItemEquiped("clck26",scstarget)
	scsloopvar
THEN DO
	Action(ForceSpell,WIZARD_MAGIC_MISSILE)
END

END LOOP

