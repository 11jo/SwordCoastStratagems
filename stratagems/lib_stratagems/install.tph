DEFINE_ACTION_FUNCTION find_language
RET language_num
BEGIN
ACTION_DEFINE_ASSOCIATIVE_ARRAY ~language_map~ BEGIN
   ~stratagems/tra/english~ => 0
   ~stratagems/tra/spanish~ => 1
   ~stratagems/tra/polish~ => 2
   ~stratagems/tra/german~ => 3
   ~stratagems/tra/french~ => 4
   ~stratagems/tra/italian~ => 5
   ~stratagems/tra/russian~ => 6
   ~stratagems/tra/korean~ => 7
END

OUTER_SET ~language_num~ = $language_map(~%LANGUAGE%~)

END

DEFINE_ACTION_FUNCTION make_label
STR_VAR label=""
BEGIN 
 COPY_EXISTING misc01.itm ~%workspace%/%label%.mrk~
END

DEFINE_ACTION_FUNCTION run
STR_VAR version=""
BEGIN
   ACTION_IF !FILE_EXISTS ~%workspace%/autoinstall.txt~ BEGIN
      <<<<<<<< autoinstall.txt
      >>>>>>>>
      COPY autoinstall.txt ~%workspace%~
   END
   APPEND_OUTER ~%workspace%/autoinstall.txt~ ~ %COMPONENT_NUMBER%~
END

DEFINE_ACTION_FUNCTION make_weidu_string
RET weidustring
BEGIN
   LAF find_language RET language_num=language_num END
   OUTER_SPRINT weidustring ~setup-stratagems --uninstall --language %language_num%  --force-install-list ~
   ACTION_IF FILE_EXISTS ~%workspace%/do_not_install_from_scratch.mrk~ BEGIN
      OUTER_INNER_PATCH_SAVE ~weidustring~ ~%weidustring%~ BEGIN
         REPLACE_TEXTUALLY ~1000~ ~~
         REPLACE_TEXTUALLY ~--uninstall~ ~~
      END
   END
   COPY ~%workspace%/autoinstall.txt~ ~%workspace%~
        READ_2DA_ENTRIES_NOW auto_entries 1
        FOR (i=0;i<auto_entries;i+=1) BEGIN
           READ_2DA_ENTRY_FORMER auto_entries i 0 add_me
           SPRINT weidustring ~%weidustring% %add_me%~
        END
   BUT_ONLY
END


DEFINE_ACTION_MACRO autoinstall_core BEGIN

COPY ~%scsroot%/setup-stratagems.tp2~ ~%scsroot%/auto.tp2~
      REPLACE_TEXTUALLY ~///---///~ ~~
      REPLACE_TEXTUALLY "REQUIRE_PREDICATE FILE_EXISTS ~override/dw#" "REQUIRE_PREDICATE FILE_EXISTS ~%workspace%/dw#"

OUTER_SET quit=0
ACTION_FOR_EACH component IN 2 3 4 5 6 16040 16041 BEGIN
   ACTION_IF quit=0 BEGIN
     SILENT
     OUTER_SPRINT group_string ~GROUP @%component%~
   <<<<<<<< .../stratagems-inline/to_include
      OUTER_SPRINT group_name @xyz
   >>>>>>>>
     COPY ~.../stratagems-inline/to_include~ ~%workspace%~
       REPLACE_TEXTUALLY xyz ~%component%~
     REINCLUDE ~%workspace%/to_include~
     PRINT ~~
     PRINT ~~
     PRINT ~//////////// %group_name% \\\\\\\\\\\\~
     PRINT ~What do you want to do with this group of components?~
     PRINT ~1: Install all of them (you will be asked about multiple-choice components)~
     PRINT ~2: Choose whether to install each component~
     PRINT ~3: Skip this group of components~
     PRINT ~Q: Quit without installing anything~
     OUTER_SET done=0
     OUTER_WHILE done=0 BEGIN
      ACTION_READLN choice
      ACTION_MATCH ~%choice%~ WITH
      1 BEGIN
           COPY ~%scsroot%/auto.tp2~ ~%scsroot%~
              REPLACE_TEXTUALLY ~%group_string% +SUBCOMPONENT~ ~NO_LOG_RECORD SUBCOMPONENT~
              REPLACE_TEXTUALLY ~%group_string% +ASK_ANYWAY~ ~NO_LOG_RECORD~
              REPLACE_TEXTUALLY ~%group_string%~ ~NO_LOG_RECORD INSTALL_BY_DEFAULT~
      OUTER_SET done=1
      END
      2 BEGIN
           COPY ~%scsroot%/auto.tp2~ ~%scsroot%~
              REPLACE_TEXTUALLY ~%group_string% +ASK_ANYWAY~ ~NO_LOG_RECORD~
              REPLACE_TEXTUALLY ~%group_string%~ ~NO_LOG_RECORD~
      OUTER_SET done=1
      END
      3 BEGIN
           COPY ~%scsroot%/auto.tp2~ ~%scsroot%~
              REPLACE_TEXTUALLY ~%group_string% +ASK_ANYWAY~ ~DEPRECATED ""~
              REPLACE_TEXTUALLY ~%group_string%~ ~DEPRECATED ""~
      OUTER_SET done=1
      END
      Q q BEGIN

      OUTER_SET quit=1
      OUTER_SET done=1
      END
      DEFAULT
      PRINT ~Please choose 1,2,3, or Q~
      END
    END
  END
END

  ACTION_IF quit=0 BEGIN
     OUTER_SPRINT weidustring ~setup-stratagems --no-auto-tp2 --language %language_num% --autolog --no-exit-pause "%scsroot%\auto.tp2"~
  //   COPY ~%scsroot%/auto.tp2~ ~auto.tp2~
  //   OUTER_SPRINT weidustring ~setup-auto --language %language_num% --no-exit-pause~
     AT_EXIT ~%weidustring%~

  END
  // --language %language_num%

END