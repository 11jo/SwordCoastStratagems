// basically dw#melee

VARIABLE(IsMainAI=True)
VARIABLE(IgnoreCloud=True)
VARIABLE(MyWeaponStrength=4)
VARIABLE(MyWeaponDamageType=Crushing)

BEGIN_ACTION_DEFINITION
Name(GolemSpell)
	TRIGGER
		!GlobalTimerNotExpired("castspell","LOCALS")
		!GlobalTimerNotExpired("scsargument2","LOCALS")
	ACTION
		RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		SetGlobalTimer("scsargument2","LOCALS",scsargument3)
		ForceSpell(scstarget,scsargument1)
END
BEGIN_ACTION_DEFINITION
Name(GolemSpellRES)
	TRIGGER
		!GlobalTimerNotExpired("castspell","LOCALS")
		!GlobalTimerNotExpired("scsargument2","LOCALS")
	ACTION
		RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		SetGlobalTimer("scsargument2","LOCALS",scsargument3)
		ForceSpellRES("scsargument1",scstarget)
END

INCLUDE FILE(%scsroot%/genai/ssl/genai_definitions.ssl)
INCLUDE FILE(%scsroot%/genai/ssl/initial.ssl)

IF TRIGGER
      RequireBlock(IsBrainGolem)
      TargetBlock(EnemiesInOrderShort)
      HPPercentLT(Myself,50)
THEN DO
     Action(GolemSpell,BRAIN_PSIONIC_BLAST,brain,1000)
END

IF TRIGGER
      RequireBlock(IsIronGolem)
      TargetBlock(EnemiesInOrderShort)
      Range(scstarget,15)
THEN DO
     Action(GolemSpell,GOLEM_GASCLOUD,gascloud,18|100|100)
END

IF TRIGGER
      RequireBlock(IsStoneGolem)
      TargetBlock(EnemiesInOrderShort)
      Range(scstarget,15)
THEN DO
     Action(GolemSpell,GOLEM_SLOW,slow,18|100|100)
END

IF
      RequireBlock(IsClayGolem)
      See(NearestEnemyOf(Myself))
      !Global("waitstart","LOCALS",1)
THEN
    RESPONSE #100
        SetGlobal("waitstart","LOCALS",1)
        SetGlobalTimer("wait","LOCALS",6)
END

IF TRIGGER
      RequireBlock(IsClayGolem)
      See(NearestEnemyOf(Myself))
      Target(Myself)
      !Global("waitstart","LOCALS",0)
      !GlobalTimerNotExpired("wait","LOCALS")
THEN DO
     Action(GolemSpell,GOLEM_HASTE,haste,18)
END

IF
    RequireBlock(IsIceGolem)
    HitBy([ANYONE],FIRE)
THEN
	RESPONSE #100
		ForceSpell(Myself,ICE_GOLEM_STEAM)
END

IF TRIGGER
     RequireBlock(IsIceGolem)
     TargetBlock(EnemiesInOrderShort)
     Range(scstarget,10)
THEN DO
     Action(GolemSpellRES,%WIZARD_CONE_OF_COLD_INSTANT%,cold,12|100|100)
END

INCLUDE FILE(%scsroot%/genai/ssl/chase.ssl)
INCLUDE FILE(%scsroot%/genai/ssl/dw#wtacor.ssl)
INCLUDE FILE(%scsroot%/genai/ssl/move.ssl)
INCLUDE FILE(%scsroot%/genai/ssl/chase.ssl)
INCLUDE FILE(%scsroot%/genai/ssl/random.ssl)

