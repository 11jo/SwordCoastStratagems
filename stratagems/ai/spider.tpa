DEFINE_ACTION_FUNCTION spider BEGIN

     // compile scripts
     
     LAF ssl_to_bcs STR_VAR script=spider location=~ai/ssl~  rename_to=dw#spids END
     LAF ssl_to_bcs STR_VAR script=spider location=~ai/ssl~ variables=~HasWeb=True~ rename_to=dw#spidg END
     LAF ssl_to_bcs STR_VAR script=spider location=~ai/ssl~ variables=~IsPhaseSpider=True~ rename_to=EVALUATE_BUFFER ~%tutu_var%pspider~ END

     ACTION_IF is_bg2=1 BEGIN
          LAF ssl_to_bcs STR_VAR script=spider location=~ai/ssl~ variables=~IsPhaseSpider=True&IsVortexSpider=True&IsLasharra=True~ rename_to=EVALUATE_BUFFER ~senspi01~ END
          LAF ssl_to_bcs STR_VAR script=spider location=~ai/ssl~ variables=~IsPhaseSpider=True&IsVortexSpider=True~ rename_to=EVALUATE_BUFFER ~spidvo01~ END
     END

     // give out web scripts

     ACTION_IF is_bg1=1 BEGIN
       LAF edit_creature STR_VAR creature=EVALUATE_BUFFER ~%tutu_scripts%pidland %tutu_var%spidhu %tutu_var%spidsw~ editstring=~strip_scs_scripts=>null strip_script=>dw#spids insert_script_low=>dw#spids~ END
       LAF edit_creature STR_VAR creature=EVALUATE_BUFFER ~%tutu_scripts%pidphas %tutu_var%spidph~ editstring=~strip_scs_scripts=>null~ END
       LAF edit_creature STR_VAR tv=yes  creature=~spidgi spidwr~  editstring=~strip_scs_scripts=>null strip_script=>dw#spidg insert_script_low=>dw#spidg~ END
     END
     ACTION_IF is_bg2=1 BEGIN
       LAF edit_creature STR_VAR creature=~sensmsp spidhu01 spidsm01 spidsm02 spidsw01 spspid07 spspider~ editstring=~strip_scs_scripts=>null insert_script_low=>dw#spids~ END
       LAF edit_creature STR_VAR creature=~spidph01 spspid06 spidvo01 senspi01 smspid02~ editstring=~strip_scs_scripts=>null~ END
       LAF edit_creature STR_VAR creature=~spidgi01 spspid05 spidwr01~  editstring=~strip_scs_scripts=>null insert_script_low=>dw#spidg~ END
     END
     // generate nonmagical single-target web, using FIGURINE_SPIDER_WEB as a starting point
     
     MAKE_PATCH
        type=>effect
        dispel=>0
        savebonus=>"0"
     END
     LAF clone_spell STR_VAR spell=EVALUATE_BUFFER ~%FIGURINE_SPIDER_WEB%=>dw#web~ editstring=~patch_conditionally=>patch_data~ END

     // generate nonmagical multi-target web, using WIZARD_WEB as a starting point
     MAKE_PATCH
        type=>effect
        dispel=>0
        savebonus=>"-4"
     END
     LAF clone_spell STR_VAR spell=EVALUATE_BUFFER ~%WIZARD_WEB%=>dw#mweb~ editstring=~patch_conditionally=>patch_data~ END


     // faster phase-spider jump

     MAKE_PATCH
        type=>effect
        match=>opcode
        check=>124
        timing=>1
        duration=>0
     END
     LAF edit_spell STR_VAR spell=EVALUATE_BUFFER ~%PHASE_SPIDER_TELEPORT%~ editstring=~patch_conditionally=>patch_data~ END

     // modified bite
     
     LAF install STR_VAR file=dw#spbit.itm location=resource END


     // BG1=specific
     
     ACTION_IF is_bg1=1 BEGIN
               
             LAF edit_creature STR_VAR creature=centeo tv=yes editstring=~remove_items=>antiweb add_items=>antiweb~ END


     
     END


END
