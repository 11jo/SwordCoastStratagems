IF
	HPPercentLT(Myself,15)
	HPPercentGT(Myself,0)
	Range(NearestEnemyOf(Myself),10)
	HPPercentGT(NearestEnemyOf(Myself),20)
	Global("GateAway","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("GateAway","LOCALS",1)
		SpellNoDec(Myself,DRYAD_TELEPORT)
END

IF
	Allegiance(Myself,ALLY)
	!Detect([ENEMY])
	CombatCounter(0)
	Global("GateAway","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("GateAway","LOCALS",1)
		SpellNoDec(Myself,DRYAD_TELEPORT)
END


//////////////////////////////////////////////////////////////////////////////////////////
///	Ascension control block
//////////////////////////////////////////////////////////////////////////////////////////


INCLUDE FILE(%scsroot%/ascension/ssl/asc_control.ssl)


//////////////////////////////////////////////////////////////////////////////////////
///	Alu-fiend combat script
//////////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/genai/ssl/genai_definitions.ssl)
INCLUDE FILE(%scsroot%/fiend/ssl/fiend_definitions.ssl)

INCLUDE FILE(%scsroot%/fiend/ssl/initial.ssl)  // rest-and-recover, mill in confusion, etc.

/////////////////////////////////////////////////////////////////////////////////////////
///	Kill Baatezu
/////////////////////////////////////////////////////////////////////////////////////////

BEGIN LOOP(scsdemon||LAWFUL_EVIL)
	INCLUDE FILE(%scsroot%/fiend/ssl/bloodwar.ssl)
END LOOP

///////////////////////////////////////////////////////////////////////////////
////	Haste
///////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	!GlobalTimerNotExpired("haste","LOCALS")
	See(NearestEnemyOf(Myself))
	!StateCheck(Myself,STATE_HASTED)
THEN DO
	Action(SpellNoDecMyself,WIZARD_HASTE)
	SetGlobalTimer("haste","LOCALS",1000)
END

//////////////////////////////////////////////////////////////////////////////////////
///	Renew mirror image
//////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	See(NearestEnemyOf(Myself))
	!StateCheck(Myself,STATE_MIRRORIMAGE)
	TriggerBlock(TruesightSafe)
	!GlobalGT("mirror","LOCALS",2)
THEN DO
	Action(SpellNoDecMyself,WIZARD_MIRROR_IMAGE|100|100)
	IncrementGlobal("mirror","LOCALS",1)
END


///////////////////////////////////////////////////////////////////////////////
////	Seduce weak-willed
///////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	Target([PC.0.0.0.0.MALE])
	Target(SecondNearest([PC.0.0.0.0.MALE]))
	Target(ThirdNearest([PC.0.0.0.0.MALE]))
	Target(FourthNearest([PC.0.0.0.0.MALE]))
	TriggerBlock(Enemy|Disabled|Charm|MR)
	CheckStatGT(scstarget,0,SAVEVSSPELL)
THEN DO
	Action(SpellNoDec,SUCCUBUS_CHARM_MALE|100|50)
END

IF TRIGGER
	Target([PC.0.0.0.0.FEMALE])
	Target(SecondNearest([PC.0.0.0.0.FEMALE]))
	Target(ThirdNearest([PC.0.0.0.0.FEMALE]))
	Target(FourthNearest([PC.0.0.0.0.FEMALE]))
	TriggerBlock(Enemy|Disabled|Charm|MR)
	CheckStatGT(scstarget,0,SAVEVSSPELL)
THEN DO
	Action(SpellNoDec,SUCCUBUS_CHARM_FEMALE|100|50)
END


///////////////////////////////////////////////////////////////////////
////	Attack antimagic
///////////////////////////////////////////////////////////////////////



IF TRIGGER
	TargetBlock(EnemiesInOrder)
	OR(4)
		CheckStatGT(scstarget,0,WIZARD_SPELL_TURNING)
		CheckStatGT(scstarget,0,WIZARD_SPELL_DEFLECTION)
		CheckStat(scstarget,1,WIZARD_SPELL_TRAP)
		CheckStatGT(scstarget,0,MINORGLOBE)
	TriggerBlock(Helpless)
	!GlobalGT("secretword","LOCALS",2)
THEN DO
	Combine()
	IncrementGlobal("secretword","LOCALS",1)
	Action(SpellNoDec,WIZARD_SECRET_WORD|100|100)
END

///////////////////////////////////////////////////////////////////////
////	Combat
///////////////////////////////////////////////////////////////////////

BEGIN LOOP(MyWeaponStrength||3)
BEGIN LOOP(MyWeaponDamageType||Slashing)
INCLUDE FILE(%scsroot%/genai/ssl/potionuse.ssl)

BEGIN LOOP(scstarget,10||scstarget,5)
	INCLUDE FILE(%scsroot%/genai/ssl/dw#wtacor.ssl)
END LOOP


INCLUDE FILE(%scsroot%/genai/ssl/shoot.ssl)
INCLUDE FILE(%scsroot%/genai/ssl/chase.ssl)
END LOOP
END LOOP