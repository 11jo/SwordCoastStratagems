//////////////////////////////////////////////////////////////////////////////////////
///	Glabrezu combat script
//////////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/fiend/ssl/fiend_definitions.ssl)

INCLUDE FILE(%scsroot%/fiend/ssl/initial.ssl)  // rest-and-recover, mill in confusion, etc.

//////////////////////////////////////////////////////////////////////////////////////////
///	Ascension control block
//////////////////////////////////////////////////////////////////////////////////////////


INCLUDE FILE(%scsroot%/ascension/ssl/asc_control.ssl)


//////////////////////////////////////////////////////////////////////////////////////
///	Gate
//////////////////////////////////////////////////////////////////////////////////////


IF
	Allegiance(Myself,ENEMY)
	Delay(6)
	!GlobalTimerNotExpired("castspell","LOCALS")
	OR(2)
		NumCreatureLT([ENEMY.0.DEMONIC],3)
		NumCreatureVsPartyLT([ENEMY.0.DEMONIC],0)
	Global("gated","LOCALS",0)
	!Range(NearestEnemyOf(Myself),10)
	OR(2)
		NumInPartyAliveGT(2)
		DifficultyGT(EASY)
THEN
	RESPONSE #100
		SetGlobalTimer("castspell","LOCALS",6)
		SetGlobal("gated","LOCALS",1)
		ForceSpellRES("GATE4",Myself) // Gate
	RESPONSE #100
		Continue()
END

//////////////////////////////////////////////////////////////////////////////////////
///	Renew its mirror image
//////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	See(NearestEnemyOf(Myself))
	!StateCheck(Myself,STATE_MIRRORIMAGE)
	TriggerBlock(TruesightSafe)
THEN DO
	Action(SpellNoDecMyself,WIZARD_MIRROR_IMAGE|100|100)
END

/////////////////////////////////////////////////////////////////////////////////////////
///	Kill Baatezu
/////////////////////////////////////////////////////////////////////////////////////////

BEGIN LOOP(scsdemon||LAWFUL_EVIL)
	INCLUDE FILE(%scsroot%/fiend/ssl/bloodwar.ssl)
END LOOP

//////////////////////////////////////////////////////////////////////////////////////
///	Consider moving to a more interesting location in the battle
//////////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/fiend/ssl/jump.ssl)

//////////////////////////////////////////////////////////////////////////////////////
///	Consider a Dispel Magic
//////////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/fiend/ssl/dispel.ssl)

//////////////////////////////////////////////////////////////////////////////////////
///	Use offensive magic
//////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////
////  Power Word: Stun
/////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(EnemiesInOrder)
	TriggerBlock(MR|SpellTurn|Stun|Helpless|SIConjuration)
	OR(3)
		HPPercentLT(scstarget,60)
		Class(scstarget,MAGE)
		Class(scstarget,MAGE_THIEF)
THEN DO
	Action(SpellNoDec,WIZARD_POWER_WORD_STUN|100|100)
END

////////////////////////////////////////////////////////////////////////////////////
///	Unholy Blight
//////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsPreferringWeak)
	TriggerBlock(MR100|Enemy|Helpless|SINecromancy)
	Alignment(scstarget,MASK_GOOD)	
THEN DO
	Action(SpellNoDec,CLERIC_UNHOLY_BLIGHT|200|100)
END

///////////////////////////////////////////////////////////////////////////
////	Confusion
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(EnemiesInOrder)
	TriggerBlock(MR100|Confused|SIEnchantment|Helpless)
THEN DO
	Combine()
	Action(SpellNoDec,WIZARD_CONFUSION|200|100)
END

//////////////////////////////////////////////////////////////////////////////
///	Combat
//////////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////
////	At least *consider* chewing on stunned victims
///////////////////////////////////////////////////////////////////////

IF TRIGGER
	Target(NearestEnemyOfType([PC]))
	StateCheck(scstarget,STATE_STUNNED)
	TriggerBlock(Enemy|Plus4Safe|CrushingSafe)
THEN DO
	Action(Attack|100|100)
END

//////////////////////////////////////////////////////////////////////////////
////	Core combat block
///////////////////////////////////////////////////////////////////////////////

BEGIN LOOP(MyWeaponStrength||4)
BEGIN LOOP(MyWeaponDamageType||Crushing)
INCLUDE FILE(%scsroot%/genai/ssl/dw#wtacor.ssl)
INCLUDE FILE(%scsroot%/genai/ssl/move.ssl)
INCLUDE FILE(%scsroot%/fiend/ssl/chase.ssl)
END LOOP
END LOOP
