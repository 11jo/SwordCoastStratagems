
VARIABLE(HasL1=True)
VARIABLE(HasL2=True)
VARIABLE(HasL3=True)
VARIABLE(HasL4=True)
VARIABLE(HasL5=True)
VARIABLE(HasL6=True)
VARIABLE(HasL7=True)
VARIABLE(HasL8=True)
VARIABLE(HasL9=True)
VARIABLE(slowspell=)

//////////////////////////////////////////////////////////////////////////////////
///
///   Define actions
///
////////////////////////////////////////////////////////////////////////////////////////

BEGIN LOOP(scsspellsubstitute1||!StateCheck(scstarget,STATE_NOT_TARGETABLE))
BEGIN LOOP(scsspellsubstitute2||!StateCheck(scstarget,STATE_INVISIBLE))
	INCLUDE FILE(%scsroot%/mage/ssl/mage_definitions.ssl)
END LOOP
END LOOP



////////////////////////////////////////////////////////////////////////////////
////	For testing
//////////////////////////////////////////////////////////////////////////////////

IF
	Global("DMWWSendaiTest","GLOBAL",1)
	!Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		Enemy()
END

/////////////////////////////////////////////////////////////////////////////////
//	Ascension stuff
//////////////////////////////////////////////////////////////////////////////////

IF
	HPLT(Myself,20)
	Global("DeathScriptSend","AR6200",0)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("DeathScriptSend","AR6200",1)
		ClearActions(Myself)
		CreateVisualEffectObject("ICRMPARI",Myself)
		CreateVisualEffectObject("SPROTECT",Myself)
		ActionOverride("finmel01",ClearActions(Myself))
		ActionOverride("finmel01",DestroyItem("finsend")) // Sendai
		ReallyForceSpellDeadRES("melspl10","finmel01") // Feedback
		Kill(Myself)
END

IF
	Global("IMPRISON","LOCALS",1)
	Global("SendaiImprisoned","AR6200",0)
THEN
	RESPONSE #100
		ClearActions(Myself)
		SetInterrupt(FALSE)
		SetGlobal("IMPRISON","LOCALS",0)
		SetGlobal("SendaiImprisoned","AR6200",1)
		DisplayStringHead(Myself,46151) // help!
		SetInterrupt(TRUE)
END


/////////////////////////////////////////////////////////////////////////////////
//	Teleport
//////////////////////////////////////////////////////////////////////////////////

IF
	!GlobalTimerNotExpired("castspell","LOCALS")
	!GlobalTimerNotExpired("DMWWTimestopRunning","GLOBAL")
	!GlobalTimerNotExpired("SendaiRing","LOCALS")
	!CheckStatGT(Myself,0,WIZARD_IMPROVED_ALACRITY)
	Allegiance(Myself,ENEMY)
	Global("SendaiTelTrack","LOCALS",0)
	GlobalLT("SendaiTeleport","LOCALS",20)
	Range(NearestEnemyOf(Myself),8)
	OR(4)
		Range(SecondNearestEnemyOf(Myself),8)
		CheckStatGT(NearestEnemyOf(Myself),4,NUMBEROFATTACKS)
		HPPercentLT(Myself,50)
		Range("finbodh",8)
	OR(4)
		Range(SecondNearestEnemyOf(Myself),8)
		CheckStatGT(NearestEnemyOf(Myself),4,NUMBEROFATTACKS)
		HPPercentLT(Myself,50)
		!Dead("finbodh")
THEN
	RESPONSE #100
		IncrementGlobal("SendaiTeleport","LOCALS",1)
		SetGlobal("SendaiTelTrack","LOCALS",1)
		SetGlobalTimer("SendaiRing","LOCALS",6)
		DisplayStringHead(Myself,73938) // Twists onyx ring
		ReallyForceSpellPoint([928.778],WIZARD_DIMENSION_DOOR)
	RESPONSE #100
		IncrementGlobal("SendaiTeleport","LOCALS",1)
		SetGlobal("SendaiTelTrack","LOCALS",2)
		SetGlobalTimer("SendaiRing","LOCALS",6)
		DisplayStringHead(Myself,73938) // Twists onyx ring
		ReallyForceSpellPoint([1652.811],WIZARD_DIMENSION_DOOR)
	RESPONSE #100
		IncrementGlobal("SendaiTeleport","LOCALS",1)
		SetGlobal("SendaiTelTrack","LOCALS",3)
		SetGlobalTimer("SendaiRing","LOCALS",6)
		DisplayStringHead(Myself,73938) // Twists onyx ring
		ReallyForceSpellPoint([1296.1278],WIZARD_DIMENSION_DOOR)
END


IF
	!GlobalTimerNotExpired("castspell","LOCALS")
	!GlobalTimerNotExpired("DMWWTimestopRunning","GLOBAL")
	!GlobalTimerNotExpired("SendaiRing","LOCALS")
	!CheckStatGT(Myself,0,WIZARD_IMPROVED_ALACRITY)
	Allegiance(Myself,ENEMY)
	Global("SendaiTelTrack","LOCALS",1)
	GlobalLT("SendaiTeleport","LOCALS",20)
	Range(NearestEnemyOf(Myself),8)
	OR(4)
		Range(SecondNearestEnemyOf(Myself),8)
		CheckStatGT(NearestEnemyOf(Myself),4,NUMBEROFATTACKS)
		HPPercentLT(Myself,50)
		Range("finbodh",8)
	OR(4)
		Range(SecondNearestEnemyOf(Myself),8)
		CheckStatGT(NearestEnemyOf(Myself),4,NUMBEROFATTACKS)
		HPPercentLT(Myself,50)
		!Dead("finbodh")
THEN
	RESPONSE #100
		IncrementGlobal("SendaiTeleport","LOCALS",1)
		SetGlobal("SendaiTelTrack","LOCALS",2)
		SetGlobalTimer("SendaiRing","LOCALS",6)
		DisplayStringHead(Myself,73938) // Twists onyx ring
		ReallyForceSpellPoint([1652.811],WIZARD_DIMENSION_DOOR)
	RESPONSE #100
		IncrementGlobal("SendaiTeleport","LOCALS",1)
		SetGlobal("SendaiTelTrack","LOCALS",3)
		SetGlobalTimer("SendaiRing","LOCALS",6)
		DisplayStringHead(Myself,73938) // Twists onyx ring
		ReallyForceSpellPoint([1296.1278],WIZARD_DIMENSION_DOOR)
END


IF
	!GlobalTimerNotExpired("castspell","LOCALS")
	!GlobalTimerNotExpired("DMWWTimestopRunning","GLOBAL")
	!GlobalTimerNotExpired("SendaiRing","LOCALS")
	!CheckStatGT(Myself,0,WIZARD_IMPROVED_ALACRITY)
	Allegiance(Myself,ENEMY)
	Global("SendaiTelTrack","LOCALS",2)
	GlobalLT("SendaiTeleport","LOCALS",20)
	Range(NearestEnemyOf(Myself),8)
	OR(4)
		Range(SecondNearestEnemyOf(Myself),8)
		CheckStatGT(NearestEnemyOf(Myself),4,NUMBEROFATTACKS)
		HPPercentLT(Myself,50)
		Range("finbodh",8)
	OR(4)
		Range(SecondNearestEnemyOf(Myself),8)
		CheckStatGT(NearestEnemyOf(Myself),4,NUMBEROFATTACKS)
		HPPercentLT(Myself,50)
		!Dead("finbodh")
THEN
	RESPONSE #100
		IncrementGlobal("SendaiTeleport","LOCALS",1)
		SetGlobal("SendaiTelTrack","LOCALS",1)
		SetGlobalTimer("SendaiRing","LOCALS",6)
		DisplayStringHead(Myself,73938) // Twists onyx ring
		ReallyForceSpellPoint([928.778],WIZARD_DIMENSION_DOOR)
	RESPONSE #100
		IncrementGlobal("SendaiTeleport","LOCALS",1)
		SetGlobal("SendaiTelTrack","LOCALS",3)
		SetGlobalTimer("SendaiRing","LOCALS",6)
		DisplayStringHead(Myself,73938) // Twists onyx ring
		ReallyForceSpellPoint([1296.1278],WIZARD_DIMENSION_DOOR)
END


IF
	!GlobalTimerNotExpired("castspell","LOCALS")
	!GlobalTimerNotExpired("DMWWTimestopRunning","GLOBAL")
	!GlobalTimerNotExpired("SendaiRing","LOCALS")
	!CheckStatGT(Myself,0,WIZARD_IMPROVED_ALACRITY)
	Allegiance(Myself,ENEMY)
	Global("SendaiTelTrack","LOCALS",3)
	GlobalLT("SendaiTeleport","LOCALS",20)
	Range(NearestEnemyOf(Myself),8)
	OR(4)
		Range(SecondNearestEnemyOf(Myself),8)
		CheckStatGT(NearestEnemyOf(Myself),4,NUMBEROFATTACKS)
		HPPercentLT(Myself,50)
		Range("finbodh",8)
	OR(4)
		Range(SecondNearestEnemyOf(Myself),8)
		CheckStatGT(NearestEnemyOf(Myself),4,NUMBEROFATTACKS)
		HPPercentLT(Myself,50)
		!Dead("finbodh")
THEN
	RESPONSE #100
		IncrementGlobal("SendaiTeleport","LOCALS",1)
		SetGlobal("SendaiTelTrack","LOCALS",1)
		SetGlobalTimer("SendaiRing","LOCALS",6)
		DisplayStringHead(Myself,73938) // Twists onyx ring
		ReallyForceSpellPoint([928.778],WIZARD_DIMENSION_DOOR)
	RESPONSE #100
		IncrementGlobal("SendaiTeleport","LOCALS",1)
		SetGlobal("SendaiTelTrack","LOCALS",2)
		SetGlobalTimer("SendaiRing","LOCALS",6)
		DisplayStringHead(Myself,73938) // Twists onyx ring
		ReallyForceSpellPoint([1652.811],WIZARD_DIMENSION_DOOR)
END


IF
	!GlobalTimerNotExpired("castspell","LOCALS")
	!CheckStatGT(Myself,0,WIZARD_IMPROVED_ALACRITY)
	OR(6)
		Dead("finilla") // Illasera
		Dead("finabaz") // Abazigal
		Dead("finyaga") // Yaga-Shura
		Dead("finbalth") // Balthazar
		Dead("fingrom") // Gromnir Il-Khan
		Dead("finsarev") // Sarevok
	Global("IMPRISON","GLOBAL",1)
	HasItem("SDScrl3",Myself) // Freedom
	!GlobalTimerNotExpired("castspell","LOCALS")
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("castspell","LOCALS",6)
		DisplayStringHead(Myself,46153) // uses a scroll
		UseItem("SDScrl3",Myself) // Freedom
		SetGlobal("IMPRISON","GLOBAL",0)
		SetGlobal("IllaseraImprisoned","AR6200",0)
		SetGlobal("AbazigalImprisoned","AR6200",0)
		SetGlobal("YagaImprisoned","AR6200",0)
		SetGlobal("BalthImprisoned","AR6200",0)
		SetGlobal("GromnirImprisoned","AR6200",0)
		SetGlobal("SarevokImprisoned","AR6200",0)
		SetInterrupt(TRUE)
END


/////////////////////////////////////////////////////////////////////////////////////////
///	Chain contingency fires
/////////////////////////////////////////////////////////////////////////////////////////

IF
	Global("prep","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("prep","LOCALS",1)
		DisplayStringHead(Myself,26328)
		ReallyForceSpell(Myself,WIZARD_SPELL_IMMUNITY_DIVINATION)
		ReallyForceSpell(Myself,WIZARD_SPELL_IMMUNITY_ABJURATION)
		ReallyForceSpellRES("dw#devae",Myself)
		SetGlobal("prep","LOCALS",1)
		SetGlobalTimer("redefend","LOCALS",0)
		ApplySpell(Myself,RESTORE_FULL_HEALTH) // her hp are down 15 because of WF's Ioun Stone
		Continue()
END	

///////////////////////////////////////////////////////////////////////////////////////
///	Set up triggers
///////////////////////////////////////////////////////////////////////////////////////

IF
	Global("setuptriggers","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("setuptriggers","LOCALS",1)
		SetGlobal("SpellTrigger","LOCALS",21)
		SetGlobal("SpellSequencer","LOCALS",21)
		SetGlobal("MinorSequencer","LOCALS",21)
END

////////////////////////////////////////////////////////////////////////////
///	Contingency contains healing
////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/caster_shared/clericmage/ssl/contingency.ssl)

////////////////////////////////////////////////////////////////////////////////////////
////	Kill the timer if we're using Alacrity
////////////////////////////////////////////////////////////////////////////////////////

IF
	CheckStatGT(Myself,0,WIZARD_IMPROVED_ALACRITY)
	GlobalTimerNotExpired("castspell","LOCALS")
THEN
	RESPONSE #100
		SetGlobalTimer("castspell","LOCALS",0)
END



///////////////////////////////////////////////////////////////////////////
//	Melee
///////////////////////////////////////////////////////////////////////////
/*
IF
	GlobalTimerNotExpired("castspell","LOCALS")
	GlobalLT("SendaiTeleport","LOCALS",20)
	!Range(NearestEnemyOf(Myself),5)
THEN
	RESPONSE #100
		NoAction()
END
*/


INCLUDE FILE(%scsroot%/priest/ssl/bg2/meleeblocks/touchattack.ssl)
INCLUDE FILE(%scsroot%/priest/ssl/bg2/meleeblocks/standard_priest.ssl)

//////////////////////////////////////////////////////////////////////////
// Renew and retreat
//////////////////////////////////////////////////////////////////////////

DEFAULT TRIGGER(!GlobalTimerNotExpired("castspell","LOCALS"))

IF	
	HPPercentLT(Myself,50)
	HasItem("send01",Myself)
THEN
	RESPONSE #100
		SetGlobalTimer("castspell","LOCALS",6)
		UseItem("send01",Myself)
END

INCLUDE FILE(%scsroot%/caster_shared/clericmage/ssl/renew.ssl)

//////////////////////////////////////////////////////////////////////////
// Look for PCs
//////////////////////////////////////////////////////////////////////////

DEFAULT TRIGGER()

IF
	!Detect(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		MoveToObject(Player1)
END

////////////////////////////////////////////////////////////////////////////
// Core magic block
///////////////////////////////////////////////////////////////////////////


DEFAULT TRIGGER(!GlobalTimerNotExpired("castspell","LOCALS"))


INCLUDE FILE(%scsroot%/mage/ssl/bg2\combatblocks\alacrity.ssl)
INCLUDE FILE(%scsroot%/priest/ssl/bg2\combatblocks\creeping_doom.ssl)

DEFAULT TRIGGER(OR(5) Global("BalthazarFights","GLOBAL",0)	Dead("balth2") 	!CheckStatGT("balth2",0,SCRIPTINGSTATE3) HPPercentLT(Myself,75) Global("FinalFight","AR6200",8))

INCLUDE FILE(%scsroot%/mage/ssl/bg2\combatblocks\wish.ssl)
INCLUDE FILE(%scsroot%/mage/ssl/bg2\combatblocks\timestop.ssl)

DEFAULT TRIGGER(!GlobalTimerNotExpired("castspell","LOCALS"))

IF
	!GlobalTimerNotExpired("castspell","LOCALS")
	!CheckStatGT(Myself,0,WIZARD_IMPROVED_ALACRITY)
	OR(6)
		Dead("finilla") // Illasera
		Dead("finabaz") // Abazigal
		Dead("finyaga") // Yaga-Shura
		Dead("finbalth") // Balthazar
		Dead("fingrom") // Gromnir Il-Khan
		Dead("finsarev") // Sarevok
	Global("IMPRISON","GLOBAL",1)
	HasItem("SDScrl3",Myself) // Freedom
	!GlobalTimerNotExpired("castspell","LOCALS")
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("castspell","LOCALS",6)
		DisplayStringHead(Myself,46153) // uses a scroll
		UseItem("SDScrl3",Myself) // Freedom
		SetGlobal("IMPRISON","GLOBAL",0)
		SetGlobal("IllaseraImprisoned","AR6200",0)
		SetGlobal("AbazigalImprisoned","AR6200",0)
		SetGlobal("YagaImprisoned","AR6200",0)
		SetGlobal("BalthImprisoned","AR6200",0)
		SetGlobal("GromnirImprisoned","AR6200",0)
		SetGlobal("SarevokImprisoned","AR6200",0)
		SetInterrupt(TRUE)
END




INCLUDE FILE(%scsroot%\mage\ssl\bg2\combatblocks\alacrity.ssl)
INCLUDE FILE(%scsroot%\caster_shared\clericmage\ssl\union.ssl)
INCLUDE FILE(%scsroot%\mage\ssl\bg2\combatblocks\imprisonment.ssl)
INCLUDE FILE(%scsroot%\mage\ssl\bg2\combatblocks\hla.ssl)
INCLUDE FILE(%scsroot%\priest\ssl\bg2\combatblocks\implosion.ssl)
INCLUDE FILE(%scsroot%\mage\ssl\bg2\combatblocks\planetar.ssl)
INCLUDE FILE(%scsroot%\priest\ssl\bg2\combatblocks\storm_of_vengeance.ssl)
INCLUDE FILE(%scsroot%\priest\ssl\bg2\combatblocks\symbol_stun.ssl)
INCLUDE FILE(%scsroot%\priest\ssl\bg2\combatblocks\holy_word.ssl)
INCLUDE FILE(%scsroot%\mage\ssl\bg2\combatblocks\kill-maze-stun.ssl)
DEFAULT TRIGGER(!GlobalTimerNotExpired("castspell","LOCALS") !CheckStatGT(Myself,0,WIZARD_IMPROVED_ALACRITY))
INCLUDE FILE(%scsroot%\priest\ssl\bg2\combatblocks\touch_attacks.ssl)
DEFAULT TRIGGER(!GlobalTimerNotExpired("castspell","LOCALS"))
INCLUDE FILE(%scsroot%\priest\ssl\bg2\combatblocks\find_invisible.ssl)
INCLUDE FILE(%scsroot%\mage\ssl\bg2\combatblocks\highpowered_area_attacks.ssl)
INCLUDE FILE(%scsroot%\priest\ssl\bg2\combatblocks\holy_smite.ssl)
INCLUDE FILE(%scsroot%\mage\ssl\bg2\combatblocks\attacks_on_pc_defences.ssl)
INCLUDE FILE(%scsroot%\mage\ssl\bg2\combatblocks\attack_antimagic.ssl)
INCLUDE FILE(%scsroot%\priest\ssl\bg2\combatblocks\my_defences.ssl)
INCLUDE FILE(%scsroot%\mage\ssl\bg2\combatblocks\arrows.ssl)
INCLUDE FILE(%scsroot%\mage\ssl\bg2\combatblocks\kill_summons.ssl)





DEFAULT TRIGGER()



//////////////////////////////////////////////////////
////	If we get this far, we can't find anything to do, so
////	set the timer (this means we attack in the gap) 
//////////////////////////////////////////////////////

IF
!GlobalTimerNotExpired("castspell","LOCALS")
THEN
	RESPONSE #100
		SetGlobalTimer("castspell","LOCALS",6)
END
	

////////////////////////////////////////////////////////
///	Closedown: avoid interaction with other scripts
////////////////////////////////////////////////////////////

IF
	!See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		NoAction()
END
