///////////// Key changes to vanilla Ascension to match Asc 2.0

DEFINE_ACTION_FUNCTION update_vanilla_ascension BEGIN


    LAF check_label STR_VAR label=fin_ascension_main RET value1=value END
    LAF check_label STR_VAR label=dw_ascension_update RET value2=value END

    ACTION_IF !( value1 || value2 ) BEGIN

       LAF make_label STR_VAR label=dw_ascension_update END
       LAF extend_area_script STR_VAR area=ar6200 top=fix_ar6200 location=resource END


    END
END

///////////// Turn flat Asc difficulty checks into SCS-style ones

// This code is not especially robust: it handles only four checks, and can't cope if they're embedded in an OR()


DEFINE_ACTION_FUNCTION ascension_to_scs BEGIN

   LAF define_difficulty STR_VAR type=ascension RET difficulty_variable END

   LAF check_label STR_VAR label=ascension_to_scs RET value END
   ACTION_IF !value BEGIN
         COPY_EXISTING
             ar6200.bcs override
             finmel01.bcs override
           DECOMPILE_AND_PATCH BEGIN
               REPLACE_TEXTUALLY 
                      ~DifficultyLT(NORMAL)~
                      ~GlobalLT("%difficulty_variable%","GLOBAL",3)
                       OR(2)
                       DifficultyLT(NORMAL)
                       !Global("%difficulty_variable%","GLOBAL",0)~
               REPLACE_TEXTUALLY
                      ~Difficulty(NORMAL)~
                      ~OR(2)
                      Global("%difficulty_variable%","GLOBAL",0)
                      Global("%difficulty_variable%","GLOBAL",3)
                      OR(2)
                      Difficulty(NORMAL)
                      Global("%difficulty_variable%","GLOBAL",3)~
               REPLACE_TEXTUALLY
                      ~Difficulty(HARD)~
                      ~OR(2)
                      Global("%difficulty_variable%","GLOBAL",0)
                      Global("%difficulty_variable%","GLOBAL",4)
                      OR(2)
                      Difficulty(HARD)
                      Global("%difficulty_variable%","GLOBAL",4)~
               REPLACE_TEXTUALLY
                      ~Difficulty(HARDEST)~
                      ~OR(2)
                      Global("%difficulty_variable%","GLOBAL",0)
                      Global("%difficulty_variable%","GLOBAL",5)
                      OR(2)
                      Difficulty(HARDEST)
                      Global("%difficulty_variable%","GLOBAL",5)~
           END
         BUT_ONLY

   
   
   END

END
