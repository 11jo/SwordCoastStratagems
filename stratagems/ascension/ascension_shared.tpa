///////////// Key changes to vanilla Ascension to match Asc 2.0

DEFINE_ACTION_FUNCTION update_vanilla_ascension BEGIN

    OUTER_SPRINT component_loc ascension // Melissan calls this
    LAF check_label STR_VAR label=fin_ascension_main RET value1=value END
    LAF check_label STR_VAR label=dw_ascension_update RET value2=value END

    ACTION_IF !( value1 || value2 ) BEGIN

       LAF make_label STR_VAR label=dw_ascension_update END
       LAF extend_area_script STR_VAR area=ar6200 top=fix_ar6200 location=resource END
       COPY "gate5.spl" "override/gate5a.spl"
       LAF install STR_VAR files="usar1ca.bam usar1cae.bam" location=resource locbase=ascension END

    END
END

///////////// Turn flat Asc difficulty checks into SCS-style ones

// This code is not especially robust: it handles only these four checks, and can't cope if they're embedded in an OR()


DEFINE_ACTION_FUNCTION ascension_to_scs BEGIN

   OUTER_SPRINT component_loc ascension // Melissan calls this
   LAF check_label STR_VAR label=dw_ascension_to_scs RET value END
   ACTION_IF !value BEGIN
         LAF make_label STR_VAR label=dw_ascension_to_scs END

         LAF define_difficulty STR_VAR type=ascension RET difficulty_variable END

         LAF DELETE_SCRIPT_BLOCK STR_VAR script=ar6200 match=melcomes END
         LAF DELETE_SCRIPT_BLOCK STR_VAR script=ar6200 match=allfive END
         LAF extend STR_VAR script=ar6200 top=ar6200_final location=ssl ssl=yes END

         COPY_EXISTING
             finmel01.dlg override
             finmel01.bcs override
           DECOMPILE_AND_PATCH BEGIN
               REPLACE_TEXTUALLY
                      ~!DifficultyLT(HARD)~
                      ~DifficultyGT(NORMAL)~
               REPLACE_TEXTUALLY
                      ~!DifficultyLT(NORMAL)~
                      ~DifficultyGT(EASY)~
               REPLACE_TEXTUALLY
                      ~!DifficultyGT(NORMAL)~
                      ~DifficultyLT(HARD)~
               REPLACE_TEXTUALLY
                      ~!DifficultyGT(EASY)~
                      ~DifficultyLT(NORMAL)~
               REPLACE_TEXTUALLY
                      ~DifficultyGT(NORMAL)~
                      ~OR(2)
                      Global("%difficulty_variable%","GLOBAL",0)
                      GlobalGT("%difficulty_variable%","GLOBAL",3)
                      OR(2)
                      DifficultyGT(NORMAL)
                      GlobalGT("%difficulty_variable%","GLOBAL",3)~
               REPLACE_TEXTUALLY
                      ~DifficultyLT(HARD)~
                      ~GlobalLT("%difficulty_variable%","GLOBAL",4)
                      OR(2)
                      DifficultyLT(HARD)
                      !Global("%difficulty_variable%","GLOBAL",0)~
               REPLACE_TEXTUALLY
                      ~DifficultyGT(EASY)~
                      ~OR(2)
                      Global("%difficulty_variable%","GLOBAL",0)
                      GlobalGT("%difficulty_variable%","GLOBAL",2)
                      OR(2)
                      DifficultyGT(EASY)
                      GlobalGT("%difficulty_variable%","GLOBAL",2)~
               REPLACE_TEXTUALLY
                      ~DifficultyLT(NORMAL)~
                      ~GlobalLT("%difficulty_variable%","GLOBAL",3)
                      OR(2)
                      DifficultyLT(NORMAL)
                      !Global("%difficulty_variable%","GLOBAL",0)~
           END
         BUT_ONLY

   
   
   END

END
