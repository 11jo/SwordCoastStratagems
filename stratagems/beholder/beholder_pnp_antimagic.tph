//////////////////////////////////////////////////////////////////////////
///  Because of Ardanis' Spell Shield fix, this actually has to be
///  run in "initial", so isn't called by the beholder component proper
///
//////////////////////////////////////////////////////////////////////////


DEFINE_ACTION_FUNCTION ~beholder_pnp_antimagic~ BEGIN

    // make a spell which prevents the caster from using magic for one round
    ACTION_DEFINE_ASSOCIATIVE_ARRAY ab_data BEGIN
        type => ability
        ability_type => 1
        ability_icon_loc => 2
        ability_target => 5
        ability_min_level => 1
    END

    ACTION_CLEAR_ARRAY patch_data
    ACTION_DEFINE_ASSOCIATIVE_ARRAY patch_data BEGIN
         // overall ability
          add_entry=> ab_data
         // temporary effects: spell levels, casting failure
          add_effect_inline=>~opcode=> 60  parameter1=>100 parameter2=>0 duration=>6 target=>2~ // casting failure: inc/dec
          add_effect_inline'=>~opcode=> 60  parameter1=>100 parameter2=>1 duration=>6 target=>2~ // casting failure: set
          add_effect_inline''=>~opcode=> 102  parameter1=>1 duration=>6 target=>2~                // spell level immunity
          add_effect_inline'''=>~opcode=> 102  parameter1=>2 duration=>6 target=>2~
          add_effect_inline''''=>~opcode=> 102  parameter1=>3 duration=>6 target=>2~
          add_effect_inline'''''=>~opcode=> 102  parameter1=>4 duration=>6 target=>2~
          add_effect_inline''''''=>~opcode=> 102  parameter1=>5 duration=>6 target=>2~
          add_effect_inline'''''''=>~opcode=> 102  parameter1=>6 duration=>6 target=>2~
          add_effect_inline''''''''=>~opcode=> 102  parameter1=>7 duration=>6 target=>2~
          add_effect_inline'''''''''=>~opcode=> 102  parameter1=>8 duration=>6 target=>2~
          add_effect_inline''''''''''=>~opcode=> 102  parameter1=>9 duration=>6 target=>2~
          add_effect_inline'''''''''''=>~opcode=> 142  parameter2=>28 duration=>6 target=>2~               // icon
          // permanent effects: dispel
          add_effect_inline''''''''''''=>~opcode=> 58 timing=>1 target=>2~
          // cosmetic
          add_effect_inline'''''''''''''=>~opcode=> 141 timing=>1 target=>2 parameter2=>14~                   // initial flash
          add_effect_inline''''''''''''''=>~opcode=> 174 timing=>1 target=>2 parameter2=>14 resource=>eff_m02~              // initial sound
          add_effect_inline'''''''''''''''=>~opcode=> 174 timing=>4 duration=>6 target=>2 parameter2=>14 resource=>eff_e04~   // final sound
          add_effect_inline''''''''''''''''=>~opcode=> 50 duration=>2 target=>2 parameter1=>0x573f2100 parameter2=>0x140000~  // short-duration colour change
    END

    LAF make_spell STR_VAR spell=~dw#bhpan~ edits=patch_data END

    // modify the beholder ray itself

    ACTION_CLEAR_ARRAY patch_data
    ACTION_DEFINE_ASSOCIATIVE_ARRAY patch_data BEGIN
       delete_opcodes => ~112 58 60~  // remove item-removal effects overlay
       patch_cond_inline => ~type=>ability
                             projectile=>1~ // direct gaze attack - no projectile
       patch_cond_inline => ~match=>duration
                             check=>20
                             type=>effect
                             duration=>6~  // 1-rd duration for antimagic effect
       add_effect_inline => ~opcode=>58 target=>2 timing=>4 duration=>1~  // once-per-second dispels
       add_effect_inline' => ~opcode=>58 target=>2 timing=>4 duration=>2~
       add_effect_inline'' => ~opcode=>58 target=>2 timing=>4 duration=>3~
       add_effect_inline''' => ~opcode=>58 target=>2 timing=>4 duration=>4~
       add_effect_inline'''' => ~opcode=>58 target=>2 timing=>4 duration=>5~
       add_effect_inline''''' => ~opcode=>58 target=>2 timing=>4 duration=>6~
       add_effect_inline'''''' => ~opcode=>101 target=>2 parameter2=>17 duration=>6~ // immune to healing
       add_effect_inline''''''' => ~opcode=>146 target=>2 timing=>1 resist_dispel=>2 parameter2=>1 resource=>dw#bhpan~ // apply the antimagic spell
       add_effect_inline'''''''' => ~opcode=>144 target=>2 parameter2=>9 duration=>6~ // block quickslots
       add_effect_inline''''''''' => ~opcode=>144 target=>2 parameter2=>11 duration=>6~
       add_effect_inline'''''''''' => ~opcode=>144 target=>2 parameter2=>12 duration=>6~
       add_effect_inline''''''''''' => ~opcode=>144 target=>2 parameter2=>8 duration=>6~  //block useable items
       add_effect_inline'''''''''''' => ~opcode=>166 target=>2 parameter1=>110 parameter2=>1 duration=>6~ //110% A-M (for targetting)
       add_effect_inline''''''''''''' => ~opcode=>282 target=>2 parameter1=>7 parameter2=>8 duration=>6~  // WIZARD_SPELL_TRAP=7 marker
    END


    LAF clone_spell STR_VAR spell=~spin992=>dw#bhan spin550=>dw#hvan~ edits=patch_data END
END