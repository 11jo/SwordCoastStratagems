////////////////////////////////////////////////////////////////////
//	Define beholder eyestalk power
/////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%\beholder\ssl\beholder_definitions.ssl)

//////////////////////////////////////////////////////////////////////
//	Initialise
//////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////
//	Go hostile (because Amn is simply swarming with neutral beholders...)
//////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%\beholder\ssl\gohostile.ssl)


IF
	See(NearestEnemyOf(Myself))
	!Global("initialise","LOCALS",1)
THEN
	RESPONSE #500
		SetGlobalTimer("SeeBounce","LOCALS",2)
		SetGlobalTimer("SeeImmune","LOCALS",8)
		SetGlobal("initialise","LOCALS",1)
		SetGlobalTimer("rtime","LOCALS",2000)
		SetGlobalTimer("RoundEnded","LOCALS",6)
		Continue()
	RESPONSE #500
		SetGlobalTimer("SeeBounce","LOCALS",3)
		SetGlobalTimer("SeeImmune","LOCALS",9)
		SetGlobal("initialise","LOCALS",1)
		SetGlobalTimer("rtime","LOCALS",2000)
		SetGlobalTimer("RoundEnded","LOCALS",6)
		Continue()
	RESPONSE #500
		SetGlobalTimer("SeeBounce","LOCALS",5)
		SetGlobalTimer("SeeImmune","LOCALS",11)
		SetGlobal("initialise","LOCALS",1)
		SetGlobalTimer("rtime","LOCALS",2000)
		SetGlobalTimer("RoundEnded","LOCALS",6)
		Continue()
	RESPONSE #500
		SetGlobalTimer("SeeBounce","LOCALS",6)
		SetGlobalTimer("SeeImmune","LOCALS",12)
		SetGlobal("initialise","LOCALS",1)
		SetGlobalTimer("rtime","LOCALS",2000)
		SetGlobalTimer("RoundEnded","LOCALS",6)
		Continue()
END

//////////////////////////////////////////////////////////////////////
//	Rest and recover
//////////////////////////////////////////////////////////////////////

IF
	Global("initialise","LOCALS",1)
	GlobalTimerExpired("rtime","LOCALS")
	!See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		SetGlobal("initialise","LOCALS",0)
		ApplySpell(Myself,RESTORE_FULL_HEALTH) 
	        SetGlobal("ParalyseEyeLost","LOCALS",0)
		SetGlobalTimer("paralyse","LOCALS",0)
	       	SetGlobal("CauseWoundsEyeLost","LOCALS",0)
		SetGlobalTimer("woundingeye","LOCALS",0)
        	SetGlobal("CharmEyeLost","LOCALS",0)
		SetGlobalTimer("charmeye","LOCALS",0)
        	SetGlobal("SlowEyeLost","LOCALS",0)
		SetGlobalTimer("sloweye","LOCALS",0)
        	SetGlobal("LightningEyeLost","LOCALS",0)
		SetGlobalTimer("lightning","LOCALS",0)
		Rest()
END

/////////////////////////////////////////////////////////////////////////////
///	Conserve CPU cycles
//////////////////////////////////////////////////////////////////////////////

IF
	!Detect(NearestEnemyOf(Myself))
	!Global("inafight","LOCALS",1)
THEN
	RESPONSE #100
		NoAction()
END


///////////////////////////////////////////////////////////////////////
///	Damaged eyestalks (borrowed from SimDing0)
///////////////////////////////////////////////////////////////////////

INCLUDE FILE (%scsroot%/beholder/ssl/damage_gauth.ssl)

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///  For eye attacks, we go around in 3 phases: lethal eyes; less lethal eyes; clearup. This is all controlled by
///  the variable BeholderBehavior, which cycles 0,1,2.
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//
//  WOUNDING EYE 
//
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	Global("BeholderBehavior","LOCALS",0)
	TargetBlock(PCsPreferringWeak)
	HPPercentLT(scstarget,40)
	!HasItem("clck26",scstarget)
	TriggerBlock(Enemy|Disabled)
THEN DO
	Combine()
	Action(Eyestalk,BEHOLDER_CAUSE_SERIOUS_WOUNDS,woundingeye|100|100)
END

IF TRIGGER
	Global("BeholderBehavior","LOCALS",0)
	TargetBlock(PCsPreferringWeak)
	TargetBlock(EnemiesInOrder)
	!HasItem("clck26",scstarget)
	TriggerBlock(Enemy|Disabled)
THEN DO
	Combine()
	Action(Eyestalk,BEHOLDER_CAUSE_SERIOUS_WOUNDS,woundingeye|100|100)
	Continue()
END
	


//////////////////////////////////////////////////////////////////////////////////////////////////////
//
// LIGHTNING EYE
//
//////////////////////////////////////////////////////////////////////////////////////////////////////

BEGIN LOOP(scsloopvar||!GlobalTimerExpired("SeeImmune","LOCALS");TriggerBlock(ResistElectric)&GlobalTimerExpired("SeeImmune","LOCALS"))

IF TRIGGER
	Global("BeholderBehavior","LOCALS",0)
	scsloopvar
	TargetBlock(PCsInOrder)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(Enemy|Disabled)
THEN DO
	Combine()
	Action(Eyestalk,BEHOLDER_LIGHTNING_BOLT,lightning|100|100)
	Continue()
END
	
END LOOP

//////////////////////////////////////////////////////////////////////////////////////////////////////////
///	Block, to prevent us firing everything at once
//////////////////////////////////////////////////////////////////////////////////////////////////////////

IF
	See(NearestEnemyOf(Myself))
	Global("BeholderBehavior","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("BeholderBehavior","LOCALS",1)
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////
//
// PARALYSATION EYE
//
/////////////////////////////////////////////////////////////////////////////////////////////////////////

BEGIN LOOP(scsloopvar||!GlobalTimerExpired("SeeImmune","LOCALS");TriggerBlock(Hold)&GlobalTimerExpired("SeeImmune","LOCALS"))

IF TRIGGER
	scsloopvar
	TargetBlock(PCsInOrder)
	TriggerBlock(Enemy|Disabled)
THEN DO
	Combine()
	Action(Eyestalk,BEHOLDER_PARALYZATION,paralyse|100|100)
	Continue()
END
	
END LOOP


/////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//
// CHARM EYE
//
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////

BEGIN LOOP(scsloopvar||!GlobalTimerExpired("SeeImmune","LOCALS");TriggerBlock(Charm)&GlobalTimerExpired("SeeImmune","LOCALS"))

IF TRIGGER
	General(scstarget,HUMANOID)
	scsloopvar
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(Enemy|Disabled)
THEN DO
	Combine()
	Action(Eyestalk,BEHOLDER_CHARM_PERSON,charmeye|100|100)
END
	
END LOOP

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//
//  SLOW EYE 
//
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

BEGIN LOOP(scsloopvar||!GlobalTimerExpired("SeeImmune","LOCALS");TriggerBlock(Slow)&GlobalTimerExpired("SeeImmune","LOCALS"))

IF TRIGGER
	scsloopvar
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(Enemy|Disabled)
THEN DO
	Combine()
	Action(Eyestalk,BEHOLDER_SLOW,sloweye|100|100)
	Continue()
END
	
END LOOP


//////////////////////////////////////////////////////////////////////////////////////////////////////////
///	Block, to prevent us firing everything at once
//////////////////////////////////////////////////////////////////////////////////////////////////////////

IF
	See(NearestEnemyOf(Myself))
	Global("BeholderBehavior","LOCALS",1)
THEN
	RESPONSE #100
		SetGlobal("BeholderBehavior","LOCALS",2)
END

///////////////////////////////////////////////////////////////////////////////////////////////////////////////
///	Now re-use lethal eyestalks on helpless foes (but not, at this stage, charmed ones)
///////////////////////////////////////////////////////////////////////////////////////////////////////////////


IF TRIGGER
	GlobalGT("BeholderBehavior","LOCALS",1)
	TargetBlock(PCsPreferringWeak)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(Enemy|MR100)
	!HasItem("clck26",scstarget)
THEN DO
	Combine()
	Action(Eyestalk,BEHOLDER_CAUSE_SERIOUS_WOUNDS,woundingeye)
	Continue()
END


IF TRIGGER
	Global("BeholderBehavior","LOCALS",0)
	TargetBlock(PCsInOrder)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(Enemy|MR100|ResistElectric)
THEN DO
	Combine()
	Action(Eyestalk,BEHOLDER_LIGHTNING_BOLT,lightning)
	Continue()
END
	
////////////////////////////////////////////////////////////////////////////////////////////////////
//	Kill charmed allies if they're all that's left standing
////////////////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	GlobalGT("BeholderBehavior","LOCALS",1)
	TargetBlock(PlayersInRandomOrder)
	!See(NearestEnemyOf(Myself))
	!StateCheck(Myself,STATE_BLIND)
	!GlobalTimerNotExpired("deathray","LOCALS")
	!GlobalTimerNotExpired("woundingeye","LOCALS")
	!GlobalTimerNotExpired("petrify","LOCALS")
	!CheckStatGT(scstarget,0,SANCTUARY)
	OR(2)
		!GlobalTimerExpired("SeeBounce","LOCALS")
		!HasItem("wa2shiel",scstarget)
	!HasItem("clck26",scstarget)
THEN DO
	Action(Literal,SetGlobalTimer("woundingeye"\"LOCALS"\6)&SetGlobalTimer("lightning"\"LOCALS"\6)&ForceSpell(scstarget\BEHOLDER_CAUSE_SERIOUS_WOUNDS)&ForceSpell(scstarget\BEHOLDER_LIGHTNING_BOLT))
END	

///////////////////////////////////////////////////////////////////////////////////////////////////////////
////	If we get to here with unused eyestalks, it's reasonable to infer that we're avoiding targetting people
////	who are magic-resistant or carrying the nerfed Shield of Balduran. Stop being fastidious for a round or two and see 
////	if it helps
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

IF
	See(NearestEnemyOf(Myself))
	!GlobalTimerNotExpired("IgnoreResistance","LOCALS")
	OR(2)
		!GlobalTimerNotExpired("lightning","LOCALS")
		!GlobalTimerNotExpired("woundingeye","LOCALS")
THEN
	RESPONSE #100
		SetGlobalTimer("IgnoreResistance","LOCALS",12)
END



/////////////////////////////////////////////////////////////////////////////////////////////////////////
//	Reset behaviour
///////////////////////////////////////////////////////////////////////////////////////////////////////////

IF
	See(NearestEnemyOf(Myself))
	GlobalGT("BeholderBehavior","LOCALS",1)
	!GlobalTimerNotExpired("RoundEnded","LOCALS")
		OR(2)
		!GlobalTimerNotExpired("lightning","LOCALS")
		!GlobalTimerNotExpired("woundingeye","LOCALS")
THEN
	RESPONSE #100
		SetGlobal("BeholderBehavior","LOCALS",0)
		SetGlobalTimer("RoundEnded","LOCALS",6)
END


/////////////////////////////////////////////////////////////////////
// Combat block kicks in here
/////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsPreferringWeak)
	TargetBlock(CloseEnemies)
	TriggerBlock(Enemy|Plus3Safe)
	Range(scstarget,10)
THEN DO
	Action(Attack)
END

IF
	!Range(NearestEnemyOf(Myself),30)
	See(NearestEnemyOf(Myself))
	NumCreatureGT([0.0.BEHOLDER],1)
THEN
	RESPONSE #100
		MoveToObject(LastSeenBy(Myself))
END

INCLUDE FILE(%scsroot%\genai\ssl\chase.ssl)
