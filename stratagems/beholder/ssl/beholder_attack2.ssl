

//////////////////////////////////////////////////////////////////////////////////////////////////////////
//
// PARALYSATION EYE
//
/////////////////////////////////////////////////////////////////////////////////////////////////////////

BEGIN LOOP(scsloopvar||!GlobalTimerExpired("SeeImmune","LOCALS");TriggerBlock(Hold)&GlobalTimerExpired("SeeImmune","LOCALS"))

IF TRIGGER
	scsloopvar
	TargetBlock(PCsInOrder)
	TriggerBlock(Enemy|Disabled|MR100)
THEN DO
	Combine()
	Action(Eyestalk,BEHOLDER_PARALYZATION,paralyse|100|100)
	Continue()
END

END LOOP

//////////////////////////////////////////////////////////////////////////////////////////////////////////
//
// TELEKINESIS EYE
//
/////////////////////////////////////////////////////////////////////////////////////////////////////////

// steal stuff

INCLUDE FILE(%scsroot%/beholder/ssl/beholder_steal.ssl)

// push back

IF TRIGGER
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(Enemy|Disabled|MR100)
THEN DO
	Combine()
	Action(EyestalkRES,dw#betel,telekinesis|100|100)
	Continue()
END

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//
// DOMINATION EYE
//
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////

BEGIN LOOP(scsloopvar||!GlobalTimerExpired("SeeImmune","LOCALS");TriggerBlock(Charm)&GlobalTimerExpired("SeeImmune","LOCALS"))

IF TRIGGER
	IgnoreBlock(IsUndead)
	scsloopvar
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(Enemy|Disabled|MR100)
THEN DO
	Combine()
	Action(Eyestalk,BEHOLDER_DOMINATION,dominate|100|100) // no Continue() here as it leads to killing your own charmed foes!
END
	
END LOOP




/////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//
// FEAR EYE 
//
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////

BEGIN LOOP(scsloopvar||!GlobalTimerExpired("SeeImmune","LOCALS");TriggerBlock(Panic)&GlobalTimerExpired("SeeImmune","LOCALS"))

IF TRIGGER
	scsloopvar
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(Enemy|Disabled|MR100)
THEN DO
	Combine()
	Action(Eyestalk,BEHOLDER_FEAR,feareye|100|100)
	Continue()
END
	
END LOOP

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//
// CHARM EYE
//
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////

BEGIN LOOP(scsloopvar||!GlobalTimerExpired("SeeImmune","LOCALS");TriggerBlock(Charm)&GlobalTimerExpired("SeeImmune","LOCALS"))

IF TRIGGER
	IgnoreBlock(IsUndead)
	General(scstarget,HUMANOID)
	scsloopvar
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(Enemy|Disabled|MR100)
THEN DO
	Combine()
	Action(Eyestalk,BEHOLDER_CHARM_PERSON,charmeye|100|100)
END
	
END LOOP

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//
//  SLOW EYE 
//
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

BEGIN LOOP(scsloopvar||!GlobalTimerExpired("SeeImmune","LOCALS");TriggerBlock(Slow)&GlobalTimerExpired("SeeImmune","LOCALS"))

IF TRIGGER
	scsloopvar
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(Enemy|Disabled|MR100)
THEN DO
	Combine()
	Action(Eyestalk,BEHOLDER_SLOW,sloweye|100|100)
	Continue()
END
	
END LOOP

//////////////////////////////////////////////////////////////////////////////////////////////////////////
///	Block, to prevent us firing everything at once
//////////////////////////////////////////////////////////////////////////////////////////////////////////

IF
	See(NearestEnemyOf(Myself))
	Global("BeholderBehavior","LOCALS",1)
THEN
	RESPONSE #100
		SetGlobal("BeholderBehavior","LOCALS",2)
END

///////////////////////////////////////////////////////////////////////////////////////////////////////////////
///	Now re-use lethal eyestalks on helpless foes (but not, at this stage, charmed ones)
///////////////////////////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	GlobalGT("BeholderBehavior","LOCALS",1)
	TriggerBlock(MR100|Slay|Enemy)
	TargetBlock(PlayersInRandomOrder)
	TargetBlock(EnemiesInOrder)
	!HasItemEquiped("clck26",scstarget)
THEN DO
	Combine()
	Action(Eyestalk,BEHOLDER_DEATH_RAY,deathray)
	Continue()
END

IF TRIGGER
	GlobalGT("BeholderBehavior","LOCALS",1)
	TargetBlock(PCsPreferringWeak)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(Enemy|MR100)
	!HasItem("clck26",scstarget)
	!HasItemEquiped("clck26",scstarget)
	!StateCheck(scstarget,STATE_STONE_DEATH)
THEN DO
	Combine()
	Action(Eyestalk,BEHOLDER_CAUSE_SERIOUS_WOUNDS,woundingeye)
	Continue()
END
	
IF TRIGGER
	GlobalGT("BeholderBehavior","LOCALS",1)
	TargetBlock(PCsInOrder)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(Enemy|MR100|Petrify)
	!HasItemEquiped("clck26",scstarget)
THEN DO
	Combine()
	Action(Eyestalk,BEHOLDER_FLESH_TO_STONE,petrify)
	Continue()
END

//////////////////////////////////////////////////////////////////////////////////////////
// Finish off weak summons
//////////////////////////////////////////////////////////////////////////////////////////


IF TRIGGER
	GlobalGT("BeholderBehavior","LOCALS",1)
	TriggerBlock(MR100|Slay|Enemy)
	TargetBlock(Summons)
	Gender(scstarget,SUMMONED)
THEN DO
	Combine()
	Action(EyestalkRES,dw#besud,deathray|100|100)
	Continue()
END

////////////////////////////////////////////////////////////////////////////////////////////////////
//	Kill charmed allies if they're all that's left standing
////////////////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PlayersInRandomOrder)
	!See(NearestEnemyOf(Myself))
	!StateCheck(Myself,STATE_BLIND)
	!GlobalTimerNotExpired("deathray","LOCALS")
	!GlobalTimerNotExpired("woundingeye","LOCALS")
	!GlobalTimerNotExpired("petrify","LOCALS")
	!CheckStatGT(scstarget,0,SANCTUARY)
	OR(2)
		!GlobalTimerExpired("SeeBounce","LOCALS")
		!HasItem("wa2shiel",scstarget)
	!HasItem("clck26",scstarget)
THEN DO
	Action(Literal,SetGlobalTimer("woundingeye"\"LOCALS"\6)&SetGlobalTimer("deathray"\"LOCALS"\6)&SetGlobalTimer("petrify"\"LOCALS"\6)&ForceSpell(scstarget\BEHOLDER_CAUSE_SERIOUS_WOUNDS)&ForceSpell(scstarget\BEHOLDER_FLESH_TO_STONE)&ForceSpell(scstarget\BEHOLDER_DEATH_RAY))
END	


///////////////////////////////////////////////////////////////////////////////////////////////////////////
////	If we get to here with unused eyestalks, it's reasonable to infer that we're avoiding targetting people
////	who are magic-resistant. Stop being fastidious for a round or two and see 
////	if it helps
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

IF
	See(NearestEnemyOf(Myself))
	!GlobalTimerNotExpired("IgnoreResistance","LOCALS")
	OR(3)
		!GlobalTimerNotExpired("deathray","LOCALS")
		!GlobalTimerNotExpired("petrify","LOCALS")
		!GlobalTimerNotExpired("woundingeye","LOCALS")
THEN
	RESPONSE #100
		SetGlobalTimer("IgnoreResistance","LOCALS",12)
END



/////////////////////////////////////////////////////////////////////////////////////////////////////////
//	Reset behaviour
///////////////////////////////////////////////////////////////////////////////////////////////////////////

IF
	See(NearestEnemyOf(Myself))
	GlobalGT("BeholderBehavior","LOCALS",1)
	!GlobalTimerNotExpired("RoundEnded","LOCALS")
		OR(4)
		!GlobalTimerNotExpired("antimagicray","LOCALS")
		!GlobalTimerNotExpired("deathray","LOCALS")
		!GlobalTimerNotExpired("petrify","LOCALS")
		!GlobalTimerNotExpired("woundingeye","LOCALS")
THEN
	RESPONSE #100
		SetGlobal("BeholderBehavior","LOCALS",0)
		SetGlobalTimer("RoundEnded","LOCALS",6)
END

/////////////////////////////////////////////////////////////////////////////////////////////////////////////
///	Check if we have any lethal eyestalks unused. If not, and no enemies are in strike range, and if we aren't
///	currently avoiding fastidiousness, stop, instead
///	of going into melee - it means that there are lethal things we can still do from a distance.
/////////////////////////////////////////////////////////////////////////////////////////////////////////////

IF
	False()
	See(NearestEnemyOf(Myself)) // if not we want various bits of WTASIGHT to kick in
	OR(3)
		!GlobalTimerExpired("deathray","LOCALS") //we've fired this eyestalk recently - hence, some targets
		!GlobalTimerExpired("petrify","LOCALS")
		!GlobalTimerExpired("woundingeye","LOCALS")
	!GlobalTimerNotExpired("IgnoreResistance","LOCALS")
	!Range(NearestEnemyOf(Myself),5)
THEN
	RESPONSE #100
		NoAction()
END
/////////////////////////////////////////////////////////////////////
// Combat block kicks in here
/////////////////////////////////////////////////////////////////////



IF TRIGGER
	TargetBlock(PCsPreferringWeak)
	TargetBlock(CloseEnemies)
	TriggerBlock(Enemy|Plus3Safe|PiercingSafe)
	Range(scstarget,10)
THEN DO
	Action(Attack)
END


IF
	!Range(NearestEnemyOf(Myself),30)
	See(NearestEnemyOf(Myself))
	NumCreatureGT([0.0.BEHOLDER],1)
THEN
	RESPONSE #100
		MoveToObject(LastSeenBy(Myself))
END


INCLUDE FILE(%scsroot%\genai\ssl\chase.ssl)
