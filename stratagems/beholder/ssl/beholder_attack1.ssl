


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///  For eye attacks, we go around in 3 phases: lethal eyes; less lethal eyes; clearup. This is all controlled by
///  the variable BeholderBehavior, which cycles 0,1,2.
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////


INCLUDE FILE(%scsroot%/beholder/ssl/beholder_antimagic.ssl)

///////////////////////////////////////////////////////////////////////////////
/// DISINTEGRATION EYE (only for masochists)
///////////////////////////////////////////////////////////////////////////////

BEGIN LOOP(scsloopvar||!GlobalTimerExpired("SeeImmune","LOCALS");TriggerBlock(Disintegrate)&GlobalTimerExpired("SeeImmune","LOCALS")) // The reason for checking that the timer HAS expired is to avoid confusing the multiple-shots-per-round routine

IF TRIGGER
        IgnoreBlock(Demivrgvs)
	Global("BeholderBehavior","LOCALS",0)
	Global("DMWWBeholderDisintegrate","GLOBAL",1)
	scsloopvar
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(Enemy|Disabled|MR100)

THEN DO
	Combine()
	Action(Eyestalk,BEHOLDER_DISINTEGRATE,disintegrate|100|100)
	Continue()
END

IF TRIGGER
        RequireBlock(Demivrgvs)
	Global("BeholderBehavior","LOCALS",0)
	Global("DMWWBeholderDisintegrate","GLOBAL",0)
	scsloopvar
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(Enemy|Disabled|MR100)
THEN DO
	Combine()
	Action(Eyestalk,BEHOLDER_DISINTEGRATE,disintegrate|100|100)
	Continue()
END

END LOOP

/////////////////////////////////////////////////////////////////////////////////////
// 
// DEATH RAY EYE
//
/////////////////////////////////////////////////////////////////////////////////////

/////////////////////////////////////////////////////////////////////////////////////
//  Kill summons (don't waste time on pathetic ones)
/////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	Global("BeholderBehavior","LOCALS",0) // we check this explicitly to avoid using lethal eyestalks twice in one script round
	TargetBlock(Summons)
	OR(2)
		Gender(scstarget,SUMMONED)
		Gender(scstarget,20) // allowing for Sim's "remove summoning limit"
	TriggerBlock(Enemy)
	CheckStatGT(scstarget,5,Level)
THEN DO
	Combine()
	Action(EyestalkRES,dw#besud,deathray|100|100)
END

////////////////////////////////////////////////////////////////////////////////////
//  Kill players
////////////////////////////////////////////////////////////////////////////////////	

BEGIN LOOP(scsloopvar||!GlobalTimerExpired("SeeImmune","LOCALS");TriggerBlock(Slay)&GlobalTimerExpired("SeeImmune","LOCALS")) // The reason for checking that the timer HAS expired is to avoid confusing the multiple-shots-per-round routine

IF TRIGGER
	Global("BeholderBehavior","LOCALS",0)
	scsloopvar
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(Enemy|Disabled|MR100)
THEN DO
	Combine()
	Action(Eyestalk,BEHOLDER_DEATH_RAY,deathray|100|100)
	Continue()
END

END LOOP



///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//
//  WOUNDING EYE 
//
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	Global("BeholderBehavior","LOCALS",0)
	TargetBlock(PCsPreferringWeak)
	HPPercentLT(scstarget,40)
	!HasItem("clck26",scstarget)
	TriggerBlock(Enemy|Disabled|MR100)
	!StateCheck(scstarget,STATE_STONE_DEATH)
THEN DO
	Combine()
	Action(Eyestalk,BEHOLDER_CAUSE_SERIOUS_WOUNDS,woundingeye|100|100)
END

IF TRIGGER
	Global("BeholderBehavior","LOCALS",0)
	TargetBlock(PCsPreferringWeak)
	TargetBlock(EnemiesInOrder)
	!HasItem("clck26",scstarget)
	TriggerBlock(Enemy|Disabled|MR100)
THEN DO
	Combine()
	Action(Eyestalk,BEHOLDER_CAUSE_SERIOUS_WOUNDS,woundingeye|100|100)
	Continue()
END
	



//////////////////////////////////////////////////////////////////////////////////////////////////////
//
// FLESH TO STONE EYE
//
//////////////////////////////////////////////////////////////////////////////////////////////////////

BEGIN LOOP(scsloopvar||!GlobalTimerExpired("SeeImmune","LOCALS");TriggerBlock(Petrify)&GlobalTimerExpired("SeeImmune","LOCALS"))

IF TRIGGER
	Global("BeholderBehavior","LOCALS",0)
	scsloopvar
	TargetBlock(PCsInOrder)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(Enemy|Disabled|MR100)
THEN DO
	Combine()
	Action(Eyestalk,BEHOLDER_FLESH_TO_STONE,petrify|100|100)
	Continue()
END
	
END LOOP

//////////////////////////////////////////////////////////////////////////////////////////////////////////
///	Block, to prevent us firing everything at once
//////////////////////////////////////////////////////////////////////////////////////////////////////////

IF
	See(NearestEnemyOf(Myself))
	Global("BeholderBehavior","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("BeholderBehavior","LOCALS",1)
END
