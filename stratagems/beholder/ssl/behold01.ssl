
////////////////////////////////////////////////////////////////////
//	Define beholder eyestalk power
/////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%\beholder\ssl\beholder_definitions.ssl)


//////////////////////////////////////////////////////////////////////
//	Go hostile (because Amn is simply swarming with neutral beholders...)
//////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%\beholder\ssl\gohostile.ssl)


IF
	See(NearestEnemyOf(Myself))
	!Global("initialise","LOCALS",1)
THEN
	RESPONSE #500
		SetGlobalTimer("SeeBounce","LOCALS",2)
		SetGlobalTimer("SeeImmune","LOCALS",8)
		SetGlobal("inafight","LOCALS",1)
		SetGlobal("initialise","LOCALS",1)
		SetGlobalTimer("rtime","LOCALS",2000)
		SetGlobalTimer("RoundEnded","LOCALS",6)
		Continue()
	RESPONSE #500
		SetGlobalTimer("SeeBounce","LOCALS",3)
		SetGlobalTimer("SeeImmune","LOCALS",9)
		SetGlobal("inafight","LOCALS",1)
		SetGlobal("initialise","LOCALS",1)
		SetGlobalTimer("rtime","LOCALS",2000)
		SetGlobalTimer("RoundEnded","LOCALS",6)
		Continue()
	RESPONSE #500
		SetGlobalTimer("SeeBounce","LOCALS",5)
		SetGlobalTimer("SeeImmune","LOCALS",11)
		SetGlobal("inafight","LOCALS",1)
		SetGlobal("initialise","LOCALS",1)
		SetGlobalTimer("rtime","LOCALS",2000)
		SetGlobalTimer("RoundEnded","LOCALS",6)
		Continue()
	RESPONSE #500
		SetGlobalTimer("SeeBounce","LOCALS",6)
		SetGlobalTimer("SeeImmune","LOCALS",12)
		SetGlobal("inafight","LOCALS",1)
		SetGlobal("initialise","LOCALS",1)
		SetGlobalTimer("rtime","LOCALS",2000)
		SetGlobalTimer("RoundEnded","LOCALS",6)
		Continue()
END

//////////////////////////////////////////////////////////////////////
//	Rest and recover
//////////////////////////////////////////////////////////////////////

IF
	Global("initialise","LOCALS",1)
	GlobalTimerExpired("rtime","LOCALS")
	!See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		SetGlobal("initialise","LOCALS",0)
		ApplySpell(Myself,RESTORE_FULL_HEALTH) 
	        SetGlobal("ParalyseEyeLost","LOCALS",0)
		SetGlobalTimer("paralyse","LOCALS",0)
        	SetGlobal("FearEyeLost","LOCALS",0)
		SetGlobalTimer("feareye","LOCALS",0)
	        SetGlobal("DominationEyeLost","LOCALS",0)
		SetGlobalTimer("domination","LOCALS",0)
        	SetGlobal("CauseWoundsEyeLost","LOCALS",0)
		SetGlobalTimer("woundingeye","LOCALS",0)
        	SetGlobal("CharmEyeLost","LOCALS",0)
		SetGlobalTimer("charmeye","LOCALS",0)
        	SetGlobal("StoneEyeLost","LOCALS",0)
		SetGlobalTimer("petrify","LOCALS",0)
        	SetGlobal("DeathEyeLost","LOCALS",0)
		SetGlobalTimer("deathray","LOCALS",0)
        	SetGlobal("SlowEyeLost","LOCALS",0)
		SetGlobalTimer("sloweye","LOCALS",0)
        	SetGlobal("TelekinesisEyeLost","LOCALS",0)
		SetGlobalTimer("telekinesis","LOCALS",0)
        	SetGlobal("CentralEyeLost","LOCALS",0)
		SetGlobalTimer("antimagicray","LOCALS",0)
		SetGlobal("dispel","LOCALS",0) // these last 4 for Hive Mothers
		SetGlobal("maze","LOCALS",0)
		SetGlobal("spray","LOCALS",0)
		SetGlobal("lightning","LOCALS",0)
		Rest()
END

/////////////////////////////////////////////////////////////////////////////
///	Conserve CPU cycles
//////////////////////////////////////////////////////////////////////////////

IF
	!Detect(NearestEnemyOf(Myself))
	!Global("inafight","LOCALS",1)
THEN
	RESPONSE #100
		NoAction()
END


///////////////////////////////////////////////////////////////////////
///	Damaged eyestalks (borrowed from SimDing0)
///////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/beholder/ssl/damage.ssl)

///////////////////////////////////////////////////////////////////////////////
//	First attack section
/////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/beholder/ssl/beholder_attack1.ssl)

////////////////////////////////////////////////////////////////////////////
///   Hive mother section
/////////////////////////////////////////////////////////////////////////////

DEFAULT TRIGGER(!GlobalTimerNotExpired("castspell","LOCALS"))



/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//
// Hit nearby targets with a Maze
//
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	RequireBlock(IsHiveMother)
	!Global("maze","LOCALS",3)
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(Enemy|Disabled|Maze|SpellTurn)
	Range(scstarget,10)
THEN DO
	Action(SpellNoDec,WIZARD_MAZE|100|100)
	IncrementGlobal("maze","LOCALS",1)
	SetGlobalTimer("antimagicray","LOCALS",3) // this is to block antimagic attacks on the victim
	Wait(2)
END

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//
// Lay waste with a Prismatic Spray
//
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	RequireBlock(IsHiveMother)
	!Global("spray","LOCALS",3)
	TargetBlock(EnemiesInOrder)
	TriggerBlock(Enemy|Disabled)
	Range(scstarget,5)
THEN DO
	Action(SpellNoDec,WIZARD_PRISMATIC_SPRAY|100|100)
	IncrementGlobal("spray","LOCALS",1)
END

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//
// Find someone to electrocute
//
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	RequireBlock(IsHiveMother)
	!Global("lightning","LOCALS",3)
	TargetBlock(PCsInOrder)
	TriggerBlock(Enemy|Disabled|MR100|ResistElectric)
THEN DO
	Action(SpellNoDec,WIZARD_CHAIN_LIGHTNING|100|50)
	IncrementGlobal("lightning","LOCALS",1)
END

DEFAULT TRIGGER()

//// End of Hive Mother section ////////////////////////////////////////

///////////////////////////////////////////////////////////////////////////////
//	Remaining attack section
/////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/beholder/ssl/beholder_attack2.ssl)


