DEFINE_ACTION_FUNCTION ~mirror_image_AoE~ BEGIN      // unedited code

ACTION_FOR_EACH ~pronumber~ IN
~3~ //ARROWEX
~18~ //BULLETEX
~38~ //FIREBALL
~40~ //Lightning Bolt
~81~ //Call Lightning
~92~ //FIRESTOR
~95~ //CLOUD
~96~ //TRAPSKUL
~98~ //ICESTORM
~100~ //TRAPGLYP
~187~ //CLOUDKIL
~211~ //FIREBLNS
~213~ //LIGHTCHA
~223~ //CLOUDPC
~224~ //GOLCLOUD
~229~ //METSWARM
~230~ //INSEC3
~232~ //INSEC4
~234~ //PFIRE2
~235~ // PSKULLT
~236~ //PCOMETT
~237~ //PWILT
~244~ //DFIREBL
~246~ //DRAGRED
~247~ //DRAGBLCK
~248~ //DRAGSILV
~250~ //CONECOLD
~251~ //PHOLYST
~252~ //PUNHLST
~253~ //BIGNAREA
~256~ //REDHIT
~258~ //PFIRE3
~261~ //HOLYAREA
~262~ //UNLYAREA
~266~ //Comet
~270~ //SPDRBRTH
~272~ //DRAGGREE
~273~ //GREEHIT
~276~ //BURNHAND
BEGIN
   OUTER_SPRINT $mirror_ignore(~%pronumber%~) ~~
END

ACTION_CLEAR_ARRAY patch_data
ACTION_DEFINE_ASSOCIATIVE_ARRAY patch_data BEGIN
   match=>should_bypass_mirror
   bypass_mirror_image=>1
END

LAF edit_all_spells STR_VAR editstring=~patch_effect=>patch_data~ END
LAF edit_all_items STR_VAR editstring=~patch_effect=>patch_data~ END

OUTER_FOR (i=5;i<10;i+=1) BEGIN
   LAF edit_effect STR_VAR effect=~sp313l0%i%~ editstring=~bypass_mirror_image=>1~ END
END
OUTER_FOR (i=11;i<=20;i+=1) BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~sp313l%i%.eff~ BEGIN // because one is missing without FP
   LAF edit_effect STR_VAR effect=~sp313l%i%~ editstring=~bypass_mirror_image=>1~ END
  END
END
OUTER_FOR (i=14;i<=20;i+=1) BEGIN
   LAF edit_effect STR_VAR effect=~sp707l%i%~ editstring=~bypass_mirror_image=>1~ END
END


END

DEFINE_PATCH_FUNCTION should_bypass_mirror
   RET value
BEGIN
   LPF ~%file_ext%_read_projectile~ RET pronum=value END
   PATCH_IF VARIABLE_IS_SET $mirror_ignore( ~%pronum%~) BEGIN
        SET value=1
   END ELSE BEGIN
        SET value=0
   END
END
