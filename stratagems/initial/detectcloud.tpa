////////////////////////////////////////////////////////////////////////
////        Controlling table
////////////////////////////////////////////////////////////////////////

<<<<<<<< .../stratagems-inline/clouds.2da
cloudcode  resource                  type  shout:i  timerlength:i  zone
dw#cldki   WIZARD_CLOUDKILL          spl   404    52           True
dw#incen   WIZARD_INCENDIARY_CLOUD   spl   409    52           True
dw#meteo   WIZARD_METEOR_SWARM       spl   405    18           False
dw#fstor   CLERIC_FIRE_STORM         spl   405    18           False
dw#vstor   CLERIC_STORM_OF_VENGEANCE spl   407    13           False
dw#defog   WIZARD_DEATH_FOG          spl   408    52           True
dw#cldki   wand13                    itm   404	  52           True
dw#clweb   WIZARD_WEB                spl    410   52           False
dw#stink   WIZARD_STINKING_CLOUD     spl    411   52           True
dw#sweet   CLERIC_ZONE_OF_SWEET_AIR  spl    123   99           True
dw#istor   WIZARD_ICE_STORM          spl    412   18           False
dw#suffo   WIZARD_SUFFOCATE          spl    413   18           False
dw#clpes   CLERIC_CLOUD_OF_PESTILENCE spl   413   18           True
dw#wrfog   CLERIC_WRITHING_FOG        spl   412   52           True
>>>>>>>>

////////////////////////////////////////////////////////////////////////
////        Scripts
////////////////////////////////////////////////////////////////////////

<<<<<<<<    .../stratagems-inline/dw#sweet.baf
IF
	!Global("init","LOCALS",1)
THEN
	RESPONSE #100
		SetGlobal("init","LOCALS",1)
		SetGlobalTimer("DMWWZoneOfSweetAir","GLOBAL",3)
END

IF
	GlobalTimerExpired("DMWWZoneOfSweetAir","GLOBAL")
THEN
	RESPONSE #100
		DestroySelf()
END
>>>>>>>>

<<<<<<<<    .../stratagems-inline/cloud_template.ssl
IF
	Global("CKInit","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("CKInit","LOCALS",1)
		SetGlobalTimer("CKtimer","LOCALS",3)
		SetGlobalTimer("CKtimer2","LOCALS",%timerlength%)
END

IF
	RequireBlock(scszone)
	GlobalTimerNotExpired("DMWWZoneOfSweetAir","GLOBAL")
THEN
	RESPONSE #100
		DestroySelf()
END


IF
	GlobalTimerExpired("CKtimer2","LOCALS")
THEN
	RESPONSE #100
		DestroySelf()
END



IF
	GlobalTimerExpired("CKtimer","LOCALS")
THEN
	RESPONSE #100
		Shout(%shout%)
END
>>>>>>>>

<<<<<<<< .../stratagems-inline/storm_add.baf

	IF
		Allegiance(LastSummonerOf(Myself),ENEMY)
	THEN
		RESPONSE #100
			DestroySelf()
	END

>>>>>>>>

////////////////////////////////////////////////////////////////////////
////        Main function
////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION detectcloud BEGIN

	ACTION_IF is_bg2 BEGIN
		 //// versions of Met.Sw. and Inc. Cl. for traps
		 
		COPY_EXISTING
			"%WIZARD_INCENDIARY_CLOUD%.spl" "override/dw#trpin.spl"
			"%WIZARD_METEOR_SWARM%.spl" "override/dw#trpms.spl"
		IF_EXISTS
		
		COPY_EXISTING "gt016.bcs" override
			DECOMPILE_AND_PATCH BEGIN
				REPLACE_TEXTUALLY ~ForceSpell(LastTrigger,WIZARD_INCENDIARY_CLOUD)~ ~ForceSpellRES("dw#trpin",LastTrigger)~
			END

		COPY_EXISTING "gt020.bcs" override
			DECOMPILE_AND_PATCH BEGIN
				REPLACE_TEXTUALLY ~ForceSpell(LastTrigger,WIZARD_METEOR_SWARM)~ ~ForceSpellRES("dw#trpms",LastTrigger)~
			END     
		 
	 
	END
    // main work
     

    LAF 2da_process_table INT_VAR inline=1 STR_VAR table=clouds.2da function=detectcloud_core END
     
	ACTION_IF is_bg2 BEGIN
		EXTEND_TOP ~dw#vstor.bcs~ ~.../stratagems-inline/storm_add.baf~
	END
	COMPILE ".../stratagems-inline/dw#sweet.baf"

END

DEFINE_ACTION_FUNCTION detectcloud_core
   INT_VAR shout=0
			timerlength=0
   STR_VAR cloudcode=""
           resource=""
           type=""
           zone=""
BEGIN
    ACTION_IF VARIABLE_IS_SET "%resource%" THEN BEGIN
        OUTER_SPRINT resource EVAL "%%resource%%"
    END

	ACTION_IF FILE_EXISTS_IN_GAME ~%resource%.spl~ BEGIN
		//////////////////////////////////////////
		// the script for the invisible creature
		//////////////////////////////////////////

		LAF ssl_to_bcs INT_VAR inline=1 STR_VAR script="cloud_template=>%cloudcode%" variables="scszone=%zone%" END
				
		//////////////////////////////////////////
		// the creature itself
		/////////////////////////////////////////

		COPY ~%MOD_FOLDER%\%component_loc%\cre\dw#cloud.cre~ ~override/%cloudcode%.cre~
			WRITE_ASCII SCRIPT_OVERRIDE "%cloudcode%"

		//////////////////////////////////////////
		// the effect that summons it
		//////////////////////////////////////////

		eff.make[%cloudcode%]
		[
			m_opcode=67
			m_target=1
			m_power=9
			m_timing=1
			m_duration=6
			m_resource2:=NONE
			m_resource:="%cloudcode%"
		]

		//////////////////////////////////////////
		// the spell
		//////////////////////////////////////////

		COPY_EXISTING "%resource%.%type%" override
			PATCH_IF "%type%" STR_EQ "spl" BEGIN
				LPF ADD_SPELL_EFFECT INT_VAR opcode=177 target=1 power=0 parameter2=3 duration=60 STR_VAR resource="%cloudcode%" END
			END ELSE BEGIN
				LPF ADD_ITEM_EFFECT INT_VAR opcode=177 target=1 power=0 parameter2=3 duration=60 STR_VAR resource="%cloudcode%" END			
			END
		BUT_ONLY
		// look for a WP or PW or WI or PI version
		ACTION_IF "%type%" STR_EQ "spl" BEGIN
			ACTION_MATCH "%resource%" WITH
			"SPWI.*" BEGIN
				ACTION_FOR_EACH prefix IN "DWWP" "DWWI" BEGIN
					OUTER_PATCH_SAVE alt_resource "%resource%" BEGIN
						REPLACE_TEXTUALLY "SPWI" "%prefix%"
					END
					COPY_EXISTING "%alt_resource%.spl" override
						LPF ADD_SPELL_EFFECT INT_VAR opcode=177 target=1 power=0 parameter2=3 duration=60 STR_VAR resource="%cloudcode%" END
					BUT_ONLY
					IF_EXISTS
				END
			END
			"SPPR.*" BEGIN
				ACTION_FOR_EACH prefix IN "DWPW" "DWPI" BEGIN
					OUTER_PATCH_SAVE alt_resource "%resource%" BEGIN
						REPLACE_TEXTUALLY "SPPR" "%prefix%"
					END
					COPY_EXISTING "%alt_resource%.spl" override
						LPF ADD_SPELL_EFFECT INT_VAR opcode=177 target=1 power=0 parameter2=3 duration=60 STR_VAR resource="%cloudcode%" END
					BUT_ONLY
					IF_EXISTS
				END			
			END
			DEFAULT
			END
		END
	END
END
