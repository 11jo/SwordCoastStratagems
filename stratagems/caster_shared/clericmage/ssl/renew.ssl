/////////////////////////////////////////////////////////
//// Have my defences just been splattered? If so, use a sequencer
/////////////////////////////////////////////////////////

IF TRIGGER
	RequireBlock(HasL7)
	OR(2)
		!CheckStatGT(Myself,0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		!CheckStatGT(Myself,0,STONESKINS)
	!CheckStatGT(Myself,0,WIZARD_SPELL_DEFLECTION)
	!CheckStatGT(Myself,0,WIZARD_SPELL_TURNING)
	!CheckStatGT(Myself,0,WIZARD_SPELL_TRAP)
THEN DO
	Action(TriggerMyself,21,CLERIC_BLADE_BARRIER,WIZARD_SPELL_DEFLECTION,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
	Action(TriggerMyself,22,CLERIC_BLADE_BARRIER,WIZARD_SPELL_DEFLECTION,WIZARD_SPELL_SHIELD)
	SetGlobalTimer("redefend","LOCALS",8) // don't get stuck in a defend-defend-defend loop and forget to do any damage
END


/////////////////////////////////////////////////////////
////	PMW (we only do PMW, not Immunity etc)
/////////////////////////////////////////////////////////

IF TRIGGER	
	!CheckStatGT(Myself,0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
	!GlobalTimerNotExpired("timestop","LOCALS")
	!GlobalTimerNotExpired("DMWWTimestopRunning","GLOBAL")
	GlobalTimerExpired("redefend","LOCALS")
	Detect(NearestEnemyOf(Myself))
THEN DO
	Action(SpellMyself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
	Action(TriggerMyself,21,CLERIC_BLADE_BARRIER,WIZARD_SPELL_DEFLECTION,WIZARD_PROTECTION_FROM_NORMAL_WEAPONS)
	SetGlobalTimer("redefend","LOCALS",8) // don't get stuck in a defend-defend-defend loop and forget to do any damage
END
////////////////////////////////////////////////////////////////////
////	Iron Skins / Stoneskin
////////////////////////////////////////////////////////////////////

IF TRIGGER
        RequireBlock(HasL4)
	GlobalTimerExpired("redefend","LOCALS")
	!CheckStatGT(Myself,2,STONESKINS)
THEN DO

	Action(SpellMyself,CLERIC_IRONSKIN)
	Action(SpellMyself,WIZARD_STONE_SKIN)
	Action(TriggerMyself,22,CLERIC_BLADE_BARRIER,WIZARD_SPELL_DEFLECTION,WIZARD_SPELL_SHIELD)
	SetGlobalTimer("redefend","LOCALS",8) // don't get stuck in a defend-defend-defend loop and forget to do any damage
END


//////////////////////////////////////////////////////////////////////
//// Full-on Invisibility in case I'm in big trouble and need to pull off
//////////////////////////////////////////////////////////////////////

IF TRIGGER
        RequireBlock(HasL2)
	HPPercentLT(Myself,75)
	!General(Myself,UNDEAD)
	!StateCheck(Myself,STATE_INVISIBLE)
	TriggerBlock(TruesightSafe)
	GlobalTimerExpired("redefend","LOCALS")
THEN DO
	Action(SpellMyself,WIZARD_MISLEAD)
	Action(SpellMyself,WIZARD_SHADOW_DOOR)
	Action(MinorSequencerMyself,21,WIZARD_INVISIBILITY,CLERIC_SANCTUARY)
	Action(SpellMyself,WIZARD_INVISIBILITY)
	Action(Potion,"potn10",@1010)
END

/////////////////////////////////////////////////////////////////////
////	Emergency healing for me
/////////////////////////////////////////////////////////////////////

IF TRIGGER
        RequireBlock(HasL6)
	HPPercentLT(Myself,50)
THEN DO
	Action(SpellMyself,CLERIC_HEAL)
END

IF TRIGGER
        RequireBlock(HasL5)
	HPPercentLT(Myself,50)
	CheckStatGT(Myself,0,SANCTUARY)
THEN DO
	Action(SpellMyself,CLERIC_CURE_CRITICAL_WOUNDS)
	Action(Potion,"potn55",@1055)

END

IF TRIGGER
        RequireBlock(HasL5)
	TargetBlock(EnemiesInOrderShort)
	HPPercentLT(Myself,50)
	OR(2)
		!Range(scstarget,10)
		StateCheck(scstarget,STATE_DISABLED)
THEN DO
	Action(Potion,"potn55",@1055)
	Action(SpellMyself,CLERIC_CURE_CRITICAL_WOUNDS|100|100)
END

//////////////////////////////////////////////////////////////////////
////	Healing for high-powered allies
//////////////////////////////////////////////////////////////////////

IF TRIGGER
        RequireBlock(HasL6)
	TargetBlock(HumanoidVillains)
	CheckStatGT(scstarget,12,Level)
	HPPercentLT(scstarget,50)
	!CheckStatGT(Myself,20,Level)
	Range(scstarget,15)
	Allegiance(scstarget,ENEMY)
THEN DO
	Action(Spell,CLERIC_HEAL|100|50)
END

IF TRIGGER
	TargetBlock(HumanoidVillains)
	CheckStatGT(scstarget,16,Level)
	HPPercentLT(scstarget,50)
	CheckStatGT(Myself,20,Level)
	Range(scstarget,15)
	Allegiance(scstarget,ENEMY)
THEN DO
	Action(Spell,CLERIC_HEAL|100|50)
END

IF TRIGGER
	TargetBlock(HumanoidVillains)
	CheckStatGT(scstarget,5,Level)
	!CheckStatGT(Myself,14,Level)
	HPPercentLT(scstarget,50)
	Allegiance(scstarget,ENEMY)
THEN DO
	Action(Spell,CLERIC_MASS_CURE|100|50)
END

IF TRIGGER
	TargetBlock(HumanoidVillains)
	CheckStatGT(scstarget,8,Level)
	CheckStatGT(Myself,14,Level)
	HPPercentLT(scstarget,50)
	Allegiance(scstarget,ENEMY)
THEN DO
	Action(Spell,CLERIC_MASS_CURE|100|50)
END

/////////////////////////////////////////////////////////////////////////////
///	Are any of my own team panicked or paralysed? If so, bring them out. (But don't waste
///	it on low-powered scum)
/////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(HumanoidVillains)
	StateCheck(scstarget,STATE_PANIC)	
	CheckStatGT(scstarget,8,Level)
	!Global("remfear","LOCALS",1)
	Allegiance(scstarget,ENEMY)
THEN DO
	Combine()
	Action(SpellNoDec,CLERIC_REMOVE_FEAR)
	SetGlobal("remfear","LOCALS",1)
END

IF TRIGGER
	TargetBlock(HumanoidVillains)
	CheckStatGT(scstarget,0,HELD)
	CheckStatGT(scstarget,8,Level)
	Allegiance(scstarget,ENEMY)
THEN DO
	Combine()
	Action(Spell,CLERIC_REMOVE_PARALYSIS)
	ActionCondition(SpellNoDec,CLERIC_REMOVE_PARALYSIS;CheckStatGT(Myself,9,Level)&!Global("rempara","LOCALS",1))
	ActionCondition(SpellNoDec,CLERIC_REMOVE_PARALYSIS;CheckStatGT(Myself,7,Level)&!Global("rempara","LOCALS",1)|300|100)
	ActionCondition(SpellNoDec,CLERIC_REMOVE_PARALYSIS;CheckStatGT(Myself,5,Level)&!CheckStatGT(Myself,7,Level)&!Global("rempara","LOCALS",1)|100|100)
	SetGlobal("rempara","LOCALS",1)
	OnContinue(SetGlobal("rempara","LOCALS",1))
END

///////////////////////////////////////////////////////////////////////////////////////
////	More healing, if it's appropriate
//////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	HPPercentLT(Myself,95)
	OR(2)
		CheckStatGT(Myself,0,SANCTUARY)
		!See(NearestEnemyOf(Myself))
	OR(2)
		HPPercentLT(Myself,75)
		!See(NearestEnemyOf(Myself))
THEN DO
	Action(Potion,"potn55",@1055)
	Action(Potion,"potn52",@1052)
	Action(SpellMyself,CLERIC_CURE_CRITICAL_WOUNDS)
	ActionCondition(Potion,"potn08",@1008;OR(2)&!See(NearestEnemyOf(Myself))&CheckStatLT(Myself,11,Level))
	ActionCondition(SpellMyself,CLERIC_CURE_SERIOUS_WOUNDS;OR(2)&!See(NearestEnemyOf(Myself))&CheckStatLT(Myself,11,Level))
	ActionCondition(SpellMyself,CLERIC_CURE_MEDIUM_WOUNDS;OR(2)&!See(NearestEnemyOf(Myself))&CheckStatLT(Myself,11,Level))
	ActionCondition(SpellMyself,CLERIC_CURE_LIGHT_WOUNDS;OR(2)&!See(NearestEnemyOf(Myself))&CheckStatLT(Myself,11,Level))
END

//////////////////////////////////////////////////////////////////////
//// Full-on Invisibility in case I'm in big trouble and need to pull off
//////////////////////////////////////////////////////////////////////

IF TRIGGER
	HPPercentLT(Myself,75)
	!General(Myself,UNDEAD)
	!StateCheck(Myself,STATE_INVISIBLE)
	TriggerBlock(TruesightSafe)
	GlobalTimerExpired("redefend","LOCALS")
THEN DO
	Action(SpellMyself,WIZARD_MISLEAD)
	Action(SpellMyself,WIZARD_SHADOW_DOOR)
	Action(MinorSequencerMyself,21,WIZARD_INVISIBILITY,CLERIC_SANCTUARY)
	Action(SpellMyself,WIZARD_INVISIBILITY)
	Action(Potion,"potn10",@1010)
END

///////////////////////////////////////////////////////////////////////////
////  If I'm not Improved Invisible, change that if possible
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	!StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
	GlobalTimerExpired("redefend","LOCALS")
	TriggerBlock(TruesightSafe)
THEN DO
	Action(SequencerMyself,1,WIZARD_IMPROVED_INVISIBILITY,WIZARD_FIRE_SHIELD_RED,WIZARD_STONE_SKIN) // II, Stoneskin, fire shield
	Action(SequencerMyself,6,WIZARD_IMPROVED_INVISIBILITY,WIZARD_FIRE_SHIELD_RED,WIZARD_FIRE_SHIELD_BLUE) // II, fire shield x2
	Action(SpellMyself,WIZARD_MISLEAD)
	Action(SpellMyself,WIZARD_SHADOW_DOOR)
END		

