// assumed called from within mage or cleric with locbase=clericmage

LAF include STR_VAR file=mage_shared.tph locbase=mage END

DEFINE_ACTION_FUNCTION clericmage BEGIN
   ACTION_IF is_bg2=1 BEGIN
      LAF clericmage_scripts END
      LAF clericmage_spells END

   END


END


///////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION clericmage_scripts BEGIN

      // find prebuff code
      ACTION_IF FILE_EXISTS ~override/dw#mage_1.mrk~ BEGIN
           OUTER_SET prebuff=1
      END ELSE
      ACTION_IF FILE_EXISTS ~override/dw#mage_2.mrk~ BEGIN
           OUTER_SET prebuff=2
      END ELSE
      ACTION_IF FILE_EXISTS ~override/dw#mage_3.mrk~ BEGIN
           OUTER_SET prebuff=3
      END ELSE BEGIN
           OUTER_SET prebuff=4
      END

      // get variables
      
      LAF get_mage_prebuff_variables INT_VAR prebuff RET variables=variables END

      // compile scripts
      
      ACTION_FOR_EACH script IN gaal dw#send8 senbattl BEGIN
            LAF ssl_to_bcs STR_VAR variables=EVALUATE_BUFFER ~MageTypeContingencyFile=chaincont_core&%variables%~ script location=~caster_shared/clericmage/ssl~ END
      END

END

///////////////////////////////////////////////////////////////////////////////////////////////////////////////


DEFINE_ACTION_FUNCTION clericmage_spells
BEGIN

       // initialise
       
       ACTION_IF FILE_EXISTS ~override/dw#hla_innate.mrk~ BEGIN
          OUTER_SET hla_innate=1
       END ELSE BEGIN
          OUTER_SET hla_innate=0
       END
      
       // gaal

       MAKE_PATCH
           remove_spells=>all
           add_spells=>~GREATER_COMMAND SLAY_LIVING SHADOW_DOOR WIZARD_CHAOS
                        MENTAL_DOMINATION(2) DEFENSIVE_HARMONY MINOR_GLOBE_OF_INVULNERABILITY STONE_SKIN
                        WIZARD_DISPEL_MAGIC VAMPIRIC_TOUCH SPELL_THRUST UNHOLY_BLIGHT(3)
                        CLERIC_HOLD_PERSON(4) HORROR MIRROR_IMAGE(2) VOCALIZE
                        COMMAND(5) ARMOR_OF_FAITH MAGIC_MISSILE(3) ARMOR SHIELD~
       END
       LAF edit_creature STR_VAR creature=~gaal01 gaal02~ edits=patch_data END
       LAF edit_creature STR_VAR creature=gaal02 edits=~strip_script=>gaal insert_script_low=>gaal~ END
       LAF edit_creature STR_VAR creature=csgaal edits=~strip_scrip=>gaal~ END

       // sendai's cm statue

       MAKE_PATCH
            level1=>24
            level2=>20
            wisdom=>18
            enforce_saves=>at_worst
            enforce_thac0=>at_worst
            remove_spells=>all
            strip_scs_scripts=>null
            insert_script_low=>dw#send8
            add_spells=>~ABI_DALZIMS_HORRID_WILTING(3)
                         SPELL_TURNING WARDING_WHIP
                         PROTECTION_FROM_MAGIC_WEAPONS(2) GLOBE_OF_INVULNERABILITY DEATH_SPELL
                         SHADOW_DOOR(2) BREACH(2)
                         STONE_SKIN(3) GREATER_MALISON TELEPORT_FIELD
                         WIZARD_DISPEL_MAGIC(5)
                         MIRROR_IMAGE(2) MELF_ACID_ARROW(3)
                         MAGIC_MISSILE(5)
                         HARM(3) HEAL(3) BLADE_BARRIER
                         MASS_CURE(6) CHAOTIC_COMMANDS(3)
                         DEFENSIVE_HARMONY(2) FREE_ACTION(2) MENTAL_DOMINATION(6)
                         UNHOLY_BLIGHT(10)
                         REMOVE_FEAR(11)~
       END

       // this version of Sendai has 3 priest and 3 mage HLAs, and 3 priest L7 and 2 mage L9 spells

       ACTION_IF hla_innate=1 BEGIN
          ACTION_IF refinements=1 BEGIN
              OUTER_SPRINT $patch_data(add_spells') ~ABSOLUTE_IMMUNITY IMPRISONMENT UNHOLY_WORD CLERIC_SYMBOL_STUN(2) AEGIS DRAGONS_BREATH SUMMON_PLANATAR_EVIL UNION DIVINE_SHELL IMPLOSION~
          END ELSE BEGIN
              OUTER_SPRINT $patch_data(add_spells') ~ABSOLUTE_IMMUNITY IMPRISONMENT UNHOLY_WORD CLERIC_SYMBOL_STUN(2) COMET DRAGONS_BREATH SUMMON_PLANATAR_EVIL AURA_OF_FLAMING_DEATH IMPLOSION~
          END
       END ELSE BEGIN
          ACTION_IF refinements=1 BEGIN
              OUTER_SPRINT $patch_data(add_spells') ~UNION AEGIS DRAGONS_BREATH IMPLOSION(2)~
          END ELSE BEGIN
              OUTER_SPRINT $patch_data(add_spells') ~ABSOLUTE_IMMUNITY COMET IMPLOSION(2) UNHOLY_WORD~
          END
       END


       LAF edit_creature STR_VAR creature=sendai8 edits=patch_data END

       // sendai herself
       
       MAKE_PATCH
            level1=>26
            level2=>22
            wisdom=>19
            enforce_saves=>at_worst
            enforce_thac0=>at_worst
            remove_spells=>all
            add_spells=>~ABI_DALZIMS_HORRID_WILTING(3) POWER_WORD_BLIND
                         WARDING_WHIP POWER_WORD_STUN(2)
                         PROTECTION_FROM_MAGIC_WEAPONS(4) GLOBE_OF_INVULNERABILITY DEATH_SPELL
                         SHADOW_DOOR(2) BREACH(2)
                         STONE_SKIN(3) GREATER_MALISON TELEPORT_FIELD
                         WIZARD_DISPEL_MAGIC(5)
                         MIRROR_IMAGE(2) MELF_ACID_ARROW(3)
                         MAGIC_MISSILE(5)
                         HARM(3) HEAL(3) BLADE_BARRIER
                         MASS_CURE(3) CHAOTIC_COMMANDS(3) CLERIC_TRUE_SIGHT
                         DEFENSIVE_HARMONY(2) FREE_ACTION(2) MENTAL_DOMINATION(6)
                         UNHOLY_BLIGHT(10)
                         REMOVE_FEAR(11)~
            xp_value=>45000
       END

       // this version of Sendai has 5 priest and 5 mage HLAs, and 3 priest L7 and 3 mage L9 spells

       ACTION_IF hla_innate=1 BEGIN
          ACTION_IF refinements=1 BEGIN
              OUTER_SPRINT $patch_data(add_spells') ~IMPRISONMENT SPELL_TRAP POWER_WORD_KILL UNHOLY_WORD CLERIC_SYMBOL_STUN CLERIC_GATE AEGIS DRAGONS_BREATH SUMMON_PLANATAR_EVIL UNION DIVINE_SHELL IMPLOSION STORM_OF_VENGEANCE AURA_OF_FLAMING_DEATH IMPROVED_ALUCRITY~
          END ELSE BEGIN
              OUTER_SPRINT $patch_data(add_spells') ~ABSOLUTE_IMMUNITY SPELL_TRAP IMPRISONMENT UNHOLY_WORD CLERIC_SYMBOL_STUN(2) COMET DRAGONS_BREATH SUMMON_PLANATAR_EVIL AURA_OF_FLAMING_DEATH IMPLOSION IMPROVED_ALUCRITY STORM_OF_VENGEANCE~
          END
       END ELSE BEGIN
          ACTION_IF refinements=1 BEGIN
              OUTER_SPRINT $patch_data(add_spells') ~UNION AEGIS SPELL_TRAP IMPROVED_ALUCRITY IMPLOSION(2)~
          END ELSE BEGIN
              OUTER_SPRINT $patch_data(add_spells') ~ABSOLUTE_IMMUNITY SPELL_TRAP IMPROVED_ALUCRITY IMPLOSION(2) UNHOLY_WORD~
          END
       END


       LAF edit_creature STR_VAR creature=sendai edits=patch_data END




END