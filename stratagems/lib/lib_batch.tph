DEFINE_ACTION_FUNCTION run
	INT_VAR live_code=0
	STR_VAR file="" // name of the file containing this component's main function (you cna omit the suffix if it's 'tpa')
			files="" // legacy synonym for 'file'
			function="" // name of the main function (default value: same as file)
			location="" // path to the function (relative to %MOD_FOLDER%) - if blank, we assume 'file' is the full path, including MOD_FOLDER
			tra="" // tra file to be loaded automatically before the main function is run (relative to %sfo_tra_loc%/%LANGUAGE% - leave blank if you don't need a tra file automatically used)
			version="" // optional STR_VAR to be sent to the main function
BEGIN
	ACTION_IF "%files%" STR_CMP "" BEGIN
		OUTER_SPRINT file "%files%"
	END
	ACTION_IF "%function%" STR_CMP "" BEGIN
		OUTER_SPRINT function "%file%"
	END
	ACTION_IF IS_AN_INT SFO_batch_mode && SFO_batch_mode BEGIN // if the 'batch mode' variable is set
		ACTION_IF !VARIABLE_IS_SET batch_loc BEGIN
			OUTER_SPRINT batch_loc "weidu_external/batch"
		END
		ACTION_IF !FILE_EXISTS_IN_GAME "%batch_loc%/%MOD_FOLDER%.bat" BEGIN
			COPY + ".../stratagems-inline/batch_template.bat" "%batch_loc%/%MOD_FOLDER%.bat"
		END
		COPY + "%batch_loc%/%MOD_FOLDER%.bat" "%batch_loc%" 
			READ_ASCII 0x0 data (BUFFER_LENGTH)
			DELETE_BYTES 0x0 BUFFER_LENGTH
			SPRINT data "%data% %COMPONENT_NUMBER%"
			INSERT_BYTES 0x0 STRING_LENGTH "%data%"
			WRITE_ASCIIE 0x0 "%data%"
	END ELSE BEGIN
		ACTION_IF INDEX ("\." "%file%")<0 BEGIN
			OUTER_SPRINT file "%file%.tpa"
		END
		ACTION_IF "%location%" STR_CMP "" BEGIN
			OUTER_SPRINT file "%MOD_FOLDER%/%location%/%file%"
		END
		WITH_TRA "%sfo_tra_loc%/english/%tra%.tra" "%sfo_tra_loc%/%LANGUAGE%/%tra%.tra" BEGIN
			INCLUDE "%file%"
			ACTION_IF !live_code BEGIN
				ACTION_IF "%version%" STR_EQ "" BEGIN
					LAF "%function%" END
				END ELSE BEGIN
					LAF "%function%" STR_VAR version END
				END
			END
		END	
	END
END



<<<<<<<<.../stratagems-inline/batch_template.bat
setup-%MOD_FOLDER% --uninstall --force-install-list>>>>>>>>