DEFINE_ACTION_FUNCTION difficulty_controls BEGIN

    // check if we're using the widget
    
    ACTION_IF enhanced_edition BEGIN
       OUTER_SET ui_integration=1
       LAF check_ini STR_VAR ini=Force_Difficulty_Widget RET value END
       ACTION_IF value BEGIN
          OUTER_SET ui_integration=0
       END
    END ELSE BEGIN
       OUTER_SET ui_integration=0
    END
    
    // if not, try fixing the interface
    
    ACTION_IF ui_integration BEGIN
       LAF difficulty_ui RET abort END
       ACTION_IF abort BEGIN 
          OUTER_SET ui_integration=0
       END
    END
 
    OUTER_SPRINT component_loc lib
// define commonly-held strings

   ACTION_DEFINE_ASSOCIATIVE_ARRAY "diff_report_strings" BEGIN
      0 => 1010
      1 => 1011
      2 => 1012
      3 => 1013
      4 => 1014
      5 => 1015
      6 => 1016
      7 => 1017
   END

   ACTION_DEFINE_ASSOCIATIVE_ARRAY "diff_choice_strings" BEGIN
      0 => 1020
      1 => 1021
      2 => 1022
      3 => 1023
      4 => 1024
      5 => 1025
      6 => 1026
      7 => 1027
   END

   ACTION_DEFINE_ASSOCIATIVE_ARRAY "diff_response_strings" BEGIN
      0 => 1050
      1 => 1051
      2 => 1052
      3 => 1053
      4 => 1054
      5 => 1055
      6 => 1056
      7 => 1057
   END
   ACTION_IF GAME_IS iwdee BEGIN
      OUTER_SET $diff_report_string(7) = 1028
      OUTER_SET $diff_choice_strings(7) = 1029
      OUTER_SET $diff_response_strings(8) = 1058
   END

// build beginning section

   <<<<<<<<  .../stratagems-inline/difficulty_dialog.d
BEGIN ~dw#diffi~

IF ~True()~ THEN BEGIN initial
     SAY @1030
   >>>>>>>>

COPY ".../stratagems-inline/difficulty_dialog.d" "%workspace%"

// add the blocks corresponding to initial choices

   LAF process_table STR_VAR table=difficulty_controls.2da function=difficulty_helper_initial END

// finish the block, add exit block and acknowledgement blocks


<<<<<<<< .../stratagems-inline/difficulty_dialog_integration.txt
IF ~~ THEN REPLY @1031 GOTO exit
END

IF ~~ THEN BEGIN exit
SAY @1032
IF ~~ THEN DO ~DestroySelf()~ EXIT
END
>>>>>>>>

<<<<<<<< .../stratagems-inline/difficulty_dialog_no_integration.txt
IF ~Global("DW_difficulty_control_inactive","GLOBAL",0)~ THEN REPLY @1033 DO ~SetGlobal("DW_difficulty_control_inactive","GLOBAL",1)~ GOTO control_off
IF ~Global("DW_difficulty_control_inactive","GLOBAL",1)~ THEN REPLY @1034 DO ~SetGlobal("DW_difficulty_control_inactive","GLOBAL",0)~ GOTO control_on
IF ~~ THEN REPLY @1031 GOTO exit
END

IF ~~ THEN BEGIN control_off
SAY @1035
IF ~~ THEN GOTO initial
END

IF ~~ THEN BEGIN control_on
SAY @1036
IF ~~ THEN GOTO initial
END 

IF ~~ THEN BEGIN exit
SAY @1032
IF ~Global("DW_difficulty_control_inactive","GLOBAL",0)~ THEN DO ~ApplySpellRES("dw#diff2",Player1)DestroySelf()~ EXIT
IF ~Global("DW_difficulty_control_inactive","GLOBAL",1)~ THEN DO ~DestroySelf()~ EXIT
END
>>>>>>>>

<<<<<<<< .../stratagems-inline/difficulty_acknowledge.txt
IF ~~ THEN BEGIN acknowledge_%var% SAY (AT %string%)
IF ~~ THEN GOTO initial
END
>>>>>>>>

COPY "%workspace%/difficulty_dialog.d" "%workspace%"
    PATCH_IF ui_integration BEGIN
       APPEND_FILE TEXT ".../stratagems-inline/difficulty_dialog_integration.txt"
    END ELSE BEGIN
       APPEND_FILE TEXT ".../stratagems-inline/difficulty_dialog_no_integration.txt"
    END
    FOR (var=0;var<=7;var+=1) BEGIN
        SET string=$diff_response_strings("%var%")
        APPEND_FILE_EVALUATE TEXT ".../stratagems-inline/difficulty_acknowledge.txt"
    END

// add the main blocks

LAF process_table STR_VAR table=difficulty_controls.2da function=difficulty_helper_main END

/// compile the dialog file


COMPILE "%workspace%/difficulty_dialog.d" EVALUATE_BUFFER

// make the accompanying bits

LAF install STR_VAR file="dw#diffi.cre" location=resource END // basically a tweaked copy of cutspy

LAF check_ini STR_VAR ini=difficulty_icon RET value END
ACTION_IF FILE_EXISTS "%scsroot%/lib/diff_icons/%value%.bam" BEGIN
   COPY "%scsroot%/lib/diff_icons/%value%.bam" "override/dw#diffi.bam"
END ELSE BEGIN
   COPY "%scsroot%/lib/diff_icons/v6.bam" "override/dw#diffi.bam"  // default
END

<<<<<<<< .../stratagems-inline/dw#diffi.baf
IF
	NumberOfTimesTalkedTo(0)
	Detect([PC])
THEN
	RESPONSE #100
		StartDialogueNoSet(LastSeenBy(Myself))
END
>>>>>>>>
COMPILE ".../stratagems-inline/dw#diffi.baf"



MAKE_PATCH
   say_name=>1018
   add_basic_ability=>null
   patch_ability_inline=>~ability_icon=>dw#diffi ability_target=>5 ability_icon_loc=>4~
   spell_type=>innate
   add_effect_inline=>"opcode=>67 target=>1 duration=>60 resource=>dw#diffi"
   add_effect_inline'=>"opcode=>172 target=>1 timing=>1 resource=>dw#diff1"
END
LAF make_spell STR_VAR spell=dw#diff1 edits=patch_data END

MAKE_PATCH
   add_basic_ability=>null
   add_effect_inline=>"opcode=>171 target=>2 timing=>1 resource=>dw#diff1"
END
LAF make_spell STR_VAR spell=dw#diff2 edits=patch_data END

// install it in starting areas if we don't have integration

ACTION_IF !ui_integration BEGIN

<<<<<<<< .../stratagems-inline/diff_install.baf
IF
  !Global("DW_initialise_difficulty_widget","GLOBAL",1)
THEN
    RESPONSE #100
             SetGlobal("DW_initialise_difficulty_widget","GLOBAL",1)
             ActionOverride(Player1,ApplySpellRES("dw#diff2",Myself))
             Continue()
END
>>>>>>>>

ACTION_IF is_bg2 BEGIN
    LAF extend_area_script STR_VAR area="ar3000 ar4000 ar0602" top=diff_install inline=yes END
END
ACTION_IF is_bg1 BEGIN
    LAF extend_area_script STR_VAR area="%Candlekeep%" top=diff_install inline=yes END
END
ACTION_IF is_iwd BEGIN
    LAF extend_area_script STR_VAR area="ar1006 ar9100" top=diff_install inline=yes END
END

END


// sort out the difficulty UI strings

// the ones in dialog.tlk (I'm not sure if this is relevant for EE but no harm in doing it)

  ACTION_IF GAME_IS bgee BEGIN
         MAKE_PATCH
            help=>24424
            easiest=>24417
            easy=>24418
            normal=>24419
            hard=>11312
            insane=>24420
         END
  END ELSE BEGIN // everything else uses the same strings
         MAKE_PATCH
            help=>18020
            easiest=>11308
            easy=>11309
            normal=>11311
            hard=>11312
            insane=>11313
         END
  END
  ACTION_DEFINE_ASSOCIATIVE_ARRAY diff_strref_lookup BEGIN
     help=>1105
     easiest=>1100
     easy=>1101
     normal=>1102
     hard=>1103
     insane=>1104
  END
  ACTION_PHP_EACH diff_strref_lookup AS key=>tra BEGIN
     OUTER_SET tlk_entry = $patch_data("%key%")
     STRING_SET_EVALUATE tlk_entry (AT tra)
  END
  
// for EE only, the ones in L_XX_YY

   ACTION_IF enhanced_edition BEGIN
    MAKE_PATCH
      DIFFICULTY_DESCRIPTION => 1110
      DIFFICULTY_DESCRIPTION_CORERULES => 1111
      DIFFICULTY_DESCRIPTION_CORERULES_BG2EE => 1111
      DIFFICULTY_DESCRIPTION_CORERULES_BGEE => 1111
      DIFFICULTY_DESCRIPTION_EASY => 1112
      DIFFICULTY_DESCRIPTION_EASY_BG2EE => 1112
      DIFFICULTY_DESCRIPTION_EASY_BGEE => 1112
      DIFFICULTY_DESCRIPTION_HARD => 1133
      DIFFICULTY_DESCRIPTION_HARD_BG2EE => 1113
      DIFFICULTY_DESCRIPTION_HARD_BGEE => 1133
      DIFFICULTY_DESCRIPTION_INSANE => 1134
      DIFFICULTY_DESCRIPTION_INSANE_BG2EE => 1114
      DIFFICULTY_DESCRIPTION_INSANE_BGEE => 1134
      DIFFICULTY_DESCRIPTION_NORMAL => 1115
      DIFFICULTY_DESCRIPTION_NORMAL_BG2EE => 1115
      DIFFICULTY_DESCRIPTION_NORMAL_BGEE => 1115
      DIFFICULTY_LABEL_CORERULES => 1116
      DIFFICULTY_LABEL_CORERULES_MIXED => 1117
      DIFFICULTY_LABEL_EASY => 1118
      DIFFICULTY_LABEL_EASY_MIXED => 1119
      DIFFICULTY_LABEL_HARD => 1120
      DIFFICULTY_LABEL_HARD_MIXED => 1121
      DIFFICULTY_LABEL_NORMAL => 1124
      DIFFICULTY_LABEL_NORMAL_MIXED => 1125
      NO_DAMAGE_INCREASE_LABEL => 1126
      NO_DAMAGE_INCREASE_DESCRIPTION => 1127
      DIFFICULTY_DESCRIPTION_LEGACYOFBHAAL_BG2EE =>1135
      DIFFICULTY_DESCRIPTION_STORYMODE => 1136
      DIFFICULTY_DESCRIPTION_STORYMODE_BG2EE => 1136
      DIFFICULTY_DESCRIPTION_STORYMODE_BGEE => 1136

    END
    COPY_EXISTING "L_%EE_LANGUAGE%.lua" override
       PHP_EACH patch_data AS key=>value BEGIN
          SPRINT string (AT value)
          REPLACE_TEXTUALLY ~%key% *=.*~ ~%key% = "%string%",~
       END
    BUT_ONLY

   END

   // default the difficulty-damage setting to turn off difficulty-based changes
  
  LAF check_ini STR_VAR ini="enable_damage_based_difficulty_changes" RET value END
  ACTION_IF !value BEGIN
   ACTION_IF enhanced_edition BEGIN
      ACTION_IF FILE_EXISTS "%USER_DIRECTORY%/baldur.lua" BEGIN
         COPY "%USER_DIRECTORY%/baldur.lua" "%USER_DIRECTORY%"
             REPLACE_TEXTUALLY ~SetPrivateProfileString('Game Options','Suppress Extra Difficulty Damage','0')~ ~SetPrivateProfileString('Game Options','Suppress Extra Difficulty Damage','1')~
         BUT_ONLY
      END
   END ELSE BEGIN
      <<<<<<<< .../stratagems-inline/diffmod.2da
2DA V1.0
0		
	DAMAGE_MOD	LUCK_MOD
EASIEST	0               0
EASY	0               0
NORMAL	0	        0
HARD	0	        0
HARDEST	0	        0
      >>>>>>>>
      COPY ".../stratagems-inline/diffmod.2da" override
   END
  END
END

///////////////////////////////////////////////////////////////////
//// Helper functions
///////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION difficulty_helper_initial
    STR_VAR codename=""
            string=""
            variable=""
            require=""
            diff_1=""
            diff_2=""
            diff_3=""
            diff_4=""
            diff_5=""
            diff_6=""
            diff_7=""
BEGIN
    OUTER_FOR (entry=0;entry<=7;entry+=1) BEGIN
       ACTION_IF $diff("%entry%") STRING_COMPARE_CASE "" BEGIN
          OUTER_SPRINT globalstring ~Global("%variable%","GLOBAL",%entry%)Global("DW_%codename%_disable","GLOBAL",99)~
          OUTER_SPRINT append_me "IF ~%globalstring%~ THEN REPLY (AT %string%) GOTO %codename%_%entry%"
          APPEND_OUTER "%workspace%/difficulty_dialog.d" "%append_me%"
       END
    END
END

DEFINE_ACTION_FUNCTION difficulty_helper_main
    STR_VAR codename=""
            string=""
            variable=""
            require=""
            diff_1=""
            diff_2=""
            diff_3=""
            diff_4=""
            diff_5=""
            diff_6=""
            diff_7=""
BEGIN
    OUTER_FOR (entry=0;entry<=7;entry+=1) BEGIN
     ACTION_IF $diff("%entry%") STRING_COMPARE_CASE "" BEGIN
       OUTER_SET saystring = $diff_report_strings("%entry%")
       APPEND_OUTER "%workspace%/difficulty_dialog.d" "IF ~~ THEN BEGIN %codename%_%entry% SAY (AT %saystring%)"
       APPEND_OUTER "%workspace%/difficulty_dialog.d" "IF ~~ THEN REPLY @1019 GOTO initial"
       OUTER_FOR (option=0;option<=7;option+=1) BEGIN
         ACTION_IF (option=0 || $diff("%option%")>=0) BEGIN
             OUTER_SET replystrref = $diff_choice_strings("%option%")
             OUTER_SPRINT replystring (AT "%replystrref%")
             OUTER_SPRINT globalstring ~SetGlobal("%variable%","GLOBAL",%option%)~
             ACTION_IF option>=1 BEGIN
                OUTER_SET temp_strref="%diff_%option%%"
                OUTER_SPRINT temp (AT %temp_strref%)
                OUTER_SPRINT replystring "%replystring%%temp%"
             END
             APPEND_OUTER "%workspace%/difficulty_dialog.d" "IF ~~ THEN REPLY ~%replystring%~ DO ~%globalstring%~ GOTO acknowledge_%option%"
         END
       END
       APPEND_OUTER "%workspace%/difficulty_dialog.d" "END"
     END
    END


END

DEFINE_ACTION_FUNCTION difficulty_ui
   RET abort
BEGIN

           OUTER_SET abort=0

           // read in the UI
           LAF read_file_into_array STR_VAR file=ui.menu locabs=override RET_ARRAY ui_array=file_contents END

           // get required data
           LAF UI_extract_object STR_VAR ui_array object_ident=menu object_name_field=name object_name=OPTIONS_GAMEPLAY RET_ARRAY menu_array=object_array END
           LAF UI_get_object_entry STR_VAR ui_array=menu_array object_ident=button object_name_field=text object_name=FEEDBACK_BUTTON entry_field=area RET feedback_entry=entry END
           LAF UI_get_object_entry STR_VAR ui_array=menu_array object_ident=button object_name_field=text object_name=ASSIGN_KEYS_BUTTON entry_field=area RET keys_entry=entry END
           LAF UI_get_object_entry STR_VAR ui_array=menu_array object_ident=button object_name_field=text object_name=ASSIGN_KEYS_BUTTON entry_field=bam RET bam_string=entry END
           OUTER_INNER_PATCH "%feedback_entry%" BEGIN
              REPLACE_EVALUATE "[^0-9]*\([0-9]+\)[^0-9]*\([0-9]+\)[^0-9]*\([0-9]+\)[^0-9]*\([0-9]+\)[^0-9]*"
              BEGIN
                feedback_xloc="%MATCH1%"
                feedback_yloc="%MATCH2%"
                feedback_width="%MATCH3%"
                feedback_height="%MATCH4%"
              END
              ""
           END
           OUTER_INNER_PATCH "%keys_entry%" BEGIN
              REPLACE_EVALUATE "[^0-9]*\([0-9]+\)[^0-9]*\([0-9]+\)[^0-9]*\([0-9]+\)[^0-9]*\([0-9]+\)[^0-9]*"
              BEGIN
                keys_xloc="%MATCH1%"
                keys_yloc="%MATCH2%"
                keys_width="%MATCH3%"
                keys_height="%MATCH4%"
              END
              ""
           END
           ACTION_IF !(keys_xloc=feedback_xloc) || !(keys_width=feedback_width) BEGIN
              OUTER_SET abort=1 // in this case the setup is too different from what we're used to
           END
           OUTER_SET delta=feedback_yloc - keys_yloc
           OUTER_SET new_yloc=keys_yloc - delta
           OUTER_SPRINT area_string "%feedback_xloc% %new_yloc% %feedback_width% %feedback_height%"

           // make the patch
           
        ACTION_IF !abort BEGIN

           // main changes

   <<<<<<<< .../stratagems-inline/diff_button
	button
        {
		bam		%bam_string%
		sequence	0
		area		%area_string%
		text		"SCS_DIFFICULTY_BUTTON"
		text style	"button"
		action
		"
			scsSummonWidget()
                        e:SelectEngine(worldScreen)
		"
	}
   >>>>>>>>
   <<<<<<<< .../stratagems-inline/diff_func
function scsSummonWidget()
	  C:Eval('CreateCreatureObject("dw#diffi",Player1,0,0,0)')
end
   >>>>>>>>

          LAF read_file_into_array STR_VAR file=diff_func inline=yes RET_ARRAY diff_func=file_contents END
          LAF read_file_into_array STR_VAR file=diff_button inline=yes RET_ARRAY diff_button=file_contents END
          LAF UI_add_function STR_VAR ui_array_in=ui_array function_array=diff_func RET_ARRAY ui_array=ui_array_out END
          LAF UI_insert_into_menu STR_VAR ui_array_in=ui_array insert_array=diff_button menu_name=OPTIONS_GAMEPLAY RET_ARRAY ui_array=ui_array_out END
          LAF write_array_into_file STR_VAR file="override/ui.menu" file_contents=ui_array END

          // add the identifying string

          LAF UI_add_string INT_VAR tra_entry=1128 STR_VAR string_id="SCS_DIFFICULTY_BUTTON" END
          
          // tweaks
          
          ACTION_IF (GAME_IS BGEE && !GAME_INCLUDES SOD) BEGIN
             COPY_EXISTING "ui.menu" override
                   REPLACE_TEXTUALLY "area 44 190 360 262" "area 44 190 360 192"
             BUT_ONLY
          END

      END






END
