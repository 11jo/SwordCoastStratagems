DEFINE_ACTION_FUNCTION ini_global_setup BEGIN

	ACTION_IF !FILE_EXISTS_IN_GAME ".../stratagems-inline/m_dw_igd.lua" BEGIN
		COPY ".../stratagems-inline/m_dw_igd.lua" override
		COPY ".../stratagems-inline/m_dw_igf.lua" override
		COPY_EXISTING "ui.menu" override
			LPF UI_alter_object STR_VAR object=right_sidebar object_type=menu patch=add_ini_func END
		
	END
	
END

DEFINE_DIMORPHIC_FUNCTION ini_global_add 
	INT_VAR default=0
	STR_VAR type=""
			id=""
			global=""
BEGIN
	LAF ini_global_setup END
	APPEND "m_dw_igd.lua" "table.insert(dw_initial_globals,{type='%type%',id='%id%',global='%global%',default=%default%})"
END

DEFINE_PATCH_FUNCTION add_ini_func BEGIN

	REPLACE_TEXTUALLY ~\(onOpen[^"]*"\)~ ~\1%WNL%%TAB%%TAB%dwIniGlobals()%WNL%~

END

<<<<<<<<.../stratagems-inline/m_dw_igd.lua
dw_initial_globals={}
>>>>>>>>
<<<<<<<<.../stratagems-inline/m_dw_igf.lua
function dwIniGlobals()

	for k,entry in pairs (dw_initial_globals) do
		val=Infinity_GetINIValue(entry.type,entry.id,entry.default)
		str='SetGlobal("'..entry.global..'","GLOBAL",'..val..')'
		C:Eval(str)	
	end
end
>>>>>>>>