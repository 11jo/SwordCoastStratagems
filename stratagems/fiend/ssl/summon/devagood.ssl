VARIABLE(IsSummoned=True)
INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/summon/shared_summons.ssl)

//////////////////////////////////////////////////////////////////////////////////////////
///	death effects
//////////////////////////////////////////////////////////////////////////////////////////

IF
	Die()
	Alignment(Myself,MASK_EVIL)
THEN
	RESPONSE #100
		CreateVisualEffectObject("SPUNHOLY",Myself)
		DestroySelf()
END

IF
	Die()
	!Alignment(Myself,MASK_EVIL)
THEN
	RESPONSE #100
		CreateVisualEffectObject("SPHOLY",Myself)
		DestroySelf()
END


//////////////////////////////////////////////////////////////////////////////////////
///	Deva combat script
//////////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/fiend_definitions_summoned.ssl)
INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/initial.ssl)  // rest-and-recover, mill in confusion, etc.

/////////////////////////////////////////////////////////////////////////////////////////
///	Emergency healing for me
/////////////////////////////////////////////////////////////////////////////////////////


IF TRIGGER
	HPPercentLT(Myself,70)
THEN DO
    Action(SpellMyself,CLERIC_HEAL)
END

IF TRIGGER
    StateCheck(Myself,STATE_BLIND)
	!CheckSpellState(Myself,DO_NOT_CURE_BLINDNESS)
THEN DO
	   Action(SpellMyself,CLERIC_NEUTRALIZE_POISON)
       Action(SpellMyself,CLERIC_HEAL)
END


/////////////////////////////////////////////////////////////////////////////////////////
///	Healing for allies
/////////////////////////////////////////////////////////////////////////////////////////


// start with disabled-and-injured

IF TRIGGER
	TargetBlock(HumanoidVillains)
	OR(2)
            StateCheck(scstarget,STATE_BLIND)
            StateCheck(scstarget,STATE_FEEBLEMINDED)
	!CheckSpellState(scstarget,DO_NOT_CURE_BLINDNESS)
	HPPercentLT(scstarget,75)
        CheckStatGT(scstarget,15,LEVEL)
	Allegiance(scstarget,ENEMY)
THEN DO
       Action(Spell,CLERIC_HEAL)
       SetGlobalTimer("DMWWCuringBlindness","GLOBAL",18)
END

// then disabled-but-not-injured

IF TRIGGER
	TargetBlock(HumanoidVillains)
        StateCheck(scstarget,STATE_FEEBLEMINDED)
	!CheckSpellState(scstarget,DO_NOT_CURE_BLINDNESS)
        CheckStatGT(scstarget,15,LEVEL)
	Allegiance(scstarget,ENEMY)
THEN DO
       Combine()
       Action(Spell,CLERIC_HEAL)
       SetGlobalTimer("DMWWCuringBlindness","GLOBAL",18)
END

IF TRIGGER
        IgnoreBlock(Demivrgvs)
	TargetBlock(HumanoidVillains)
	StateCheck(scstarget,STATE_BLIND)
	!CheckSpellState(scstarget,DO_NOT_CURE_BLINDNESS)
       CheckStatGT(scstarget,15,LEVEL)
	Allegiance(scstarget,ENEMY)
THEN DO
       Action(Spell,CLERIC_NEUTRALIZE_POISON)
       Action(Spell,CLERIC_HEAL)
       SetGlobalTimer("DMWWCuringBlindness","GLOBAL",18)
END

// then just injured

IF TRIGGER
	TargetBlock(HumanoidVillains)
	HPPercentLT(scstarget,50)
        CheckStatGT(scstarget,15,LEVEL)
	Allegiance(scstarget,ENEMY)
THEN DO
	Action(Spell,CLERIC_HEAL|100|50)
	Action(Spell,CLERIC_CURE_CRITICAL_WOUNDS|100|50)
END

////////////////////////////////////////
/// Remove conditions from my team
//////////////////////////////////////////

IF
	HaveSpell(CLERIC_LESSER_RESTORATION)  // SPPR417.SPL (Lesser Restoration)
	Allegiance(Myself,GOODCUTOFF)
	See([PC])
	CheckStatGT(LastSeenBy(Myself),0,LEVELDRAIN)
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),CLERIC_LESSER_RESTORATION)  // SPPR417.SPL (Lesser Restoration)
END

IF
	HaveSpell(CLERIC_NEUTRALIZE_POISON)  // SPPR404.SPL (Neutralize Poison)
	Allegiance(Myself,GOODCUTOFF)
	See([PC])
	StateCheck(LastSeenBy(Myself),STATE_POISONED)
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),CLERIC_NEUTRALIZE_POISON)  // SPPR404.SPL (Neutralize Poison)
END

IF TRIGGER
	TargetBlock(PCsInOrder)
	Allegiance(Myself,GOODCUTOFF)
	StateCheck(scstarget,STATE_PANIC)
THEN DO
	Action(Spell,CLERIC_REMOVE_FEAR)
END

IF TRIGGER
	TargetBlock(VillainsInOrder)
	TriggerBlock(Enemy)
	CheckStatGT(scstarget,14,Level)
	StateCheck(scstarget,STATE_PANIC)
THEN DO
	Action(Spell,CLERIC_REMOVE_FEAR)
END

IF TRIGGER
	TargetBlock(PCsInOrder)
	Allegiance(Myself,GOODCUTOFF)
	StateCheck(scstarget,STATE_POISONED)
THEN DO
	Action(Spell,CLERIC_NEUTRALIZE_POISON)
END

IF TRIGGER
	TargetBlock(VillainsInOrder)
	TriggerBlock(Enemy)
	CheckStatGT(scstarget,14,Level)
	StateCheck(scstarget,STATE_POISONED)
THEN DO
	Action(Spell,CLERIC_NEUTRALIZE_POISON)
END

IF TRIGGER
	TargetBlock(PCsInOrder)
	Allegiance(Myself,GOODCUTOFF)
	CheckStatGT(scstarget,0,LEVELDRAIN)
THEN DO
	Action(Spell,CLERIC_LESSER_RESTORATION)
END

IF TRIGGER
	TargetBlock(VillainsInOrder)
	TriggerBlock(Enemy)
	CheckStatGT(scstarget,14,Level)
	CheckStatGT(scstarget,0,LEVELDRAIN)
THEN DO
	Action(Spell,CLERIC_LESSER_RESTORATION)
END


//////////////////////////////////////////////////////////////////////////////////////
///	Consider moving to a more interesting location in the battle
//////////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/jump.ssl)

//////////////////////////////////////////////////////////////////////////////////////
///	Consider a Dispel Magic
//////////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/dispel.ssl)


//////////////////////////////////////////////////////////////////////////////
////	Core melee block
///////////////////////////////////////////////////////////////////////////////

IF TRIGGER
  !Allegiance(Myself,ENEMY)
  TargetBlock(EnemiesInOrderShort)
  TriggerBlock(Plus3Safe)
THEN DO
   Action(Literal)
   AttackOneRound(scstarget)
END


BEGIN LOOP(MyWeaponStrength||3)
BEGIN LOOP(MyWeaponDamageType||Slashing)
INCLUDE FILE(%MOD_FOLDER%/genai/ssl/easy_melee.ssl)
INCLUDE FILE(%MOD_FOLDER%/genai/ssl/dw#wtacor.ssl)
INCLUDE FILE(%MOD_FOLDER%/genai/ssl/move.ssl)
END LOOP
END LOOP

