//////////////////////////////////////////////////////////////////////////////////////
///	Glabrezu combat script
//////////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/fiend/ssl/fiend_definitions.ssl)

INCLUDE FILE(%scsroot%/fiend/ssl/initial.ssl)  // rest-and-recover, mill in confusion, etc.

//////////////////////////////////////////////////////////////////////////////////////
///	Renew its mirror image
//////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	See([ANYONE])
	!StateCheck(Myself,STATE_MIRRORIMAGE)
	TriggerBlock(TruesightSafe)
THEN DO
	Action(SpellNoDecMyself,WIZARD_MIRROR_IMAGE|100|100)
END

/////////////////////////////////////////////////////////////////////////////////////////
///	Kill Baatezu
/////////////////////////////////////////////////////////////////////////////////////////

BEGIN LOOP(scsdemon||LAWFUL_EVIL)
	INCLUDE FILE(%scsroot%/fiend/ssl/bloodwar.ssl)
END LOOP

//////////////////////////////////////////////////////////////////////////////////////
///	Consider moving to a more interesting location in the battle
//////////////////////////////////////////////////////////////////////////////////////

/// INCLUDE FILE(%scsroot%/fiend/ssl/jump.ssl)  excluded for summoned demon

//////////////////////////////////////////////////////////////////////////////////////
///	Consider a Dispel Magic
//////////////////////////////////////////////////////////////////////////////////////

/// INCLUDE FILE(%scsroot%/fiend/ssl/dispel.ssl) excluded for summoned demon (not least because dispelling the Pro/Evil is cheating)

//////////////////////////////////////////////////////////////////////////////////////
///	Use offensive magic
//////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////
////  Power Word: Stun
/////////////////////////////////////////////////////////////////////////

IF TRIGGER
	Target([0.0.0.MAGE_ALL])
	Target([0.0.0.CLERIC_ALL])
	Target([0.0.0.BARD])
	Target([0.0.0.DRUID_ALL])
	TriggerBlock(MR|SpellTurnNew|Stun|Helpless|SIEnchantment)
	OR(3)
		HPPercentLT(scstarget,60)
		Class(scstarget,MAGE)
		Class(scstarget,MAGE_THIEF)
THEN DO
	Action(SpellNoDec,WIZARD_POWER_WORD_STUN|100|100)
END

////////////////////////////////////////////////////////////////////////////////////
///	Unholy Blight
//////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	Target([ANYONE])
	TriggerBlock(Enemy)
THEN DO
	Action(SpellNoDec,CLERIC_UNHOLY_BLIGHT|200|100)
END

///////////////////////////////////////////////////////////////////////////
////	Confusion - skipped as I'm not sure who it targets
///////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////
///	Combat
//////////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////
////	At least *consider* chewing on stunned victims
///////////////////////////////////////////////////////////////////////


IF
	See([ANYONE])
	StateCheck(LastSeenBy(Myself),STATE_STUNNED)
THEN
	RESPONSE #100
		AttackOneRound(LastSeenBy(Myself))
END

IF
	See([ANYONE])
THEN
	RESPONSE #100
		AttackOneRound(LastSeenBy(Myself))
END

//////////////////////////////////////////////////////////////////////////////
////	Core combat block
///////////////////////////////////////////////////////////////////////////////


BEGIN LOOP(MyWeaponStrength||4)
BEGIN LOOP(MyWeaponDamageType||Crushing)
INCLUDE FILE(%scsroot%/genai/ssl/dw#wtacor.ssl)
INCLUDE FILE(%scsroot%/genai/ssl/move.ssl)
INCLUDE FILE(%scsroot%/fiend/ssl/chase.ssl)
END LOOP
END LOOP

