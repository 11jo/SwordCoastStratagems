//////////////////////////////////////////////////////////////////////
////	Pit Fiend combat script
////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/fiend/ssl/fiend_definitions.ssl)

IF
	Die()
THEN
	RESPONSE #100
		DestroySelf()
END

IF	
	!StateCheck(Myself,STATE_INVISIBLE)
	Allegiance(Myself,ENEMY)
	!See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		ApplySpellRES("dw#silin",Myself)
END

INCLUDE FILE(%scsroot%/fiend/ssl/initial.ssl)  // rest-and-recover, mill in confusion, etc.

/////////////////////////////////////////////////////////////////////////////////////////
///	Kill Baatezu
/////////////////////////////////////////////////////////////////////////////////////////

BEGIN LOOP(scsdemon||CHAOTIC_EVIL)
	INCLUDE FILE(%scsroot%/fiend/ssl/bloodwar.ssl)
END LOOP

////////////////////////////////////////////////////////////////////////////
///	Begin fight with fear aura
////////////////////////////////////////////////////////////////////////////

IF
	See(NearestEnemyOf(Myself))
	!GlobalTimerNotExpired("fearaura","LOCALS")
THEN 
	RESPONSE #100
		SetGlobalTimer("fearaura","LOCALS",6)
		ReallyForceSpell(Myself,DEMON_FEAR)
		Continue()
END

//////////////////////////////////////////////////////////////////////////
///	Stay Improved Invisible where possible
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TriggerBlock(TruesightSafe)
	!StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
THEN DO	
	Action(SpellNoDecMyself,WIZARD_IMPROVED_INVISIBILITY|200|50)
END

/////////////////////////////////////////////////////////////////////////////////
///	Ultramagic
/////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	Target([ANYONE])
	!GlobalTimerNotExpired("meteorswarm","LOCALS")
	TriggerBlock(Enemy|Disabled|MR100|SIEvocation|ResistFire)
THEN DO
	SetGlobalTimer("meteorswarm","LOCALS",1000)
	Action(SpellNoDec,WIZARD_METEOR_SWARM|100|100)
END

IF TRIGGER
	Target([ANYONE])
	!GlobalTimerNotExpired("unholyword","LOCALS")
	Alignment(scstarget,MASK_GOOD)
	Range(scstarget,10)
THEN DO
	SetGlobalTimer("unholyword","LOCALS",1000)
	Action(SpellNoDecMyself,CLERIC_UNHOLY_WORD|100|100)
END

///////////////////////////////////////////////////////////////////////
///	Fireball
///////////////////////////////////////////////////////////////////////

IF TRIGGER
	Target([ANYONE])
	Target(SecondNearest([ANYONE]))
	TriggerBlock(Enemy|Disabled|MR100|SIEvocation|ResistFire)
THEN DO
	Combine()
	Action(SpellNoDec,DEATHKNIGHT_FIREBALL|100|100)
END


///////////////////////////////
////	Symbol: Fear
///////////////////////////////

IF TRIGGER
	Target([ANYONE])
	Target(SecondNearest([ANYONE]))
	TriggerBlock(Enemy|Disabled|MR|Panic|SIConjuration)
THEN DO
	Combine()
	Action(SpellNoDec,WIZARD_NPC_SYMBOL_FEAR)
END

//////////////////////////////////////////////////////////////////////////////
////	Core combat block
///////////////////////////////////////////////////////////////////////////////

IF
	See([ANYONE])
THEN
	RESPONSE #100
		AttackOneRound(LastSeenBy(Myself))
END
