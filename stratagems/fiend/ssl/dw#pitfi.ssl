//////////////////////////////////////////////////////////////////////
////	Pit Fiend combat script
////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/fiend/ssl/fiend_definitions.ssl)

IF
	Die()
THEN
	RESPONSE #100
		ReallyForceSpellDead(Myself,RED_HOLY_MIGHT)
		DestroySelf()
END

IF	
	!StateCheck(Myself,STATE_INVISIBLE)
	Allegiance(Myself,ENEMY)
	!See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		ApplySpellRES("dw#silin",Myself)
END

INCLUDE FILE(%scsroot%/fiend/ssl/initial.ssl)  // rest-and-recover, mill in confusion, etc.

/////////////////////////////////////////////////////////////////////////////////////////
///	Kill Baatezu
/////////////////////////////////////////////////////////////////////////////////////////

BEGIN LOOP(scsdemon||CHAOTIC_EVIL)
	INCLUDE FILE(%scsroot%/fiend/ssl/bloodwar.ssl)
END LOOP

////////////////////////////////////////////////////////////////////////////
///	Begin fight with fear aura
////////////////////////////////////////////////////////////////////////////

IF
	See(NearestEnemyOf(Myself))
	!GlobalTimerNotExpired("fearaura","LOCALS")
THEN 
	RESPONSE #100
		SetGlobalTimer("fearaura","LOCALS",6)
		ReallyForceSpell(Myself,DEMON_FEAR)
		Continue()
END

//////////////////////////////////////////////////////////////////////////////////////
///	Consider moving to a more interesting location in the battle
//////////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/fiend/ssl/jump.ssl)

//////////////////////////////////////////////////////////////////////////////////////
///	Consider a Dispel Magic
//////////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/fiend/ssl/dispel.ssl)

//////////////////////////////////////////////////////////////////////////
///	Stay Improved Invisible where possible
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TriggerBlock(TruesightSafe)
	!StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
THEN DO	
	Action(SpellNoDecMyself,WIZARD_IMPROVED_INVISIBILITY|100|100)
END

/////////////////////////////////////////////////////////////////////////////////
///	Ultramagic
/////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	IgnoreBlock(IsSummoned)
	TargetBlock(PCsInOrder)
	!GlobalTimerNotExpired("meteorswarm","LOCALS")
	TriggerBlock(Enemy|Disabled|MR100|SIEvocation|ResistFire)
THEN DO
	Combine()
	SetGlobalTimer("meteorswarm","LOCALS",1000)
	Action(SpellNoDec,WIZARD_METEOR_SWARM|200|100)
END

IF TRIGGER
	TargetBlock(PCsInOrder)
	!GlobalTimerNotExpired("unholyword","LOCALS")
	Alignment(scstarget,MASK_GOOD)
	Range(scstarget,10)
THEN DO
	Combine()
	SetGlobalTimer("unholyword","LOCALS",1000)
	Action(SpellNoDecMyself,CLERIC_UNHOLY_WORD|200|100)
END

///////////////////////////////////////////////////////////////////////
///	Fireball
///////////////////////////////////////////////////////////////////////

IF TRIGGER
	IgnoreBlock(IsSummoned)
	TargetBlock(PCsInOrder)
	TriggerBlock(Enemy|Disabled|MR100|SIEvocation|ResistFire)
THEN DO
	Combine()
	Action(SpellNoDec,DEATHKNIGHT_FIREBALL|200|100)
END


///////////////////////////////
////	Symbol: Fear
///////////////////////////////

IF TRIGGER
	TargetBlock(EnemiesInOrder)
	TriggerBlock(MR|Panic|SIConjuration)
THEN DO
	Combine()
	Action(SpellNoDec,WIZARD_NPC_SYMBOL_FEAR)
END

//////////////////////////////////////////////////////////////////////////////
////	Core combat block
///////////////////////////////////////////////////////////////////////////////



BEGIN LOOP(MyWeaponStrength||4)
BEGIN LOOP(MyWeaponDamageType||Piercing)
INCLUDE FILE(%scsroot%/genai/ssl/dw#wtacor.ssl)
INCLUDE FILE(%scsroot%/genai/ssl/move.ssl)
INCLUDE FILE(%scsroot%/fiend/ssl/chase.ssl)
END LOOP
END LOOP