

///////////////////////////////////////////////////////////////////////////////////////
///	Remove pre-summoning ring
///////////////////////////////////////////////////////////////////////////////////////

IF
	HasItem("dw#sumfi",Myself)
THEN
	RESPONSE #100
		DestroyItem("dw#sumfi")
		Continue()
END


//////////////////////////////////////////////////////////
////	When to attack the bad guys (borrowed from Weimer Tactics)
////////////////////////////////////////////////////////////

IF
	!Global("inafight","LOCALS",1)
    OR(8)
      AttackedBy([GOODCUTOFF.0.0.0.0.SUMMONED],DEFAULT)
      AttackedBy([PC],DEFAULT)
      AttackedBy([FAMILIAR],DEFAULT)
      AttackedBy([ALLY],DEFAULT)
      AttackedBy([CONTROLLED],DEFAULT)
      AttackedBy([CHARMED],DEFAULT)
      SpellCastOnMe([GOODCUTOFF.0.0.0.0.SUMMONED],0)
	See(LastSummonerOf(Myself))
THEN
  RESPONSE #100
    Enemy()
    SetGlobal("inafight","LOCALS",1)
	SetGlobalTimer("stayontarget","LOCALS",12)
	SetGlobalTimer("seedefences","LOCALS",6)
    Continue()
END

IF	
	!Global("inafight","LOCALS",1)
	See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		SetGlobal("inafight","LOCALS",1)
		SetGlobalTimer("stayontarget","LOCALS",12)
		SetGlobalTimer("seedefences","LOCALS",6)
		Continue()
END

////////////////////////////////////////////////////////////
////	This is a rest-and-recover step for when
////	enemies leave you alone...
////////////////////////////////////////////////////////////

IF
	See(NearestEnemyOf(Myself))
	Global("rest","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobalTimer("rtime","LOCALS",2000)
		SetGlobal("rest","LOCALS",1)
		Continue()
END

IF
	Global("rest","LOCALS",1)
	GlobalTimerExpired("rtime","LOCALS")
	!See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		SetGlobal("rest","LOCALS",0)
		ApplySpell(Myself,RESTORE_FULL_HEALTH)
		Rest()
END

///////////////////////////////////////////////////
////	If attacked by an unseen foe, mill around
////	in confusion. You never know, you might
////	bump into them!
//////////////////////////////////////////////////

IF
	Allegiance(Myself,ENEMY)
    OR(12)
      AttackedBy([GOODCUTOFF.0.0.0.0.SUMMONED],DEFAULT)
      AttackedBy([PC],DEFAULT)
      AttackedBy([FAMILIAR],DEFAULT)
      AttackedBy([ALLY],DEFAULT)
      AttackedBy([CONTROLLED],DEFAULT)
      AttackedBy([CHARMED],DEFAULT)
      SpellCastOnMe([GOODCUTOFF.0.0.0.0.SUMMONED],0)
      SpellCastOnMe([PC],0)
      SpellCastOnMe([FAMILIAR],0)
      SpellCastOnMe([ALLY],0)
      SpellCastOnMe([CONTROLLED],0)
      SpellCastOnMe([CHARMED],0) 
!GlobalTimerNotExpired("whatthe","LOCALS")
  !See(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    SetGlobalTimer("whatthe","LOCALS",12)
    Continue()
END

IF
  !See(NearestEnemyOf(Myself))
  GlobalTimerNotExpired("whatthe","LOCALS")
THEN
  RESPONSE #100
    RandomWalkContinuous()
END

/////////////////////////////////////////////////////
///	Cancel the "chase" move so you actually stop and shoot
/////////////////////////////////////////////////////////////

IF
	See(NearestEnemyOf(Myself))
	!Global("chasePC","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("chasePC","LOCALS",0)
		ClearActions(Myself)
END

///////////////////////////////////////////////////////////
///	CPU saver
///////////////////////////////////////////////////////////

IF
	!Detect(NearestEnemyOf(Myself))
	!Global("inafight","LOCALS",1)
THEN
	RESPONSE #100
		NoAction()
END
		

INCLUDE FILE(%scsroot%/genai/ssl/chase_top.ssl)