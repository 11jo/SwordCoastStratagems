//////////////////////////////////////////////////////////////////////////////////////
///	Glabrezu combat script
//////////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/fiend_definitions.ssl)

INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/initial.ssl)  // rest-and-recover, mill in confusion, etc.


//////////////////////////////////////////////////////////////////////////////////////
///	Renew its mirror image
//////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	See(NearestEnemyOf(Myself))
	!StateCheck(Myself,STATE_MIRRORIMAGE)
	TriggerBlock(TruesightSafe|CorePlus)
THEN DO
	Action(SpellMyself,INNATE_FIEND_MIRROR_IMAGE|100|100)
END

/////////////////////////////////////////////////////////////////////////////////////////
///	Kill Baatezu
/////////////////////////////////////////////////////////////////////////////////////////

BEGIN LOOP(scsdemon||LAWFUL_EVIL)
	INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/bloodwar.ssl)
END LOOP

//////////////////////////////////////////////////////////////////////////////////////
///	Consider moving to a more interesting location in the battle
//////////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/jump.ssl)

//////////////////////////////////////////////////////////////////////////////////////
///	Consider a Truesight
//////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(EnemiesInOrder)
	Gender(scstarget,ILLUSIONARY)
	!Allegiance(scstarget,ENEMY)
	TriggerBlock(TruesightAlreadyCast|Enemy|SIDivination|EasyPlus)
THEN DO
	Action(SpellMyself,INNATE_FIEND_TRUE_SIGHT)
END

//////////////////////////////////////////////////////////////////////////////////////
///	Consider a Dispel Magic
//////////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/dispel.ssl)

//////////////////////////////////////////////////////////////////////////////////////
///	Use offensive magic
//////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////
////  Power Word: Stun
/////////////////////////////////////////////////////////////////////////

IF TRIGGER
        IgnoreBlock(Demivrgvs)
	TargetBlock(PCsPreferringWeak)
	!CheckStatGT(scstarget,0,WIZARD_SPELL_TRAP)
	TriggerBlock(MR|Stun|Enemy|Helpless|SIConjuration|SRPowerWord)
	OR(3)
		HPPercentLT(scstarget,60)
		Class(scstarget,MAGE)
		Class(scstarget,MAGE_THIEF)
	OR(2)
	        HPGT(Myself,90)
	        !CheckStatGT(scstarget,0,WIZARD_SPELL_TURNING)
THEN DO
	Action(Spell,WIZARD_POWER_WORD_STUN|100|100)
END

IF TRIGGER
        RequireBlock(Demivrgvs)
	TargetBlock(PCsPreferringWeak)
	TriggerBlock(MR|SpellTurn|Stun|Enemy|Helpless|SIConjuration|SRPowerWord)
	OR(3)
		HPPercentLT(scstarget,60)
		Class(scstarget,MAGE)
		Class(scstarget,MAGE_THIEF)
THEN DO
	Action(Spell,WIZARD_POWER_WORD_STUN|100|100)
END

////////////////////////////////////////////////////////////////////////////////////
///	Unholy Blight
//////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsPreferringWeak)
	TriggerBlock(MR100|Enemy|Helpless|SINecromancy|ResistMagicDamage|EasyPlus)
	Alignment(scstarget,MASK_GOOD)	
THEN DO
	Action(Spell,INNATE_FIEND_UNHOLY_BLIGHT|200|100)
END

///////////////////////////////////////////////////////////////////////////
////	Confusion
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(EnemiesInOrder)
	TriggerBlock(MR100|Confused|SIEnchantment|Enemy|Helpless|ConfuseProof|EasyPlus)
THEN DO
	Combine()
	Action(SpellNoDec,INNATE_FIEND_CONFUSION|200|100)
END

//////////////////////////////////////////////////////////////////////////////
///	Combat
//////////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////
////	At least *consider* chewing on stunned victims
///////////////////////////////////////////////////////////////////////

IF TRIGGER
	Target(NearestEnemyOfType([PC]))
	StateCheck(scstarget,STATE_STUNNED)
	TriggerBlock(Enemy|Plus4Safe)
THEN DO
	Action(Attack|100|100)
END

//////////////////////////////////////////////////////////////////////////////
////	Core combat block
///////////////////////////////////////////////////////////////////////////////


BEGIN LOOP(MyWeaponStrength||4)
BEGIN LOOP(MyWeaponDamageType||Crushing)
INCLUDE FILE(%MOD_FOLDER%/genai/ssl/easy_melee.ssl)
INCLUDE FILE(%MOD_FOLDER%/genai/ssl/dw#wtacor.ssl)
INCLUDE FILE(%MOD_FOLDER%/genai/ssl/move.ssl)
INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/chase.ssl)
END LOOP
END LOOP
