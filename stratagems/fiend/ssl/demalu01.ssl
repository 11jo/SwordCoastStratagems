//////////////////////////////////////////////////////////////////////////////////////
///	Alu-fiend combat script
//////////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%MOD_FOLDER%/lib/ssl/shared_definitions.ssl)
INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/fiend_definitions.ssl)

INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/initial.ssl)  // rest-and-recover, mill in confusion, etc.

/////////////////////////////////////////////////////////////////////////////////////////
///	Kill Baatezu
/////////////////////////////////////////////////////////////////////////////////////////

BEGIN LOOP(scsdemon||LAWFUL_EVIL)
	INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/bloodwar.ssl)
END LOOP

///////////////////////////////////////////////////////////////////////////////
////	Haste
///////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	!GlobalTimerNotExpired("haste","LOCALS")
	See(NearestEnemyOf(Myself))
	!StateCheck(Myself,STATE_HASTED)
THEN DO
	Action(SpellMyself,WIZARD_HASTE)
	SetGlobalTimer("haste","LOCALS",1000)
END

//////////////////////////////////////////////////////////////////////////////////////
///	Renew mirror image
//////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	See(NearestEnemyOf(Myself))
	!StateCheck(Myself,STATE_MIRRORIMAGE)
	TriggerBlock(TruesightSafe)
THEN DO
	Action(SpellMyself,WIZARD_MIRROR_IMAGE|100|100)
END


///////////////////////////////////////////////////////////////////////////////
////	Seduce weak-willed
///////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	Target([PC.0.0.0.0.MALE])
	Target(SecondNearest([PC.0.0.0.0.MALE]))
	Target(ThirdNearest([PC.0.0.0.0.MALE]))
	Target(FourthNearest([PC.0.0.0.0.MALE]))
	TriggerBlock(Enemy|Disabled|Charm|MR)
	CheckStatGT(scstarget,0,SAVEVSSPELL)
THEN DO
	Action(SpellNoDec,SUCCUBUS_CHARM_MALE|100|50)
END

IF TRIGGER
	Target([PC.0.0.0.0.FEMALE])
	Target(SecondNearest([PC.0.0.0.0.FEMALE]))
	Target(ThirdNearest([PC.0.0.0.0.FEMALE]))
	Target(FourthNearest([PC.0.0.0.0.FEMALE]))
	TriggerBlock(Enemy|Disabled|Charm|MR)
	CheckStatGT(scstarget,0,SAVEVSSPELL)
THEN DO
	Action(SpellNoDec,SUCCUBUS_CHARM_FEMALE|100|50)
END


///////////////////////////////////////////////////////////////////////
////	Attack antimagic
///////////////////////////////////////////////////////////////////////



IF TRIGGER
	TargetBlock(PCsInOrder)
	TriggerBlock(Enemy|Helpless|SpellProtectionTarget)
THEN DO
	Combine()
	Action(Spell,WIZARD_SECRET_WORD|100|100)
END

///////////////////////////////////////////////////////////////////////
////	Combat
///////////////////////////////////////////////////////////////////////

BEGIN LOOP(MyWeaponStrength||3)
BEGIN LOOP(MyWeaponDamageType||Slashing)
INCLUDE FILE(%MOD_FOLDER%/genai/ssl/potionuse.ssl)

BEGIN LOOP(scstarget,10||scstarget,5)
	INCLUDE FILE(%MOD_FOLDER%/genai/ssl/dw#wtacor.ssl)
END LOOP

INCLUDE FILE(%MOD_FOLDER%/genai/ssl/shoot.ssl)
INCLUDE FILE(%MOD_FOLDER%/genai/ssl/chase.ssl)
END LOOP
END LOOP
