//////////////////////////////////////////////////////////////////////////////////////
///	Demon Knight combat script
//////////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/fiend/ssl/fiend_definitions.ssl)

INCLUDE FILE(%scsroot%/fiend/ssl/initial.ssl)  // rest-and-recover, mill in confusion, etc.

//////////////////////////////////////////////////////////////////////////////////////
////	Dispel Magic
//////////////////////////////////////////////////////////////////////////////////////

BEGIN LOOP(SpellNoDec||Spell)

	INCLUDE FILE(%scsroot%/fiend/ssl/dispel.ssl)

END LOOP

/////////////////////////////
///   Symbol: Fear
//////////////////////////////

IF TRIGGER
	TargetBlock(EnemiesInOrder)
	!Gender(scstarget,SUMMONED)
	TriggerBlock(Panic|MR100|Enemy|Helpless)
THEN DO
	Combine()
	Action(Spell,WIZARD_NPC_SYMBOL_FEAR|100|200)
END

///////////////////////////////
////	Symbol: Death
///////////////////////////////

IF TRIGGER
	TargetBlock(EnemiesInOrder)
	!Gender(scstarget,SUMMONED)
	TriggerBlock(Slay|MR)
THEN DO
	Combine()
	Action(Spell,WIZARD_NPC_SYMBOL_DEATH|100|200)
END

////////////////////////////////////////////
///	Fireball
////////////////////////////////////////////

IF TRIGGER
	TargetBlock(EnemiesInOrder)
	!GlobalTimerNotExpired("fireball","LOCALS")
	TriggerBlock(MR100|ResistFire|MinorGlobe|Enemy)
THEN DO
        Combine()
	SetGlobalTimer("fireball","LOCALS",1000)
	Action(SpellNoDec,DEATHKNIGHT_FIREBALL|100|100)
END

/////////////////////////////////////////////////////////////////////////
///// Power Word: Kill
////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(PCsInOrderShort)
	TriggerBlock(Enemy)
	OR(3)
		HPPercentLT(scstarget,50)
		Class(scstarget,MAGE)
		Class(scstarget,MAGE_THIEF)
	TriggerBlock(MR100|Slay|SIEnchantment|SpellTurnNew|Enemy)
THEN DO
	Combine()
	Action(Spell,WIZARD_POWER_WORD_KILL|100|100)
END


////////////////////////////////////////
////	Power Word: Blind
///////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsPreferringStrong)
	!Gender(scstarget,SUMMONED)
	TriggerBlock(MR|Enemy|SpellTurnNew)
THEN DO
	Combine()
	Action(Spell,WIZARD_POWER_WORD_BLIND|100|100)
END

////////////////////////////////////////////////////////////////////////
////  Power Word: Stun
/////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC))

	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(PCsInOrderShort)
	TriggerBlock(MR100|SpellTurnNew|Stun|Enemy|Helpless|SIEnchantment)
	OR(3)
		HPPercentLT(scstarget,60)
		Class(scstarget,MAGE)
		Class(scstarget,MAGE_THIEF)
THEN DO
	Action(Spell,WIZARD_POWER_WORD_STUN|100|100)
END

//////////////////////////////////////////////////////////////////////////////
////	Core combat block
///////////////////////////////////////////////////////////////////////////////

BEGIN LOOP(MyWeaponStrength||Unknown)
BEGIN LOOP(MyWeaponDamageType||Slashing)
INCLUDE FILE(%scsroot%/genai/ssl/dw#wtacor.ssl)
INCLUDE FILE(%scsroot%/genai/ssl/move.ssl)
INCLUDE FILE(%scsroot%/fiend/ssl/chase.ssl)
END LOOP
END LOOP