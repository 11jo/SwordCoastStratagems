DEFINE_ACTION_FUNCTION tobex_immunity_labels BEGIN

   <<<<<<<< .../stratagems-inline/immune_stats.2da
   SPWI511.spl 318 1 ENCHANTMENT_IMMUNITY 120 n a
   SPWI708.spl 318 3 ENCHANTMENT_IMMUNITY 120 n a
   SPWI808.spl 318 4 ENCHANTMENT_IMMUNITY 120 n a
   SPWI907.spl 318 5 ENCHANTMENT_IMMUNITY 120 n a
   >>>>>>>>
   
   COPY ~.../stratagems-inline/immune_stats.2da~ ~%workspace%~

   COPY_EXISTING_REGEXP ~.*\.itm~ ~override~
        GET_OFFSET_ARRAY fx_eq_array ITM_V10_GEN_EFFECTS
        SET enchantment_level=0
        PHP_EACH fx_eq_array AS int=>offset BEGIN
           PATCH_IF SHORT_AT offset=120 BEGIN
             PATCH_MATCH (LONG_AT (offset+0x8)) WITH
             0 BEGIN
                READ_LONG (offset+0x4) ench_here
                SET ench_here+=1
                SET enchantment_level=enchantment_level>ench_here? enchantment_level : ench_here
             END
             2 BEGIN
                SET enchantment_level=enchantment_level>1? enchantment_level : 1
             END
             DEFAULT
             END
           END
        END
        PATCH_IF enchantment_level>0 BEGIN
            SPRINT to_add ~%SOURCE_RES%.itm     318     %enchantment_level%     ENCHANTMENT_IMMUNITY  120   n    g~
            INNER_ACTION BEGIN
               APPEND_OUTER ~%workspace%/immune_stats.2da~ ~%to_add%~ UNLESS ~%to_add%~
            END
        END
   BUT_ONLY

   INCLUDE ~%scsroot%/ds/ds.tph~
   LAF detectable_spells STR_VAR path=~%workspace%~ table = ~immune_stats.2da~ END











END