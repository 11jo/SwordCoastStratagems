/// Astrally travel to stunned opponents - for mind flayers

IF TRIGGER
	GlobalTimerExpired("redefend","LOCALS")
	TargetBlock(PCsInReverseOrder)
	TriggerBlock(Plus2Safe|Enemy)
	!CheckStatGT(scstarget,3,STONESKINS)
	StateCheck(scstarget,STATE_IMMOBILE)
	!Range(scstarget,15)
	OR(3)
		!See(LastTargetedBy(Myself))
		!Range(LastTargetedBy(Myself),8)
		!StateCheck(LastTargetedBy(Myself),STATE_HELPLESS)
	OR(3)
		!See(LastTargetedBy(Myself))
		!Range(LastTargetedBy(Myself),8)
		CheckStatGT(LastTargetedBy(Myself),6,INT)
THEN DO
	Combine()
	Action(PsionicRES,"dw#mftel",teleport,16|100|100)
END


////  Try spontaneously moving to a more attractive target anyway

IF TRIGGER
	GlobalTimerExpired("redefend","LOCALS")
	Target(WorstAC)
	TargetBlock(PCsInReverseOrder)
	TriggerBlock(Plus2Safe|Enemy)
	!CheckStatGT(scstarget,3,STONESKINS)
	!Range(scstarget,15)
	OR(3)
		!See(LastTargetedBy(Myself))
		!Range(LastTargetedBy(Myself),8)
		!StateCheck(LastTargetedBy(Myself),STATE_HELPLESS)
	OR(3)
		!See(LastTargetedBy(Myself))
		!Range(LastTargetedBy(Myself),8)
		CheckStatGT(LastTargetedBy(Myself),6,INT)
THEN DO
	Combine()
	Action(PsionicRES,"dw#mftel",teleport,16|100|100)
END


///// better part of valor (borrowed from Blucher)

IF
	HPPercentLT(Myself,30)
	!AreaCheck("AR6200") // Throne of Bhaal
	!AreaCheck("AR2100") // Underdark main cavern (affects gith death)
  	!AreaCheck("AR1508") // Spellhold (need to detect its death)
  	!AreaCheck("AR3021") //Watchers' Keep (interferes with the count of creatures)
THEN
  RESPONSE #100
    DropInventory()
    SpellNoDec(Myself,DRYAD_TELEPORT)
  RESPONSE #100
    Continue()
END

