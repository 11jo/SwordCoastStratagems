/////////////////////////////////////////////////////////////////////
/// Set up definitions
////////////////////////////////////////////////////////////////////

BEGIN LOOP(scsspellsubstitute1|| )
BEGIN LOOP(scsspellsubstitute2|| )
	INCLUDE FILE (%scsroot%\psionic\ssl\psionic_defs.ssl)
END LOOP
END LOOP

INCLUDE FILE(%scsroot%\psionic\ssl\illithid\flayertop.ssl)

DEFAULT TRIGGER(!GlobalTimerNotExpired("castspell","LOCALS"))

///////////////////////////////////////////////////////////////////////
/// Consider moving somewhere more interesting
////////////////////////////////////////////////////////////////////////

INCLUDE FILE (%scsroot%\psionic\ssl\illithid\jump.ssl)

/////////////////////////////////////////////////////////////////////////
///	Be invisible
/////////////////////////////////////////////////////////////////////////

IF TRIGGER
	!StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
	TriggerBlock(TruesightSafe)
THEN DO
	Action(PsionicMyself,PSIONIC_SUPERIOR_INVISIBILITY,invisible,12|100|50)
END	

/////////////////////////////////////////////////////////////////////////
///	Heal myself
/////////////////////////////////////////////////////////////////////////

IF TRIGGER
	HPPercentLT(Myself,65)
THEN DO
	Action(PsionicMyself,ULITHARID_CURE_SERIOUS_WOUNDS,cure,12|100|50)
END	

/////////////////////////////////////////////////////////////////////////
///	Psionic Blast
/////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInOrder)
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(Stun|Enemy|Helpless|PsiBlast)
THEN DO
	Combine()
	Action(Psionic,ULITHARID_PSIONIC_BLAST,psionicblast,12|200|50)
END

//////////////////////////////////////////////////////////////////////////
////	Domination
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsPreferringStrong)
	ConditionalTargetBlock(EnemiesInOrder;CheckStatGT(scstarget,7,Level)) // don't dominate weak creatures
	!CheckStatGT(scstarget,0,CLERIC_CHAOTIC_COMMANDS)
	TriggerBlock(Enemy|Helpless|PsiDom)
	CheckStatGT(scstarget,3,SAVEVSSPELL)
THEN DO
	Action(Psionic,MIND_FLAYER_DOMINATION,domination,12|100|50)
END

/////////////////////////////////////////////////////////////////////////
///  If you have a Spell Strike wand, use it here
/////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInOrder)
	TriggerBlock(SpellTurn|Enemy|Helpless)
	OR(3)
		CheckStatGT(scstarget,0,CLERIC_CHAOTIC_COMMANDS)
		CheckStatGT(scstarget,0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		CheckStatGT(scstarget,2,STONESKINS)
THEN DO
	Action(Wand,"wand18"|100|50)
END
//////////////////////////////////////////////////////////////////////////////
////	Maze
//////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsPreferringStrong)
	ConditionalTargetBlock(EnemiesInOrder;CheckStatGT(scstarget,7,Level)) // don't dominate weak creatures
	TriggerBlock(Enemy|Helpless|PsiMaze)
	CheckStatGT(scstarget,1,SAVEVSSPELL)
THEN DO
	Combine()
	Action(Psionic,PSIONIC_MAZE,maze,18|100|50)
END



///////////////////////////////////////////////////////////////////////////////	
////	Detonation and Ballistic attack
///////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCsInOrder;HPPercentLT(scstarget,25))
	TargetBlock(PCsPreferringWeak)
	TriggerBlock(Enemy|Helpless)
THEN DO
	Combine()
	Action(Psionic,PSIONIC_DETONATE,detonate,12|100|50)
	Action(Psionic,PSIONIC_BALLISTIC,ballistic,12|100|50)
END

///////////////////////////////////////////////////////////////////////////////
////	Melee
///////////////////////////////////////////////////////////////////////////////

DEFAULT TRIGGER()

INCLUDE FILE(%scsroot%\psionic\ssl\illithid\mfcombat.ssl)
