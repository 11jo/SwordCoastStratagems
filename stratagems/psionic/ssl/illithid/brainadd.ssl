/////////////////////////////////////////////////////////////////////
/// Set up definitions
////////////////////////////////////////////////////////////////////

BEGIN LOOP(scsspellsubstitute1|| )
BEGIN LOOP(scsspellsubstitute2|| )
	INCLUDE FILE (%scsroot%\psionic\ssl\psionic_defs.ssl)
END LOOP
END LOOP

DEFAULT TRIGGER(!GlobalTimerNotExpired("castspell","LOCALS"))
	
///////////////////////////////////////////////////////////////////////////
///	Psionic Disjunction
///////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInOrder)
	TriggerBlock(Enemy|Helpless)
THEN DO
	Action(PsionicRES,"dw#psirm",disjunction,1000)
END

//////////////////////////////////////////////////////////////////////////
////	Domination
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsPreferringStrong)
	ConditionalTargetBlock(EnemiesInOrder;CheckStatGT(scstarget,7,Level)) // don't dominate weak creatures
	!CheckStatGT(scstarget,0,CLERIC_CHAOTIC_COMMANDS)
	TriggerBlock(Enemy|Helpless|PsiDom)
	CheckStatGT(scstarget,3,SAVEVSSPELL)
THEN DO
	Action(Psionic,MIND_FLAYER_DOMINATION,domination,12|100|50)
END

//////////////////////////////////////////////////////////////////////////////
////	Maze
//////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsPreferringStrong)
	ConditionalTargetBlock(EnemiesInOrder;CheckStatGT(scstarget,7,Level)) // don't dominate weak creatures
	TriggerBlock(Enemy|Helpless)
	CheckStatGT(scstarget,1,SAVEVSSPELL)
THEN DO
	Combine()
	Action(Psionic,PSIONIC_MAZE,maze,18|100|50)
END


///////////////////////////////////////////////////////////////////////////////	
////	Detonation and Ballistic attack
///////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCsInOrder;HPPercentLT(scstarget,25))
	TargetBlock(PCsPreferringWeak)
	TriggerBlock(Enemy|Helpless)
THEN DO
	Combine()
	Action(Psionic,PSIONIC_DETONATE,detonate,12|100|50)
	Action(Psionic,PSIONIC_BALLISTIC,ballistic,12|100|50)
END


