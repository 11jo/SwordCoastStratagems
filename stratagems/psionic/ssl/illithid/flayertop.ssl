//////////////////////////////////////////////////////////
////	When to attack the bad guys (borrowed from Weimer Tactics)
////////////////////////////////////////////////////////////

IF
	!Global("inafight","LOCALS",1)
    OR(7)
      AttackedBy([GOODCUTOFF.0.0.0.0.SUMMONED],DEFAULT)
      AttackedBy([PC],DEFAULT)
      AttackedBy([FAMILIAR],DEFAULT)
      AttackedBy([ALLY],DEFAULT)
      AttackedBy([CONTROLLED],DEFAULT)
      AttackedBy([CHARMED],DEFAULT)
      SpellCastOnMe([GOODCUTOFF.0.0.0.0.SUMMONED],0)
THEN
  RESPONSE #100
    Enemy()
    SetGlobal("inafight","LOCALS",1)
	SetGlobalTimer("stayontarget","LOCALS",12)
	SetGlobalTimer("seedefences","LOCALS",6)
    Continue()
END

IF	
	!Global("inafight","LOCALS",1)
	See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		SetGlobal("inafight","LOCALS",1)
		SetGlobalTimer("stayontarget","LOCALS",12)
		SetGlobalTimer("seedefences","LOCALS",6)
		SetGlobalTimer("redefend","LOCALS",3)
		Continue()
END

///////////////////////////////////////////////////////////////
///	Start invisible
///////////////////////////////////////////////////////////////

IF
	Allegiance(Myself,ENEMY)
	!See(NearestEnemyOf(Myself))
BEGIN LOOP(scsplayer||1;2;3;4;5;6)  // without this loop we ceaselessly re-invisibilise when we can only see charmed people
	!See(Playerscsplayer)
END LOOP
	!StateCheck(Myself,STATE_INVISIBLE)
THEN
	RESPONSE #100
		ApplySpellRES("dw#illin",Myself)
END

////////////////////////////////////////////////////////////
////	This is a rest-and-recover step for when
////	enemies leave you alone...
////////////////////////////////////////////////////////////

IF
	See(NearestEnemyOf(Myself))
	Global("rest","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobalTimer("rtime","LOCALS",2000)
		SetGlobal("rest","LOCALS",1)
		Continue()
END

IF
	Global("rest","LOCALS",1)
	GlobalTimerExpired("rtime","LOCALS")
	!See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		SetGlobal("rest","LOCALS",0)
		ApplySpell(Myself,RESTORE_FULL_HEALTH)
		Rest()
END

///////////////////////////////////////////////////
//// seek out foes if taking damage (borrowed from Blucher)
///////////////////////////////////////////////////


IF
	!Detect([PC])
	OR(2)
		HitBy([GOODCUTOFF],CRUSHING)
		See(NearestEnemyOf(Myself))
	Allegiance(Myself,ENEMY)
	BEGIN LOOP(scsplayer||1;2;3;4;5;6)
		InMyArea(Playerscsplayer)
	END LOOP
THEN
BEGIN LOOP(scsplayer||1;2;3;4;5;6)
	RESPONSE #100
		SetGlobalTimer("teleport","LOCALS",16)
		SetGlobalTimer("castspell","LOCALS",6)
		ForceSpellRES("dw#mftel",Playerscsplayer)		
END LOOP
END

IF
	!Detect([PC])
	OR(2)
		HitBy([GOODCUTOFF],CRUSHING)
		See(NearestEnemyOf(Myself))
	Allegiance(Myself,ENEMY)
	BEGIN LOOP(scsplayer||1;2;3)
		InMyArea(Playerscsplayer)
	END LOOP
THEN
BEGIN LOOP(scsplayer||1;2;3)
	RESPONSE #100
		SetGlobalTimer("teleport","LOCALS",16)
		SetGlobalTimer("castspell","LOCALS",6)
		ForceSpellRES("dw#mftel",Playerscsplayer)		
END LOOP
END

IF
	!Detect([PC])
	OR(2)
		HitBy([GOODCUTOFF],CRUSHING)
		See(NearestEnemyOf(Myself))
	Allegiance(Myself,ENEMY)
		InMyArea(Player1)
THEN
	RESPONSE #100
		SetGlobalTimer("teleport","LOCALS",16)
		SetGlobalTimer("castspell","LOCALS",6)
		ForceSpellRES("dw#mftel",Player1)
END



INCLUDE FILE(%scsroot%\genai\ssl\chase_top.ssl)

///////////////////////////////////////////////////////////
///	CPU saver
///////////////////////////////////////////////////////////

IF
	!Detect(NearestEnemyOf(Myself))
	!Global("inafight","LOCALS",1)
THEN
	RESPONSE #100
		NoAction()
END
