///////////////////////////////////////////////////////////////////////////////
///     Blow up skeletons (close range)
////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
   !Global("DMWWNoSkeletonDetonate","GLOBAL",1)
   TargetBlock(EnemiesInOrderShort)
   Range(scstarget,5)
   TriggerBlock(Enemy)
   OR(2)
        Class(scstarget,SKELETON)
        Class(scstarget,SKELETON_WARRIOR)
THEN DO
       Combine()
       Action(PsionicRES,"dw#detun",detonate,12)
END

///////////////////////////////////////////////////////////////////////
/// Consider moving somewhere more interesting
////////////////////////////////////////////////////////////////////////

INCLUDE FILE (%scsroot%\psionic\ssl\illithid\jump.ssl)

/////////////////////////////////////////////////////////////////////////
///	Be invisible
/////////////////////////////////////////////////////////////////////////

IF TRIGGER
	!StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
	TriggerBlock(TruesightSafe)
THEN DO
	Action(PsionicMyself,PSIONIC_SUPERIOR_INVISIBILITY,invisible,12|100|50)
END




/////////////////////////////////////////////////////////////////////////
///	Psionic Blast
/////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInOrder)
	TargetBlock(EnemiesInOrderShort)
	!GlobalTimerNotExpired("firstblast","LOCALS")
	TriggerBlock(Enemy|Helpless)
THEN DO
	Combine()
	SetGlobalTimer("firstblast","LOCALS",1000)
	Action(Psionic,MIND_FLAYER_PSIONIC_BLAST,psionicblast,12|100|50)
END

IF TRIGGER
	TargetBlock(PCsInOrder)
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(Stun|Enemy|Helpless|PsiBlast)
THEN DO
	Combine()
	Action(Psionic,MIND_FLAYER_PSIONIC_BLAST,psionicblast,12|100|50)
END



//////////////////////////////////////////////////////////////////////////
////	Domination
//////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringStrong;CheckStatGT(scstarget,6,SAVEVSSPELL))
	TargetBlock(PCsPreferringStrong)
	ConditionalTargetBlock(EnemiesInOrder;CheckStatGT(scstarget,7,Level)) // don't dominate weak creatures
	OR(2)
		!CheckStatGT(scstarget,0,CLERIC_CHAOTIC_COMMANDS)
		!GlobalTimerNotExpired("firstdom","LOCALS")
	TriggerBlock(Enemy|Helpless|PsiDom)
	CheckStatGT(scstarget,3,SAVEVSSPELL)
THEN DO
	Combine()
	SetGlobalTimer("firstdom","LOCALS",1000)
	Action(Psionic,MIND_FLAYER_DOMINATION,domination,12|100|50)
END
/////////////////////////////////////////////////////////////////////////
///  If you have a Spell Strike wand, use it here
/////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInOrder)
	TriggerBlock(SpellTurn|Enemy|Helpless)
	OR(3)
		CheckStatGT(scstarget,0,CLERIC_CHAOTIC_COMMANDS)
		CheckStatGT(scstarget,0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		CheckStatGT(scstarget,2,STONESKINS)
THEN DO
	Action(Wand,"wand18"|100|50)
END

//////////////////////////////////////////////////////////////////////////////
////	Maze
//////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringStrong;CheckStatGT(scstarget,5,SAVEVSSPELL))
	TargetBlock(PCsPreferringStrong)
	ConditionalTargetBlock(EnemiesInOrder;CheckStatGT(scstarget,7,Level)) // don't maze weak creatures
	TriggerBlock(Enemy|Helpless|PsiMaze)
	CheckStatGT(scstarget,1,SAVEVSSPELL)
THEN DO
	Combine()
	Action(Psionic,PSIONIC_MAZE,maze,18|100|50)
END

///////////////////////////////////////////////////////////////////////////////
///     Blow up skeletons (ranged)
////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
   !Global("DMWWNoSkeletonDetonate","GLOBAL",1)
   TargetBlock(EnemiesInOrderShort)
   TriggerBlock(Enemy)
   OR(2)
        Class(scstarget,SKELETON)
        Class(scstarget,SKELETON_WARRIOR)
THEN DO
       Combine()
       Action(PsionicRES,"dw#detun",detonate,12)
END

///////////////////////////////////////////////////////////////////////////////	
////	Detonation and Ballistic attack
///////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCsInOrder;HPPercentLT(scstarget,25))
	TargetBlock(PCsPreferringWeak)
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(Enemy|Helpless)
THEN DO
	Combine()
	Action(Psionic,PSIONIC_DETONATE,detonate,12|100|100)
END

IF TRIGGER
	ConditionalTargetBlock(PCsInOrder;HPPercentLT(scstarget,25))
	TargetBlock(PCsPreferringWeak)
	TargetBlock(EnemiesInOrderShort)
	TriggerBlock(Enemy|Helpless|Stoneskin)
THEN DO
	Combine()
	Action(Psionic,PSIONIC_BALLISTIC,ballistic,12)
END

