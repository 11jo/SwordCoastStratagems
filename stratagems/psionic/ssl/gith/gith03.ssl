//////// Definitions //////////////////////////////////////////


BEGIN LOOP(scsspellsubstitute1||!StateCheck(scstarget,STATE_NOT_TARGETABLE))
BEGIN LOOP(scsspellsubstitute2||!StateCheck(scstarget,STATE_INVISIBLE))
	INCLUDE FILE (%scsroot%\psionic\ssl\psionic_defs.ssl)
	INCLUDE FILE (%scsroot%\mage\ssl\mage_definitions.ssl)
END LOOP
END LOOP

INCLUDE FILE (%scsroot%\genai\ssl\genai_definitions.ssl)

/////// Give potions ///////////////////////////////////////////////////

IF
	!Global("potionprep","LOCALS",1)
THEN
	RESPONSE #100
		GiveItemCreate("potn14",Myself,0,0,0)
		SetGlobal("potionprep","LOCALS",1)
		Continue()
	RESPONSE #100
		GiveItemCreate("potn03",Myself,0,0,0)
		SetGlobal("potionprep","LOCALS",1)
		Continue()
	RESPONSE #100
		GiveItemCreate("potn04",Myself,0,0,0)
		SetGlobal("potionprep","LOCALS",1)
		Continue()
END

///////////// Initialise /////////////////////////////////////////////////////

BEGIN LOOP(scscloudmelee||!See([PC]))
INCLUDE FILE(%scsroot%/genai/ssl/initial.ssl)
END LOOP

IF
	Global("prep","LOCALS",0)
	See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		SetGlobal("prep","LOCALS",1)
		SetGlobal("hands","LOCALS",0)
		SetGlobalTimer("redefend","LOCALS",3)
		Continue()
END
	

DEFAULT TRIGGER(!GlobalTimerNotExpired("castspell","LOCALS"))


IF TRIGGER
	!StateCheck(Myself,State_INVISIBLE)
	TriggerBlock(TruesightSafe)
	See(NearestEnemyOf(Myself))
	!StateCheck(Myself,STATE_BLUR)
THEN DO
	Action(PsionicMyselfRES,"dw#psibl",blur,18|100|50)
END

//////////////// Lay on Hands ///////////////////////////////////////////////////////

IF TRIGGER
	!Global("hands","LOCALS",2)
	HPPercentLT(Myself,60)
THEN DO
	Action(ForceSpellMyself,EVIL_LAYONHANDS)
	IncrementGlobal("hands","LOCALS",1)
END


/////////////// Psionics ////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(Enemy|Helpless)
	!GlobalTimerNotExpired("firstmaze","LOCALS")
THEN DO
	Combine()
	SetGlobalTimer("firstmaze","LOCALS",1000)
	Action(Psionic,PSIONIC_MAZE,maze,18|100|50)
END

IF TRIGGER
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(Enemy|Helpless|PsiMaze)
THEN DO
	Combine()
	Action(Psionic,PSIONIC_MAZE,maze,18|100|50)
END

IF TRIGGER
	TargetBlock(PCsInOrder)
	TriggerBlock(Enemy|Helpless|PsiBlast)
	OR(2)
		!GlobalTimerNotExpired("firstblast","LOCALS")
		!CheckStatGT(scstarget,0,CLERIC_CHAOTIC_COMMANDS)
THEN DO
	Combine()
	Action(Psionic,PSIONIC_MIND_BLAST,mindblast,18|100|100)
	SetGlobalTimer("firstblast","LOCALS",1000)
END


////////////// Jump ///////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/psionic/ssl/gith/githjump.ssl)


DEFAULT TRIGGER()

///////////// Potions //////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/genai/ssl/potionuse.ssl)

///////////// Melee //////////////////////////////////////////////////////////////

BEGIN LOOP(MyWeaponStrength||5)
BEGIN LOOP(MyWeaponDamageType||Slashing)
INCLUDE FILE(%scsroot%/genai/ssl/chase.ssl)
INCLUDE FILE(%scsroot%/genai/ssl/dw#wtacor.ssl)
INCLUDE FILE(%scsroot%/genai/ssl/move.ssl)
INCLUDE FILE(%scsroot%/genai/ssl/chase.ssl)
END LOOP
END LOOP

