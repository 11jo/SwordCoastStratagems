//////// Definitions //////////////////////////////////////////


BEGIN LOOP(scsspellsubstitute1||!StateCheck(scstarget,STATE_NOT_TARGETABLE))
BEGIN LOOP(scsspellsubstitute2||!StateCheck(scstarget,STATE_INVISIBLE))
	INCLUDE FILE (%scsroot%\psionic\ssl\psionic_defs.ssl)

END LOOP
END LOOP

INCLUDE FILE (%scsroot%\genai\ssl\genai_definitions.ssl)

///////////// Initialise /////////////////////////////////////////////////////

BEGIN LOOP(scscloudmelee||!See([PC]))
INCLUDE FILE(%scsroot%/genai/ssl/initial.ssl)
END LOOP

IF
	Global("prep","LOCALS",0)
	See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		SetGlobal("prep","LOCALS",1)
		SetGlobal("hands","LOCALS",0)
		SetGlobalTimer("redefend","LOCALS",3)
		Continue()
END
	

DEFAULT TRIGGER(!GlobalTimerNotExpired("castspell","LOCALS"))

IF TRIGGER
	!StateCheck(Myself,State_INVISIBLE)
	TriggerBlock(TruesightSafe)
	See(NearestEnemyOf(Myself))
	!StateCheck(Myself,STATE_BLUR)
THEN DO
	Action(PsionicMyselfRES,"dw#psibl",blur,18|100|50)
END

//////////////// HLAs ///////////////////////////////////////////////////////

IF
	!GlobalTimerNotExpired("angurvurdal","LOCALS")
	See(NearestEnemyOf(Myself))
	HasItem("sw1h60",Myself)
THEN 
	RESPONSE #100
		SetGlobalTimer("castspell","LOCALS",6)
		SetGlobalTimer("angurvurdal","LOCALS",1000)
		UseItem("sw1h60",Myself)
END
		


IF TRIGGER
	See(NearestEnemyOf(Myself))
	Range(NearestEnemyOf(Myself),5)
THEN DO
	Action(SpellMyself,WARRIOR_HARDINESS|100|100)
	Action(SpellMyself,WARRIOR_GREATER_WHIRLWIND|100|100)
	Action(SpellMyself,WARRIOR_CRITICAL_STRIKE|100|100)
END


/////////////// Psionics ////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInOrder)
	HPPercentLT(Myself,60)
	Range(scstarget,10)
THEN DO
	Combine()
	Action(Psionic,PSIONIC_LIFE_DRAIN,lifedrain,12|100|50)
END


IF TRIGGER
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(Enemy|Helpless)
	!GlobalTimerNotExpired("firstmaze","LOCALS")
THEN DO
	Combine()
	SetGlobalTimer("firstmaze","LOCALS",1000)
	Action(Psionic,PSIONIC_MAZE,maze,12|100|50)
END

IF TRIGGER
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(Enemy|Helpless|PsiMaze)
	CheckStatGT(scstarget,3,SAVEVSSPELL)
THEN DO
	Combine()
	Action(Psionic,PSIONIC_MAZE,maze,12|100|50)
END

IF TRIGGER
	TargetBlock(PCsInOrder)
	TriggerBlock(Enemy|Disabled)
	OR(2)
		Class(scstarget,FIGHTER_ALL)
		Class(scstarget,MONK)
	CheckStatGT(scstarget,4,SAVEVSSPELL)
THEN DO
	Combine()
	Action(Psionic,PSIONIC_PROJECT_FORCE,projectforce,12|100|100)
END

///////////////////////////////////////////////////////////////////////////////	
////	Detonation and Ballistic attack
///////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCsInOrder;HPPercentLT(scstarget,25))
	TargetBlock(PCsPreferringWeak)
	TriggerBlock(Enemy|Helpless)
THEN DO
	Combine()
	Action(Psionic,PSIONIC_BALLISTIC,ballistic,12|100|50)
END

////////////// Jump ///////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/psionic/ssl/gith/githjump.ssl)


DEFAULT TRIGGER()

///////////// Potions //////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/genai/ssl/potionuse.ssl)

///////////// Melee //////////////////////////////////////////////////////////////

BEGIN LOOP(MyWeaponStrength||4)
BEGIN LOOP(MyWeaponDamageType||Slashing)
INCLUDE FILE(%scsroot%/genai/ssl/chase.ssl)
INCLUDE FILE(%scsroot%/genai/ssl/dw#wtacor.ssl)
INCLUDE FILE(%scsroot%/genai/ssl/move.ssl)
INCLUDE FILE(%scsroot%/genai/ssl/chase.ssl)
END LOOP
END LOOP

