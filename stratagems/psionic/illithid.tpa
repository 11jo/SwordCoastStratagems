LAF include STR_VAR file=psionic_shared.tph END

DEFINE_ACTION_FUNCTION illithid STR_VAR version="" BEGIN

      ACTION_MATCH ~%version%~ WITH
      1 2 BEGIN
         LAF make_label STR_VAR label=dw#flayer_detect_invisible END
      END
      3 4 BEGIN
      END
      DEFAULT
         PRINT ~Weird version in illithid.tpa~
      END

      ACTION_MATCH ~%version%~ WITH
      2 4 BEGIN
         LAF make_label STR_VAR label=dw#flayer_resistance END
      END
      1 3 BEGIN
      END
      DEFAULT 
         PRINT ~Weird version in illithid.tpa~
      END

      LAF shared_psionic_resources END
      LAF illithid_resources END
      LAF illithid_scripts END
      LAF illithid_cre END
      LAF illithid_compatibility END


      // alhoon
      
      LAF check_label STR_VAR label=dw#mage RET value=value END
      ACTION_IF value BEGIN
          LAF run STR_VAR file=alhoon END
      END


END

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////     Install scripts
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION illithid_scripts BEGIN
         LAF ssl_to_bcs STR_VAR script=~flayer01 flayer02 flayun udflayer brainadd~ location=~ssl/illithid~ END

	 LAF extend STR_VAR file=udmaster bottom=brainadd locabs=~%workspace%\ssl_out~ END

         COPY_EXISTING ~udmaster.bcs~ ~override~
	     DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY ~ReallyForceSpell(LastSeenBy(Myself),PSIONIC_EGOWHIP)~ ~ForceSpell(LastSeenBy(Myself),PSIONIC_EGOWHIP)~
		REPLACE_TEXTUALLY ~ForceSpell(LastSeenBy(Myself),PSIONIC_EGOWHIP)~ ~SetGlobalTimer("castspell","LOCALS",2) ForceSpell(Player1,PSIONIC_EGOWHIP) ForceSpell(Player2,PSIONIC_EGOWHIP) ForceSpell(Player3,PSIONIC_EGOWHIP)  ForceSpell(Player4,PSIONIC_EGOWHIP) ForceSpell(Player5,PSIONIC_EGOWHIP) ForceSpell(Player6,PSIONIC_EGOWHIP)~
	     END
         BUT_ONLY



END

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////     Modify cre files
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION illithid_cre BEGIN

       // mind flayers
        
       LAF edit_creature STR_VAR creature=~damind1 damind2 eleill01 elmind01 mindfl01 ppmind01 
                                           shmind  udill01 udmind01 udmind02 udmind03 udmind04
                                           udmind10~
                                 editstring=~make_illithid=>null~
       END

       // ulitharids
       
       LAF edit_creature STR_VAR creature=~gormind mindul01 ppmind01 senill01 senill02~ editstring=~make_ulitharid=>null~ END

       // vampiric mind flayers

       LAF edit_creature STR_VAR creature=~mindvam mindva01 udvamp~ editstring=~make_vampiric_illithid=>null~ END

       // master brain
       
       MAKE_PATCH
          level=>20
          class=>mage
          illithid_general=>null
       END
       LAF edit_creature STR_VAR creature=udmaster edits=patch_data END


END

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////     Install illithid-specific resources
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////


DEFINE_ACTION_FUNCTION illithid_resources BEGIN

	// make version of Detonation used for blowing up skeletons
	
	LAF install STR_VAR file=dw#detun.spl location=resource END

        // make longer-range Ulitharid attack
        
        MAKE_PATCH
           patch_ability_inline=>~ability_rangeGT=>3~
        END
        LAF clone_item STR_VAR item=~mindflay=>dw#ulith~ edits=patch_data END

        // vampiric illithid attacks still ought to drain intelligence 
        MAKE_PATCH
           add_effect_inline=>~match=>"ability_type=1" opcode=>19 target=>2 parameter1=>"-5" duration=>30~
           add_effect_inline'=>~match=>"ability_type=1" opcode=>142 target=>2 parameter2=>86 duration=>30~
           add_effect_inline''=>~match=>"ability_type=1" opcode=>139 timing=>1 target=>2  parameter1=>32089~
        END
        LAF edit_item STR_VAR item=mindfl02 edits=patch_data END



END

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////     Compatibility fixes
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////


DEFINE_ACTION_FUNCTION illithid_compatibility BEGIN

        // WoP compatibility

	ACTION_IF FILE_EXISTS_IN_GAME ~dw#melis.cre~ THEN BEGIN
	        LAF edit_creature STR_VAR creature=dw#viill editstring=~make_illithid=>null~ END
	END




END

