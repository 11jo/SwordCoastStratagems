DEFINE_ACTION_FUNCTION alhoon BEGIN

   // get prebuff status
   
   LAF check_label STR_VAR label=dw#mage_1 RET value=value END
   ACTION_IF value=1 BEGIN
      OUTER_SPRINT variables ~HasInstantPrep=True~
   END ELSE BEGIN
      OUTER_SPRINT variables ~HasInstantPrep=False~
   END

   // compile scripts

   LAF ssl_to_bcs STR_VAR script=dw#alhoo location=~psionic/ssl/illithid~ variables END
   
   // handle alhoon rage-cry differently
   
   LAF swap_text STR_VAR files=ds2138.bcs swaps=~Global("Display","LOCALS",0)=>False()~ END

   // modify creature files
   
   MAKE_PATCH
      level=>20
      level2=>20
      class=>FIGHTER_MAGE
      delete_opcodes=>297 // no longer uninterruptible
      quick_effect=>~opcode=>188 parameter2=>1~ // aura cleansing (we enforce 1 spell, 1 power/rd by script)
      quick_effect'=>~opcode=>193 parameter2=>1~ // invisible detection by script [lich]
      remove_spells=>all
      strip_script=>~mindal01 bpmindal~
      strip_scs_scripts=>null
      insert_script_low=>dw#alhoo
      swap_items=>~immchs=>lich~
      add_spells=>~MAGIC_MISSILE(5) MIRROR_IMAGE(5) WIZARD_DISPEL_MAGIC FLAME_ARROW SKULL_TRAP
                   STONE_SKIN(3) TELEPORT_FIELD MINOR_GLOBE_OF_INVULNERABILITY PROTECTION_FROM_MAGIC_WEAPONS
                   BREACH(3) SHADOW_DOOR(2) DEATH_SPELL WIZARD_TRUE_SIGHT WIZARD_PROTECTION_FROM_THE_ELEMENTS
                   WIZARD_WARDING_WHIP WIZARD_MORDENKAINENS_SWORD WIZARD_SUMMON_FIEND WIZARD_ABI_DALZIMS_HORRID_WILTING
                   WIZARD_SPELL_STRIKE WIZARD_ABSOLUTE_IMMUNITY~
   END
   LAF check_label STR_VAR label=dw#flayer_resistance RET value=value END
   ACTION_IF value=1 BEGIN
           OUTER_SPRINT $patch_data(resist_slashingGT) 50
           OUTER_SPRINT $patch_data(resist_piercingGT) 50
           OUTER_SPRINT $patch_data(resist_crushingGT) 50
           OUTER_SPRINT $patch_data(resist_missileGT) 50
   END


   LAF edit_creature STR_VAR creature=~mindal01 melsum06~ END




END