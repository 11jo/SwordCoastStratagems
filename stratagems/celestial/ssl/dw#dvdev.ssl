BEGIN LOOP(scsspellsubstitute1|| )
BEGIN LOOP(scsspellsubstitute2|| )
	INCLUDE FILE(%scsroot%/mage/ssl/mage_definitions.ssl)
END LOOP
END LOOP
INCLUDE FILE(%scsroot%/genai/ssl/genai_definitions.ssl)
INCLUDE FILE(%scsroot%/celestial/ssl/celestial_definitions.ssl)
VARIABLE(IsNotMoron=True)

INCLUDE FILE(%scsroot%/genai/ssl/chase_top.ssl)


IF
	Die()
THEN
	RESPONSE #100
		CreateVisualEffectObject("SPUNHOLY",Myself)
		DestroySelf()
END

/// Blade barrier

IF TRIGGER
	!CheckStatGT(Myself,0,CLERIC_BLADE_BARRIER)
	Range(NearestEnemyOf(Myself),10)
THEN DO
	Action(SpellMyself,NPC_BLADE_BARRIER|100|50)
END


/// Remove Fear (don't waste on low-powered scum)

IF TRIGGER
	TargetBlock(HumanoidVillains)
	StateCheck(scstarget,STATE_PANIC)
	CheckStatGT(scstarget,8,Level)
	Allegiance(scstarget,ENEMY)
THEN DO
	Combine()
	Action(SpellArea,CLERIC_REMOVE_FEAR)
END


/// Unholy Word

IF TRIGGER
	TargetBlock(EnemiesInOrderShort)
	Range(scstarget,10)
	TriggerBlock(MR|Enemy|Helpless|SIConjuration)
	Alignment(scstarget,MASK_GOOD)
THEN DO
	Action(SpellMyself,CLERIC_UNHOLY_WORD|200|50)
END

/// Truesight

IF TRIGGER
	TargetBlock(EnemiesInOrder)
	!CheckStatGT(FourthNearest([ENEMY]),0,TRUE_SIGHT)
	!CheckStatGT(ThirdNearest([ENEMY]),0,TRUE_SIGHT)
	!CheckStatGT(SecondNearest([ENEMY]),0,TRUE_SIGHT)
	!CheckStatGT([ENEMY],0,TRUE_SIGHT)
	!CheckStatGT(Myself,0,TRUE_SIGHT)
	OR(4)
		StateCheck(scstarget,STATE_INVISIBLE)
		StateCheck(scstarget,STATE_IMPROVEDINVISIBILITY)
		StateCheck(scstarget,STATE_MIRRORIMAGE)
		Gender(scstarget,ILLUSIONARY)
	!Allegiance(scstarget,ENEMY)
	TriggerBlock(SIDivination)
THEN DO
	Action(SpellMyself,CLERIC_TRUE_SIGHT|100|100)
END

// Doom

BEGIN LOOP(scsplayer||2;4;6;1;3;5)


      IF TRIGGER
         Target(Playerscsplayer)
         TriggerBlock(Enemy|Disabled|SpellTurn|MinorGlobe)
         !GlobalTimerNotExpired("doomedscsplayer","LOCALS")
         !GlobalTimerNotExpired("doomed","LOCALS")
      THEN DO
           Action(Spell,CLERIC_DOOM|100|200)
           SetGlobalTimer("doomedscsplayer","LOCALS",60)
           SetGlobalTimer("doomed","LOCALS",12)
      END


END LOOP

// Silence

IF TRIGGER
	TargetBlock(PCSpellcasters)
	TriggerBlock(MR100|SpellTurn|SIConjuration|Silence) // alas, can't detect the Vocalise spell
	TriggerBlock(Enemy|Helpless)
	!StateCheck(scstarget,STATE_SILENCED)

THEN DO
	Action(Spell,CLERIC_SILENCE_15_FOOT|150|50)
END


// Greater Command

IF TRIGGER
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(Disabled|Sleep|MR|Enemy)
THEN DO
	Action(Spell,CLERIC_GREATER_COMMAND|80|20)
END

// Harm 

IF TRIGGER
	TargetBlock(PCsInOrderShort)
	Range(scstarget,10)
	!CheckStat(scstarget,4,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
	TriggerBlock(SpellTurn|MRTouch|Enemy|SINecromancy|ResistMagicDamage)
	Global("shapeshift","LOCALS",0)
THEN DO
	Action(SpellMyself,CLERIC_HARM|100|50)
END

// Hold Person

IF TRIGGER
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(Disabled|Hold|MinorGlobe|MR|Enemy)
THEN DO	
	Action(Spell,CLERIC_HOLD_PERSON|70|30)
END

// Flame Strike

IF TRIGGER
	TargetBlock(PCsPreferringWeak)
	TriggerBlock(Disabled|MR|ResistFire|Enemy|SIEvocation)
THEN DO
	Action(Spell,CLERIC_FLAME_STRIKE|80|20)
END

// Poison

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringWeak;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(EnemiesInOrderShort)
	Range(scstarget,15)
	TriggerBlock(SpellTurn|MR100|Enemy|SINecromancy)
	TriggerBlock(DirectDamageSafe)
THEN DO
	Action(Spell,CLERIC_POISON|100|50)
	Action(Spell,CLERIC_RIGID_THINKING|100|50) // actually Contagion
END

// Unholy Blight

IF TRIGGER
	ConditionalTargetBlock(PCsPreferringStrong;!CheckStatGT(scstarget,50,RESISTMAGIC))
	TargetBlock(EnemiesInOrderShort)
	!Alignment(Myself,MASK_GOOD)
	Alignment(scstarget,MASK_GOOD)
	TriggerBlock(DirectDamageSafe)
	TriggerBlock(MR100|SINecromancy|ResistMagicDamage|Enemy|MirrorImage)
THEN DO
	Action(Spell,CLERIC_UNHOLY_BLIGHT|200|100)
END

BEGIN LOOP(MyWeaponStrength||Unknown)
BEGIN LOOP(MyWeaponDamageType||Crushing)
INCLUDE FILE(%scsroot%/genai/ssl/dw#wtacor.ssl)
INCLUDE FILE(%scsroot%/genai/ssl/move.ssl)
INCLUDE FILE(%scsroot%/mage/ssl/bg2/generalblocks/chase.ssl)
END LOOP
END LOOP

