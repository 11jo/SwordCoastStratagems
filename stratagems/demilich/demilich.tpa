INCLUDE ~%scsroot%/lib_stratagems/ai_wrap.tph~
DEFINE_ACTION_FUNCTION demilich BEGIN

       // CRE files

       MAKE_PATCH
          level=>35
          class=>MAGE
          ac_natural=>~-6~
          ac_effective=>~-6~
          resist_magic=>100
          quick_effect=>~opcode=>292 parameter2=>1~ // immunity to backstab
          quick_effect'=>~opcode=>189 parameter1=>4~ // increased casting speed
          enforce_mage=>~accept_level no_script spells_are_not_bg1~
       END
       OUTER_SPRINT $patch_data(~script_class~) ~dw#kanga~
       LAF edit_creature STR_VAR creature=hldemi END
       OUTER_SPRINT $patch_data(~script_class~) ~dw#wkdem~
       LAF edit_creature STR_VAR creature=demilich END

       // scripts

       LAF ssl_to_bcs STR_VAR script=~dw#kanga dw#wkdem~ location=~demilich/ssl~ END

       // demiliches lose immunity to spells above L5

       MAKE_PATCH
          type=>effect_global
          match=>parameter1
          checkGT=>6
       END
       LAF edit_item STR_VAR item=demilich editstring=~delete_entries=>patch_data~ END

       // if SR isn't installed, modify demilich imprisonment to grant a saving throw at -5
       
       ACTION_IF !demivrgvs BEGIN
          MAKE_PATCH
             say_name=>2700
             patch_cond_inline=>~type=>effect save_vs_death=>1 savebonus=>"-5"~
          END
          LAF edit_spell STR_VAR spell=~%DEMILICH_TRAP_SOUL%~ END

       END

       // let Kangaxx rampage
       
       ACTION_FOR_EACH areacode IN ~00~ ~01~ ~04~ ~06~ ~07~ ~08~ ~09~ ~11~
                                   ~12~ ~13~ ~14~ ~21~ ~22~ ~23~ ~24~ ~25~
                                   ~27~ ~28~ ~29~ ~30~ ~34~
       BEGIN
          LAF extend_area_script STR_VAR area=EVALUATE_BUFFER ~ar03%areacode%~ top=kangaxx_walks location=resource END
       END

	// WK Demilich seal on the door

     OUTER_INNER_PATCH ~~ BEGIN SET lichstring=RESOLVE_STR_REF (@2702) END
     MAKE_PATCH
        type=>trigger
        match=>trigger_name
        check=>Tran3022
        trigger_name=>DMWWFakeExit
        trigger_info=>~%lichstring%~
        trigger_type=>1
     END

     LAF edit_area STR_VAR area=ar3027 editstring=~clone_entry=>patch_data~ END

     OUTER_INNER_PATCH ~~ BEGIN SET lichstring=RESOLVE_STR_REF (@2702) END
     MAKE_PATCH
        type=>trigger
        match=>trigger_name
        check=>Tran3027
        trigger_name=>DMWWFakeExit
        trigger_info=>~%lichstring%~
        trigger_type=>1
     END
     LAF edit_area STR_VAR area=ar3022 editstring=~clone_entry=>patch_data~ END

     LAF clone_spell STR_VAR spell=EVALUATE_BUFFER ~%CUTSCENE_BREACH%=>dw#sealp~ editstring=~say_name=>2702~ END

     ACTION_FOR_EACH area IN ar3022 ar3027 BEGIN
        LAF extend_area_script STR_VAR area bottom=EVALUATE_BUFFER ~%area%~ location=resource END
     END
END
