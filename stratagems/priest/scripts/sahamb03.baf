//////////////////////////////////////////////////////////////////
/////		Flee if war is over
//////////////////////////////////////////////////////////////////

IF
	OR(2)
		GlobalGT("villynatyjob","GLOBAL",0)
		Dead("sahpr2") // Prince Villynaty
THEN
	RESPONSE #100
		EscapeArea()
END

///////////////////////////////////////////////////////////////////////
/////	Rest and recover
///////////////////////////////////////////////////////////////////////


IF
	See(NearestEnemyOf(Myself))
	Global("rest","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobalTimer("rtime","LOCALS",2000)
		SetGlobal("rest","LOCALS",1)
		Continue()
END

IF
	Global("rest","LOCALS",1)
	GlobalTimerExpired("rtime","LOCALS")
	!See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
// reset all variables here
		ApplySpell(Myself,RESTORE_FULL_HEALTH) //healing spell
		Rest()
END

//////////////////////////////////////////////////////////////////////
/////	Hate PCs if appropriate
//////////////////////////////////////////////////////////////////////

IF
	!GlobalTimerNotExpired("hatePCs","LOCALS")
	OR(9)	
		Allegiance(LastAttackerOf(Myself),GOODCUTOFF)
		HitBy([GOODCUTOFF],CRUSHING)
		AttackedBy([GOODCUTOFF],DEFAULT)
		AttackedBy([PC],DEFAULT)
		AttackedBy([FAMILIAR],DEFAULT)
		AttackedBy([ALLY],DEFAULT)
		AttackedBy([CONTROLLED],DEFAULT)
		AttackedBy([CHARMED],DEFAULT)
		SpellCastOnMe([GOODCUTOFF],0)
THEN
	RESPONSE #100
		SetGlobalTimer("hatePCs","LOCALS",1000)
		SetGlobalTimer("DMWWHostileFish","GLOBAL",6)
		Continue()
END

IF
	!GlobalTimerNotExpired("hatePCs","LOCALS")
	See([PC])
	!See([0.0.0.0.30])
THEN
	RESPONSE #100
		SetGlobalTimer("hatePCs","LOCALS",1000)
END

IF
	!GlobalTimerNotExpired("hatePCs","LOCALS")
	GlobalTimerNotExpired("DMWWHostileFish","GLOBAL")
THEN
	RESPONSE #100
		SetGlobalTimer("hatePCs","LOCALS",1000)
END

IF
	Global("prep","LOCALS",0)
	Detect([PC])
	OR(2)
           See([0.0.0.0.30])
           See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		ReallyForceSpellRES("%WIZARD_STONE_SKIN_PREBUFF%",Myself)
		ReallyForceSpellRES("%CLERIC_CHAOTIC_COMMANDS_PREBUFF%",Myself)
		ReallyForceSpellRES("%CLERIC_DEATH_WARD_PREBUFF%",Myself)
		SetGlobal("prep","LOCALS",1)
		SetGlobalTimer("redefend","LOCALS",0)
END

IF
	!GlobalTimerNotExpired("hatePCs","LOCALS")
	Detect([PC])
	See([0.0.0.0.30])
THEN
	RESPONSE #100
		SpellNoDec(LastSeenBy(Myself),CLERIC_FLAME_STRIKE)
	RESPONSE #100
		SpellNoDec(LastSeenBy(Myself),CLERIC_COMMAND)
	RESPONSE #100
		SpellNoDec(LastSeenBy(Myself),CLERIC_RIGID_THINKING)
	RESPONSE #50
		DisplayStringHead(Myself,4407) // From soil to sea, grant me the bones of the slain to slay our enemies in turn!
		Wait(1)
		CreateCreatureObjectEffect("SAHSKEL","SPMONSUM",LastSeenBy(Myself))
	RESPONSE #50
		DisplayStringHead(Myself,4407) // From soil to sea, grant me the bones of the slain to slay our enemies in turn!
		Wait(1)
		CreateCreatureObjectEffect("SAHLACE","SPMONSUM",LastSeenBy(Myself))
	RESPONSE #50
		DisplayStringHead(Myself,4407) // From soil to sea, grant me the bones of the slain to slay our enemies in turn!
		Wait(1)
		CreateCreatureObjectEffect("SAHZOMB","SPMONSUM",LastSeenBy(Myself))
END

IF 
   GlobalTimerNotExpired("hatePCs","LOCALS")
   See([PC])
   !GlobalTimerNotExpired("summon","LOCALS")
THEN
   RESPONSE #150
      SetGlobalTimer("summon","LOCALS",6)
   RESPONSE #50
		SetGlobalTimer("summon","LOCALS",12)
                DisplayStringHead(Myself,4407) // From soil to sea, grant me the bones of the slain to slay our enemies in turn!
		Wait(1)
		CreateCreatureObjectEffect("SAHSKEL","SPMONSUM",LastSeenBy(Myself))
   RESPONSE #50
		SetGlobalTimer("summon","LOCALS",12)
		DisplayStringHead(Myself,4407) // From soil to sea, grant me the bones of the slain to slay our enemies in turn!
		Wait(1)
		CreateCreatureObjectEffect("SAHLACE","SPMONSUM",LastSeenBy(Myself))
   RESPONSE #50
		SetGlobalTimer("summon","LOCALS",12)
		DisplayStringHead(Myself,4407) // From soil to sea, grant me the bones of the slain to slay our enemies in turn!
		Wait(1)
		CreateCreatureObjectEffect("SAHZOMB","SPMONSUM",LastSeenBy(Myself))
END





IF
	!GlobalTimerNotExpired("hatePCs","LOCALS")
	!Detect([PC])
	GlobalGT("prep","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("prep","LOCALS",0)
END

IF
	!GlobalTimerNotExpired("hatePCs","LOCALS")
	!Detect([PC])
THEN
	RESPONSE #100
		NoAction()
END

