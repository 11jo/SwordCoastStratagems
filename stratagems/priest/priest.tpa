LAF include STR_VAR file=priest_shared.tph END

DEFINE_ACTION_FUNCTION priest
        STR_VAR version="1"
BEGIN
      OUTER_SET prebuff=version
      LAF make_label STR_VAR label= ~dw#priest_%version%~ END

      OUTER_SPRINT type priest
      // data read-in
      LAUNCH_ACTION_MACRO read_in_spells_per_level
      LAUNCH_ACTION_MACRO read_in_spell_choices
      LAUNCH_ACTION_MACRO read_in_individual_overrides
      LAUNCH_ACTION_MACRO read_in_priest_scripts

      // instant and prebuff spells

      LAUNCH_ACTION_MACRO read_in_instant_prebuff_spells

      // scripts

      LAF build_individual_priest_scripts INT_VAR prebuff END

      // resources

      LAF make_priest_resources END

      // core
      
      LAF priest_edits_before END
      LAF priest_edits_main INT_VAR prebuff END
      LAF priest_edits_after END
      
      // cleric-mages
      

      LAF check_label STR_VAR label=dw#mage RET value END
      ACTION_IF value BEGIN
         OUTER_SPRINT component_loc ~caster_shared/clericmage~
         LAF run STR_VAR file=clericmage END
      END

END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////               Primary edit
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////



DEFINE_ACTION_FUNCTION priest_edits_main
       INT_VAR prebuff=1
BEGIN
    OUTER_SPRINT arguments ~prebuff=%prebuff%~
    ACTION_IF FILE_EXISTS ~override/dw#standardise_nobg2.mrk~ BEGIN
       OUTER_SPRINT arguments ~%arguments% spells_are_bg1~
    END ELSE BEGIN
       OUTER_SPRINT arguments ~%arguments% spells_are_not_bg1~
    END
    // load in the innocent priests

    LAUNCH_ACTION_FUNCTION innocent_priests RET list=list END

    // get a list of priests (this is hardcoded, on speed grounds)

    COPY_EXISTING_REGEXP GLOB ~.*\.cre~ ~override~
        READ_BYTE 0x273 class
        PATCH_MATCH class WITH
        3 // cleric
        15 // cleric_thief
        11 // druid
        8 // fighter_cleric
        16 // fighter_druid
        BEGIN
             SPRINT filename ~%SOURCE_RES%~
             LPF CRE_is_PC RET is_pc=value END
             LPF CRE_is_dead RET is_dead=value END
             LPF skipped_priest RET is_skipped=value END
             PATCH_IF (!is_dead && !is_pc && !is_skipped) BEGIN
                PUSH list ~%filename%~
             END

        END
        DEFAULT END
    BUT_ONLY

    LAF edit_creature STR_VAR creature= ~%list%~ editstring= ~priest_edits_main_patch=>"%arguments%"~ END
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION priest_edits_main_patch
      STR_VAR filename=""
              arguments=""
BEGIN
      LPF CRE_find_priest_scripts STR_VAR filename RET value=value END
      PATCH_IF value=1 BEGIN
          SPRINT arguments ~%arguments% game_is_bg1~
      END
      PATCH_IF value>0 BEGIN
         PATCH_PRINT ~Determining spells and script for %filename%~
         PATCH_SILENT
         LPF enforce_priest STR_VAR filename arguments END
      END

END

OUTER_SPRINT $SFO_do_not_parse_arguments("priest_edits_main_patch") ""

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION skipped_priest
      STR_VAR filename=""
      RET value
BEGIN
      SET value=0
      PATCH_FOR_EACH skip IN xzar BEGIN
         PATCH_IF ~%filename%~ STRING_EQUAL_CASE ~%skip%~ BEGIN
            SET value=1
         END
      END
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////               Handle INNOCENTs
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION innocent_priests RET list BEGIN

    OUTER_SPRINT list ~~
    LAF install STR_VAR file=dw#innpr.baf location=resource END
    ACTION_IF is_bg1 BEGIN
       ACTION_FOR_EACH innocent IN AGNASI BENTAN BRATHL CADDER CHANTH
                       GALILE GAXIR KELDDA LASKAL MADARC
                       NALIN ORDULI PALLON POGHMA4 PRIHEL  SHAELL TREMAI UNSHEY
   BEGIN
          OUTER_PUSH list ~%tutu_var%%innocent%~
       END
    END
    ACTION_IF is_bg2 BEGIN
       OUTER_PUSH list ~AMCLER02 SARTEM01 DOGHMA ECOLT~
    END
    ACTION_IF !enhanced_edition BEGIN
        LAF edit_creature STR_VAR creature= ~%list%~ editstring=~insert_script_high=>dw#innpr~ END
    END
    // special handling for druids
    ACTION_IF is_bg1 BEGIN
        ACTION_IF enhanced_edition BEGIN
              LAF edit_creature STR_VAR creature=laskal editstring=~class=>druid~ END
        END ELSE BEGIN
              LAF edit_creature STR_VAR creature=laskal editstring=~class=>druid script_override=>dw#inndr~ tv=1 END
        END
    END

END


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////               Pre-main edits (put things here if possible)
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION priest_edits_before BEGIN
   ACTION_IF is_bg2 BEGIN

         // class corrections
         
         LAF edit_creature STR_VAR creature=drshnl01 editstring=~class=>DRUID~ END
         LAF edit_creature STR_VAR creature=~sahpri02 obssah02 sahamb03 sahamb06~ editstring=~class=>CLERIC~ END

         // missing scripts
         
         LAF edit_creature STR_VAR creature=cedruid2 editstring=~insert_script_low=>druid8b~ END

         // play-dead spell for Nilthiri
         MAKE_PATCH
            add_basic_ability=>null
            add_effect_inline=>~opcode=>28 target=>2 duration=>24 parameter2=>1 parameter1=>100~
            clone_entry_inline=>~type=>effect match=>"opcode=28" opcode=>"entry_index from [29 31 85 86 87 88 89]" number_to_add=>7~
            add_effect_inline'=>~opcode=>173 target=>2 duration=>24 parameter1=>100~
            add_effect_inline'0=>~opcode=>101 target=>2 duration=>24 parameter2=>25~
            add_effect_inline'1=>~opcode=>267 target=>2 duration=>24 parameter1=>14017~
            add_effect_inline'2=>~opcode=>267 target=>2 duration=>24 parameter1=>14662~
            add_effect_inline'3=>~opcode=>39 target=>2 duration=>24~
            add_effect_inline'4=>~opcode=>101 target=>2 duration=>24 parameter2=>2~
            add_effect_inline'5=>~opcode=>143 timing=>4 parameter1=>7 duration=>24 target=>2 resource=>monhp1~
            add_effect_inline'6=>~opcode=>112 target=>2 resource=>monhp1~
            add_effect_inline'7=>~opcode=>17 target=>2 timing=>4 duration=>24 parameter1=>30 parameter2=>1~
            add_effect_inline'8=>~opcode=>267 target=>2 duration=>24 parameter1=>20438~
         END
         LAF make_spell STR_VAR spell=dw#nilde edits=patch_data END

         // missing weapons/armor
         
         LAF edit_creature STR_VAR creature=~druid10a druid10b druid12a druid12b
                                             druid6a druid8a druid8b gdruid trevil02
                                             tdr10a tdr10b tdr8a tdr8b cedruid1~
                                   editstring=~add_items=>staf01(WEAPON1)~
         END

         LAF edit_creature STR_VAR creature=~cedruid2 cechal02 cechalle druid10a druid10b druid12a druid12b druid6a druid8a druid8b~
                                   editstring=~add_items=>leat01~
         END


         // shapeshift
         
         ACTION_IF FILE_EXISTS ~override/dw#shapeshift.mrk~ BEGIN

                LAF edit_creature STR_VAR creature=~cechal02 cechalle cedruid1 cedruid2 cefgt02 cefgt04
                                                    druid10a druid10b druid12a druid12b druid8a druid8b
                                                    gdruid pardru tdr8a tdr8b trevil02 trevil03~
                                           editstring=~add_items=>dw#shnbr(WEAPON2)~
                END
         
                LAF edit_creature STR_VAR creature=tdr10a editstring=~add_items=>"dw#shnsa(WEAPON2) dw#shnsp(WEAPON3)"~ END
                LAF edit_creature STR_VAR creature=sppain editstring=~add_items=>"dw#shnsp(WEAPON2) dw#shnsp(WEAPON3)"~ END // Pai'na always takes spider form
                LAF edit_creature STR_VAR creature=tdr10b editstring=~add_items=>"dw#shnw2(WEAPON2)"~ END
                LAF edit_creature STR_VAR creature=hgnya01 editstring=~add_items=>"dw#shnea(WEAPON2)"~ END

         END

         // blind priests in beholder caves
         
         LAF install STR_VAR files=blind.baf location=resource END

     // Kuo-toa clerics need a Continue() in their truesight

     ACTION_IF !FILE_EXISTS_IN_GAME ~dw#mage.mrk~ BEGIN
        LAF swap_text STR_VAR files=~kuotoa.bcs~ swaps=~ReallyForceSpell(Myself,KOA_TRUE_SIGHT_NO_VIS)=>ReallyForceSpell(Myself,KOA_TRUE_SIGHT_NO_VIS)Continue()~ END
     END

     // Sendai statue needs to be quiet till activated
     
     	<<<<<<<< .../stratagems-inline/dw#send2.baf
	IF
		HasItem("stonring",Myself)
	THEN
		RESPONSE #100
			NoAction()
	END

	>>>>>>>>

        LAF install STR_VAR file=dw#send2.baf inline=yes END
        LAF edit_creature STR_VAR creature=sendai2 editstring=~insert_script_high=>dw#send2~ END


        // Berenn is too big to get certain protective effects (he looks stupid)
        
        LAF edit_creature STR_VAR creature=hgber01 editstring=~add_items=>dw#prbig~ END

	// Dawnmaster Kreel is supposed to wander around

        <<<<<<<< .../stratagems-inline/dw#kreel.baf
        IF
	!Global("inafight","GLOBAL",1)
	!See(NearestEnemyOf(Myself))
	HPPercent(Myself,100)
THEN
	RESPONSE #100
		RandomWalk()
END
        >>>>>>>>

        LAF install STR_VAR file=dw#kreel.baf inline=yes END
        LAF edit_creature STR_VAR creature=dawnmas editstring=~insert_script=>"dw#kreel below sht0902"~ END

   END

   ACTION_IF is_bg1 BEGIN

   // missing scripts
   
   LAF edit_creature STR_VAR creature=x#black allow_missing=1 editstring=~insert_script_low=>priest3~ END
   LAF edit_creature STR_VAR creature=beland tv=1 editstring=~script_race=>priest1~ END


    // shapeshift
    LAF check_label STR_VAR label=dw#shapeshift RET value END
    ACTION_IF value BEGIN
       LAF edit_creature STR_VAR creature=~seniya takiyah osmadi voltin corsone~ tv=1 editstring=~add_items=>dw#shnbr(WEAPON2)~ END
       LAF edit_creature STR_VAR creature=amaran tv=1 editstring=~add_items=>"dw#shnsa(WEAPON2) dw#shnsp(WEAPON3)"~ END
       LAF edit_creature STR_VAR creature=x#andart allow_missing=1 editstring=~add_items=>"dw#shnsa(WEAPON2) dw#shnsp(WEAPON3)"~ END
       LAF edit_creature STR_VAR creature=izefia tv=1 editstring=~remove_items=>%tutu_var%leat04 add_items=>dw#shnw2(WEAPON2)~ END
       // in BGEE, Osmadi's Cursed Backbiter spear gets in the way of his shapeshift
       ACTION_IF enhanced_edition BEGIN
          LAF edit_creature STR_VAR creature=osmadi editstring="remove_items=>sper03 add_items=>staf01(WEAPON1)" END
       END
    END

    // prevent Corsone from attacking his own summons

    LAF install STR_VAR file=dw#corso.baf location=resource END
    LAF edit_creature STR_VAR creature=corsone tv=1 editstring=~swap_script=>"%tutu_scripts%eeenemy=>dw#corso"~ END

    // bishops arguably shouldn't buff

    LAF edit_creature STR_VAR creature= ~%tutu_scriptbg%bishop~ editstring=~add_items=>dw#nopre~ END

   END


END
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////               Post-main edits (if possible put things in pre-main instead)
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION priest_edits_after BEGIN
   ACTION_IF is_bg2 BEGIN




   END

   ACTION_IF is_bg1 BEGIN

         LAF edit_creature STR_VAR creature=laskal tv=1 editstring="class=>innocent" END
   
   
   END


END



//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////            Build scripts for individual priests               //////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION build_individual_priest_scripts
    INT_VAR prebuff=1
BEGIN

   ACTION_FOR_EACH game IN bg1 bg2 BEGIN
      ACTION_IF $is(~%game%~)=1 BEGIN
         LAF get_priest_prebuff_variables STR_VAR game prebuff RET variables=variables END
         LAF check_label STR_VAR label=dw#shapeshift RET value=value END
         ACTION_IF value BEGIN
            OUTER_SPRINT variables ~%variables%&ImprovedShapeshift=True~
         END
         ACTION_BASH_FOR ~%scsroot%/priest/ssl/%game%/special~ ~.*\.ssl~ BEGIN
            LAF ssl_to_bcs STR_VAR variables script= ~%BASH_FOR_RES%~ location= ~ssl/%game%/special~ END
         END
      END
   END

   ACTION_IF !is_bg2 BEGIN
       LAF get_priest_prebuff_variables STR_VAR game=bg2 prebuff RET variables END
       LAF ssl_to_bcs STR_VAR variables script=dw#nymph location=~priest/ssl/bg2/special~ END
   END
END


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////
///      Find priest scripts. Return 1 if found with a BG1 script, 2 if with a BG2 script. If there's a replacement in priest/scripts,
///      compile it; otherwise, blank the slot
///////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION CRE_find_priest_scripts
   STR_VAR filename=""
   RET value
BEGIN
   LAUNCH_PATCH_MACRO read_in_priest_scripts
   SET value=0
   FOR (i=0x248;i<0x270;i+=8) BEGIN
      READ_ASCII i script
      TO_LOWER script
      INNER_PATCH_SAVE script ~%script%~ BEGIN
         REPLACE_TEXTUALLY "_" ""
      END
      SET here=0
      PATCH_FOR_EACH game IN 1 2 BEGIN
         PATCH_IF ($is( ~bg%game%~)=1 && VARIABLE_IS_SET $priest_script( ~bg%game%~ ~%script%~)) BEGIN
            SET here=1
            SET value=game
         END
      END
      PATCH_IF here=1 BEGIN
         SET replace=0
         INNER_ACTION BEGIN
              ACTION_IF FILE_EXISTS ~%scsroot%/%component_loc%/scripts/%script%.baf~ BEGIN
                 ACTION_IF GAME_IS ~tutu tutu_totsc~ BEGIN
                    COPY ~%scsroot%/%component_loc%/scripts/%script%.baf~ ~%workspace%/_%script%.baf~
                    COMPILE EVALUATE_BUFFER ~%workspace%/_%script%.baf~
                    SILENT
                 END ELSE BEGIN
                    LAF install STR_VAR file= ~%script%.baf~ location=scripts END
                    SILENT
                 END
                 OUTER_SET replace=1
              END
         END
         PATCH_IF replace=0 BEGIN
            WRITE_ASCII i ~~ (8)
         END
      END
   END
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////            Make needed resources                 //////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION make_priest_resources BEGIN

    // required for BG1 and BG2

        LAF clone_creature STR_VAR creature=~nymphsu=>dw#nymsu~ editstring=~script_override=>dw#nymph~ END

	COPY_EXISTING ~%CLERIC_CALL_WOODLAND_BEINGS%.spl~ ~override/dw#nymph.spl~
		REPLACE_TEXTUALLY CASE_INSENSITIVE ~nymphsu~ ~dw#nymph~ (8)
		REPLACE_TEXTUALLY CASE_INSENSITIVE ~dvnymph~ ~dw#nymph~ (8)

	COPY_EXISTING ~nymphsu.eff~ ~override/dw#nymph.eff~
		REPLACE_TEXTUALLY CASE_INSENSITIVE ~nymphsu~ ~dw#nymsu~ (8)

       MAKE_PATCH // check if Holy Smite has been cast in the last round (if not, don't bother removing blindness)
             add_effect_inline=>"opcode=>265 target=>1 timing=>1 resource=>DMWWHLSM parameter1=>1"
             add_effect_inline'=>"opcode=>265 target=>1 timing=>4 duration=>6 resource=>DMWWHLSM parameter1=>0"
       END
       LAF edit_spell STR_VAR spell=CLERIC_HOLY_SMITE edits=patch_data END


    // required for BG2 only
    
    ACTION_IF is_bg2=1 BEGIN

    // if SR is installed, make NPC versions of Symbol spells

    ACTION_IF ~demivrgvs~ BEGIN
       LAF clone_spell STR_VAR spell= ~%WIZARD_SYMBOL_FEAR%=>%WIZARD_NPC_SYMBOL_FEAR%
                                                      %WIZARD_SYMBOL_DEATH%=>%WIZARD_NPC_SYMBOL_DEATH%
                                                      %WIZARD_SYMBOL_STUN%=>%WIZARD_NPC_SYMBOL_STUN%~
       END
    END

    // if Refinements is installed, copy its versions of HLAs back over the originals

    ACTION_IF refinements BEGIN
      ACTION_IF demivrgvs BEGIN
       LAF clone_spell STR_VAR spell=~%CLERIC_ELEMENTAL_SWARM%=>tg#elsu %CLERIC_GREATER_ELEMENTAL_SWARM%=>tg#prca~ END
      END ELSE BEGIN
       LAF clone_spell STR_VAR spell=~tg#elsu=>%CLERIC_ELEMENTAL_SWARM% tg#prca=>%CLERIC_GREATER_ELEMENTAL_SWARM%~ END
      END
    END

    LAF edit_spell STR_VAR spell=CLERIC_GREATER_COMMAND editstring=~patch_ability_inline=>"projectile=>159"~ END

     // make an NPC version of Summon Elemental Prince
        
        COPY_EXISTING ~%CLERIC_GREATER_ELEMENTAL_SWARM%.spl~ ~override/dw#elemp.spl~
           REPLACE_TEXTUALLY CASE_INSENSITIVE ~sumzaam~ ~dw#szaam~ (8)
           REPLACE_TEXTUALLY CASE_INSENSITIVE ~sumchan~ ~dw#schan~ (8)
           REPLACE_TEXTUALLY CASE_INSENSITIVE ~sumsunn~ ~dw#ssunn~ (8)
           REPLACE_TEXTUALLY CASE_INSENSITIVE ~tg#benh~ ~dw#benh~ (8)
        BUT_ONLY

        COPY_EXISTING ~sumzaam.eff~ ~override/dw#szaam.eff~
           REPLACE_TEXTUALLY CASE_INSENSITIVE ~elemzaam~ ~dw#ezaam~
        BUT_ONLY

        COPY_EXISTING ~sumchan.eff~ ~override/dw#schan.eff~
           REPLACE_TEXTUALLY CASE_INSENSITIVE ~elemchan~ ~dw#echan~
        BUT_ONLY

        COPY_EXISTING ~sumsunn.eff~ ~override/dw#ssunn.eff~
           REPLACE_TEXTUALLY CASE_INSENSITIVE ~elemsunn~ ~dw#esunn~
        BUT_ONLY

        ACTION_IF FILE_EXISTS_IN_GAME ~tg#benh.eff~ BEGIN
          COPY_EXISTING ~tg#benh.eff~ ~override/dw#benh.eff~
           REPLACE_TEXTUALLY CASE_INSENSITIVE ~tg#benh~ ~dw#benh~
          BUT_ONLY
        END

       LAF clone_creature STR_VAR creature=~elemsunn=>dw#esunn~ editstring=~say_both_names=>4300 dv=>dw#esunn~ END
       LAF clone_creature STR_VAR creature=~elemzaam=>dw#ezaam~ editstring=~say_both_names=>4301 dv=>dw#ezaam~ END
       LAF clone_creature STR_VAR creature=~elemchan=>dw#echan~ editstring=~say_both_names=>4302 dv=>dw#echan~ END
       LAF clone_creature STR_VAR creature=~tg#benh=>dw#benh~ editstring=~say_both_names=>4306 dv=>dw#benh~ allow_missing=1 END

         ACTION_IF ~demivrgvs~=1   BEGIN
           ACTION_IF FILE_EXISTS_IN_GAME ~dw#mehge.bcs~ BEGIN
              OUTER_SPRINT ~elemental_prince_script~ ~dw#mehge~
           END ELSE BEGIN
              OUTER_SPRINT ~elemental_prince_script~ ~wtasight~
           END
           ACTION_FOR_EACH prince IN dw#echan dw#ezaam dw#esunn BEGIN
              LAF edit_creature STR_VAR creature= ~%prince%~ editstring= ~script_override=>%prince% script_default=>%elemental_prince_script%~ END
              LAF install STR_VAR file= ~%prince%.baf~ location=resource END
           END
       END

        LAF clone_item STR_VAR item=~misc16=>dw#prbig~ editstring=~droppable=>0~ END // too large to use Shield of the Archons (looks stupid)
        LAF install STR_VAR files=~dw#bear.spl dw#ctrlu.spl dw#destu.spl~ location=resource END

        ACTION_DEFINE_ASSOCIATIVE_ARRAY priest_spell_replace_map BEGIN
           dw#harmn=>"%CLERIC_HARM%"
           dw#devae=>"%CLERIC_SUMMON_FALLEN_DEVA%"
           dw#devag=>"%CLERIC_SUMMON_DEVA%"
           dw#gatep=>"%CLERIC_CONJURE_EARTH_ELEMENTAL%"
        END
        
        ACTION_PHP_EACH priest_spell_replace_map AS new=>old BEGIN
           ACTION_IF !FILE_EXISTS_IN_GAME ~%new%.spl~ BEGIN
              LAF clone_spell STR_VAR spell= ~%old%=>%new%~ END
           END
        END

      END // end of BG2-only block

END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////
///      Read in priest scripts (i.e. those we aim to replace)
///////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_MACRO read_in_priest_scripts BEGIN
   OUTER_INNER_PATCH ~~ BEGIN
      LAUNCH_PATCH_MACRO read_in_priest_scripts
   END
END

DEFINE_PATCH_MACRO read_in_priest_scripts BEGIN
    PATCH_IF !(VARIABLE_IS_SET ~priest_scripts_read~) BEGIN
       SET priest_scripts_read=1
       PATCH_FOR_EACH script IN
            priest1 priest2 priest3 priest4 priest5 priest6
            seniyad druid3 x#shdr6 x#shdr8 x#andart x#beador x#mareth x#belgin
       BEGIN
          TO_LOWER ~script~
          SPRINT $priest_script_bg1(~%script%~) ~~
       END
       PATCH_FOR_EACH script IN
AMERAL DEMPRI      GENPRST1 PAINA     PRIES10A PRIES10B  PRIES10C PRIES12A
PRIES12C PRIES14A  PRIES14T PRIES18A  PRIES18B PRIES18C  PRIES18D PRIES6A
PRIES6B  PRIES6C   PRIES8A  PRIES8B   PRIES8C  PRIEST12  PRIET12B SAHPRI12  DAQILUE  HGNYA01
GPHEALER BERENN    BARL     CCLER20B  DRSHNL01 DRSHNL11  DRUID10A  DRUID10B DRUID12A
DRUID12B DRUID14G  DRUID6A  DRUID8A   DRUID8B  KBSHAM01  GORPRI1  HGSHAM01
SENDAI2  GORCAMB3  HGBER01  CEFALD01  SAHAMB03 SAHAMB06
BEGIN
          TO_LOWER ~script~
          SPRINT $priest_script_bg2(~%script%~) ~~
       END
    END
END




