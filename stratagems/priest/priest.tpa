LAF include STR_VAR file=priest_shared.tph END

DEFINE_ACTION_FUNCTION priest
        STR_VAR version="1"
BEGIN
      OUTER_SET prebuff=version
      LAF make_label STR_VAR label=EVALUATE_BUFFER ~dw#priest_%version%~ END

      OUTER_SPRINT type priest
      // data read-in
      LAUNCH_ACTION_MACRO read_in_spells_per_level
      LAUNCH_ACTION_MACRO read_in_spell_choices
      LAUNCH_ACTION_MACRO read_in_individual_overrides
      LAUNCH_ACTION_MACRO read_in_priest_scripts

      // instant and prebuff spells

      LAUNCH_ACTION_MACRO read_in_instant_prebuff_spells

      // scripts

      LAF build_individual_priest_scripts INT_VAR prebuff END

      // resources

      LAF make_priest_resources END

      // core
      
      LAF priest_edits_before END
      LAF priest_edits_main INT_VAR prebuff END
      LAF priest_edits_after END
      
      // cleric-mages
      
      ACTION_IF FILE_EXISTS_IN_GAME ~dw#mage.mrk~ BEGIN
         OUTER_SPRINT component_loc ~caster_shared/clericmage~
         LAF run STR_VAR file=clericmage END
      END

END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////               Primary edit
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////



DEFINE_ACTION_FUNCTION priest_edits_main
       INT_VAR prebuff=1
BEGIN
    OUTER_SPRINT arguments ~prebuff=%prebuff%~
    ACTION_IF FILE_EXISTS ~override/dw#standardise_nobg2.mrk~ BEGIN
       OUTER_SPRINT arguments ~%arguments% spells_are_bg1~
    END ELSE BEGIN
       OUTER_SPRINT arguments ~%arguments% spells_are_not_bg1~
    END
    // load in the innocent priests

    LAUNCH_ACTION_FUNCTION innocent_priests RET list=list END

    // get a list of priests (this is hardcoded, on speed grounds)

    COPY_EXISTING_REGEXP GLOB ~.*\.cre~ ~override~
        READ_BYTE 0x273 class
        PATCH_MATCH class WITH
        3 // cleric
        15 // cleric_thief
        11 // druid
        8 // fighter_cleric
        16 // fighter_druid
        BEGIN
             SPRINT filename ~%SOURCE_RES%~
             LPF CRE_is_PC RET is_pc=value END
             LPF CRE_is_dead RET is_dead=value END
             LPF skipped_priest RET is_skipped=value END
             PATCH_IF (!is_dead && !is_pc && !is_skipped) BEGIN
                PUSH list ~%filename%~
             END

        END
        DEFAULT END
    BUT_ONLY

    LAF edit_creature STR_VAR creature=EVALUATE_BUFFER ~%list%~ editstring=EVALUATE_BUFFER ~priest_edits_main_patch=>"%arguments%"~ END
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION priest_edits_main_patch
      STR_VAR filename=""
              arguments=""
BEGIN
      LPF CRE_find_priest_scripts STR_VAR filename RET value=value END
      PATCH_IF value=1 BEGIN
          SPRINT arguments ~%arguments% game_is_bg1~
      END
      PATCH_IF value>0 BEGIN
         PATCH_PRINT ~Determining spells and script for %filename%~
         PATCH_SILENT
         LPF enforce_priest STR_VAR filename arguments END
      END

END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION skipped_priest
      STR_VAR filename=""
      RET value
BEGIN
      SET value=0
      PATCH_FOR_EACH skip IN xzar BEGIN
         PATCH_IF ~%filename%~ STRING_EQUAL_CASE ~%skip%~ BEGIN
            SET value=1
         END
      END
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////               Handle INNOCENTs
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION innocent_priests RET list BEGIN

    OUTER_SPRINT list ~~
    LAF install STR_VAR file=dw#innpr.baf location=resource END
    ACTION_IF is_bg1=1 BEGIN
       ACTION_FOR_EACH innocent IN AGNASI BENTAN BRATHL CADDER CHANTH
                       GALILE GAXIR KELDDA LASKAL MADARC
                       NALIN ORDULI PALLON POGHMA4 PRIHEL  SHAELL TREMAI UNSHEY
   BEGIN
          OUTER_PUSH list ~%tutu_var%%innocent%~
       END
    END
    ACTION_IF is_bg2=1 BEGIN
       OUTER_PUSH list ~AMCLER02 SARTEM01 DOGHMA ECOLT~
    END
    LAF edit_creature STR_VAR creature=EVALUATE_BUFFER ~%list%~ editstring=~insert_script_high=>dw#innpr~ END

END


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////               Pre-main edits (put things here if possible)
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION priest_edits_before BEGIN
   ACTION_IF is_bg2=1 BEGIN

         // class corrections
         
         LAF edit_creature STR_VAR creature=drshnl01 editstring=~class=>DRUID~ END
         LAF edit_creature STR_VAR creature=~sahpri02 obssah02 sahamb03 sahamb06~ editstring=~class=>CLERIC~ END

         // missing scripts
         
         LAF edit_creature STR_VAR creature=cedruid2 editstring=~insert_script_low=>druid8b~ END

         // play-dead spell for Nilthiri
         MAKE_PATCH
            add_basic_ability=>null
            add_effect_inline=>~opcode=>28 target=>2 duration=>24 parameter2=>1 parameter1=>100~
            clone_entry_inline=>~type=>effect match=>opcode check=>28 opcode=>29~
            clone_entry_inline'=>~type=>effect match=>opcode check=>28 opcode=>31~
            clone_entry_inline''=>~type=>effect match=>opcode check=>28 opcode=>85~
            clone_entry_inline'''=>~type=>effect match=>opcode check=>28 opcode=>86~
            clone_entry_inline''''=>~type=>effect match=>opcode check=>28 opcode=>87~
            clone_entry_inline'''''=>~type=>effect match=>opcode check=>28 opcode=>88~
            clone_entry_inline''''''=>~type=>effect match=>opcode check=>28 opcode=>89~
            add_effect_inline'=>~opcode=>173 target=>2 duration=>24 parameter1=>100~
            add_effect_inline''=>~opcode=>101 target=>2 duration=>24 parameter2=>25~
            add_effect_inline'''=>~opcode=>267 target=>2 duration=>24 parameter1=>14017~
            add_effect_inline''''=>~opcode=>267 target=>2 duration=>24 parameter1=>14662~
            add_effect_inline'''''=>~opcode=>39 target=>2 duration=>24~
            add_effect_inline''''''=>~opcode=>101 target=>2 duration=>24 parameter2=>2~
            add_effect_inline'''''''=>~opcode=>143 timing=>4 parameter1=>7 duration=>24 target=>2 resource=>monhp1~
            add_effect_inline''''''''=>~opcode=>112 target=>2 resource=>monhp1~
            add_effect_inline'''''''''=>~opcode=>17 target=>2 timing=>4 duration=>24 parameter1=>30 parameter2=>1~
            add_effect_inline''''=>~opcode=>267 target=>2 duration=>24 parameter1=>20438~
         END
         LAF make_spell STR_VAR spell=dw#nilde edits=patch_data END

         // missing weapons/armor
         
         LAF edit_creature STR_VAR creature=~druid10a druid10b druid12a druid12b
                                             druid6a druid8a druid8b gdruid trevil02~
                                   editstring=~add_items=>staf01(WEAPON1)~
         END

         LAF edit_creature STR_VAR creature=~cedruid2 cechal02 cechalle druid10a druid10b druid12a druid12b druid6a druid8a druid8b~
                                   editstring=~add_items=>leat01~
         END


         // shapeshift
         
         ACTION_IF FILE_EXISTS ~override/dw#shapeshift.mrk~ BEGIN

                LAF edit_creature STR_VAR creature=~cechal02 cechalle cedruid1 cedruid2 cefgt02 cefgt04
                                                    druid10a druid10b druid12a druid12b druid8a druid8b
                                                    gdruid pardru tdr10a tdr10b tdr8a tdr8b trevil02 trevil03~
                                           editstring=~add_items=>dw#shnbr(WEAPON2)~
                END
         
                LAF edit_creature STR_VAR creature=tdr10a editstring=~add_items=>"dw#shnsa(WEAPON2) dw#shnsp(WEAPON3)"~ END
                LAF edit_creature STR_VAR creature=sppain editstring=~add_items=>"dw#shnsp(WEAPON2) dw#shnsp(WEAPON3)"~ END // Pai'na always takes spider form
                LAF edit_creature STR_VAR creature=tdr10b editstring=~add_items=>"dw#shnw2(WEAPON2)"~ END
                LAF edit_creature STR_VAR creature=hgnya01 editstring=~add_items=>"dw#shnea(WEAPON2)"~ END

         END

         // blind priests in beholder caves
         
         LAF install STR_VAR files=blind.baf location=resource END

     // Kuo-toa clerics need a Continue() in their truesight

     ACTION_IF !FILE_EXISTS_IN_GAME ~dw#mage.mrk~ BEGIN
        LAF swap_text STR_VAR files=~kuotoa.bcs~ swaps=~ReallyForceSpell(Myself,KOA_TRUE_SIGHT_NO_VIS)=>ReallyForceSpell(Myself,KOA_TRUE_SIGHT_NO_VIS)Continue()~ END
     END

     // Sendai statue needs to be quiet till activated
     
     	<<<<<<<< .../stratagems-inline/dw#send2.baf
	IF
		HasItem("stonring",Myself)
	THEN
		RESPONSE #100
			NoAction()
	END

	>>>>>>>>

        LAF install STR_VAR file=dw#send2.baf inline=yes END
        LAF edit_creature STR_VAR creature=sendai2 editstring=~insert_script_high=>dw#send2~ END


        // Berenn is too big to get certain protective effects (he looks stupid)
        
        LAF edit_creature STR_VAR creature=hgber01 editstring=~add_items=>dw#prbig~ END

	// Dawnmaster Kreel is supposed to wander around

        <<<<<<<< .../stratagems-inline/dw#kreel.baf
        IF
	!Global("inafight","GLOBAL",1)
	!See(NearestEnemyOf(Myself))
	HPPercent(Myself,100)
THEN
	RESPONSE #100
		RandomWalk()
END
        >>>>>>>>

        LAF install STR_VAR file=dw#kreel.baf inline=yes END
        LAF edit_creature STR_VAR creature=dawnmas editstring=~insert_script=>"dw#kreel below sht0902"~ END

   END

   ACTION_IF is_bg1=1 BEGIN

   // missing scripts
   
   LAF edit_creature STR_VAR creature=x#black allow_missing=yes editstring=~insert_script_low=>priest3~ END
   LAF edit_creature STR_VAR creature=beland tv=yes editstring=~script_race=>priest1~ END


    // shapeshift

    ACTION_IF FILE_EXISTS_IN_GAME ~dw#shapeshift.mrk~ BEGIN
       LAF edit_creature STR_VAR creature=~seniya takiyah osmadi voltin corsone~ tv=yes editstring=~add_items=>dw#shnbr(WEAPON2)~ END
       LAF edit_creature STR_VAR creature=amaran tv=yes editstring=~add_items=>"dw#shnsa(WEAPON2) dw#shnsp(WEAPON3)"~ END
       LAF edit_creature STR_VAR creature=x#andart allow_missing=yes editstring=~add_items=>"dw#shnsa(WEAPON2) dw#shnsp(WEAPON3)"~ END
       LAF edit_creature STR_VAR creature=izefia tv=yes editstring=EVALUATE_BUFFER ~remove_items=>%tutu_var%leat04 add_items=>dw#shnw2(WEAPON2)~ END
    END

    // prevent Corsone from attacking his own summons
    
    LAF install STR_VAR file=dw#corso.baf location=resource END
    LAF edit_creature STR_VAR creature=corsone tv=yes editstring=EVALUATE_BUFFER ~swap_script=>"%tutu_scripts%eeenemy=>dw#corso"~ END

    // bishops arguably shouldn't buff
    
    LAF edit_creature STR_VAR creature=EVALUATE_BUFFER ~%tutu_scriptbg%bishop~ editstring=~add_items=>dw#nopre~ END

   END


END
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////               Post-main edits (if possible put things in pre-main instead)
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION priest_edits_after BEGIN
   ACTION_IF is_bg2=1 BEGIN




   END

   ACTION_IF is_bg1=1 BEGIN

   
   
   END


END



//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////            Build scripts for individual priests               //////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION build_individual_priest_scripts
    INT_VAR prebuff=1
BEGIN

   ACTION_FOR_EACH game IN bg1 bg2 BEGIN
      ACTION_IF $is(~%game%~)=1 BEGIN
         LAF get_priest_prebuff_variables STR_VAR game prebuff RET variables=variables END
         LAF check_label STR_VAR label=dw#shapeshift RET value=value END
         ACTION_IF value BEGIN
            OUTER_SPRINT variables ~%variables%&ImprovedShapeshift=True~
         END
         ACTION_BASH_FOR ~%scsroot%/priest/ssl/%game%/special~ ~.*\.ssl~ BEGIN
            LAF ssl_to_bcs STR_VAR variables script=EVALUATE_BUFFER ~%BASH_FOR_RES%~ location=EVALUATE_BUFFER ~priest/ssl/%game%/special~ END
         END
      END
   END

   ACTION_IF is_bg2=0 BEGIN
       LAF get_priest_prebuff_variables STR_VAR game=bg2 prebuff RET variables=variables END
       LAF ssl_to_bcs STR_VAR variables script=dw#nymph location=~priest/ssl/bg2/special~ END
   END
END


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////
///      Find priest scripts. Return 1 if found with a BG1 script, 2 if with a BG2 script. If there's a replacement in priest/scripts,
///      compile it; otherwise, blank the slot
///////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION CRE_find_priest_scripts
   STR_VAR filename=""
   RET value
BEGIN
   LAUNCH_PATCH_MACRO read_in_priest_scripts
   SET value=0
   FOR (i=0x248;i<0x270;i+=8) BEGIN
      READ_ASCII i script
      TO_LOWER script
      INNER_PATCH_SAVE script ~%script%~ BEGIN
         REPLACE_TEXTUALLY "_" ""
      END
      SET here=0
      PATCH_FOR_EACH game IN 1 2 BEGIN
         PATCH_IF ($is(EVALUATE_BUFFER ~bg%game%~)=1 && VARIABLE_IS_SET $priest_script(EVALUATE_BUFFER ~bg%game%~ ~%script%~)) BEGIN
            SET here=1
            SET value=game
         END
      END
      PATCH_IF here=1 BEGIN
         SET replace=0
         INNER_ACTION BEGIN
              ACTION_IF FILE_EXISTS ~%scsroot%/%component_loc%/scripts/%script%.baf~ BEGIN
                 ACTION_IF GAME_IS ~tutu tutu_totsc~ BEGIN
                    COPY ~%scsroot%/%component_loc%/scripts/%script%.baf~ ~%workspace%/_%script%.baf~
                    COMPILE EVALUATE_BUFFER ~%workspace%/_%script%.baf~
                    SILENT
                 END ELSE BEGIN
                    LAF install STR_VAR file=EVALUATE_BUFFER ~%script%.baf~ location=scripts END
                    SILENT
                 END
                 OUTER_SET replace=1
              END
         END
         PATCH_IF replace=0 BEGIN
            WRITE_ASCII i ~~ (8)
         END
      END
   END
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////            Make needed resources                 //////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION make_priest_resources BEGIN

    // required for BG1 and BG2

        LAF clone_creature STR_VAR creature=~nymphsu=>dw#nymsu~ editstring=~script_override=>dw#nymph~ END

	COPY_EXISTING ~%CLERIC_CALL_WOODLAND_BEINGS%.spl~ ~override/dw#nymph.spl~
		REPLACE_TEXTUALLY CASE_INSENSITIVE ~nymphsu~ ~dw#nymph~ (8)
		REPLACE_TEXTUALLY CASE_INSENSITIVE ~dvnymph~ ~dw#nymph~ (8)

	COPY_EXISTING ~nymphsu.eff~ ~override/dw#nymph.eff~
		REPLACE_TEXTUALLY CASE_INSENSITIVE ~nymphsu~ ~dw#nymsu~ (8)

    // required for BG2 only
    
    ACTION_IF is_bg2=1 BEGIN

    // if SR is installed, make NPC versions of Symbol spells

    ACTION_IF ~demivrgvs~=1 BEGIN
       LAF clone_spell STR_VAR spell=EVALUATE_BUFFER ~%WIZARD_SYMBOL_FEAR%=>%WIZARD_NPC_SYMBOL_FEAR%
                                                      %WIZARD_SYMBOL_DEATH%=>%WIZARD_NPC_SYMBOL_DEATH%
                                                      %WIZARD_SYMBOL_STUN%=>%WIZARD_NPC_SYMBOL_STUN%~
       END
    END

    // if Refinements is installed, copy its versions of HLAs back over the originals

    ACTION_IF refinements=1 BEGIN
      ACTION_IF demivrgvs=1 BEGIN
       LAF clone_spell STR_VAR spell=EVALUATE_BUFFER ~%CLERIC_ELEMENTAL_SWARM%=>tg#elsu %CLERIC_GREATER_ELEMENTAL_SWARM%=>tg#prca~ END
      END ELSE BEGIN
       LAF clone_spell STR_VAR spell=EVALUATE_BUFFER ~tg#elsu=>%CLERIC_ELEMENTAL_SWARM% tg#prca=>%CLERIC_GREATER_ELEMENTAL_SWARM%~ END
      END
    END

    MAKE_PATCH
         type=>ability
         projectile=>159
    END

    LAF edit_spell STR_VAR spell=EVALUATE_BUFFER ~%CLERIC_GREATER_COMMAND%~ editstring=~patch_conditionally=>patch_data~ END

     // make an NPC version of Summon Elemental Prince
        
        COPY_EXISTING ~%CLERIC_GREATER_ELEMENTAL_SWARM%.spl~ ~override/dw#elemp.spl~
           REPLACE_TEXTUALLY CASE_INSENSITIVE ~sumzaam~ ~dw#szaam~ (8)
           REPLACE_TEXTUALLY CASE_INSENSITIVE ~sumchan~ ~dw#schan~ (8)
           REPLACE_TEXTUALLY CASE_INSENSITIVE ~sumsunn~ ~dw#ssunn~ (8)
           REPLACE_TEXTUALLY CASE_INSENSITIVE ~tg#benh~ ~dw#benh~ (8)
        BUT_ONLY

        COPY_EXISTING ~sumzaam.eff~ ~override/dw#szaam.eff~
           REPLACE_TEXTUALLY CASE_INSENSITIVE ~elemzaam~ ~dw#ezaam~
        BUT_ONLY

        COPY_EXISTING ~sumchan.eff~ ~override/dw#schan.eff~
           REPLACE_TEXTUALLY CASE_INSENSITIVE ~elemchan~ ~dw#echan~
        BUT_ONLY

        COPY_EXISTING ~sumsunn.eff~ ~override/dw#ssunn.eff~
           REPLACE_TEXTUALLY CASE_INSENSITIVE ~elemsunn~ ~dw#esunn~
        BUT_ONLY

        ACTION_IF FILE_EXISTS_IN_GAME ~tg#benh.eff~ BEGIN
          COPY_EXISTING ~tg#benh.eff~ ~override/dw#benh.eff~
           REPLACE_TEXTUALLY CASE_INSENSITIVE ~tg#benh~ ~dw#benh~
          BUT_ONLY
        END

       LAF clone_creature STR_VAR creature=~elemsunn=>dw#esunn~ editstring=~say_both_names=>4300 dv=>dw#esunn~ END
       LAF clone_creature STR_VAR creature=~elemzaam=>dw#ezaam~ editstring=~say_both_names=>4301 dv=>dw#esunn~ END
       LAF clone_creature STR_VAR creature=~elemchan=>dw#echan~ editstring=~say_both_names=>4302 dv=>dw#esunn~ END
       LAF clone_creature STR_VAR creature=~tg#benh=>dw#benh~ editstring=~say_both_names=>4306 dv=>dw#esunn~ allow_missing=yes END

         ACTION_IF ~demivrgvs~=1   BEGIN
           ACTION_IF FILE_EXISTS_IN_GAME ~dw#mehge.bcs~ BEGIN
              OUTER_SPRINT ~elemental_prince_script~ ~dw#mehge~
           END ELSE BEGIN
              OUTER_SPRINT ~elemental_prince_script~ ~wtasight~
           END
           ACTION_FOR_EACH prince IN dw#echan dw#ezaam dw#esunn BEGIN
              LAF edit_creature STR_VAR creature=EVALUATE_BUFFER ~%prince%~ editstring=EVALUATE_BUFFER ~script_override=>%prince% script_default=>%elemental_prince_script%~ END
              LAF install STR_VAR file=EVALUATE_BUFFER ~%prince%.baf~ location=resource END
           END
       END

        LAF clone_item STR_VAR item=~misc16=>dw#prbig~ editstring=~droppable=>0~ END // too large to use Shield of the Archons (looks stupid)
        LAF install STR_VAR files=~dw#bear.spl dw#ctrlu.spl dw#destu.spl~ location=resource END

        ACTION_DEFINE_ASSOCIATIVE_ARRAY priest_spell_replace_map BEGIN
           dw#harmn=>sppr508
           dw#devae=>sppr727
           dw#gatep=>sppr702
        END
        
        ACTION_PHP_EACH priest_spell_replace_map AS new=>old BEGIN
           ACTION_IF !FILE_EXISTS_IN_GAME ~%new%.spl~ BEGIN
              LAF clone_spell STR_VAR spell=EVALUATE_BUFFER ~%old%=>%new%~ END
           END
        END

      END // end of BG2-only block

END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////
///      Read in priest scripts (i.e. those we aim to replace)
///////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_MACRO read_in_priest_scripts BEGIN
   OUTER_INNER_PATCH ~~ BEGIN
      LAUNCH_PATCH_MACRO read_in_priest_scripts
   END
END

DEFINE_PATCH_MACRO read_in_priest_scripts BEGIN
    PATCH_IF !(VARIABLE_IS_SET ~priest_scripts_read~) BEGIN
       SET priest_scripts_read=1
       PATCH_FOR_EACH script IN
            priest1 priest2 priest3 priest4 priest5 priest6
            seniyad druid3 x#shdr6 x#shdr8 x#andart x#beador x#mareth x#belgin
       BEGIN
          TO_LOWER ~script~
          SPRINT $priest_script_bg1(EVALUATE_BUFFER ~%script%~) ~~
       END
       PATCH_FOR_EACH script IN
AMERAL DEMPRI      GENPRST1 PAINA     PRIES10A PRIES10B  PRIES10C PRIES12A
PRIES12C PRIES14A  PRIES14T PRIES18A  PRIES18B PRIES18C  PRIES18D PRIES6A
PRIES6B  PRIES6C   PRIES8A  PRIES8B   PRIES8C  PRIEST12  PRIET12B SAHPRI12  DAQILUE  HGNYA01
GPHEALER BERENN    BARL     CCLER20B  DRSHNL01 DRSHNL11  DRUID10A  DRUID10B DRUID12A
DRUID12B DRUID14G  DRUID6A  DRUID8A   DRUID8B  KBSHAM01  GORPRI1  HGSHAM01
SENDAI2  GORCAMB3  HGBER01  CEFALD01  SAHAMB03 SAHAMB06
BEGIN
          TO_LOWER ~script~
          SPRINT $priest_script_bg2(EVALUATE_BUFFER ~%script%~) ~~
       END
    END
END




