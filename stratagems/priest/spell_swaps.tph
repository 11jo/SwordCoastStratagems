////////////////////////////// based on race //////////////////////////////////////

DEFINE_PATCH_FUNCTION priest_spell_swap_race_preparatory
    STR_VAR race=""
            spells=""
    RET spells
BEGIN
    TO_LOWER race
    PATCH_MATCH ~%race%~ WITH
    VAMPIRE BEGIN
       LPF substitute_spells STR_VAR spells swap_out=FLAME_BLADE swap_in="" RET spells END
       LPF substitute_spells STR_VAR spells swap_out=SPIRITUAL_HAMMER swap_in="" RET spells END
    END
    DEFAULT END
END

////////////////////////////// based on specific creature type //////////////////////////////////////

DEFINE_PATCH_FUNCTION priest_spell_swap_specific_preparatory
     STR_VAR filename=""
             spells=""
     RET spells
BEGIN
     TO_LOWER filename
     PATCH_MATCH "%filename%" WITH
     dwyuap2 dwyuap3 dwyuap4 dwyuap5 dwyuap6 dwyuap7
     talonam talonaf dwtalonm dwtalonf lsham1 lsham2 dwlsha5 dwlsha6 dwlsha10 dwlsha11 dwlsha12
     dwselfc1 dwselfc2 dwselfc3 sdelfpr dwacoly1 dwacoly2 dwlmshk1 dwlmshk2 dwlmshk3
     BEGIN
          LPF substitute_spells STR_VAR spells swap_out=RECITATION swap_in="" RET spells END
          LPF substitute_spells STR_VAR spells swap_out=PRAYER swap_in="" RET spells END
          LPF substitute_spells STR_VAR spells swap_out=BLESS swap_in="" RET spells END
          LPF substitute_spells STR_VAR spells swap_out=CHANT swap_in="" RET spells END
          LPF substitute_spells STR_VAR spells swap_out=RIGHTEOUS_WRATH_OF_THE_FAITHFUL swap_in="" RET spells END
          LPF substitute_spells STR_VAR spells swap_out=DEFENSIVE_HARMONY swap_in="" RET spells END
     END
     DEFAULT END
END
////////////////////////////// based on level //////////////////////////////////////

DEFINE_PATCH_FUNCTION priest_spell_swap_level
     INT_VAR level=0
     STR_VAR spells=""
             kit=""
     RET spells
BEGIN
     PATCH_IF (level>14) BEGIN
        LPF substitute_spells STR_VAR spells swap_out=CONJURE_ANIMALS swap_in=CONJURE_FIRE_ELEMENTAL RET spells=spells END
     END
     PATCH_IF (level>9) BEGIN
        LPF substitute_spells STR_VAR spells swap_out=RIGID_THINKING swap_in=~MISCAST_MAGIC MISCAST_MAGIC RIGID_THINKING~ RET spells=spells END
     END


END


////////////////////////////// based on BG1 //////////////////////////////////////


DEFINE_PATCH_FUNCTION priest_spell_swap_bg1_preparatory
     STR_VAR spells=""
     RET spells
BEGIN
       LPF substitute_spells STR_VAR spells swap_out=~ARMOR_OF_FAITH ZONE_OF_SWEET_AIR CURE_DISEASE~ swap_in=~~ RET spells=spells END
END

DEFINE_PATCH_FUNCTION priest_spell_swap_bg1
     STR_VAR spells=""
     RET spells
BEGIN
      LPF substitute_spells STR_VAR spells swap_out=~MASS_CURE~ swap_in=~CURE_CRITICAL_WOUNDS~ RET spells=spells END
      LPF substitute_spells STR_VAR spells swap_out=~SUMMON_INSECTS~ swap_in=~CALL_LIGHTNING~ RET spells=spells END
      LPF substitute_spells STR_VAR spells swap_out=~CALL_WOODLAND_BEINGS~ swap_in=~ANIMAL_SUMMONING_1~ RET spells=spells END
      LPF substitute_spells STR_VAR spells swap_out=~CLOAK_OF_FEAR~ swap_in=~MENTAL_DOMINATION~ RET spells=spells END
      LPF substitute_spells STR_VAR spells swap_out=~HOLY_SMITE~ swap_in=~RIGID_THINKING MISCAST_MAGIC~ RET spells=spells END
      LPF substitute_spells STR_VAR spells swap_out=~UNHOLY_BLIGHT~ swap_in=~ANIMATE_DEAD ANIMATE_DEAD RIGID_THINKING MISCAST_MAGIC~ RET spells=spells END

END

////////////////////////////// special //////////////////////////////////////


DEFINE_PATCH_FUNCTION priest_spell_swap_special_preparatory
     STR_VAR spells=""
     RET spells
BEGIN
	PATCH_IF !IS_AN_INT shield_of_lathander_rebalance BEGIN
		LPF check_label STR_VAR label=dw_rebalance_lathander RET shield_of_lathander_rebalance=value END
	END
	PATCH_IF !shield_of_lathander_rebalance BEGIN
       LPF substitute_spells STR_VAR spells swap_out=~SHIELD_OF_LATHANDER GREATER_SHIELD_OF_LATHANDER~ swap_in=~~ RET spells=spells END
	END
END



////////////////////////////// based on god //////////////////////////////////////


DEFINE_PATCH_FUNCTION priest_spell_swap_god_preparatory
     STR_VAR spells="" god="" filename=""
     RET spells
BEGIN
      
	PATCH_MATCH "%god%" WITH
	kossuth BEGIN
		LPF substitute_spells STR_VAR spells swap_out=~SPIRITUAL_HAMMER SHIELD_OF_THE_ARCHONS FIENDISH_WARDING~ swap_in=~~ RET spells END		
	END
	helm BEGIN
		LPF substitute_spells STR_VAR spells swap_out=~SHIELD_OF_THE_ARCHONS FIENDISH_WARDING~ swap_in=~~ RET spells END			
	END
	tempus BEGIN
		LPF substitute_spells STR_VAR spells swap_out=~FREE_ACTION SHIELD_OF_THE_ARCHONS FIENDISH_WARDING~ swap_in=~~ RET spells END			
	END
	tymora BEGIN
		LPF substitute_spells STR_VAR spells swap_out="SPIRITUAL_HAMMER BLADE_BARRIER" swap_in="" RET spells END
	END
	gruumsh BEGIN
		LPF substitute_spells STR_VAR spells swap_out="SPIRITUAL_HAMMER" swap_in="" RET spells END	
	END
	helm BEGIN
		LPF substitute_spells STR_VAR spells swap_out="SPIRITUAL_HAMMER" swap_in="" RET spells END			
	END
	ilmater BEGIN
		LPF substitute_spells STR_VAR spells swap_out="REMOVE_FEAR SPIRITUAL_HAMMER BLADE_BARRIER" swap_in="" RET spells END	
	END
	malar BEGIN
		LPF substitute_spells STR_VAR spells swap_out=~SPIRITUAL_HAMMER SHIELD_OF_THE_ARCHONS FIENDISH_WARDING~ swap_in=~~ RET spells END			
	END
	mask BEGIN
		LPF substitute_spells STR_VAR spells swap_out=~SPIRITUAL_HAMMER BLADE_BARRIER~ swap_in=~~ RET spells END			
	END
	myrkul velsharoon BEGIN
		LPF substitute_spells STR_VAR spells swap_out=~SPIRITUAL_HAMMER~ swap_in=~~ RET spells END					
	END
	shar BEGIN
		LPF substitute_spells STR_VAR spells swap_out=~SPIRITUAL_HAMMER BLADE_BARRIER~ swap_in=~~ RET spells END					
	END
	talona BEGIN
		LPF substitute_spells STR_VAR spells swap_out=~CURE_DISEASE~ swap_in=~~ RET spells END
	END
	DEFAULT
	END
END


////////////////////////////// based on vanilla IWD //////////////////////////////////////

DEFINE_PATCH_FUNCTION priest_spell_swap_iwd
     STR_VAR spells="" filename=""
     RET spells
BEGIN
END

////////////////////////////// special //////////////////////////////////////

DEFINE_PATCH_FUNCTION priest_spell_swap_misc
   STR_VAR spells=""
           filename=""
		   god=""
		   spell_list=""
   RET spells
BEGIN
    // people with Wands of the Heavens don't need Flame Strike
    PATCH_MATCH ~%filename%~ WITH
    aasim _aasim pbhunt04 saerk BEGIN
       LPF substitute_spells STR_VAR spells swap_out=FLAME_STRIKE swap_in=~SLAY_LIVING SLAY_LIVING MASS_CURE CURE_CRITICAL_WOUNDS~ RET spells END

    END
    DEFAULT END
    // MR creatures don't use MAGIC_RESISTANCE
    PATCH_IF (BYTE_AT 0x5d)>40 BEGIN
        LPF substitute_spells STR_VAR spells swap_out=MAGIC_RESISTANCE swap_in=MASS_CURE RET spells END
    END
    // undead don't heal
    PATCH_IF (BYTE_AT 0x271)=4 BEGIN
       LPF substitute_spells STR_VAR spells swap_out=MASS_CURE swap_in=SLAY_LIVING RET spells END
       LPF substitute_spells STR_VAR spells swap_out=CURE_CRITICAL_WOUNDS swap_in=SLAY_LIVING RET spells END
       LPF substitute_spells STR_VAR spells swap_out=HEAL swap_in=HARM RET spells END
    END
	// elementals might not be available
	PATCH_IF !VARIABLE_IS_SET "%CLERIC_CONJURE_WATER_ELEMENTAL%" BEGIN
		PATCH_IF "%god%" STR_EQ "" BEGIN
			LPF substitute_spells STR_VAR spells swap_out=CONJURE_WATER_ELEMENTAL swap_in=CONJURE_FIRE_ELEMENTAL RET spells END		
			LPF substitute_spells STR_VAR spells swap_out=CONJURE_AIR_ELEMENTAL swap_in=CONJURE_EARTH_ELEMENTAL RET spells END		
		END ELSE BEGIN
			LPF substitute_spells STR_VAR spells swap_out="CONJURE_WATER_ELEMENTAL CONJURE_AIR_ELEMENTAL" swap_in="" RET spells END
		END
	END
	// giant insects might not be available
	PATCH_IF !VARIABLE_IS_SET "%CLERIC_GIANT_INSECT%" BEGIN
		LPF substitute_spells STR_VAR spells swap_out=GIANT_INSECT swap_in=ANIMAL_SUMMONING_1 RET spells END
	END
	// Druids only get Flame Strike in ToF
	PATCH_IF !tof_spheres && "%spell_list%" STR_EQ druid BEGIN
		 LPF substitute_spells STR_VAR spells swap_out=FLAME_STRIKE swap_in=INSECT_PLAGUE RET spells END	
	END
	
END

DEFINE_PATCH_FUNCTION priest_spell_swap_alignment
     STR_VAR filename=""
             spells=""
     RET spells
BEGIN
     TO_LOWER filename
     LPF CRE_is_good RET is_good=value END
     PATCH_IF is_good BEGIN
             LPF substitute_spells STR_VAR spells swap_out="SUMMON_DEVA_EVIL" swap_in="SUMMON_DEVA_GOOD" RET spells END
             LPF substitute_spells STR_VAR spells swap_out="CIRCLE_OF_BONES" swap_in="" RET spells END
     END ELSE BEGIN
			PATCH_IF VARIABLE_IS_SET "CLERIC_FIENDISH_WARDING" BEGIN
				LPF substitute_spells STR_VAR spells swap_out="SHIELD_OF_THE_ARCHONS" swap_in="FIENDISH_WARDING" RET spells END	 
			END
	 END
END

