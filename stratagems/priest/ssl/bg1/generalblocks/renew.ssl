
////////////////////////////////////////////////////////////////////
////	Iron Skins
////////////////////////////////////////////////////////////////////

IF TRIGGER
	RequireBlock(IsDruid)
	RequireBlock(HasL4)
	GlobalTimerExpired("redefend","LOCALS")
	!CheckStatGT(Myself,2,STONESKINS)
THEN DO
	Action(SpellMyself,CLERIC_IRONSKIN|100|100)
	SetGlobal("redefend","LOCALS",8) // don't get stuck in a defend-defend-defend loop and forget to do any damage
END


/////////////////////////////////////////////////////////////////////
////	Sanctuary
/////////////////////////////////////////////////////////////////////

IF TRIGGER
	HPPercentLT(Myself,70)
	!CheckStatGT(Myself,0,SANCTUARY)
THEN DO
	Action(SpellMyself,CLERIC_SANCTUARY|100|50)
END

/////////////////////////////////////////////////////////////////////
////	Emergency healing for me, including remove blindness
/////////////////////////////////////////////////////////////////////

IF TRIGGER
	RequireBlock(HasL6)
	HPPercentLT(Myself,50)
THEN DO
	Action(SpellMyself,CLERIC_HEAL)
END

IF TRIGGER
        RequireBlock(HasL3)
        StateCheck(Myself,STATE_BLIND)
	!Global("DMWWHLSM","GLOBAL",1)
THEN DO
       Action(SpellMyself,CLERIC_HEAL)
       Action(SpellMyself,CLERIC_CURE_DISEASE)
       Action(SpellMyself,CLERIC_NEUTRALIZE_POISON)
END

IF TRIGGER
	RequireBlock(HasL5)
	HPPercentLT(Myself,50)
	CheckStatGT(Myself,0,SANCTUARY)
THEN DO
	Action(SpellMyself,CLERIC_CURE_CRITICAL_WOUNDS)

END

IF TRIGGER
	HPPercentLT(Myself,50)
THEN DO
	Action(Potion,"potn52",@1052)

END

IF TRIGGER
	RequireBlock(HasL5)
	TargetBlock(EnemiesInOrderShort)
	HPPercentLT(Myself,50)
	OR(2)
		!Range(scstarget,10)
		StateCheck(scstarget,STATE_DISABLED)
THEN DO
	Action(SpellMyself,CLERIC_CURE_CRITICAL_WOUNDS|100|100)
END

//////////////////////////////////////////////////////////////////////
// Remove blindness
//////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(HumanoidVillains)
	StateCheck(scstarget,STATE_BLIND)
	HPPercentLT(scstarget,75)
        RequireBlock(HasL6)
	Allegiance(scstarget,ENEMY)
	!Global("DMWWHLSM","GLOBAL",1)
THEN DO
       Action(SpellCuringBlindness,CLERIC_HEAL)
END

IF TRIGGER
	TargetBlock(HumanoidVillains)
	StateCheck(scstarget,STATE_BLIND)
        RequireBlock(HasL3)
	Allegiance(scstarget,ENEMY)
	!Global("DMWWHLSM","GLOBAL",1)
THEN DO
       Combine()
       Action(SpellCuringBlindness,CLERIC_CURE_DISEASE)
       Action(SpellCuringBlindness,CLERIC_NEUTRALIZE_POISON)
       Action(SpellCuringBlindness,CLERIC_HEAL)
END


//////////////////////////////////////////////////////////////////////
////	Clear clouds
//////////////////////////////////////////////////////////////////////

IF TRIGGER
	IgnoreBlock(IsDruid)
	RequireBlock(HasL3)
	IgnoreBlock(SpellsAreBG1)
	OR(4)
		Heard([ANYONE],404) // Cloudkill
		Heard([ANYONE],406) // Stinking Cloud
		Heard([ANYONE],408) // Death Fog
		Heard([ANYONE],409) // Incendiary Cloud
THEN DO
	Action(SpellMyself,CLERIC_ZONE_OF_SWEET_AIR)
END

IF TRIGGER
	IgnoreBlock(IsDruid)
	RequireBlock(HasL3)
	IgnoreBlock(SpellsAreBG1)
	OR(4)
		Heard([ANYONE],404) // Cloudkill
		Heard([ANYONE],406) // Stinking Cloud
		Heard([ANYONE],408) // Death Fog
		Heard([ANYONE],409) // Incendiary Cloud
	CheckStatGT(Myself,6,Level)
	!CheckStatGT(Myself,10,Level)
	!GlobalTimerNotExpired("sweetair","LOCALS")
THEN DO
	Action(SpellNoDecMyself,CLERIC_ZONE_OF_SWEET_AIR|100|100)
	SetGlobalTimer("sweetair","LOCALS",1000)
	OnContinue(SetGlobalTimer("sweetair","LOCALS",1000))
END

IF TRIGGER
	IgnoreBlock(IsDruid)
	RequireBlock(HasL3)
	IgnoreBlock(SpellsAreBG1)
	OR(4)
		Heard([ANYONE],404) // Cloudkill
		Heard([ANYONE],406) // Stinking Cloud
		Heard([ANYONE],408) // Death Fog
		Heard([ANYONE],409) // Incendiary Cloud
	CheckStatGT(Myself,10,Level)
	!GlobalTimerNotExpired("sweetair","LOCALS")
THEN DO
	Action(SpellNoDecMyself,CLERIC_ZONE_OF_SWEET_AIR)
	SetGlobalTimer("sweetair","LOCALS",1000)
	OnContinue(SetGlobalTimer("sweetair","LOCALS",1000))
END

//////////////////////////////////////////////////////////////////////
////	Healing for high-powered allies
//////////////////////////////////////////////////////////////////////

IF TRIGGER
	RequireBlock(HasL6)
	TargetBlock(HumanoidVillains)
	CheckStatGT(scstarget,10,Level)
	HPPercentLT(scstarget,50)
	Range(scstarget,15)
	Allegiance(scstarget,ENEMY)
THEN DO
	Action(Spell,CLERIC_HEAL|100|50)
END


IF TRIGGER
	RequireBlock(HasL5)
	IgnoreBlock(SpellsAreBG1)
	TargetBlock(HumanoidVillains)
	CheckStatGT(scstarget,5,Level)
	HPPercentLT(scstarget,50)
	Allegiance(scstarget,ENEMY)
THEN DO
	Action(Spell,CLERIC_MASS_CURE|100|50)
END

/////////////////////////////////////////////////////////////////////////////
///	Are any of my own team panicked or paralysed? If so, bring them out.
/////////////////////////////////////////////////////////////////////////////

IF TRIGGER

	TargetBlock(HumanoidVillains)
	StateCheck(scstarget,STATE_PANIC)
	!Global("remfear","LOCALS",1)
	Allegiance(scstarget,ENEMY)
THEN DO
	Combine()
	Action(Spell,CLERIC_REMOVE_FEAR)
END

IF TRIGGER
	RequireBlock(HasL3)
	IgnoreBlock(IsDruid)
	TargetBlock(HumanoidVillains)
	CheckStatGT(scstarget,0,HELD)
	Allegiance(scstarget,ENEMY)
THEN DO
	Combine()
	Action(Spell,CLERIC_REMOVE_PARALYSIS)
END



///////////////////////////////////////////////////////////////////////////////////////
////	More healing, if it's appropriate
//////////////////////////////////////////////////////////////////////////////////////


IF TRIGGER
	HPPercentLT(Myself,50)
	!Global("shapeshift","LOCALS",1)
THEN DO
	Action(Potion,"potn52",@1052)
	Action(Potion,"dw#ptn52",@1052)
END

IF TRIGGER
	HPPercentLT(Myself,90)
	OR(2)
           !See(NearestEnemyOf(Myself))
           CheckStatGT(Myself,0,SANCTUARY)
	!Global("shapeshift","LOCALS",1)
THEN DO
	Action(Potion,"%tutu_var%potn08",@1008)
	Action(Potion,"potn52",@1052)
	Action(Potion,"dw#ptn08",@1008)
	Action(Potion,"dw#ptn52",@1052)
	Action(SpellMyself,CLERIC_CURE_SERIOUS_WOUNDS)
	Action(SpellMyself,CLERIC_CURE_MEDIUM_WOUNDS)
	Action(SpellMyself,CLERIC_CURE_LIGHT_WOUNDS)
END
