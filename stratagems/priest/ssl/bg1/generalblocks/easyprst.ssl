///////////////////////////////////////////////////////////////
/////
/////  Intended as a low-grade priest AI at about the same level as
/////  the vanilla AI, but using the wider range of (BG1+BG2)
/////  spells SCS provides.
/////
/////  We do some minimal checks (don't use diabling attacks against
/////  already-held opponents) but basically we just cast our spells in order.
/////
/////  We don't do any healing, because no-one in vanilla BG1 does
/////
/////  We use these spells (most, but not all, SCS spells):
/////
/////  OFFENSIVE
/////
/////  L6: AERIAL_SERVANT CONJURE_ANIMALS CONJURE_FIRE_ELEMENTAL
/////  L5: FLAME_STRIKE GREATER_COMMAND ANIMAL_SUMMONING_2 INSECT_PLAGUE
/////  L4: POISON MENTAL_DOMINATION CLOAK_OF_FEAR ANIMAL_SUMMONING_1
/////  L3: RIGID_THINKING UNHOLY_BLIGHT ANIMATE_DEAD CALL_LIGHTNING SUMMON_INSECTS
/////  L2: HOLD_PERSON CHARM_PERSON
/////  L1: COMMAND
/////
/////  DEFENSIVE
/////
/////  BLESS CHANT DEFENSIVE_HARMONY BARKSKIN FLAME_BLADE
/////


INCLUDE FILE(%scsroot%/genai/ssl/easy_shared.ssl)
INCLUDE FILE(%scsroot%/genai/ssl/easy_definitions.ssl)

////////////////
//   BUFFS / SUMMONS
////////////////

IF TRIGGER
        RequireBlock(EasyOptionAvailable)
     See(NearestEnemyOf(Myself))
THEN DO
     Action(EasySpellMyself,CLERIC_DEFENSIVE_HARMONY|100|100)
     Action(EasySpellMyself,CLERIC_CHANT|100|100)
     Action(EasySpellMyself,CLERIC_DRAW_UPON_HOLY_MIGHT|100|100)
     Action(EasySpellMyself,CLERIC_AERIAL_SERVANT|100|100)
     Action(EasySpellMyself,CLERIC_CONJURE_FIRE_ELEMENTAL|100|100)
     Action(EasySpellMyself,CLERIC_CONJURE_ANIMALS|100|100)
     Action(EasySpellMyself,CLERIC_ANIMAL_SUMMONING_2|100|100)
     Action(EasySpellMyself,CLERIC_ANIMAL_SUMMONING_1|100|100)
     Action(EasySpellMyself,CLERIC_ANIMATE_DEAD|100|100)
     Action(EasySpellMyself,CLERIC_BLESS|100|100)
     Action(EasySpellMyself,CLERIC_BARKSKIN|100|100)
END

/////////////////
//   DISABLERS
/////////////////

IF TRIGGER
        RequireBlock(EasyOptionAvailable)
       Target(NearestEnemyOf(Myself))
       TriggerBlock(Enemy|Disabled)
       RequireBlock(HasL5)
THEN DO
       Combine()
       Action(EasySpellArea,CLERIC_GREATER_COMMAND|100|100)
END

IF TRIGGER
       TargetBlock(EnemiesInOrderShort)
       TriggerBlock(Enemy|Disabled)
THEN DO
       Combine()
       Action(EasySpell,CLERIC_INSECT_PLAGUE|100|100)
       Action(EasySpell,CLERIC_SUMMON_INSECTS|100|100)
       Action(EasySpell,CLERIC_MENTAL_DOMINATION|100|100)
       Action(EasySpell,CLERIC_RIGID_THINKING|100|100)
       Action(EasySpell,CLERIC_HOLD_PERSON|100|100)
       Action(EasySpell,CLERIC_CHARM_PERSON|100|100)
       Action(EasySpell,CLERIC_COMMAND|100|100)
END

/////////////////
//   DIRECT DAMAGE
/////////////////

IF TRIGGER
        RequireBlock(EasyOptionAvailable)
       TargetBlock(EnemiesInOrderShort)
       RequireBlock(HasL3)
THEN DO
        Action(EasySpell,CLERIC_FLAME_STRIKE|100|100)
        Action(EasySpell,CLERIC_CALL_LIGHTNING|100|100)
END

IF TRIGGER
        RequireBlock(EasyOptionAvailable)
       Target(NearestEnemyOf(Myself))
       RequireBlock(HasL3)
       IgnoreBlock(IsDruid)
THEN DO
        Action(EasySpellArea,CLERIC_UNHOLY_BLIGHT|100|100)
END

/////////////////
//   MELEE
/////////////////

IF TRIGGER
        RequireBlock(EasyOptionAvailable)
       See(NearestEnemyOf(Myself))
       RequireBlock(IsDruid)
THEN DO
       Action(EasySpellMyself,CLERIC_FLAME_BLADE)
END
/////////////////////////////
///   Thick-as-can-be melee block
////////////////////////////

IF TRIGGER
        RequireBlock(EasyOptionAvailable)
        TriggerBlock(EasyMinus)

        See(NearestEnemyOf(Myself))
THEN DO
        Action(Literal)
        Attack(NearestEnemyOf(Myself))
END

/////////////////////////////
///   Lock out the *real* AI
////////////////////////////

IF TRIGGER
        RequireBlock(EasyOptionAvailable)
        TriggerBlock(EasyMinus)

THEN DO
       Action(Nothing)
END
