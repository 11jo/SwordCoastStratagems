//////////////////////////////////////////////////////////////////////////////////
///
///   Define actions
///
////////////////////////////////////////////////////////////////////////////////////////

VARIABLE(IsNotMoron=True)
VARIABLE(SPELLFAILUREMAGE=SPELLFAILUREPRIEST)

VARIABLE(scsspellsubstitute1=!StateCheck(scstarget,STATE_NOT_TARGETABLE))
VARIABLE(scsspellsubstitute2=!StateCheck(scstarget,STATE_INVISIBLE))
INCLUDE FILE(%scsroot%/mage/ssl/mage_definitions.ssl)

BEGIN_ACTION_DEFINITION
	Name(MoveIfEmpty)     // we're redefining this for the benefit of shapeshifters
	TRIGGER
		ActionListEmpty()
	ACTION
		RESPONSE #scsprob1
		EquipMostDamagingMelee()
		MoveToObject(scstarget)
END

//////////////////////////////////////////////////////////////////////////////////
////	Dumb version
//////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/priest/ssl/bg1/generalblocks/easyprst.ssl)

//////////////////////////////////////////////////////////////////////////////////
////	Prep
//////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/priest/ssl/bg1/main/priestsetup.ssl)

//////////////////////////////////////////////////////////////////////////////////
////	Deal with rogue undead
//////////////////////////////////////////////////////////////////////////////////

IF
	AttackedBy([ENEMY.UNDEAD],DEFAULT)
	Allegiance(LastAttackerOf(Myself),ENEMY)
	General(LastAttackerOf(Myself),UNDEAD)
THEN
	RESPONSE #100
		ClearActions(LastAttackerOf(Myself))
		Continue()
END


//////////////////////////////////////////////////////////////////////////////////
//      Handle fear
//////////////////////////////////////////////////////////////////////////////////

IF
  StateCheck(Myself,STATE_PANIC)
THEN
    RESPONSE #100
             RandomWalkContinuous()
END

//////////////////////////////////////////////////////////////////////////////////
////	If non-hostile, closedown - this script doesn't pretend to fight bad guys
//////////////////////////////////////////////////////////////////////////////////

IF
	!Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		NoAction()
END


///////////////////////////////////////////////////////////////////////////
//	Turn undead
///////////////////////////////////////////////////////////////////////////

IF
	IgnoreBlock(IsDruid)
	See([GOODCUTOFF.0.0.SKELETON])
	!StateCheck(LastSeenBy(Myself),STATE_DISABLED)
	!GlobalTimerNotExpired("castspell","LOCALS")
	!GlobalTimerNotExpired("turninitialise","LOCALS")
THEN
	RESPONSE #100
		SetGlobalTimer("turninitialise","LOCALS",30)
		SetGlobalTimer("turning","LOCALS",4)
		Continue()
END

IF	
	IgnoreBlock(IsDruid)
	See([GOODCUTOFF.0.0.SKELETON])
	!StateCheck(LastSeenBy(Myself),STATE_DISABLED)
	!GlobalTimerNotExpired("castspell","LOCALS")
	OR(2)
		!GlobalTimerNotExpired("turninitialise","LOCALS")
		GlobalTimerNotExpired("turning","LOCALS")
THEN
	RESPONSE #100
		Turn()
END

///////////////////////////////////////////////////////////////////////////
//	Melee
///////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%scsroot%/priest/ssl/bg1/meleeblocks/touchattack.ssl)
INCLUDE FILE(%scsroot%/priest/ssl/bg1/meleeblocks/scsmeleechoice.ssl)

//////////////////////////////////////////////////////////////////////////
// Renew and retreat
//////////////////////////////////////////////////////////////////////////

DEFAULT TRIGGER(!GlobalTimerNotExpired("castspell","LOCALS") Global("shapeshift","LOCALS",0))

INCLUDE FILE(%scsroot%/priest/ssl/bg1/generalblocks/renew.ssl)


//////////////////////////////////////////////////////////////////////////
// Look for PCs
//////////////////////////////////////////////////////////////////////////

DEFAULT TRIGGER()

INCLUDE FILE(%scsroot%/mage/ssl/bg1/generalblocks/mill.ssl)
INCLUDE FILE(%scsroot%/priest/ssl/bg1/generalblocks/chase.ssl)

////////////////////////////////////////////////////////////////////////////
// Core magic block
///////////////////////////////////////////////////////////////////////////

DEFAULT TRIGGER(!GlobalTimerNotExpired("castspell","LOCALS") Global("shapeshift","LOCALS",0))

IF
   RequireBlock(IsDruid)
   RequireBlock(HasL4)
   RequireBlock(ImprovedShapeshift)
   OR(5)
      HasItemEquipedReal("dw#shnbr",Myself)
      HasItemEquipedReal("dw#shnw1",Myself)
      HasItemEquipedReal("dw#shnw2",Myself)
      HasItemEquipedReal("dw#shnsp",Myself)
      HasItemEquipedReal("dw#shnsa",Myself)
THEN
     RESPONSE #100
                SelectWeaponAbility(SLOT_WEAPON,0)
             Continue()
END

IF
   RequireBlock(IsDruid)
   IgnoreBlock(HasL4)
   RequireBlock(ImprovedShapeshift)
   HasItemEquipedReal("dw#shnw1",Myself)
THEN
     RESPONSE #100
                SelectWeaponAbility(SLOT_WEAPON,0)
             Continue()
END

INCLUDE FILE(%scsroot%/priest/ssl/bg1/priesttypes/standard_priest.ssl)

//////////////////////////////////////////////////////
///	If everyone is helpless, kill 'em.
//////////////////////////////////////////////////////

IF
	See(NearestEnemyOf(Myself))
	BEGIN LOOP(scsloopvar||1;2;3;4;5;6)
		OR(3)
			!See(Playerscsloopvar)
			StateCheck(Playerscsloopvar,STATE_IMMOBILE)
			CheckStatGT(Playerscsloopvar,0,HELD)
	END LOOP
THEN
	RESPONSE #100
		AttackReevaluate(NearestEnemyOf(Myself),15)
END

DEFAULT TRIGGER()
INCLUDE FILE(%scsroot%\priest\ssl\bg1\generalblocks\closedown.ssl)
