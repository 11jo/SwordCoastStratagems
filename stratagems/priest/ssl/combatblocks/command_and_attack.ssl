/////////////////////////////////////////////////////////////////////
////      Invoke a Cloak of Fear
/////////////////////////////////////////////////////////////////////

IF TRIGGER
          IgnoreBlock(God:bane_xvim)
		  TargetBlock(PCsInOrderShort)
          Range(scstarget,6)
          TriggerBlock(Enemy|Disabled|Panic)
THEN DO
       Combine()
       Action(Spell,CLERIC_CLOAK_OF_FEAR|100|100)
END

IF TRIGGER
          RequireBlock(God:bane_xvim)
		  TargetBlock(PCsInOrderShort)
          Range(scstarget,6)
          TriggerBlock(Enemy|Disabled|Panic)
THEN DO
       Combine()
       Action(SpellRES,"%INNATE_CLERIC_CLOAK_OF_FEAR%"|100|100)
END

////////////////////////////////////////////////////////////////////
///    Generate a weapon if needed
///////////////////////////////////////////////////////////////////

IF TRIGGER
          Target([PC])
          Range(scstarget,6)
          !Class(Myself,FIGHTER_ALL)
		  !HasItem("ghoult",Myself)
	!HasItem("harm",Myself)
	!HasItem("critical",Myself)
	!HasItem("serious",Myself)
	!HasItem("slaylive",Myself)
THEN DO
       Action(SpellRESBooleanMyself,CLERIC_GHOUL_TOUCH)
END

IF TRIGGER
          IgnoreBlock(Demivrgvs)
          Target([PC])
          Range(scstarget,6)
          !Class(Myself,FIGHTER_ALL)
		  !HasItem("shammr",Myself)
		  !HasItem("shammr2",Myself)
		  !HasItem("shammr3",Myself)
	!HasItem("harm",Myself)
	!HasItem("critical",Myself)
	!HasItem("serious",Myself)
	!HasItem("slaylive",Myself)
THEN DO
       Action(SpellMyself,CLERIC_SPIRITUAL_HAMMER)
END

IF TRIGGER
          IgnoreBlock(Demivrgvs)
          RequireBlock(HasL4)
          IgnoreBlock(ImprovedShapeshift)
          Target([PC])
          Range(scstarget,6)
          !Class(Myself,FIGHTER_ALL)
		  !HasItem("fblade",Myself)
	!HasItem("harm",Myself)
	!HasItem("critical",Myself)
	!HasItem("serious",Myself)
	!HasItem("slaylive",Myself)
THEN DO
       Action(SpellMyself,CLERIC_FLAME_BLADE)
END

IF TRIGGER
          RequireBlock(IsIWDDivine)
          IgnoreBlock(Demivrgvs)
          IgnoreBlock(HasL4)
          Target([PC])
          Range(scstarget,6)
          !Class(Myself,FIGHTER_ALL)
		  !HasItem("BCLAW",Myself)
	!HasItem("harm",Myself)
	!HasItem("critical",Myself)
	!HasItem("serious",Myself)
	!HasItem("slaylive",Myself)
THEN DO
       Action(SpellMyself,CLERIC_BEAST_CLAW)
END

IF TRIGGER
          RequireBlock(God:malar)
          Target([PC])
          Range(scstarget,6)
          !Class(Myself,FIGHTER_ALL)
		  !HasItem("BCLAW",Myself)
	!HasItem("harm",Myself)
	!HasItem("critical",Myself)
	!HasItem("serious",Myself)
	!HasItem("slaylive",Myself)
THEN DO
       Action(SpellRESMyself,"%spl_malar_beast_claw%")
END

IF TRIGGER
          RequireBlock(God:selune)
          Target([PC])
          Range(scstarget,6)
          !Class(Myself,FIGHTER_ALL)
		  !HasItem("MOONBLA",Myself)
		!HasItem("harm",Myself)
		!HasItem("critical",Myself)
		!HasItem("serious",Myself)
		!HasItem("slaylive",Myself)
THEN DO
       Action(SpellRESMyself,"%INNATE_CLERIC_MOONBLADE%")
END

IF TRIGGER
          RequireBlock(God:lathander)
          Target(NearestEnemyOf(Myself))
		  !CheckSpellState(Myself,LATHANDER_BOON)
          Range(scstarget,6)
THEN DO
       Action(SpellRESMyself,"%LATHANDER_BOON%")
END

IF TRIGGER
          RequireBlock(God:helm)
          Target([PC])
          Range(scstarget,6)
          !Class(Myself,FIGHTER_ALL)
		  !HasItem("sw1hseek",Myself)
		  !HasItem("%itm_enhanced_seeking_sword%",Myself)
THEN DO
       Action(SpellRESMyself,"%HELM_SEEKING_SWORD%")
END

IF TRIGGER
          RequireBlock(IsIWDDivine)
          IgnoreBlock(Demivrgvs)
          RequireBlock(HasL4)
          IgnoreBlock(ImprovedShapeshift)
          Target([PC])
          Range(scstarget,6)
          !Class(Myself,FIGHTER_ALL)
THEN DO
       Action(SpellMyself,CLERIC_BEAST_CLAW)
END

IF TRIGGER
          IgnoreBlock(Demivrgvs)
          IgnoreBlock(HasL4)
          Target([PC])
          Range(scstarget,6)
          !Class(Myself,FIGHTER_ALL)
THEN DO
       Action(SpellMyself,CLERIC_FLAME_BLADE)
END

IF TRIGGER
          RequireBlock(Demivrgvs)
          Target([PC])
          Range(scstarget,20)
          !Class(Myself,FIGHTER_ALL)
THEN DO
       Action(SpellMyself,CLERIC_SPIRITUAL_HAMMER)
END

IF TRIGGER
          Target([PC])
          Range(scstarget,6)
          !Class(Myself,FIGHTER_ALL)
THEN DO
       Action(SpellMyself,CLERIC_SHILLELAGH)
END


////////////////////////////////////////////////////////////////////
////////	Tyr divine favor
///////////////////////////////////////////////////////////////////

IF TRIGGER
	RequireBlock(God:tyr)
	TargetBlock(EnemiesInOrderShort)
	Range(scstarget,6)
	TriggerBlock(Disabled|Enemy)
	!GlobalTimerNotExpired("divinefavor","LOCALS")
THEN DO
	Action(SpellRESMyself,"%TYR_DIVINE_FAVOR%")
	SetGlobalTimer("divinefavor","LOCALS",12)
END

////////////////////////////////////////////////////////////////////
////////	This is the point to use a "Command-and-attack"
////////	routine on anyone in strike range
///////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsInOrderShort)
	Range(scstarget,6)
	TriggerBlock(Disabled|Hold|MinorGlobe|MR|Enemy)
THEN DO
	Action(Spell,CLERIC_COMMAND)
	SetGlobalTimer("killhelpless","LOCALS",6)
END

