INCLUDE ~%scsroot%/lib/ai_wrap.tph~

DEFINE_ACTION_FUNCTION druid BEGIN

    LAF run STR_VAR file=creature_swaps END

       // make flunky-summoning script

<<<<<<<< .../stratagems-inline/dw#takiy.baf
IF
	!Global("SpawnedApprentices","LOCALS",1)
	!See("dw#takiy") // sanity check against bugs that might occur from repeatedly flipping the difficulty slider
THEN
	RESPONSE #100
		CreateCreature("dw#takiy",[-1.-1],0)
		CreateCreature("dw#takiy",[-1.-1],0)
		SetGlobal("SpawnedApprentices","LOCALS",1)
END
>>>>>>>>

       LAF install STR_VAR file=dw#takiy.baf inline=yes END
       ACTION_FOR_EACH druid IN izefi amara BEGIN
          LAF clone_script STR_VAR script=~dw#takiy=>dw#%druid%~ END
          LAF swap_text STR_VAR files= ~dw#%druid%.bcs~ swaps=~takiy=>%druid%~ END
       END

       // takiyah: L9, DV, flunky script
       
       MAKE_PATCH
          level=>9
          enforce_charclass=>is_bg1
          insert_script_high=>gpshout
          insert_script_high'=>dw#takiy
          specifics=>90
       END
       LAF diff_based_clone STR_VAR creature= ~%tutu_var%takiyah~ hardname=~dw#takhd~ edits=patch_data editstring=~dv=>takiyah~ END

       // izefia: likewise

       MAKE_PATCH
          level=>9
          enforce_charclass=>is_bg1
          insert_script_high=>gpshout
          insert_script_high'=>dw#izefi
          specifics=>90
       END
       LAF diff_based_clone STR_VAR creature= ~%tutu_var%izefia~ hardname=~dw#izehd~ edits=patch_data editstring=~dv=>izefia~ END

       // amarande: 11th level, DV, flunky, shoutdlg

       MAKE_PATCH
          level=>11
          enforce_charclass=>is_bg1
          insert_script_high=>gpshout
          insert_script_high'=>dw#amara
          specifics=>90
       END
       LAF diff_based_clone STR_VAR creature=~%tutu_var%amaran~ hardname=~dw#amahd~ edits=patch_data editstring=~dv=>amara~ END

       // make remaining core druids 9th level

       MAKE_PATCH
          level=>9
          enforce_charclass=>is_bg1
       END
       LAF diff_based_clone STR_VAR creature= ~%tutu_var%corsone~ hardname=~dw#corhd~ edits=patch_data END
       LAF diff_based_clone STR_VAR creature= ~%tutu_var%osmadi~ hardname=~dw#osmhd~ edits=patch_data END

       // flunkies
       
       MAKE_PATCH
          add_items=>dw#attac
          insert_script_high=>gpshout
          specifics=>90
       END
       LAF clone_creature STR_VAR creature= ~%tutu_var%druid3=>dw#takiy~
                                  edits=patch_data
       END
       MAKE_PATCH
          add_items=>dw#attac
          insert_script_high=>gpshout
          specifics=>90
          kit=>SHAPESHIFTER
          add_items=>"dw#shnw1(WEAPON2)"
       END
       LAF clone_creature STR_VAR creature= ~%tutu_var%druid3=>dw#amara~
                                  edits=patch_data
       END



END

