/////////////////////////////////////////////////////////////////////////
///////       Aec'letec
/////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/fiend_definitions.ssl)
INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/initial.ssl)  // rest-and-recover, mill in confusion, etc.

/////////////////////////////////////////////////////////////////////////
///////       Redefine magic to include death gaze
/////////////////////////////////////////////////////////////////////////

BEGIN_ACTION_DEFINITION
	Name(SpellNoDec)
	TRIGGER
		!GlobalTimerNotExpired("castspell","LOCALS")
	ACTION
	RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		SpellNoDec(scstarget,scsargument1)
		ForceSpellRES("%tanari_gaze%",[GOODCUTOFF])
	RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		SpellNoDec(scstarget,scsargument1)
		ForceSpellRES("%tanari_gaze%",SecondNearest([GOODCUTOFF]))
	RESPONSE #scsprob1
		SetGlobalTimer("castspell","LOCALS",6)
		SpellNoDec(scstarget,scsargument1)
		ForceSpellRES("%tanari_gaze%",ThirdNearest([GOODCUTOFF]))
END

//////////////////////////////////////////////////////////////////////////////////////
///	Consider moving to a more interesting location in the battle
//////////////////////////////////////////////////////////////////////////////////////

INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/jump.ssl)

//////////////////////////////////////////////////////////////////////////////////////
///	Consider a Dispel Magic
//////////////////////////////////////////////////////////////////////////////////////
VARIABLE(FirstDispel=True)
INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/dispel.ssl)


//////////////////////////////////////////////////////////////////////////////////////
///	Silence mages and clerics (we trade the at-will PnP version for the -5-to-saves
///	BG2 version)
//////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCSpellcasters)
	TriggerBlock(Enemy|Disabled|Silence)
	!GlobalTimerNotExpired("silence","LOCALS")
THEN DO
	Combine()
	SetGlobalTimer("silence","LOCALS",1000)
	Action(SpellNoDec,TANARI_SILENCE|200|300)
END

//////////////////////////////////////////////////////////////////////////////////////////
///	Paralysis
//////////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	TargetBlock(PCsPreferringStrong)
	TriggerBlock(MR|Hold|SIEnchantment|Enemy|Helpless)
	CheckStatGT(scstarget,5,SAVEVSSPELL)
THEN DO
	Combine()
	Action(SpellNoDec,TANARI_PARALYZE|100|300)
END

//////////////////////////////////////////////////////////////////////////////////////
///	Vampiric touch
//////////////////////////////////////////////////////////////////////////////////////

IF TRIGGER
	ConditionalTargetBlock(PCsInOrder;HPPercentLT(scstarget,30))
	TargetBlock(PCsInOrderShort)
	TriggerBlock(Enemy|Disabled|MR|SINecromancy|SpellTurn|Helpless)
	Range(scstarget,10)
THEN DO
	Combine()
	Action(SpellNoDecRES,"dw#demvt")
END

//////////////////////////////////////////////////////////////////////////////
///	Combat
//////////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////
////	At least *consider* chewing on paralysed victims
///////////////////////////////////////////////////////////////////////

IF TRIGGER
	Target(NearestEnemyOfType([PC]))
	StateCheck(scstarget,STATE_HELPLESS)
	TriggerBlock(Enemy)
THEN DO
	Action(Attack|100|100)
END

//////////////////////////////////////////////////////////////////////////////
////	Core combat block
///////////////////////////////////////////////////////////////////////////////

BEGIN LOOP(MyWeaponStrength||Null)
BEGIN LOOP(MyWeaponDamageType||Null)
INCLUDE FILE(%MOD_FOLDER%/genai/ssl/dw#wtacor.ssl)
INCLUDE FILE(%MOD_FOLDER%/genai/ssl/move.ssl)
INCLUDE FILE(%MOD_FOLDER%/fiend/ssl/chase.ssl)
END LOOP
END LOOP
