// These various spells are enemy-only, so we simulate their effect on Carsa manually

// sleep effects
IF
		SpellCastOnMe([GOODCUTOFF],CLERIC_GREATER_COMMAND)
THEN
	RESPONSE #100
		SetGlobal("dw_carsa_disabled","MYAREA",1)
		SmallWait(15)
		ApplySpellRES("dw#ckslp",Myself)
END
	

// confusion effects

IF
	OR(4)
		SpellCastOnMe([GOODCUTOFF],CLERIC_CONFUSION)
		SpellCastOnMe([GOODCUTOFF],WIZARD_CONFUSION)
		SpellCastOnMe([GOODCUTOFF],WIZARD_CHAOS)
		SpellCastOnMe([GOODCUTOFF],CLERIC_RIGID_THINKING)
THEN
	RESPONSE #100
		SetGlobal("dw_carsa_disabled","MYAREA",1)
		SmallWait(15)
		ApplySpellRES("dw#ckcnf",Myself)
END


// paralyzation effects

IF
	OR(5)
		SpellCastOnMe([GOODCUTOFF],WIZARD_EMOTION_HOPELESSNESS)
		SpellCastOnMe([GOODCUTOFF],CLERIC_HOLD_PERSON)
		SpellCastOnMe([GOODCUTOFF],WIZARD_HOLD_PERSON)
		SpellCastOnMe([GOODCUTOFF],WIZARD_HOLD_MONSTER)
		SpellCastOnMe([GOODCUTOFF],WIZARD_POWER_WORD_STUN)
THEN
	RESPONSE #100
		SetGlobal("dw_carsa_disabled","MYAREA",1)
		SmallWait(15)
		ApplySpellRES("dw#ckhld",Myself)
END

// attacked

IF
	AttackedBy([GOODCUTOFF],0)
THEN
	RESPONSE #100
		SmallWait(15)
		Enemy()
END

IF
	Allegiance(Myself,ENEMY)
	!Dead("kahrk")
	Global("dw_drop_bottle","MYAREA",0)
	Global("dw_carsa_disabled","MYAREA",0)
THEN
	RESPONSE #100	
		SetGlobal("dw_drop_bottle","MYAREA",1)
		DisplayStringHead(Myself,%carsa_say_strref_8%)
		ReallyForceSpellRES("dw#kahrk",Myself)
		SmallWait(2)
		Kill(Myself)
END

// during the battle

IF
	Detect([ENEMY])
	!Dead("kahrk")
THEN
	RESPONSE #100
		DisplayStringHead(Myself,@10000)
		RunAwayFrom([ENEMY],120)
END

// after the battle

IF
	Dead("kahrk")
	!Allegiance(Myself,ENEMY)
	!Detect([PC])
THEN
	RESPONSE #100
		MoveToObject(Player1)
END


IF
	Dead("kahrk")
	!Allegiance(Myself,ENEMY)
	Detect([PC])
THEN
	RESPONSE #100
		Wait(2)
		Dialog([PC])
END

// generic babble

IF
	!Dead("kahrk")
	!Detect("kahrk")
	!NumTimesTalkedTo(0)
	!GlobalTimerNotExpired("babble","LOCALS")
	Global("dw_drop_bottle","MYAREA",0)
THEN
	RESPONSE #100
		SetGlobalTimer("babble","LOCALS",7)
		DisplayStringHead(Myself,@10002)
		RandomWalk()
	RESPONSE #100
		SetGlobalTimer("babble","LOCALS",8)
		DisplayStringHead(Myself,@10003)
		RandomWalk()
	RESPONSE #100
		SetGlobalTimer("babble","LOCALS",9)
		DisplayStringHead(Myself,@10004)
		RandomWalk()
	RESPONSE #100
		SetGlobalTimer("babble","LOCALS",10)
		DisplayStringHead(Myself,@10005)
		RandomWalk()
END

// block combat script go-hostile block

IF
	!Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		NoAction()
END

	