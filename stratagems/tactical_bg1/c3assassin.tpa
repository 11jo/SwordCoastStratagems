DEFINE_ACTION_FUNCTION c3assassin BEGIN

   // delete the Gullykin assassins
   
   ACTION_FOR_EACH assassin IN drakar halaca morvin molkar BEGIN
      MAKE_PATCH
         delete_entries_inline=>~type=>actor match=>actor_resource check=>%tutu_var%%assassin%~
      END
      LAF edit_area STR_VAR area=EVALUATE_BUFFER ~%Gullykin%~ END
   END

   // delete the amazons

   ACTION_FOR_EACH assassin IN lamalh zeela telka maneir BEGIN
      MAKE_PATCH
         delete_entries_inline=>~type=>actor match=>actor_resource check=>%tutu_var%%assassin%~
      END
      LAF edit_area STR_VAR area=EVALUATE_BUFFER ~%ValleyoftheTombs%~ END
   END

   // extend the main script

   LAF extend STR_VAR file=baldur bottom=assassinadd location=resource END

   // create scripts for assassins
   
   LAF install STR_VAR file=dw#c3as1.baf location=resource END
   LAF clone_script STR_VAR script=~dw#c3as1=>dw#c3as2~ END
   LAF swap_text STR_VAR files=dw#c3as2.bcs swaps=~dmwwC3asshostileA=>dmwwC3asshostileB~ END

   // install scripts

   LAF edit_creature STR_VAR creature=~molkar drakar halaca morvin~ tv=yes editstring=~insert_script_high=>dw#c3as1~ END
   LAF edit_creature STR_VAR creature=~maneir telka zeela lamalh~ tv=yes editstring=~insert_script_high=>dw#c3as1~ END

   // sort out allegiance

   LAF edit_creature STR_VAR creature=~lamalh molkar~ tv=yes editstring=~allegiance=>NEUTRAL~ END
   LAF edit_creature STR_VAR creature=~maneir telka zeela drakar halaca morvin~ tv=yes editstring=~allegiance=>ENEMY~ END

   // sort out dialogs

   COPY_EXISTING ~%tutu_var%molkar.dlg~ ~override~
      DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY CASE_INSENSITIVE ~ActionOverride("drakar",Enemy())~ ~CreateCreatureOffscreen("%tutu_var%drakar",0)~
      REPLACE_TEXTUALLY CASE_INSENSITIVE ~ActionOverride("halacan",Enemy())~ ~CreateCreatureOffscreen("%tutu_var%halaca",0)~
      REPLACE_TEXTUALLY CASE_INSENSITIVE ~ActionOverride("morvin",Enemy())~ ~CreateCreatureOffscreen("%tutu_var%morvin",0)~
      END
   BUT_ONLY

   COPY_EXISTING ~%tutu_var%lamalh.dlg~ ~override~
      DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY CASE_INSENSITIVE ~ActionOverride("zeela",Enemy())~ ~CreateCreatureOffscreen("%tutu_var%zeela",0)~
      REPLACE_TEXTUALLY CASE_INSENSITIVE ~ActionOverride("telka",Enemy())~ ~CreateCreatureOffscreen("%tutu_var%telka",0)~
      REPLACE_TEXTUALLY CASE_INSENSITIVE ~ActionOverride("maneira",Enemy())~ ~CreateCreatureOffscreen("%tutu_var%maneir",0)~
      END
   BUT_ONLY

   // script tweak
   
   LAF swap_text STR_VAR files=~molkar.bcs lamalha.bcs~ tv=yes swaps=~SmallWait(2)=>SmallWait(2)FaceObject([PC])~ END

END


