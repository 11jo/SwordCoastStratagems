DEFINE_ACTION_FUNCTION c3assassin BEGIN

   LAF c3assassin_shared END

   ACTION_IF GAME_IS bgt BEGIN
      ACTION_IF FILE_EXISTS_IN_GAME ~fw01230.bcs~ BEGIN
         OUTER_SPRINT version new
      END ELSE BEGIN
         OUTER_SPRINT version old
      END
   END ELSE BEGIN
         OUTER_SPRINT version new
   END
   
   LAF action_check_ini STR_VAR ini=Force_Old_Assassin_Spawn RET value=value END
   ACTION_IF value BEGIN
      OUTER_SPRINT version old
   END

   LAF ~c3assassin_%version%~ END

END

///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION c3assassin_shared BEGIN


   // delete the Gullykin assassins
   
   ACTION_FOR_EACH assassin IN drakar halaca morvin molkar BEGIN
      MAKE_PATCH
         delete_entries_inline=>~type=>actor match=>actor_resource check=>%tutu_var%%assassin%~
      END
      LAF edit_area STR_VAR area=EVALUATE_BUFFER ~%Gullykin%~ END
   END

   // delete the amazons

   ACTION_FOR_EACH assassin IN lamalh zeela telka maneir BEGIN
      MAKE_PATCH
         delete_entries_inline=>~type=>actor match=>actor_resource check=>%tutu_var%%assassin%~
      END
      LAF edit_area STR_VAR area=EVALUATE_BUFFER ~%ValleyoftheTombs%~ END
   END

   // sort out allegiance

   LAF edit_creature STR_VAR creature=~lamalh molkar~ tv=yes editstring=~allegiance=>NEUTRAL strip_script=>shout~ END
   LAF edit_creature STR_VAR creature=~maneir telka zeela drakar halaca morvin~ tv=yes editstring=~allegiance=>ENEMY strip_script=>shout~ END

   // script tweak
   
   LAF swap_text STR_VAR files=~molkar.bcs lamalha.bcs~ tv=yes swaps=~SmallWait(2)=>SmallWait(2)FaceObject([PC])~ END

   // create scripts for assassins
   
   LAF ssl_to_bcs STR_VAR script=dw#c3as1 location=ssl END
   LAF clone_script STR_VAR script=~dw#c3as1=>dw#c3as2~ END
   LAF swap_text STR_VAR files=dw#c3as2.bcs swaps=~dmwwC3asshostileA=>dmwwC3asshostileB~ END

   // install scripts

   LAF edit_creature STR_VAR creature=~drakar halaca morvin~ tv=yes editstring=~insert_script_high=>dw#c3as1~ END
   LAF edit_creature STR_VAR creature=molkar tv=yes editstring=EVALUATE_BUFFER ~insert_script=>"dw#c3as1 after %tutu_var%molkar"~ END
   LAF edit_creature STR_VAR creature=~maneir telka zeela~ tv=yes editstring=~insert_script_high=>dw#c3as2~ END
   LAF edit_creature STR_VAR creature=lamalh tv=yes editstring=EVALUATE_BUFFER ~insert_script=>"dw#c3as2 after %tutu_var%lamalha"~ END


END

///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////


DEFINE_ACTION_FUNCTION c3assassin_old BEGIN



   // sort out dialogs

   COPY_EXISTING ~%tutu_var%molkar.dlg~ ~override~
      DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY CASE_INSENSITIVE ~ActionOverride("drakar",Enemy())~ ~CreateCreatureOffscreen("%tutu_var%drakar",0)~
      REPLACE_TEXTUALLY CASE_INSENSITIVE ~ActionOverride("halacan",Enemy())~ ~CreateCreatureOffscreen("%tutu_var%halaca",0)~
      REPLACE_TEXTUALLY CASE_INSENSITIVE ~ActionOverride("morvin",Enemy())~ ~CreateCreatureOffscreen("%tutu_var%morvin",0)~
      END
   BUT_ONLY

   COPY_EXISTING ~%tutu_var%lamalh.dlg~ ~override~
      DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY CASE_INSENSITIVE ~ActionOverride("zeela",Enemy())~ ~CreateCreatureOffscreen("%tutu_var%zeela",0)~
      REPLACE_TEXTUALLY CASE_INSENSITIVE ~ActionOverride("telka",Enemy())~ ~CreateCreatureOffscreen("%tutu_var%telka",0)~
      REPLACE_TEXTUALLY CASE_INSENSITIVE ~ActionOverride("maneira",Enemy())~ ~CreateCreatureOffscreen("%tutu_var%maneir",0)~
      END
   BUT_ONLY

   // extend the main script

   LAF extend STR_VAR file=baldur bottom=assassinadd location=ssl ssl=yes sslvariables="OldVersion=True" END


END

///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////


DEFINE_ACTION_FUNCTION c3assassin_new BEGIN

<<<<<<<< .../stratagems-inline/assassin_data.2da
area                 xloc_1 yloc_1 orient_1 xloc_2 yloc_2 orient_2 xloc_3 yloc_3 orient_3 xloc_4 yloc_4 orient_4
Encounter_Plains1    495    310    0        779    308    0        340    528    0        939    563    0
Encounter_Plains2    923    255    0        533    279    0        346    625    0        816    742    0
Encounter_Canyon1    632    310    0        597    346    0        565    382    0        653    363    0
Encounter_Canyon2    456    193    0        513    253    0        330    335    0        617    326    0
Encounter_Cliff1     579    143    0        821    193    0        919    150    0        688    180    0
Encounter_Cliff2     814    202    0        899    226    0        714    262    0        570    198    0
Encounter_Grassland1 382    656    0        468    374    0        662    342    0        898    561    0
Encounter_Grassland2 575    260    0        371    575    0        451    714    0        646    733    0
Encounter_Forest2    710    478    0        569    592    0        884    525    0        824    684    0
Encounter_Road       382    656    0        468    374    0        662    342    0        898    561    0
>>>>>>>>

    COPY - ~.../stratagems-inline/assassin_data.2da~ ~.../stratagems-inline~
         READ_2DA_ENTRIES_NOW assassin_data 0
         COUNT_2DA_COLS col_count
         FOR (i=1;i<assassin_data;i=i+1) BEGIN
            FOR (j=0;j<col_count;j=j+1) BEGIN
               READ_2DA_ENTRY_FORMER assassin_data 0 j var
               READ_2DA_ENTRY_FORMER assassin_data i j val
               SPRINT EVALUATE_BUFFER ~%var%~ ~%val%~
            END
            INNER_ACTION BEGIN
               LAF extend_area_script STR_VAR area=EVALUATE_BUFFER ~%%area%%~ top=assassin_random  sslvariables="OldVersion=False" location=resource END
            END
         END


   // extend the main script

   LAF extend STR_VAR file=baldur bottom=assassinadd location=ssl ssl=yes sslvariables="OldVersion=False" END







END


