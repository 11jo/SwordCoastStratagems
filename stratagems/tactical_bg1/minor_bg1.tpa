INCLUDE ~%scsroot%/lib_stratagems/ai_wrap.tph~

DEFINE_ACTION_FUNCTION minor_bg1 BEGIN

    LAF action_check_ini STR_VAR ini=integrated RET value=value END
    ACTION_IF value BEGIN
       LAF laurel END
    END ELSE BEGIN
       ACTION_FOR_EACH function IN laurel cythandria slythe jenkal minor_misc BEGIN
          LAF ~%function%~ END
       END
    END


END

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////  Makes the paladin's gibberling hunt slightly less ridiculous...
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION laurel BEGIN

     LAF install STR_VAR file=laureladd.d location=resource END
     LAF extend_area_script STR_VAR area=EVALUATE_BUFFER ~%BearRiver%~ bottom=laureladd ssl=yes location=ssl END

END

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////// Cythandria: she's too smart to get a couple of ogres as all her help against the party. Replace them with stone golems
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION cythandria BEGIN

   ACTION_IF enhanced_edition=0 BEGIN
        MAKE_PATCH
           say_both_names=>21113
           dv=>arghh
           quick_effect=>120
           remove_items=>immune2
        END
        LAF clone_creature STR_VAR creature=EVALUATE_BUFFER ~golsto01=>%tutu_var%arghh~ END
        MAKE_PATCH
           say_both_names=>21114
           dv=>ughh
           quick_effect=>120
           remove_items=>immune2
        END
        LAF clone_creature STR_VAR creature=EVALUATE_BUFFER ~golsto01=>%tutu_var%ughh~ END
   END
END

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////// Slythe: one more level gets him to quadruple-damage territory.
///////////// Also, he has a Potion of Magic Blocking and a cloak of non-detection
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION slythe BEGIN

       MAKE_PATCH
          level=>9
          add_items=>~%tutu_var%potn33 %tutu_var%clck06~
          enforce_charclass=>is_bg1
       END
       LAF edit_creature STR_VAR creature=slythe tv=yes END
END

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Jenkal (the halfling traitor): raise him from 4/4 to 6/6; let him flee the battle
// and turn up in the ogre mage's home, and in due course with the sewer ogre mage
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION jenkal BEGIN
       MAKE_PATCH
          level=>6
          level2=>6
          insert_script_high=>dw#jenka
          xp_value=>650
          enforce_charclass=>is_bg1
       END
       LAF edit_creature STR_VAR creature=slythe tv=yes END

       LAF install STR_VAR files=~dw#jenka.baf jenkal.d~ location=resource END
       
       LAF extend_area_script STR_VAR area=EVALUATE_BUFFER ~%FirewineRuins%~ bottom=ar5201_jenkal location=resource END
       LAF extend_area_script STR_VAR area=EVALUATE_BUFFER ~%ESewers%~ bottom=ar0226_jenkal location=resource END



       MAKE_PATCH
          type=>trigger
          match=>trigger_dest
          check=>~%FirewineRuins%~
          say_trigger_info=>22507
          trigger_type=>1
       END
       LAF edit_area STR_VAR area=EVALUATE_BUFFER ~%Gullykin_JenkalsHouse_Cellar%~ editstring=~patch_conditionally=>patch_data~ END
END

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////  misc. level adjustments
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION minor_misc BEGIN


     // Raises Tamoko's level slightly
     LAF edit_creature STR_VAR creature=tamoko tv=yes editstring=~level=>14 enforce_charclass=>is_bg1~ END

     // Sunin: is a self-defined "master mage", ought to be kinda more impressive....
     LAF edit_creature STR_VAR creature=sunin tv=yes editstring=~level=>11 enforce_charclass=>is_bg1~ END

     // Hairtooth and Gnarl become 5th level fighters: +18 hp, level 5 THAC0 16. And they get strength 18/95
     LAF edit_creature STR_VAR creature=~hairto gnarl~ tv=yes editstring=~level=>5 str=>18 str_ex=>95 enforce_charclass=>is_bg1~ END

     // Micro-tweak to the Amazons: raise the archers' levels to 5 to buy sleep immunity
     LAF edit_creature STR_VAR creature=~maneir telka~ tv=yes editstring=~level=>5 enforce_charclass=>is_bg1~ END


END


