INCLUDE ~%scsroot%/lib_stratagems/ai_wrap.tph~

DEFINE_ACTION_FUNCTION c3end BEGIN

    LAF action_check_ini STR_VAR ini=minimal_bandits RET minimal_bandits=value END

    ACTION_FOR_EACH function IN camp_are camp_cre camp_bcs tent_are tent_cre tent_bcs BEGIN
       LAF ~%function%~ END
    END





END

//////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION camp_are BEGIN
   
   ACTION_IF GAME_IS bgt BEGIN
      OUTER_SPRINT doorname Door8701
   END ELSE BEGIN
      OUTER_SPRINT doorname Door1901
   END

   MAKE_PATCH
   // party-required for tent entrance
   patch_cond_inline''=>~type=>trigger
                         match=>trigger_name
                         check=>%doorname%
                         party_required=>1~
   END
   LAF edit_area STR_VAR area=EVALUATE_BUFFER ~%BanditCamp%~ END

   LAF extend_area_script STR_VAR area=EVALUATE_BUFFER ~%BanditCamp%~ bottom=morebandits location=ssl ssl=yes END

   ACTION_IF !minimal_bandits BEGIN
          LAF extend_area_script STR_VAR area=EVALUATE_BUFFER ~%BanditCamp%~ top=sealtent location=ssl ssl=yes END
   END

END

//////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////


DEFINE_ACTION_FUNCTION camp_cre BEGIN



/// the new bandits

     MAKE_PATCH
        strip_script=>~dw#prsht %tutu_var%hobgobc~
        insert_script_high'=>dw#prsht
        insert_script_high=>~%tutu_var%hobgobc~
        allegiance=>NEUTRAL
     END
     LAF clone_creature STR_VAR creature=EVALUATE_BUFFER ~%tutu_scripth%obelite=>dw#c3che~ edits=patch_data END

     MAKE_PATCH
        strip_script=>~dw#prsht %tutu_var%bandit dw#swpup~
        insert_script_high'=>dw#prsht
        insert_script_high=>~%tutu_var%bandit~
        allegiance=>NEUTRAL
     END
     LAF clone_creature STR_VAR creature=EVALUATE_BUFFER ~%tutu_scripti%ronelit=>dw#c3cbt~ edits=patch_data END


// Give hobgoblins something to say

   ACTION_IF !minimal_bandits BEGIN

      LAF install STR_VAR file=dw#hobgc.d location=resource END
      LAF edit_creature STR_VAR creature=EVALUATE_BUFFER ~dw#c3che %tutu_var%hobgobc~ editstring=~dialog=>dw#hobgc~ END

   END
   
// Block Credus's dialog if camp is hostile


   <<<<<<<< .../stratagems-inline/dw#credu.baf
      IF
        !Global("JoinedBandits","GLOBAL",1)
        Range([PC],15)
        See([PC])
	NumTimesTalkedTo(0)
THEN
	RESPONSE #100
		Dialogue([PC])
END
   >>>>>>>>

    LAF install STR_VAR files=~dw#credu.baf~ inline=yes END
    LAF edit_creature STR_VAR creature=credus tv=yes editstring=~swap_script=>"initdlg=>dw#credu"~ END

END

//////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////


DEFINE_ACTION_FUNCTION camp_bcs BEGIN

      // slightly more reliable bandit go-hostile script
      LAF ssl_to_bcs STR_VAR script=bandit location=~tactical_bg1/ssl~ rename_to=EVALUATE_BUFFER ~%tutu_var%bandit~ END
      LAF extend STR_VAR file=hobgobc tv=yes bottom=bandit location=ssl ssl=yes END

      // extend it to a couple more bandits
      
      LAF edit_creature STR_VAR creature=~credus ardeno taugos~ tv=yes editstring=EVALUATE_BUFFER ~strip_script=>%tutu_var%bandit insert_script_high=>%tutu_var%bandit~ END

      ACTION_IF !minimal_bandits BEGIN

        // Taugos go-hostile block
      
        LAF ssl_to_bcs STR_VAR script=dw#taug location=~tactical_bg1/ssl~ END
        LAF edit_creature STR_VAR creature=taugos tv=yes editstring=~strip_script=>dw#taug insert_script_high=>dw#taug~ END

        // Taugos dialog

        LAF install STR_VAR file=dw#taug.d location=resource END
     END

END

//////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////


DEFINE_ACTION_FUNCTION tent_bcs BEGIN
   LAF ssl_to_bcs STR_VAR script=dw#c3hst location=ssl END
   LAF extend_area_script STR_VAR area=EVALUATE_BUFFER ~%BanditCamp_RaemonsTent%~ bottom=tazoktent location=ssl ssl=yes END
END

//////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////


DEFINE_ACTION_FUNCTION tent_are BEGIN

   MAKE_PATCH
   // move Britik and Raemon back
      patch_cond_inline=>~type=>actor
                          match=>actorname
                          check=>britik
                          actor_x_coord=>401
                          actor_y_coord=>273~
      patch_cond_inline'=>~type=>actor
                          match=>actorname
                          check=>raemon
                          actor_x_coord=>350
                          actor_y_coord=>400~
   // make exit party-required
      patch_cond_inline''=>~type=>trigger
                            match=>trigger_type
                            check=>2
                            party_required=>1~
   END
   LAF edit_area STR_VAR area=EVALUATE_BUFFER ~%BanditCamp_RaemonsTent%~ END


END

//////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////


DEFINE_ACTION_FUNCTION tent_cre BEGIN

   // britik

   MAKE_PATCH
      insert_script_high=>dw#c3hst
      level=>5
      class=>FIGHTER
      enforce_charclass=>is_bg1
      add_items=>~%tutu_var%potn12~
   END
   LAF diff_based_clone STR_VAR creature=EVALUATE_BUFFER ~%tutu_var%britik~ hardname=~dwbrithd~ edits=patch_data END

   //  Raemon

   ACTION_IF !minimal_bandits BEGIN
      LAF install STR_VAR file=raemonadd.d location=resource END
   END

   MAKE_PATCH
      swap_script=>~%tutu_var%initdlg=>shoutdlg~
      strip_script=>dw#c3hst
      insert_script_high=>dw#c3hst
      dv=>raemon
   END
   LAF edit_creature STR_VAR creature=raemon tv=yes END
   
   // Hakt, Venkt
   
   MAKE_PATCH
      strip_script=>dw#c3hst
      insert_script_high=>dw#c3hst
   END
   LAF edit_creature STR_VAR creature=~hakt venkt~ tv=yes END

   // non-hostile-at-start BTE and hobgobs
   
   MAKE_PATCH
      allegiance=>NEUTRAL
      strip_scs_scripts=>null
      strip_script=>~dw1range dw#c3hst dw#prsht dw#swpup~
      insert_script_high=>dw1range
      insert_script_high'=>dw#c3hst
      insert_script_high''=>dw#prsht
   END
   LAF clone_creature STR_VAR creature=EVALUATE_BUFFER ~%tutu_scripth%obelite=>dw#c3hb %tutu_scripti%ronelit=>dw#c3bt~ edits=patch_data END

END


