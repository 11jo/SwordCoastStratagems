INCLUDE ~%MOD_FOLDER%/lib/ai_wrap.tph~
LAF include STR_VAR file=fiend_shared.tph locbase=fiend END

DEFINE_ACTION_FUNCTION demoncult BEGIN

    LAF define_difficulty STR_VAR type=fiend RET difficulty_variable END

    // set up Aec'letec

    LAF shared_fiend_resources END
    LAF edit_creature INT_VAR tv=1 STR_VAR creature=tanar editstring=~nabassu=>null~ END
	ACTION_IF GAME_IS "bgt eet" BEGIN
		OUTER_SPRINT tanari_gaze BGIN996
	END ELSE BEGIN
		OUTER_SPRINT tanari_gaze "%TANARI_DEATH_GAZE%"
	END
    ACTION_IF FILE_EXISTS_IN_GAME "tanar2.cre" BEGIN // ee 2.6 - need to keep the bit of the tanar'ri combat script that handles death effects
		LAF DELETE_SCRIPT_BLOCK STR_VAR script=tanari match="HPGT(Myself,1)" END
		LAF extend STR_VAR script=tanari bottom=tanarri_block location=resource END
		LAF extend STR_VAR script=tanari bottom=tanari location=ssl variables="ForceTeleport=True" ssl=yes END
	END ELSE BEGIN // otherwise just overwrite tanar'ri combat script
		LAF ssl_to_bcs STR_VAR script=tanari location=~ssl~ variables="ForceTeleport=True" rename_to=~%tutu_scriptbg%tanari%eet_var%~ END
	END
    // resources
    
    <<<<<<<< .../stratagems-inline/dw#invis.baf
    IF
	OnCreation()
THEN
	RESPONSE #100
		ApplySpellRES("dw#silin",Myself)
END
   >>>>>>>>

   LAF install STR_VAR file=dw#invis.baf inline=yes END
   LAF clone_script STR_VAR script= ~dw#invis=>%tutu_scriptc%ultmage dw#invis=>%tutu_var%cultthf~ END

   // Tracea becomes 11th level
   
   MAKE_PATCH
      level=>11
      enforce_charclass=>is_bg1
      add_items=>dw#noinv
      immunity_to_spell=>~%TANARI_PARALYZE%~
   END
   LAF edit_creature INT_VAR tv=1 STR_VAR creature=tracea edits=patch_data END

   // enforcer becomes 10/10 level
   
   MAKE_PATCH
      level=>10
      level2=>10
      enforce_charclass=>~strict is_bg1~
   END
   LAF edit_creature INT_VAR tv=1 STR_VAR creature=cultt1 edits=patch_data END

   // give the Aec'letec cultists some combat ability

   MAKE_PATCH
      script_override=>~%tutu_scriptw%tasight~
      enforce_charclass=>~strict is_bg1~
      add_items=>dw#move0
      immunity_to_spell=>~%TANARI_PARALYZE%~
   END
   LAF edit_creature INT_VAR tv=1 STR_VAR creature=~cultd1 cultd2 cultd3 cultd4 cultd5 cultd6~ edits=patch_data END

   // cult wizards get better wands
   
   LAF edit_creature INT_VAR tv=1 STR_VAR creature=cult2 editstring=~swap_items=>"%tutu_var%wand08=>%tutu_var%wand04"~ END

END
