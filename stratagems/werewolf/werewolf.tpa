INCLUDE ~%scsroot%/lib_stratagems/ai_wrap.tph~

DEFINE_ACTION_FUNCTION werewolf BEGIN

   LAF werewolf_cre END
   LAF kaishas_confrontation END

   LAF check_label STR_VAR label=dw#mage RET value=value END
   ACTION_IF value BEGIN
      LAF werewolf_shapechange END
      LAF werewolf_human_form END
   END

END


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION werewolf_cre BEGIN

   // give Kaishas and Baresh greater-werewolf rings
   
   LAF edit_creature STR_VAR creature=EVALUATE_BUFFER ~%tutu_scriptk%aishwlf %tutu_var%barwlf~ editstring=EVALUATE_BUFFER ~add_items=>%tutu_scriptr%ingwolf~ END

   // give greater werewolves/wolfweres Free Action
   
   LAF edit_creature STR_VAR creature=EVALUATE_BUFFER ~%tutu_scriptk%aishwlf %tutu_var%barwlf %tutu_var%loupgar %tutu_scriptw%olfwegr~
                             editstring=~add_items=>ring09(boots)~
   END

END

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION werewolf_shapechange BEGIN

   // complicated by TUTU/BGT issues
   
   // find the KAISHAS_CHANGE spell
   
   ACTION_IF GAME_IS ~bgt~ BEGIN
      OUTER_SPRINT KAISHAS_CHANGE bgwi925
   END
   
   // make a Baresh version
   
   LAF clone_spell STR_VAR spell=EVALUATE_BUFFER ~%KAISHAS_CHANGE%=>dw#barwf~ END
   LAF swap_text STR_VAR files=dw#barwf.spl swaps=EVALUATE_BUFFER ~%tutu_scriptk%aishwlf=>%tutu_var%barwlf~ END


END

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION werewolf_human_form BEGIN

   // this whole part only applies if Smarter Mages is installed

   // scripts
   
   LAF ssl_to_bcs STR_VAR script=~baresh daese dw#selaa~ location=~werewolf/ssl~ END


   // make Baresh into a L12 fighter
   
   MAKE_PATCH
      class=>FIGHTER
      race=>LYCANTHROPE
      general=>MONSTER
      add_items=>~%tutu_var%immune1 minhp1~
      script_override=>baresh
      script_class=>wtasight
      genai=>all
   END
   LAF edit_creature STR_VAR creature=baresh2 tv=yes END
   
   // fix problems with Baresh's wolf form
   
   MAKE_PATCH
      class=>WEREWOLF
      race=>LYCANTHROPE
      general=>MONSTER
   END
   LAF edit_creature STR_VAR creature=barwlf tv=yes END

   // make Daese a L8 enchanter and Selaad a L10 enchanter
   

      MAKE_PATCH
         class=>mage
         level=>8
         kit=>ENCHANTER
         strip_script=>all
         script_override=>daese
         add_items=>~%tutu_var%immune1 minhp1 staf01~
         enforce_mage=>~accept_level game_is_bg1~
      END
      LAF edit_creature STR_VAR creature=daese tv=yes END
      MAKE_PATCH
         class=>mage
         level=>10
         kit=>ENCHANTER
         strip_script=>all
         script_override=>dw#selaa
         add_items=>~%tutu_var%immune1 minhp1 staf01~
         enforce_mage=>~accept_level game_is_bg1~
      END
      LAF edit_creature STR_VAR creature=mendas3 tv=yes END


   // make Baresh and Selaad attack as humans to start with
    COPY_EXISTING ~%tutu_var%menda4.dlg~ ~override~
          DECOMPILE_AND_PATCH BEGIN
          REPLACE_TEXTUALLY  ~ActionOverride("baresh",ApplySpell("baresh",KAISHAS_CHANGE))~ ~ActionOverride("baresh",Enemy())~
         // BGT (BG1UB [creature name restoration] may have modified it)
        REPLACE_TEXTUALLY  ~ActionOverride("baresh",ApplySpellRES("\(BGWI\|SPKI\)92[45]",Myself))~ ~ActionOverride("baresh",Enemy())~
          REPLACE_TEXTUALLY  ~ForceSpell(Myself,LOUPGAR_CHANGE)~ ~Enemy()~
       END
END

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////


DEFINE_ACTION_FUNCTION kaishas_confrontation BEGIN

   LAF clone_creature STR_VAR creature=EVALUATE_BUFFER ~%tutu_var%werewo=>dw#wwolf~ editstring=~insert_script_high=>dw#hlpka~ END
   LAF extend_area_script STR_VAR area=EVALUATE_BUFFER ~%IsleofBalduranN%~ script=werewolfadd location=resource END
   LAF extend STR_VAR file=kaisha tv=yes bottom=kaishas location=resource END
   LAF install STR_VAR files=~dw#hlpka.baf dw#kcut1.baf dw#kcut2.baf dw#kcut3.baf werewolf.d~ location=resource END
   LAF swap_text STR_VAR files=EVALUATE_BUFFER ~%tutu_var%ship.bcs~ swaps=~Dead("kaishwlf")=>OR(2)Dead("kaishwlf")Global("DMWWNoKillKaishas","GLOBAL",1)~ END


END
