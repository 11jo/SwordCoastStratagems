DEFINE_ACTION_FUNCTION modify_breach BEGIN
// breach needs to be school: abjuration, sectype: 0

MAKE_PATCH
   secondary=>0
   school=>1
END
LAF edit_spell STR_VAR spell=spwi513 edits=patch_data END

ACTION_IF is_bg2=1 BEGIN
// we let Breach affect (demi)liches and Rakshasas by removing their immunity to L5 spells and then explicitly giving them immunity
// to all L5 spells other than breach

MAKE_PATCH
   patch_cond_inline =>~match=>combine_checks
                        match_parameter=>"ITM_read_opcode=>102 ITM_read_parameter1=>5 "
                        type=>effect_global
                        check=>1
                        opcode=>999~
   delete_opcodes => 999
END
LAF edit_item STR_VAR item=~lich demilich rakring~ edits=patch_data END

ACTION_IF !FILE_EXISTS_IN_GAME ~dw#licim.spl~ BEGIN
   COPY ~%scsroot%/mage/resource/dw#licim.spl~ ~override~
END

MAKE_PATCH
   patch_cond_inline =>~match=>combine_checks
                        match_parameter=>"SPL_read_opcode=>102 SPL_read_parameter1=>5 "
                        type=>effect
                        check=>1
                        opcode=>999~
   delete_opcodes => 999
END
LAF edit_spell STR_VAR spell=dw#licim edits=patch_data END

ACTION_FOR_EACH spell IN SPDR501 SPWI508 SPWI503 SPWI506 SPWI514 SPWI523 SPPR503 SPPR517 SPPR505D BEGIN
   OUTER_SPRINT ~args~ "opcode=>206 parameter1 => ~-1~ timing=>2 target=>1 resource=>%spell%"
   LAF edit_item STR_VAR item=~lich demilich rakring~ editstring=~add_equipped_effect_inline=>"%args%"~ END
   LAF edit_spell STR_VAR spell=~dw#licim~ editstring=~add_effect_inline=>"%args%"~ END
END

END

END

