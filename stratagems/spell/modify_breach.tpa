DEFINE_ACTION_FUNCTION modify_breach BEGIN
// breach needs to be school: abjuration, sectype: 0

LAF edit_spell STR_VAR spell=spwi513 editstring="school=>1 secondary=>0" END

MAKE_PATCH
    match=>"ability_icon=spwi608b"
    ability_school=>1
    ability_secondary=>0
END
LAF edit_item INT_VAR allow_missing=1 STR_VAR item=wand18 editstring="patch_ability=>patch_data" END

ACTION_IF is_bg2=1 BEGIN
// we let Breach affect (demi)liches and Rakshasas by removing their immunity to L5 spells and then explicitly giving them immunity
// to all L5 spells other than breach


LAF edit_item STR_VAR item=~lich demilich rakring~ editstring=~delete_effect=>"opcode=102 and parameter1=5"~ END

ACTION_IF !FILE_EXISTS_IN_GAME ~dw#licim.spl~ BEGIN
   LAF install STR_VAR file="dw#licim.spl" locbase="mage/resource" END
END
LAF edit_spell STR_VAR spell=dw#licim edits=patch_data END

MAKE_PATCH
 add_effect_global_inline=>~
   opcode=>206
   parameter1=>"-1"
   timing=>2
   target=>1
   var_list=>"[SPDR501 SPWI508 SPWI503 SPWI506 SPWI514 SPWI523 SPPR503 SPPR517 SPPR505D]"
   number_to_add=>"length list"
   resource=>"entry_index from list" ~
  add_effect_global_inline'=>~
   opcode=>83
   parameter2=>186
   timing=>2
   target=>1~ // for Cloudkill wand
END
LAF edit_item STR_VAR item="lich demilich rakring" edits=patch_data END

MAKE_PATCH
 add_effect_inline=>~
   opcode=>206
   parameter1=>"-1"
   timing=>1
   target=>1
   var_list=>"[SPDR501 SPWI508 SPWI503 SPWI506 SPWI514 SPWI523 SPPR503 SPPR517 SPPR505D]"
   number_to_add=>"length list"
   resource=>"entry_index from list" ~
  add_effect_inline'=>~
   opcode=>83
   parameter2=>186
   timing=>1
   target=>1~
END
LAF edit_spell STR_VAR spell="dw#licim" edits=patch_data END

END
END

