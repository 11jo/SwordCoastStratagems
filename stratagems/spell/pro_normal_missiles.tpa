DEFINE_ACTION_FUNCTION pro_normal_missiles
BEGIN

ACTION_FOR_EACH arrow IN bolt03 arow04 arow08 arow09 BEGIN
 COPY_EXISTING ~%tutu_var%%arrow%.itm~ ~override~
   READ_LONG 0x64 ab_off
   READ_SHORT 0x2a + ab_off pro_num
   SET EVALUATE_BUFFER ~%arrow%_pro~ = pro_num
 BUT_ONLY
END

DEFINE_PATCH_FUNCTION unique_arrow_clone
   INT_VAR offset_secondary=0
   STR_VAR file_prefix=""
           offset_base=""
   RET value
BEGIN
   LPF ~%file_prefix%_read_opcode~ INT_VAR offset_secondary RET value1=value END
   LPF ~%file_prefix%_read_parameter2~ INT_VAR offset_secondary RET value2=value END
   SET value=(value1=83)&&(value2=4)
END

ACTION_CLEAR_ARRAY patch_data
ACTION_DEFINE_ASSOCIATIVE_ARRAY patch_data BEGIN
     clone_entry_inline=>~match=>unique_arrow_clone
                          type=>effect
                          check=>1
                          parameter2=>%arow04_pro%~
     clone_entry_inline'=>~match=>unique_arrow_clone
                          type=>effect
                          check=>1
                          parameter2=>%arow08_pro%~
     clone_entry_inline''=>~match=>unique_arrow_clone
                          type=>effect
                          check=>1
                          parameter2=>%arow09_pro%~
     clone_entry_inline'''=>~match=>unique_arrow_clone
                          type=>effect
                          check=>1
                          parameter2=>%bolt03_pro%~
  END

  LAF edit_spell STR_VAR spell=EVALUATE_BUFFER ~%WIZARD_PROTECTION_FROM_NORMAL_MISSILES%~ edits=patch_data END
  LAF edit_item STR_VAR item=EVALUATE_BUFFER ~%tutu_scriptm%agebrac~ edits=patch_data allow_missing=yes END


     // Flame Arrow spell needs its own projectile

      COPY_EXISTING ~arrowflm.pro~ ~%workspace%/dw#flam.pro~
      ADD_PROJECTILE ~%workspace%/dw#flam.pro~

      LAF patch_projectile INT_VAR pro_num=EVALUATE_BUFFER ~%dw#flam%~ STR_VAR spell=EVALUATE_BUFFER ~%WIZARD_FLAME_ARROW%~ END
END


DEFINE_ACTION_FUNCTION patch_projectile
   INT_VAR pro_num=0
   STR_VAR item=""
           spell=""
           tv="no"
BEGIN
     ACTION_DEFINE_ASSOCIATIVE_ARRAY proj_data BEGIN
        type=>ability
        projectile=>~%pro_num%~
      END
      ACTION_IF ~%item%~ STRING_COMPARE ~~ BEGIN
         LAF edit_item STR_VAR item tv editstring=~patch_conditionally=>proj_data~ END
      END
      ACTION_IF ~%spell%~ STRING_COMPARE ~~ BEGIN
         LAF edit_spell STR_VAR spell editstring=~patch_conditionally=>proj_data~ END
      END
END
